
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ВнешняяЛаборатория = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПараметрКоманды, "ВнешняяЛаборатория");
	Если Не ЗначениеЗаполнено(ВнешняяЛаборатория) Тогда
		Возврат;
	КонецЕсли;
	
	СписокЛаборатории = Новый СписокЗначений;
	СписокЛаборатории.Добавить(ПараметрКоманды);	
	
	ФормаОтчета = ПолучитьФорму("Отчет.ДоступностьИсследованийВнешнихЛабораторий.Форма");
	
	ФормаОтчета.УстановитьТекущийВариант("ПоЛабораториям");
	
	Для Каждого ЭлементНастроек Из ФормаОтчета.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы Цикл
		Если ТипЗнч(ЭлементНастроек) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			Если Строка(ЭлементНастроек.Параметр) = "Дата" Тогда
				Дата = НачалоДня(ТекущаяДата());
				СтандартнаяДатаНачала = Новый СтандартнаяДатаНачала;
				СтандартнаяДатаНачала.Вариант = ВариантСтандартнойДатыНачала.ПроизвольнаяДата;
				СтандартнаяДатаНачала.Дата = Дата; 
				ЭлементПользовательскихНастроекНайден = Ложь;
				Для Каждого ЭлементПользовательскихНастроек Из ФормаОтчета.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
					Если ЭлементПользовательскихНастроек.ИдентификаторПользовательскойНастройки = ЭлементНастроек.ИдентификаторПользовательскойНастройки Тогда
						ЭлементПользовательскихНастроек.Значение = СтандартнаяДатаНачала;
						ЭлементПользовательскихНастроек.Использование = Истина;
						ЭлементНастроек.Использование = Истина;
						ЭлементПользовательскихНастроекНайден = Истина;
					КонецЕсли;
				КонецЦикла;
				Если Не ЭлементПользовательскихНастроекНайден Тогда
					ЭлементНастроек.Значение = СтандартнаяДатаНачала;
					ЭлементНастроек.Использование = Истина;
				КонецЕсли;
			ИначеЕсли Строка(ЭлементНастроек.Параметр) = "Лаборатории" Тогда
				ЭлементНастроек.Значение = СписокЛаборатории;		
				ЭлементНастроек.Использование = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ФормаОтчета.СкомпоноватьРезультат();
	ФормаОтчета.Открыть();
	ФормаОтчета.ПользовательскиеНастройкиМодифицированы = Ложь;
	ФормаОтчета.ВариантМодифицирован = Ложь;
	
КонецПроцедуры
