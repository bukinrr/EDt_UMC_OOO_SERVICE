#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	Настройки = ОбъектОтчет.КомпоновщикНастроек.Настройки;
	ПараметрСКДПериод = Настройки.ПараметрыДанных.Элементы.Найти("Период");
	Период = ПараметрСКДПериод.Значение; 
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЭтаФорма.ВариантМодифицирован Тогда
		ЭтаФорма.ВариантМодифицирован = Ложь;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьСервер();
КонецПроцедуры

&НаКлиенте
Процедура Параметры(Команда)
	
	Элементы.ФормаПараметры.Пометка = Не Элементы.ФормаПараметры.Пометка;
	Элементы.ГруппаНастройки.Видимость = Элементы.ФормаПараметры.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВExcel(Команда)
	
	#Если ВебКлиент Или МобильныйКлиент Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='В режиме Веб-клиента данная операция не доступна.'"));
	#Иначе
	
	ИмяВременногоФайла=ПолучитьИмяВременногоФайла("xls");
	Результат.Записать(ИмяВременногоФайла,"xls");
	Попытка
		ЗапуститьПриложение(ИмяВременногоФайла);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Необходимо установить MS Excel'"));
	КонецПопытки;
	
	#КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьСервер()
	
	Результат.Очистить();
	Результат.ФиксацияСлева = 1;
	Результат.ФиксацияСверху = 1;
	Результат.ПовторятьПриПечатиКолонки = Результат.Область("C1");
	Результат.ПовторятьПриПечатиСтроки = Результат.Область("R1");
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = ОбъектОтчет.КомпоновщикНастроек.Настройки;
	ПараметрСКДПериод = Настройки.ПараметрыДанных.Элементы.Найти("Период");
    ПараметрСКДПериод.Значение = Период;
    ПараметрСКДПериод.Использование = Истина;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ОбъектОтчет.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"),
												  ОбъектОтчет.КомпоновщикНастроек.Настройки,,, 
												  Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);	
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТабРезультат = Новый ТаблицаЗначений;
	ТабРезультат = ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
	ТабРезультат.Сортировать("Дата, ВремяНачала, ВремяОкончания");
	
	Сотрудники = ТабРезультат.Скопировать();
	Сотрудники.Свернуть("Сотрудник,ТолькоСотрудники");
	Сотрудники.Сортировать("ТолькоСотрудники Убыв,Сотрудник Возр");
	МассивСотрудников = Сотрудники.ВыгрузитьКолонку("Сотрудник");
	Результат.Область(1,1,1,массивСотрудников.Количество()+1).ШиринаКолонки = 17;
	Результат.Область(1,1,1,массивСотрудников.Количество()+1).АвтоВысотаСтроки = Истина;
	Результат.Область(1,1).Текст = "Дата";
	Результат.Область(1,1).Обвести(Линия,Линия,Линия,Линия);
	Для Счетчик=1 По массивСотрудников.Количество() Цикл
		Результат.Область(1,Счетчик+1).Текст = МассивСотрудников[счетчик-1];
		Результат.Область(1,Счетчик+1).Расшифровка = МассивСотрудников[счетчик-1];
	КонецЦикла;
	
	ТекущаяДата = Неопределено;
	ТекущийСотрудник = Неопределено;
	ТекущийВидВремени = Неопределено;
	ТекущаяСтрока = 1;
	Для Каждого СтрокаТ Из ТабРезультат Цикл
		
		Если ТекущаяДата = Неопределено Или ТекущаяДата <> СтрокаТ.Дата Тогда
			ТекущаяДата = СтрокаТ.Дата;
			ТекущаяСтрока = ТекущаяСтрока + 1;
			Результат.Область(ТекущаяСтрока,1).Текст = Формат(ТекущаяДата,"ДФ=dd.MM.yyyy");
			
			ТекущийФилиал = Новый Соответствие;
			ТекущееПодразделение = Новый Соответствие;
			ТекущийКабинет = Новый Соответствие;
			ТекущийВидВремени = Неопределено;
		КонецЕсли;
		НомерКолонки = 0;
		Для Счетчик = 1 По МассивСотрудников.Количество() Цикл
			Если МассивСотрудников[Счетчик-1] = СтрокаТ.Сотрудник Тогда
				НомерКолонки = Счетчик+1;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ТекущийСотрудник <> СтрокаТ.Сотрудник Тогда
			ТекущийСотрудник = СтрокаТ.Сотрудник;
			ТекущийВидВремени = Неопределено;
		КонецЕсли;
		
		Текст = "";
		// Вывод филиала
		Если ВыводитьФилиал
			И ЗначениеЗаполнено(СтрокаТ.Филиал)
			И ТекущийФилиал.Получить(СтрокаТ.Сотрудник) <> СтрокаТ.Филиал
		Тогда
			Текст = Текст + СтрокаТ.Филиал + Символы.ПС;
			ТекущийФилиал.Вставить(СтрокаТ.Сотрудник, СтрокаТ.Филиал);
		КонецЕсли;
		// Вывод подразделения
		Если ВыводитьПодразделение
			И ЗначениеЗаполнено(СтрокаТ.Подразделение)
			И ТекущееПодразделение.Получить(СтрокаТ.Сотрудник) <> СтрокаТ.Подразделение
		Тогда
			Текст = Текст + СтрокаТ.Подразделение + Символы.ПС;
			ТекущееПодразделение.Вставить(СтрокаТ.Сотрудник, СтрокаТ.Подразделение);
		КонецЕсли;
		// Вывод кабинета сотрудника, сотрудника кабинета.
		Если ВыводитьКабинетСотрудника
			И ЗначениеЗаполнено(СтрокаТ.Кабинет)
			И ТекущийКабинет.Получить(СтрокаТ.Сотрудник) <> СтрокаТ.Кабинет
		Тогда
			Текст = Текст + СтрокаТ.Кабинет + Символы.ПС;
			ТекущийКабинет.Вставить(СтрокаТ.Сотрудник, СтрокаТ.Кабинет);
		КонецЕсли;
		// Вывод вида времени
		Если ВыводитьВидВремени
			И ТекущийВидВремени <> СтрокаТ.ВидВремени
		Тогда
			Если Не ЗначениеЗаполнено(СтрокаТ.ВидВремени) Тогда
				СтрокаТ.ВидВремени = Справочники.ВидыВремениГрафика.РабочееВремя;
			КонецЕсли;

			Текст = Текст + СтрокаТ.ВидВремени + Символы.ПС;
			ТекущийВидВремени = СтрокаТ.ВидВремени;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТ.Смена) Тогда
			Для Каждого Отрывок Из СтрокаТ.Смена.ПериодыСмены Цикл
				Текст = Текст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='С %1 по %2'"),
									Формат(Отрывок.ВремяНачала,"ДФ=HH:mm"),
									Формат(Отрывок.ВремяОкончания,"ДФ=HH:mm")) + Символы.ПС;
			КонецЦикла;
		Иначе
			Текст = Текст + Формат(СтрокаТ.ВремяНачала,"ДФ=HH:mm") + " - " + Формат(СтрокаТ.ВремяОкончания,"ДФ=HH:mm");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.КонкатенацияСтрок(Результат.Область(ТекущаяСтрока,НомерКолонки).Текст, Текст, Символы.ПС);
	КонецЦикла;
	Для Счетчик = 1 По ТекущаяСтрока Цикл
		Для Счетчик2 = 1 По МассивСотрудников.Количество() + 1 Цикл
			Результат.Область(Счетчик,Счетчик2).Обвести(Линия,Линия,Линия,Линия);
			Результат.Область(Счетчик,Счетчик2).ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			Результат.Область(Счетчик,Счетчик2).ВертикальноеПоложение = ВертикальноеПоложение.Центр;
		КонецЦикла;
	КонецЦикла;
	
	Результат.Область().РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	
КонецПроцедуры

#КонецОбласти