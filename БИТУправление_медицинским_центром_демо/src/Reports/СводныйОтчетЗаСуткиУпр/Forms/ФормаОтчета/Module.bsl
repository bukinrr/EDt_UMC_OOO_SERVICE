
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("СформироватьОтчет", 0.1, Истина);
	
	Элементы.ГруппаИспользованиеНастройкиФилиала.Видимость = Ложь;
	ЗагрузитьНастройкиОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекПользовательскиеНастройкиПриИзменении(Элемент)
	КомпоновщикНастроекПользовательскиеНастройкиПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомпоновщикНастроекПользовательскиеНастройкиПриИзмененииНаСервере()
	Настройки = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	ДобавитьОтборы(Настройки);	
	Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьОтчет()
	
	СкомпоноватьРезультат();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиОтчета()
		
	Настройки = ПолучитьНастройиФилиала();
	Если Настройки = Неопределено Тогда
		ХранилицеНастроекОтчета = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("НастройкаСводногоОтчетаЗаСутки");
		Настройки = ХранилицеНастроекОтчета.получить();
	КонецЕсли; 
 
	// Настройка доступности поля даты
	Если Настройки <> Неопределено Тогда
		
		ДобавитьОтборы(Настройки);
		
		Структура = Настройки.Структура;
		Для Каждого СтрокаВложеннойСхемы Из Структура Цикл
			НастройкаВыводитьОтбор = СтрокаВложеннойСхемы.Настройки.ПараметрыВывода.Элементы.Найти("ВыводитьОтбор");
			Если НастройкаВыводитьОтбор.Использование = Ложь Тогда
				НастройкаВыводитьОтбор.Использование = Истина;
				НастройкаВыводитьОтбор.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить;
			КонецЕсли;
		КонецЦикла;
		
		Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
		Отчет.КомпоновщикНастроек.Восстановить();
		ЭлементПараметраДата = Неопределено;
		Для Каждого Элемент Из Настройки.ПараметрыДанных.Элементы Цикл
			Если Строка(Элемент.Параметр) = "День" Тогда
				ЭлементПараметраДата = Элемент;
				Продолжить;
			КонецЕсли;
		КонецЦикла;
		Для Каждого Элемент Из Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
			Если ЭлементПараметраДата <> Неопределено И Элемент.ИдентификаторПользовательскойНастройки = ЭлементПараметраДата.ИдентификаторПользовательскойНастройки Тогда
				Элемент.РежимОтображения = ЭлементПараметраДата.РежимОтображения;
				Если Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
					Элементы.КомпоновщикНастроекПользовательскиеНастройки.Видимость = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	Если Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Количество() = 0 Тогда
		Элементы.КомпоновщикНастроекПользовательскиеНастройки.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОтборы(Настройки)
	
	Структура = Настройки.Структура;
	ПараметрФилиалы = Настройки.ПараметрыДанных.Элементы.Найти("Филиалы");
	Если ПараметрФилиалы <> Неопределено
		И ПараметрФилиалы.Значение <> Неопределено
		И Не (ТипЗнч(ПараметрФилиалы.Значение) = Тип("СписокЗначений") И ПараметрФилиалы.Значение.Количество() = 0)
	Тогда
		Для Каждого СтрокаВложеннойСхемы Из Структура Цикл
			СтрокаВложеннойСхемы.Настройки.Отбор.Элементы.Очистить();
			НовыйЭлементОтбора  = СтрокаВложеннойСхемы.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			Если СтрокаВложеннойСхемы.ИдентификаторОбъекта = "ДвижениеДенежныхСредствПоКассе" Тогда
				ПолеОтбора = Новый ПолеКомпоновкиДанных("Касса.Филиал");
			ИначеЕсли СтрокаВложеннойСхемы.ИдентификаторОбъекта = "ВыработкаСотрудников" Тогда
				ПолеОтбора = Новый ПолеКомпоновкиДанных("Филиал");
			ИначеЕсли СтрокаВложеннойСхемы.ИдентификаторОбъекта = "ВзаиморасчетыССотрудниками" Тогда
				ПолеОтбора = Новый ПолеКомпоновкиДанных("Филиал");
			ИначеЕсли СтрокаВложеннойСхемы.ИдентификаторОбъекта = "ДвижениеДенежныхСредствБезналичные" Тогда
				ПолеОтбора = Новый ПолеКомпоновкиДанных("Регистратор.Филиал");
			Иначе
				Продолжить;
			КонецЕсли;
			НовыйЭлементОтбора.ЛевоеЗначение = ПолеОтбора;
			НовыйЭлементОтбора.Использование = ПараметрФилиалы.Использование;
			НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			НовыйЭлементОтбора.ПравоеЗначение = ПараметрФилиалы.Значение;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
 
&НаСервере
Функция ПолучитьНастройиФилиала()
	
	Перем НастройкиОтчета;
	
	Филиал = УправлениеНастройками.ПолучитьФилиалПоУмолчаниюПользователя();
	Выб = РегистрыСведений.УчетнаяПолитикаФилиалов.Выбрать(Новый Структура("Филиал",Филиал));
	Если Выб.Следующий() Тогда
		НастройкиОтчета = Выб.НастройкаСводногоОтчетаЗаСутки.Получить();
		Если ТипЗнч(НастройкиОтчета) = Тип("НастройкиКомпоновкиДанных") Тогда
			Элементы.НадписьФилиалНастройки.Заголовок = Филиал;
			Элементы.ГруппаИспользованиеНастройкиФилиала.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкиОтчета;
	
КонецФункции
 
#КонецОбласти