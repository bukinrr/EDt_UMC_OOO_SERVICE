#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
		
	Таблица = ПолучитьДиагнозы();
	
	ВнешниеНаборыДанных = Новый Структура("Данные",Таблица);
	
	ОсновнойМакетОформления = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ОсновнойМакетОформленияСКД");
	КомпоновщикНастроек.ЗагрузитьНастройки(КомпоновщикНастроек.ПолучитьНастройки());
	ПараметрМакет = КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("МакетОформления");
	ПараметрМакет.Значение = ОсновнойМакетОформления;
	ПараметрМакет.Использование = Истина;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();    
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанных = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	ПроцессорВыводаРезультатаКомпоновкиДанных.УстановитьДокумент(ДокументРезультат);
	ПроцессорВыводаРезультатаКомпоновкиДанных.Вывести(ПроцессорКомпоновкиДанных);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДиагнозы()
	ПараметрыОтчета = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы;
	
	ДатаНачало = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[0].Значение.ДатаНачала;
	ДатаОкончание = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[0].Значение.ДатаОкончания; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачало);
	Запрос.УстановитьПараметр("КонецПериода",  ДатаОкончание);
	Запрос.УстановитьПараметр("СписокФилиалов", ПараметрыОтчета.Найти("СписокФилиалов").Значение);
	Запрос.УстановитьПараметр("Страница", ПараметрыОтчета.Найти("Страница").Значение);
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВЫБОР
				   |		КОГДА ТИПЗНАЧЕНИЯ(ЛистокНетрудоспособности.ДиагнозПоМКБ10) = ТИП(Справочник.Диагнозы)
				   |			ТОГДА ЛистокНетрудоспособности.ДиагнозПоМКБ10.КодДиагноза
	               |		ИНАЧЕ ВЫРАЗИТЬ(ЛистокНетрудоспособности.ДиагнозПоМКБ10 КАК СТРОКА(10)) 
				   |	КОНЕЦ КАК КодДиагноза,
	               |	РАЗНОСТЬДАТ(ЛистокНетрудоспособности.Клиент.ДатаРождения, ЛистокНетрудоспособности.Дата, ГОД) - ВЫБОР
	               |		КОГДА ДЕНЬГОДА(ЛистокНетрудоспособности.Клиент.ДатаРождения) >= ДЕНЬГОДА(ЛистокНетрудоспособности.Дата)
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Возраст,
	               |	ВЫБОР
	               |		КОГДА ЛистокНетрудоспособности.ПервичныйЛисток = ЗНАЧЕНИЕ(Документ.ЛистокНетрудоспособности.ПустаяСсылка)
	               |				ИЛИ ЛистокНетрудоспособности.ПервичныйЛисток = НЕОПРЕДЕЛЕНО
	               |			ТОГДА ЛистокНетрудоспособности.Ссылка
	               |		ИНАЧЕ ЛистокНетрудоспособности.ПервичныйЛисток
	               |	КОНЕЦ КАК Документ,
	               |	ЛистокНетрудоспособности.Пол КАК Пол,
	               |	ЛистокНетрудоспособности.ПричинаНетрудоспособности.Код КАК ПричинаНетрудоспособностиКод,
	               |	ВЫБОР
	               |		КОГДА ЛистокНетрудоспособности.Запись1ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ТОГДА РАЗНОСТЬДАТ(ЛистокНетрудоспособности.Запись1ДатаНачала, ЛистокНетрудоспособности.Запись1ДатаОкончания, ДЕНЬ)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ + ВЫБОР
	               |		КОГДА ЛистокНетрудоспособности.Запись2ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ТОГДА РАЗНОСТЬДАТ(ЛистокНетрудоспособности.Запись2ДатаНачала, ЛистокНетрудоспособности.Запись2ДатаОкончания, ДЕНЬ)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ + ВЫБОР
	               |		КОГДА ЛистокНетрудоспособности.Запись3ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ТОГДА РАЗНОСТЬДАТ(ЛистокНетрудоспособности.Запись3ДатаНачала, ЛистокНетрудоспособности.Запись3ДатаОкончания, ДЕНЬ)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ + ВЫБОР
	               |		КОГДА ЛистокНетрудоспособности.ПервичныйЛисток.Запись1ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ТОГДА РАЗНОСТЬДАТ(ЛистокНетрудоспособности.ПервичныйЛисток.Запись1ДатаНачала, ЛистокНетрудоспособности.ПервичныйЛисток.Запись1ДатаОкончания, ДЕНЬ)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ + ВЫБОР
	               |		КОГДА ЛистокНетрудоспособности.ПервичныйЛисток.Запись2ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ТОГДА РАЗНОСТЬДАТ(ЛистокНетрудоспособности.ПервичныйЛисток.Запись2ДатаНачала, ЛистокНетрудоспособности.ПервичныйЛисток.Запись2ДатаОкончания, ДЕНЬ)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ + ВЫБОР
	               |		КОГДА ЛистокНетрудоспособности.ПервичныйЛисток.Запись3ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ТОГДА РАЗНОСТЬДАТ(ЛистокНетрудоспособности.ПервичныйЛисток.Запись3ДатаНачала, ЛистокНетрудоспособности.ПервичныйЛисток.Запись3ДатаОкончания, ДЕНЬ)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Дней,
	               |	ЛистокНетрудоспособности.Клиент КАК Клиент,
	               |	ЛистокНетрудоспособности.ДиагнозПоМКБ10 КАК Диагноз,
	               |	ЛистокНетрудоспособности.ПричинаНетрудоспособности КАК ПричинаНетрудоспособности
	               |ПОМЕСТИТЬ СписокДиагнозов
	               |ИЗ
	               |	Документ.ЛистокНетрудоспособности КАК ЛистокНетрудоспособности
	               |ГДЕ
	               |	ЛистокНетрудоспособности.ДиагнозПоМКБ10 <> ЗНАЧЕНИЕ(Справочник.Диагнозы.ПустаяСсылка)
	               |	И ЛистокНетрудоспособности.Филиал В(&СписокФилиалов)
	               |	И ЛистокНетрудоспособности.Дата >= &НачалоПериода
	               |	И ЛистокНетрудоспособности.Дата <= &КонецПериода
	               |	И НЕ ЛистокНетрудоспособности.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЛистковНетрудоспособности.Испорчен), ЗНАЧЕНИЕ(Перечисление.СтатусыЛистковНетрудоспособности.Подготовка))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписокДиагнозов.Документ КАК Документ,
	               |	СписокДиагнозов.Клиент КАК Клиент,
	               |	СписокДиагнозов.Диагноз КАК Диагноз,
	               |	СписокДиагнозов.Дней КАК КоличествоДней,
	               |	СписокДиагнозов.ПричинаНетрудоспособности КАК Причина
	               |ИЗ
	               |	СписокДиагнозов КАК СписокДиагнозов
	               |ГДЕ
	               |	ИСТИНА";
	ДобавитьГруппыВТекстЗапроса(Запрос.Текст, ПараметрыОтчета.Найти("Группа").Значение);
	Запрос.Текст = Запрос.Текст + ПараметрыОтчета.Найти("Колонка").Значение;
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьГруппыВТекстЗапроса(Текст, Знач Группы)
	Если ЗначениеЗаполнено(Группы) Тогда
		Если Лев(Группы,1) = "ж" Тогда
			Текст = Текст + " И СписокДиагнозов.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическихЛиц.Женский) ";
			Группы = СтрЗаменить(Группы,"ж","");
		Иначе
			Текст = Текст + " И СписокДиагнозов.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическихЛиц.Мужской) ";	
		КонецЕсли; 
		
		Если Группы = "0" Тогда
			Текст = Текст + " И СписокДиагнозов.ПричинаНетрудоспособностиКод В (""09"",""08"",""03"",""05"") ";	
		ИначеЕсли Лев(Группы,1) = "0" Тогда
			Текст = Текст + " И СписокДиагнозов.ПричинаНетрудоспособностиКод = """ + Группы + """";
		Иначе 	 
			Текст = Текст + " И (";
			МассивГрупп = СтрРазделить(Группы, ",", Ложь);
			Для каждого Группа Из МассивГрупп Цикл
				НачалоКонец = СтрРазделить(Группа, "-", Ложь);
				СтрокаУсловия = " СписокДиагнозов.КодДиагноза >= " + НачалоКонец[0];
				Если НачалоКонец.Количество() = 2 Тогда
					СтрокаУсловия = " СписокДиагнозов.КодДиагноза >= """ + НачалоКонец[0] + """ И СписокДиагнозов.КодДиагноза <= """ + НачалоКонец[1] + """"; 
				Иначе
					СтрокаУсловия = " СписокДиагнозов.КодДиагноза = """ + НачалоКонец[0] + """";	
				КонецЕсли; 
				Текст = Текст +	СтрокаУсловия + " ИЛИ ";
			КонецЦикла; 
			
			Текст = Текст +	"ЛОЖЬ)";
	  	КонецЕсли;
	КонецЕсли; 		
КонецФункции

#КонецОбласти