
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - Структура - см. возвращаемое значение
//       ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ФормироватьСразу = Истина;
	
	Настройки.События.ПриЗагрузкеВариантаНаСервере					= Истина;
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере	= Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
// См.  ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных - Настройки для загрузки в компоновщик настроек.
//
Процедура ПриЗагрузкеВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
	
	// Заполнить пустой параметр прейскуранта.
	Попытка
		Прейскурант = УправлениеНастройками.ПолучитьПрейскурантФилиала();
		Если Не ЗначениеЗаполнено(Прейскурант) Тогда Возврат КонецЕсли;
		
		ПараметрДанных = НовыеНастройкиКД.ПараметрыДанных.Элементы.Найти("Прейскурант");
		Если ПараметрДанных <> Неопределено И Не ЗначениеЗаполнено(ПараметрДанных.Значение) Тогда
			ПараметрДанных.Значение = Прейскурант;
		КонецЕсли;
		
		ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Прейскурант");
		Если ПараметрДанных <> Неопределено И Не ЗначениеЗаполнено(ПараметрДанных.Значение) Тогда
			ПараметрДанных.Значение = Прейскурант;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
// См. ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   НовыеПользовательскиеНастройкиКД - НастройкиКомпоновкиДанных - Настройки для загрузки в компоновщик настроек.
//
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	// Заполнить пустой параметр прейскуранта.
	Попытка
		ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Прейскурант");
		Если ПараметрДанных <> Неопределено И ЗначениеЗаполнено(ПараметрДанных.Значение) Тогда
			Для Каждого ЭлементНастройки Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
				Если ЭлементНастройки.ИдентификаторПользовательскойНастройки = ПараметрДанных.ИдентификаторПользовательскойНастройки Тогда
					Если Не ЗначениеЗаполнено(ЭлементНастройки.Значение) Тогда
						ЭлементНастройки.Значение = ПараметрДанных.Значение;
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УниверсальныеМеханизмыСервер.ОтчетПриКомпоновкеРезультата(ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти