#Область ПрограммныйИнтерфейс

Процедура СохранитьИдентификаторыСтрокШаблоновПередЗаписью(ЭтаФорма, ПараметрыЗаписи) Экспорт
	
	ИдентификаторыСтрокШаблонов = Новый Соответствие;
	Для Каждого СтрокаШаблона Из ЭтаФорма.Объект.ШаблоныПриема Цикл
		ИдентификаторыСтрокШаблонов.Вставить(СтрокаШаблона.ПолучитьИдентификатор(), ЭтаФорма.Объект.ШаблоныПриема.Индекс(СтрокаШаблона));
	КонецЦикла;
	ПараметрыЗаписи.Вставить("ИдентификаторыСтрокШаблонов", ИдентификаторыСтрокШаблонов);
	
КонецПроцедуры

Процедура ОбновитьИдентификаторыСтрокШаблоновПослеЗаписи(ЭтаФорма, ПараметрыЗаписи) Экспорт
	
	Если Не ПараметрыЗаписи.Свойство("ИдентификаторыСтрокШаблонов") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыСтрокШаблонов = ПараметрыЗаписи.ИдентификаторыСтрокШаблонов;
	
	ТаблицыСИдентификаторомСтрокиШаблона = Новый Массив;
	ТаблицыСИдентификаторомСтрокиШаблона.Добавить(ЭтаФорма.ТаблицаПараметров);
	ТаблицыСИдентификаторомСтрокиШаблона.Добавить(ЭтаФорма.ПараметрыТаблицШаблонов);
	ТаблицыСИдентификаторомСтрокиШаблона.Добавить(ЭтаФорма.СтраницыШаблоновПриема);
	ТаблицыСИдентификаторомСтрокиШаблона.Добавить(ЭтаФорма.ШаблоныЗаполненияОсмотра);
	ТаблицыСИдентификаторомСтрокиШаблона.Добавить(ЭтаФорма.РисункиКартинок);
	ТаблицыСИдентификаторомСтрокиШаблона.Добавить(ЭтаФорма.ДобавленныеИзображения);
	
	Для Каждого ТаблицаФормы Из ТаблицыСИдентификаторомСтрокиШаблона Цикл
		СтрокиКУдалению = Новый Массив;
		Для Каждого СтрокаТаблицы Из ТаблицаФормы Цикл
			ИдентификаторСтрокиШаблона = ИдентификаторыСтрокШаблонов.Получить(СтрокаТаблицы.ИдентификаторСтрокиШаблона);
			Если ИдентификаторСтрокиШаблона = Неопределено Тогда
				СтрокиКУдалению.Добавить(СтрокаТаблицы);
			Иначе
				СтрокаТаблицы.ИдентификаторСтрокиШаблона = ЭтаФорма.Объект.ШаблоныПриема[ИдентификаторСтрокиШаблона].ПолучитьИдентификатор();
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ТаблицаФормы.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
