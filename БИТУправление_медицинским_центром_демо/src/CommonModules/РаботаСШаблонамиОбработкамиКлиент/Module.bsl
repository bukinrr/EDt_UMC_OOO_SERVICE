#Область СлужебныйПрограммныйИнтерфейс

Процедура СоздатьОбработчикиРеквизитовНаКлиенте(ЭтаФорма, РеквизитыФормы) Экспорт
	
	Элементы = ЭтаФорма.Элементы;
	
	Для Каждого Реквизит Из РеквизитыФормы Цикл
		Если ТипЗнч(Реквизит.Значение) = Тип("Строка") Тогда
			Элемент = Элементы[Реквизит.Ключ];
			Элемент.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.НеИспользовать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСтрокуПараметраПоЭлементу(Форма, ИмяЭлемента) Экспорт
	
	СоответствияПараметрам = Форма.Объект.СоответствияПараметрам;
	Для Каждого СтрокаСоответствия Из Форма.Объект.СоответствияПараметрам Цикл
		Если СтрокаСоответствия.Наименование = ИмяЭлемента Тогда
			Возврат СтрокаСоответствия;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьПараметрПоЭлементу(Объект, ИмяЭлемента) Экспорт
	
	СтрокаТЧ = ПолучитьСтрокуПараметраПоЭлементу(Объект, ИмяЭлемента);
	Если СтрокаТЧ <> Неопределено Тогда
		Возврат СтрокаТЧ.Параметр;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура ВыбратьИзДереваЗначенийВыбора(Объект, ОповещениеОВыборе, Форма, ЭлементФормы, ДеревоЗначенийВыбора) Экспорт
	
	ПараметрыОповещенияВыбора = Новый Структура("Объект, ОповещениеОВыборе, Форма, ЭлементФормы",Объект, ОповещениеОВыборе, Форма, ЭлементФормы);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьИзДереваЗначенийВыбораОбработкаВыбора", ЭтотОбъект, ПараметрыОповещенияВыбора);
	Форма.ПоказатьВыборИзМеню(ОписаниеОповещения, ДеревоЗначенийВыбора, ЭлементФормы);
	
КонецПроцедуры

Процедура ВыбратьИзДереваЗначенийВыбораОбработкаВыбора(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("ЭлементСпискаЗначений") Тогда
		ЗначениеВыбора = Результат.Значение;
		Если ТипЗнч(ЗначениеВыбора) = Тип("СписокЗначений") Тогда
			// Выбрали группу
			ВыбратьИзДереваЗначенийВыбора(Параметры.Объект, Параметры.ОповещениеОВыборе, Параметры.Форма, Параметры.ЭлементФормы, ЗначениеВыбора);
		Иначе
			// Выбрали значение
			Если ТипЗнч(ЗначениеВыбора) = Тип("ОписаниеТипов") Тогда
				ПараметрШаблона = ПолучитьПараметрПоЭлементу(Параметры.Объект, Параметры.ЭлементФормы.Имя);
				Если ЗначениеВыбора.СодержитТип(Тип("Число")) Тогда
					ПоказатьВводЧисла(Параметры.ОповещениеОВыборе, 0, Строка(ПараметрШаблона) + НСтр("ru=': введите число'"), ЗначениеВыбора.КвалификаторыЧисла.Разрядность, ЗначениеВыбора.КвалификаторыЧисла.РазрядностьДробнойЧасти);
				ИначеЕсли ЗначениеВыбора.СодержитТип(Тип("Строка")) Тогда
					ТекущееЗначение = ПолучитьТекущееЗначениеПараметраПоЭлементу(Параметры.Форма, Параметры.ЭлементФормы.Имя);
					МедицинскаяДеятельностьКлиент.ПоказатьФормуВводаТекста(ТекущееЗначение, ПараметрШаблона, Параметры.ОповещениеОВыборе);
				КонецЕсли;
			Иначе
				ВыполнитьОбработкуОповещения(Параметры.ОповещениеОВыборе, ЗначениеВыбора);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыбораЗначенияПараметраЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение = Неопределено Тогда // Выбора не было.
		Возврат;
	КонецЕсли;
	
	ПараметрШаблона = ДополнительныеПараметры.ПараметрШаблона;
	Если ДополнительныеПараметры.Свойство("ФорматнаяСтрока") Тогда
		ФорматнаяСтрока = ДополнительныеПараметры.ФорматнаяСтрока;
	Иначе
		ФорматнаяСтрока = РаботаСШаблонамиОбработкамиСервер.ПолучитьФорматнуюСтрокуПараметра(ПараметрШаблона);
	КонецЕсли;
	Форма 		= ДополнительныеПараметры.Форма;
	Элементы	= Форма.Элементы;
	Реквизит 	= ДополнительныеПараметры.Реквизит;
	
	Если (ТипЗнч(Значение) = Тип("Число")
			Или ТипЗнч(Значение) = Тип("Булево")
			Или ТипЗнч(Значение) = Тип("Дата"))
		И Значение <> 0
		И ЗначениеЗаполнено(ФорматнаяСтрока)
	Тогда
		Попытка
			Форма[Реквизит] = Формат(Значение, ФорматнаяСтрока);
		Исключение
			Форма[Реквизит] = Значение;
		КонецПопытки;
	Иначе
		Форма[Реквизит] = Значение;
	КонецЕсли;
	
	// Иначе ОбновитьТекстРедактирования() не отработает
	Элементы[Реквизит].ОбновитьТекстРедактирования();
	Форма.ОбновитьОтображениеДанных();
	//РаботаСШаблонамиОбработкамиСервер.СходитьНаСервер();
	Элементы[Реквизит].ОбновитьТекстРедактирования();
	Форма.ТекущийЭлемент = Элементы[Реквизит];
	
КонецПроцедуры

Функция ПолучитьТекущееЗначениеПараметраПоЭлементу(Форма, ИмяЭлемента) Экспорт
	
	Элемент = Форма.Элементы[ИмяЭлемента];
	Возврат Элемент.ТекстРедактирования;
	
КонецФункции

#КонецОбласти