#Область ПрограммныйИнтерфейс

// Формирует массив реквизитов документа. 
// 
// Параметры: 
//  ИмяДокумента - Строка - имя документа.
//
// Возвращаемое значение:
//   Массив - массив наименований реквизитов документа. 
//
Функция МассивРеквизитовОбъектаДляФормированияПредставления(ИмяДокумента) Экспорт
	
	МассивДопРеквизитов = Новый Массив;
	
	// Переопределяемая часть.
	
	МетаданныеДокумента = Метаданные.Документы.Найти(ИмяДокумента);
	Если МетаданныеДокумента <> Неопределено
		И МетаданныеДокумента.Реквизиты.Найти("ВидОперации") <> Неопределено
	Тогда
		МассивДопРеквизитов.Добавить("ВидОперации");
	КонецЕсли;
	
	// Конец переопределяемой части.
	
	Возврат МассивДопРеквизитов
	
КонецФункции

// Получает предстваление документа для печати
//
// Параметры
//  Выборка  - КоллекцияДанных - структура или выборка из результатов запроса
//                 в которой содержатся дополнительные реквизиты, на основании
//                 которых можно сформировать переопределенное предстваление до-
//                 кумента для вывода в отчет "Структура подчиненности"
//
// Возвращаемое значение:
//   Строка,Неопределено   - переопределенное представление документа, или Нео-
//                пределено, если для данного типа документов такое не задано.
//
Функция ПредставлениеОбъектаДляВыводаВОтчет(Выборка) Экспорт
	
	МетаданныеДокумента = Выборка.Ссылка.Метаданные();
	Если МетаданныеДокумента <> Неопределено
		И МетаданныеДокумента.Реквизиты.Найти("ВидОперации") <> Неопределено
	Тогда
	    ПредставлениеОбъекта = Выборка.Представление + " (" + Выборка.ДополнительныйРеквизит1 + ")";
		Если (Выборка.СуммаДокумента <> 0) И (Выборка.СуммаДокумента <> NULL) Тогда
			ПредставлениеОбъекта = ПредставлениеОбъекта + " " + НСтр("ru='на сумму'") + " " + Выборка.СуммаДокумента + " " + ОбщегоНазначения.ПолучитьКраткоеНаименованиеОсновнойВалюты();
		КонецЕсли;
	
		Возврат ПредставлениеОбъекта;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

#КонецОбласти
