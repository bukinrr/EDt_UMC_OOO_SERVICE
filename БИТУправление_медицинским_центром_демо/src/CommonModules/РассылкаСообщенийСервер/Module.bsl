#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСписокПолучателейРассылки(ДокументРассылки,
	Сообщения, Получатели,
	ДобавлятьСуществующих = Ложь, ДобавлятьБезКонтактнойИнформации =
	Ложь) Экспорт
	
	ИмяРегистра = ФормированиеСообщений.ПолучитьИмяРегистраПоРассылке(ДокументРассылки.Ссылка);
	
	ТипПолучателя =	Метаданные.РегистрыСведений[ИмяРегистра].Измерения.Получатель.Тип;
	
	мсВидыКонтактнойИнформации = Новый Массив;
	Если ИмяРегистра = "СообщенияSMS" Тогда
		мсВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
	Иначе
		мсВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыДомашний);
		мсВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.АдресЭлектроннойПочтыРабочий);
	КонецЕсли;
	
	ТаблицаПолучателей = Новый ТаблицаЗначений;
	ТаблицаПолучателей.Колонки.Добавить("Получатель", ТипПолучателя);
	
	Если ТипПолучателя.СодержитТип(ТипЗнч(Получатели)) Тогда
		НоваяСтрока = ТаблицаПолучателей.Добавить();
		НоваяСтрока.Получатель = Получатели;
	Иначе
		Для Каждого Получатель Из Получатели Цикл
			НоваяСтрока = ТаблицаПолучателей.Добавить();
			НоваяСтрока.Получатель = Получатель;
		КонецЦикла;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПолучателей", ТаблицаПолучателей);
	Запрос.УстановитьПараметр("ТаблицаСообщений", Сообщения.Выгрузить());
	Запрос.УстановитьПараметр("Вид", мсВидыКонтактнойИнформации);
	Запрос.УстановитьПараметр("ДобавлятьСуществующих", ДобавлятьСуществующих);
	Запрос.УстановитьПараметр("ДобавлятьБезКонтактнойИнформации", ДобавлятьБезКонтактнойИнформации);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПолучателей.Получатель КАК Получатель
	|ПОМЕСТИТЬ НовыеПолучатели
	|ИЗ
	|	&ТаблицаПолучателей КАК ТаблицаПолучателей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСообщений.Получатель КАК Получатель
	|ПОМЕСТИТЬ СуществующиеПолучатели
	|ИЗ
	|	&ТаблицаСообщений КАК ТаблицаСообщений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НовыеПолучатели.Получатель КАК Получатель
	|ПОМЕСТИТЬ ДобавляемыеПолучатели
	|ИЗ
	|	НовыеПолучатели КАК НовыеПолучатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуществующиеПолучатели КАК СуществующиеПолучатели
	|		ПО НовыеПолучатели.Получатель = СуществующиеПолучатели.Получатель
	|ГДЕ
	|	(&ДобавлятьСуществующих
	|			ИЛИ СуществующиеПолучатели.Получатель ЕСТЬ NULL)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДобавляемыеПолучатели.Получатель КАК Получатель,
	|	КонтактнаяИнформация.Представление КАК КонтактПолучателя,
	|	ВЫБОР
	|		КОГДА ДобавляемыеПолучатели.Получатель ССЫЛКА Справочник.Клиенты
	|			ТОГДА ВЫРАЗИТЬ(ДобавляемыеПолучатели.Получатель КАК Справочник.Клиенты).НеОтправлятьСМС
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеОтправлятьСМС,
	|	ВЫБОР
	|		КОГДА ДобавляемыеПолучатели.Получатель ССЫЛКА Справочник.Клиенты
	|			ТОГДА ВЫРАЗИТЬ(ДобавляемыеПолучатели.Получатель КАК Справочник.Клиенты).НеОтправлятьEMAIL
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеОтправлятьEMAIL
	|ИЗ
	|	ДобавляемыеПолучатели КАК ДобавляемыеПолучатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ПО ДобавляемыеПолучатели.Получатель = КонтактнаяИнформация.Объект
	|			И (КонтактнаяИнформация.Вид В (&Вид))
	|ГДЕ
	|	(&ДобавлятьБезКонтактнойИнформации
	|			ИЛИ НЕ КонтактнаяИнформация.Объект ЕСТЬ NULL)";

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ДобавлятьБезКонтактнойИнформации Тогда
			
			Если ИмяРегистра = "СообщенияSMS" Тогда
				
				Если Выборка.НеОтправлятьСМС Тогда
					Продолжить;
				КонецЕсли;
				
				НормализованныйСотовый = СМС_ФормированиеСообщений.НормализоватьСотовыйТелефон(Выборка.КонтактПолучателя);
				Если СтрДлина(НормализованныйСотовый) < 11 Тогда
					Продолжить;
				КонецЕсли;
				
			ИначеЕсли ИмяРегистра = "СообщенияЭлектроннойПочты" Тогда
				
				Если Выборка.НеОтправлятьEMAIL
					Или Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Выборка.КонтактПолучателя)
				Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Строка = Сообщения.Добавить();
		Строка.Получатель = Выборка.Получатель;
		Строка.Рассылка = ДокументРассылки.Ссылка;
		Строка.КонтактПолучателя = (Выборка.КонтактПолучателя);

	КонецЦикла;
	
	Если Получатели.Количество() <> Сообщения.Количество() Тогда
		СообщениеПользователю = "Перенесено в рассылку " + Сообщения.Количество() + " получателей из " + Получатели.Количество() + ".
								|Часть получателей не перенесана по причине отсутствия или некорректности требуемой контактной информации, или индивидуального запрета рассылок данного типа.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
