#Область ПрограммныйИнтерфейс

// Удаляет нечисловые символы из строки, отсекает впередистоящие символы до 10-значного номера
//  Возвращает структуру, либо нормализованный номер, либо номер для поиска телефона в запросе
//
// Параметры:
//  ИсходныйНомер	 - Строка	 - номер телефона
//  ДляПоиска		 - Строка	 - номер для поиска
// 
// Возвращаемое значение:
//  Структура, Строка - номер или структура с номером и номером для поиска
//
Функция НормализоватьСотовыйТелефон(ИсходныйНомер, ДляПоиска = Неопределено) Экспорт
	
	НомерДляПоиска = "";
	Номер = "";
	
	Для Сч = 1 По СтрДлина(ИсходныйНомер) Цикл
		Символ = Сред(ИсходныйНомер,Сч,1);
		Если КодСимвола(Символ) >= КодСимвола("0") 
			И КодСимвола(Символ) <= КодСимвола("9") 
		Тогда
			НомерДляПоиска = НомерДляПоиска + Символ + "%";
			Номер = Номер + Символ;
		КонецЕсли;
	КонецЦикла;
	длинаВнешнегоНомера = бит_ТелефонияКлиентСервер.ПолучитьДлинуВнешнегоНомера();
	НомерДляПоиска = "%" + Прав(НомерДляПоиска, длинаВнешнегоНомера * 2);
	Номер = Прав(Номер, длинаВнешнегоНомера); 
	
	Если ДляПоиска = Неопределено Тогда
		Возврат Новый Структура("Номер, ДляПоиска", Номер, НомерДляПоиска);
	ИначеЕсли ДляПоиска Тогда
		Возврат НомерДляПоиска;
	Иначе
		Возврат Номер;
	КонецЕсли;
	
КонецФункции

// Возвращает структуру, ключам которой перечислены все поля записи регистра контактной информации.
// 
// Возвращаемое значение:
//  Структура - структура полей контактной информации
//
Функция ПолучитьСтруктуруПолейКонтактнойИнформации() Экспорт
	
	Возврат	Новый Структура("Объект, 
							|Тип,
							|Вид,
							|Представление,
							|КраткоеПредставление,
							|Значение,
							|ЗначенияПолей,
							|ЗначениеПоУмолчанию,
							|Комментарий,
							|Поле1,
							|Поле2,
							|Поле3,
							|Поле4,
							|Поле5,
							|Поле6,
							|Поле7,
							|Поле8,
							|Поле9,
							|Поле10,
							|Поле11,
							|Поле12,
							|ГородскойРайон,
							|ТипДома,
							|ТипКорпуса,
							|ТипКвартиры");
КонецФункции

#КонецОбласти
