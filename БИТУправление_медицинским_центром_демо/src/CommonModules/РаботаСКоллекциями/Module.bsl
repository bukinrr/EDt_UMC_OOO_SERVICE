#Область ПрограммныйИнтерфейс

// Выполняет очередной шаг обхода дерева в глубину.
// Каждый узел обходится только один раз.
// Сначала обходится родитель, непосредственно за ним иерархии дочерних узлов.
// Результат выполнения:
//	Истина, если обход не завершен.
//	Ложь, если обход завершен.
//
// Параметры:
//  Дерево		 - ДанныеФормыДерево -  Дерево, по которому осуществляется обход.
//  УзелДерева	 - ДанныеФормыЭлементДерева - Узел дерева, полученный на предыдущем шаге. Неопределено для первого шага
//					В этот же параметр записывается следующий узел в порядке обхода.
// 
// Возвращаемое значение:
//   Булево.
Функция ОбходДерева_Следующий(Дерево, УзелДерева) Экспорт
	
	Перем ЭлементыДерева, Родитель, Индекс;
	
	ЭлементыДерева = Дерево.ПолучитьЭлементы();
	Если ЭлементыДерева.Количество() = 0 Тогда
		УзелДерева = Неопределено;
		Возврат Ложь;
	КонецЕсли;
	Если УзелДерева = Неопределено Тогда
		УзелДерева = ЭлементыДерева[0];
		Возврат Истина;
	КонецЕсли;
	
	ЭлементыДерева = УзелДерева.ПолучитьЭлементы();
	Если Не ЭлементыДерева.Количество() = 0 Тогда
		УзелДерева = ЭлементыДерева[0];
		Возврат Истина;
	КонецЕсли;
	
	Пока Не (УзелДерева = Неопределено Или УзелДерева = Дерево) Цикл
		Родитель = УзелДерева.ПолучитьРодителя();
		Если Родитель = Неопределено Или Родитель = Дерево Тогда
			ЭлементыДерева = Дерево.ПолучитьЭлементы();
		Иначе
			ЭлементыДерева = Родитель.ПолучитьЭлементы();
		КонецЕсли;
		Индекс = ЭлементыДерева.Индекс(УзелДерева);
		Если Индекс + 1 < ЭлементыДерева.Количество() Тогда
			УзелДерева = ЭлементыДерева[Индекс + 1];
			Возврат Истина;
		КонецЕсли;
		УзелДерева = Родитель;
	КонецЦикла;
	Возврат Ложь;
	
КонецФункции

// Служит для поиска в дереве (данные формы) узлов по условию, заданном в параметре Отбор
//
// Параметры:
//  Дерево		 - ДанныеФормыДерево -  Дерево.
//  Отбор		 - Структура - условие поиска в дереве.
//  УзелДерева	 - ДанныеФормыЭлементДерева - элемент дерева с которого начнется поиск.
//  ТолькоПервый - Булево - если истина то вернет первый найденный элемент, иначе массив всех найденных
// 
// Возвращаемое значение:
//   ДанныеФормыЭлементДерева.
//
Функция НайтиВДереве(Дерево, Отбор, Знач УзелДерева = Неопределено, ТолькоПервый = Ложь) Экспорт
	
	Если ТолькоПервый = Ложь Тогда
		Результат = Новый Массив;
	КонецЕсли;
	
	Пока ОбходДерева_Следующий(Дерево, УзелДерева) Цикл
		Найден = Истина;
		Для Каждого Условие Из Отбор Цикл
			Если УзелДерева[Условие.Ключ] <> Условие.Значение Тогда
				Найден = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Найден Тогда
			Если ТолькоПервый Тогда
				Возврат УзелДерева;
			Иначе
				Результат.Добавить(УзелДерева);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ?(ТолькоПервый, Неопределено, Результат);
	
КонецФункции

#КонецОбласти