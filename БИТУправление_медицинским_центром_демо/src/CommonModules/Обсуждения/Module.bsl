///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает Истина, если база зарегистрирована в системе Взаимодействия.
//
// Возвращаемое значение:
//   Булево
//
Функция СистемаВзаимодействийПодключена() Экспорт
	Если ОбсужденияСлужебный.Заблокированы() Тогда 
		Возврат Ложь;
	КонецЕсли;

	Возврат СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
КонецФункции

//+БИТ
Процедура ДобавитьПользователейВОбсуждение(ДобавляемыеПользователи, КлючСлужебногоОбсуждения) Экспорт
	
	Если Не СистемаВзаимодействийПодключена() Тогда
		Возврат;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Истина);
	
	ПользователиИБ = ПолучитьПользователиейИБ(ДобавляемыеПользователи);
	
	СлужебноеОбсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(КлючСлужебногоОбсуждения);
	Если СлужебноеОбсуждение = Неопределено Тогда
		СлужебноеОбсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
		СлужебноеОбсуждение.Отображаемое = Ложь;
		СлужебноеОбсуждение.Ключ = КлючСлужебногоОбсуждения;
	КонецЕсли;
	
	// Создаем пользователей системы взаимодействия
	Для Каждого ПользовательИБ ИЗ ПользователиИБ Цикл
		Попытка
			ИдентификаторПользователяСВ = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(ПользовательИБ.УникальныйИдентификатор);
			ПользовательСВ = СистемаВзаимодействия.ПолучитьПользователя(ИдентификаторПользователяСВ);
		Исключение
			ПользовательСВ = СистемаВзаимодействия.СоздатьПользователя(ПользовательИБ);
			ПользовательСВ.Записать();
		КонецПопытки;
		
		Если Не СлужебноеОбсуждение.Участники.Содержит(ПользовательСВ.Идентификатор) Тогда
			СлужебноеОбсуждение.Участники.Добавить(ПользовательСВ.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	СлужебноеОбсуждение.Записать();
	
КонецПроцедуры
//-БИТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+БИТ
Функция ПолучитьПользователиейИБ(ДобавляемыеПользователи)
	
	ПользователиИБ = Новый Массив;
	Для Каждого ЭлементПользователя Из ДобавляемыеПользователи Цикл
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ЭлементПользователя.Код);
		Если ПользовательИБ <> Неопределено Тогда
			ПользователиИБ.Добавить(ПользовательИБ);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПользователиИБ;
	
КонецФункции
//-БИТ

#КонецОбласти