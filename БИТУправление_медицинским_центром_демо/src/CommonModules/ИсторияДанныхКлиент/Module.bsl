#Область ПрограммныйИнтерфейс

#Область КонтактнаяИнформация

// Открывает историю данных контактной информации по владельцу. Если изменилось более 1 вида КИ, то предлагает выбор.
//
// Параметры:
//  Владелец - СправочникСсылка - Владелец (Объект) контактной информации.
//
Процедура ОткрытьИсториюДанныхКонтактнойИнформацииИзВладельца(Владелец) Экспорт
	
	ТипыВиды = ИсторияДанныхСервер.ПолучитьИзменявшиесяВидыКонтактнойИнформацииВладельца(Владелец);
	
	Если ТипыВиды.Количество() = 1 Тогда
		ОткрытьФормуИсторииДанныхКИ(Владелец, ТипыВиды[0].Тип, ТипыВиды[0].Вид);

	ИначеЕсли ТипыВиды.Количество() > 1 Тогда
		СписокИзмененныхВидов = Новый СписокЗначений;
		Для Каждого ТипВид Из ТипыВиды Цикл 
			СписокИзмененныхВидов.Добавить(ТипВид.Вид);
		КонецЦикла;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьИсториюДанныхКонтактнойИнформацииИзВладельцаЗавершение", ИсторияДанныхКлиент, Новый Структура("Владелец", Владелец));
		
		ОткрытьФорму("Справочник.ВидыКонтактнойИнформации.ФормаВыбора", Новый Структура("Отбор", Новый Структура("Ссылка", СписокИзмененныхВидов)),,,,, 
					ОписаниеОповещения,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ПоказатьПредупреждение(,"Изменения зафиксированы не были", 30);
	КонецЕсли;
	
КонецПроцедуры

// Открывает историю данных контактной информации по владельцу (завершение операции после выбора Вида КИ).
//
// Параметры:
//  Результат				 - СправочникСсылка.ВидыКонтактнойИнформации - Выбранный вид КИ.
//  ДополнительныеПараметры	 - Структура - Стурктура с полями Владелец и Тип - объект и тип контактной информации.
//
Процедура ОткрытьИсториюДанныхКонтактнойИнформацииИзВладельцаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОткрытьФормуИсторииДанныхКИ(ДополнительныеПараметры.Владелец, ДопСерверныеФункции.ПолучитьРеквизит(Результат, "Тип"), Результат);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КонтактнаяИнформация

Процедура ОткрытьФормуИсторииДанныхКИ(Объект, Тип, Вид)
	
	СтруктураКлюча = Новый Структура;
	СтруктураКлюча.Вставить("Объект", Объект);
	СтруктураКлюча.Вставить("Тип", Тип);
	СтруктураКлюча.Вставить("Вид", Вид);
	МассивКлюча = Новый Массив;
	МассивКлюча.Добавить(СтруктураКлюча);
	КлючЗаписи = Новый ("РегистрСведенийКлючЗаписи.КонтактнаяИнформация", МассивКлюча);
	ОткрытьФорму("sysForm:DataHistoryVersions", Новый Структура("Data", КлючЗаписи));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти