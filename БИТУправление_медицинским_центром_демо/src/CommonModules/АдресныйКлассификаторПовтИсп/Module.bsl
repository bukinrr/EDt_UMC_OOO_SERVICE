///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции


// WSПрокси для вызова веб-сервиса 1С.
//
// Возвращаемое значение:
//     WSПрокси - объект для вызовов сервиса.
//
Функция СервисКлассификатора1С() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Авторизация = АдресныйКлассификаторСлужебный.ПараметрыАутентификацииНаСайте();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Авторизация = Неопределено Тогда
		Логин  = Неопределено;
		Пароль = Неопределено;
	Иначе
		Логин  = Авторизация.Логин;
		Пароль = Авторизация.Пароль;
	КонецЕсли;
	
	ТекущийURLВебСервиса = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("АдресныйКлассификатор", "URLСервисаКлассификатора1С");
	Если ТекущийURLВебСервиса = Неопределено Тогда
		ТекущийURLВебСервиса = "https://api.orgaddress.1c.ru/orgaddress/v1?wsdl";
	КонецЕсли;
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = ТекущийURLВебСервиса;
	ПараметрыПодключения.URIПространстваИмен = "http://www.v8.1c.ru/ssl/AddressSystem";
	ПараметрыПодключения.ИмяСервиса = "AddressSystem";
	ПараметрыПодключения.ИмяТочкиПодключения = "AddressSystemSoap12";
	ПараметрыПодключения.ИмяПользователя = Логин;
	ПараметрыПодключения.Пароль = Пароль;
	ПараметрыПодключения.Таймаут = 10;
	
	Возврат ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	
КонецФункции

// Сведения о загрузке субъектов РФ
// 
// Возвращаемое значение:
//  Соответствие.
//
Функция СведенияОЗагрузкеСубъектовРФ() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Новый Соответствие;
	
	// Веб-сервис 1С
	Если НЕ ОбщегоНазначения.РазделениеВключено() Тогда
		СервисРезультат = АдресныйКлассификаторСлужебный.ВерсияПоставщикаДанных();
		КлассификаторДоступен = (СервисРезультат.Данные = Истина);
	Иначе
		КлассификаторДоступен = Ложь;
	КонецЕсли;
	Результат.Вставить("КлассификаторДоступен", КлассификаторДоступен);
	
	ИспользоватьЗагруженные = Ложь;
	
	СведенияОЗагрузкеСубъектовРФ = АдресныйКлассификаторСлужебный.СведенияОЗагрузкеСубъектовРФ();
	Для каждого СведенияОбСубъектеРФ Из СведенияОЗагрузкеСубъектовРФ Цикл
		
		Сведения = Новый Структура();
		Если КлассификаторДоступен Тогда
			Сведения.Вставить("ИспользоватьЗагруженные", СведенияОбСубъектеРФ.Загружено И НЕ СведенияОбСубъектеРФ.Устарело);
		Иначе
			Сведения.Вставить("ИспользоватьЗагруженные", СведенияОбСубъектеРФ.Загружено);
		КонецЕсли;
		
		Если НЕ ИспользоватьЗагруженные И Сведения.ИспользоватьЗагруженные Тогда
			ИспользоватьЗагруженные = Истина;
		КонецЕсли;
		Сведения.Вставить("ДатаЗагрузки", СведенияОбСубъектеРФ.ДатаЗагрузки);
		Результат.Вставить(СведенияОбСубъектеРФ.КодСубъектаРФ, Сведения);
		
	КонецЦикла;
	
	Результат.Вставить("ИспользоватьЗагруженные", ИспользоватьЗагруженные);
	
	Возврат Новый ФиксированноеСоответствие(Результат);
	
КонецФункции

// Набор уровней для запросов ФИАС.
//
// Возвращаемое значение:
//     ФиксированныйМассив - набор числовых уровней.
//
Функция УровниКлассификатораФИАС() Экспорт
	
	Возврат АдресныйКлассификаторКлиентСервер.УровниКлассификатораФИАС();
	
КонецФункции

#КонецОбласти
