#Область ПрограммныйИнтерфейс

// Открывает документ события, заполняя в нём заданные реквизиты.
//
// Параметры:
//  СобытиеСсылка	 - ДокументСсылка.Событие	 - документ события, если уже существует.
//  СтруктураСобытия - Структура				 - значения реквизитов для заполнения в документе события.
// 
// Возвращаемое значение:
//   ФормаКлиентскогоПриложения.
//
Функция ПолучитьФормуСобытия(СобытиеСсылка = Неопределено, СтруктураСобытия = Неопределено) Экспорт
	
	ФормаСобытия = ПолучитьФорму("Документ.Событие.ФормаОбъекта", Новый Структура("Ключ", СобытиеСсылка));
	ФормаСобытия.Записать();
	
	Если СтруктураСобытия <> Неопределено Тогда
		ИсключаемыеСвойства = "";
		Если СтруктураСобытия.Свойство("Клиент") И ЗначениеЗаполнено(ФормаСобытия.Объект.Клиент) Тогда
			ИсключаемыеСвойства = "Клиент";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ФормаСобытия.Объект, СтруктураСобытия, , ИсключаемыеСвойства);
		ФормаСобытия.Модифицированность = Истина;
		ФормаСобытия.НастроитьВидимостьДоступность();
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаСобытия, "ДопСвойства")
		И ЗначениеЗаполнено(ФормаСобытия.Объект.НомерТелефона)
	Тогда
		ФормаСобытия.ДопСвойства.Вставить("ОбновитьСотовыйКлиента", Истина);
	КонецЕсли;
	
	Возврат ФормаСобытия;
	
КонецФункции

// Открывает реквизиты документа события, возвращая его форму.
//
// Параметры:
//  СобытиеСсылка		 - ДокументСсылка.Событие	 - документ события, если уже существует.
//  СтруктураСобытия	 - Структура				 - значения реквизитов для заполнения в документе события.
//  НеПереоткрыватьФорму - Булево					 - не открывает форму, если документ уже есть.
// 
// Возвращаемое значение:
//   ФормаКлиентскогоПриложения.
//
Функция ОбновитьДанныеСобытия(СобытиеСсылка = Неопределено, СтруктураСобытия = Неопределено, НеПереоткрыватьФорму = Истина) Экспорт
	
	ФормаСобытия = ПолучитьФормуСобытия(СобытиеСсылка, СтруктураСобытия);
	Если ФормаСобытия.Открыта() Тогда 
		ФормаСобытия.Активизировать();
	ИначеЕсли ЗначениеЗаполнено(СобытиеСсылка) И НеПереоткрыватьФорму И СтруктураСобытия <> Неопределено Тогда 
		CRMСервер.ДополнитьСобытие(СобытиеСсылка, СтруктураСобытия);
	Иначе
		ФормаСобытия.Открыть();
	КонецЕсли;
	
	Возврат ФормаСобытия;
	
КонецФункции

// Открывает полную форму списка листов ожидания.
//
Процедура ОткрытьАктивизироватьОчередь() Экспорт
	
	Форма = ПолучитьФорму("Документ.ЛистОжидания.ФормаСписка");
	Если Форма.Открыта() Тогда 
		Форма.Активизировать();
	Иначе
		Форма.Открыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
