
#Область СлужебныйПрограммныйИнтерфейс

// Основная ставка НДС общей СНО.
//
// Параметры:
//  Период - Дата - дата получения ставки.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция ОбщаяСтавкаНДС(Период) Экспорт
	
	Если Период < '20190101' Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20");
	КонецЕсли;
	
КонецФункции

// Функция ОбщаяРасчетнаяСтавкаНДС.
//
// Параметры:
//  Период - Дата - дата получения ставки.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция ОбщаяРасчетнаяСтавкаНДС(Период) Экспорт
	
	Если Период < '20190101' Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120");
	КонецЕсли;
	
КонецФункции

// Функция СтавкаНДСПоУмолчанию.
//
// Параметры:
//  Период - Дата - дата получения ставки.
//  ПлательщикНДС - Булево - является ли организация плательщиком НДС.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция СтавкаНДСПоУмолчанию(Период, ПлательщикНДС = Истина) Экспорт
	
	Если ПлательщикНДС Тогда
		Возврат ОбщаяСтавкаНДС(Период);
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
	КонецЕсли;

КонецФункции

// Функция РасчетнаяСтавкаНДСПоУмолчанию.
//
// Параметры:
//  Период - Дата - дата получения ставки.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция РасчетнаяСтавкаНДСПоУмолчанию(Период) Экспорт
	
	Возврат ОбщаяРасчетнаяСтавкаНДС(Период);
	
КонецФункции

// Функция СтавкаНДСИсчисляетсяНалоговымАгентом.
//
// Параметры:
//  Период - Дата - дата получения ставки.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция СтавкаНДСИсчисляетсяНалоговымАгентом(Период) Экспорт
	
	Возврат ОбщаяРасчетнаяСтавкаНДС(Период);
	
КонецФункции

// Функция ОсновнаяСтавкаНДСПоБилетам.
//
// Параметры:
//  ПлательщикНДС - Булево - является ли организация плательщиком НДС.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция ОсновнаяСтавкаНДСПоБилетам(ПлательщикНДС = Истина) Экспорт
	
	Если ПлательщикНДС Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
	КонецЕсли;

КонецФункции

// Рассчитывает сумму НДС исходя из суммы и флагов налогообложения.
//
// Параметры:
//  Сумма            - Число - сумма от которой надо рассчитывать налоги;
//  СуммаВключаетНДС - Булево - признак включения НДС в сумму ("внутри" или "сверху");
//  СтавкаНДС        - Число - процентная ставка НДС.
//
// Возвращаемое значение:
//  Число - полученная сумма НДС.
//
Функция РассчитатьСуммуНДС(Сумма, СуммаВключаетНДС, СтавкаНДС) Экспорт

	Если СуммаВключаетНДС Тогда
		СуммаБезНДС = 100 * Сумма / (100 + СтавкаНДС);
		СуммаНДС = Сумма - СуммаБезНДС;
	Иначе
		СуммаБезНДС = Сумма;
	КонецЕсли;

	Если НЕ СуммаВключаетНДС Тогда
		СуммаНДС = СуммаБезНДС * СтавкаНДС / 100;
	КонецЕсли;
	
	Возврат СуммаНДС;

КонецФункции // РассчитатьСуммуНДС()

// Производит пересчет цен при изменении флагов учета налогов.
// Пересчет зависит от способа заполнения цен, при заполнении По ценам номенклатуры (при продаже) 
// хочется избегать ситуаций, когда компания  «теряет деньги» при пересчете налогов. 
// Поэтому если в документе флаг "Учитывать налог" выключен, то цены должны браться напрямую из справочника, 
// потому что хочется продавать по той же цене, независимо от режима налогообложения. 
// Например, если отпускная цена задана с НП для избежания ошибок округления, то это не значит, 
// что при отпуске без НП мы должны продать дешевле. Если же флаг учета налога в документе включен, 
// то цены должны пересчитываться при подстановке в документ: 
// налог должен включаться или не включаться в зависимости от флага включения налога в типе цен.
// При заполнении по ценам контрагентов (при покупке) хочется хранить цены поставщиков. 
// Поэтому нужно пересчитывать всегда по установленным флагам в документе и в типе цен. 
// Это гарантирует, что при записи цен в регистр и последующем их чтении, 
// например, при заполнении следующего документа, мы с точностью до ошибок округления при пересчете 
// получим те же самые цены.
//
// Параметры: 
//  Цена                - Число - пересчитываемое значение цены;
//  ЦенаВключаетНДС     - Булево - определяет содержит ли переданное значение цены НДС;
//  СуммаВключаетНДС    - Булево - определяет должно ли новое значение цены включать НДС;
//  СтавкаНДС           - Число - ставка НДС.
//
// Возвращаемое значение:
//  Число - новое значение цены.
//
Функция ПересчитатьЦенуПриИзмененииФлаговНалогов(Цена, ЦенаВключаетНДС, СуммаВключаетНДС, СтавкаНДС) Экспорт

	// Инициализация переменных
	НадоВключитьНДС  = Ложь;
	НадоИсключитьНДС = Ложь;
	НоваяЦена		 = Цена;

	Если СуммаВключаетНДС
		И (НЕ ЦенаВключаетНДС) Тогда
		
		// Надо добавлять НДС       
		НадоВключитьНДС = Истина;
	ИначеЕсли (НЕ СуммаВключаетНДС)
		И ЦенаВключаетНДС  Тогда
		
		// Надо исключать НДС       
		НадоИсключитьНДС = Истина;
	КонецЕсли;
		
	Если НадоИсключитьНДС Тогда
		НоваяЦена = (НоваяЦена * 100) / (100 + СтавкаНДС);
	КонецЕсли;

	Если НадоВключитьНДС Тогда
		НоваяЦена = (НоваяЦена * (100 + СтавкаНДС)) / 100;
	КонецЕсли;

	Возврат НоваяЦена;

КонецФункции // ПересчитатьЦенуПриИзмененииФлаговНалогов()

// Возвращает номер версии подсистемы НДС.
//
// Параметры:
//  Дата - Дата.
//
// Возвращаемое значение:
//  Число - номер версии.
//
Функция Версия(Дата) Экспорт
	
	Если Дата < '20120101' Тогда
		Возврат 1;
	Иначе
		Возврат 2;
	КонецЕсли;
	
КонецФункции

// Функция СтавкаНДС.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура.
//  Период - Дата - дата получения ставки.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция СтавкаНДС(Номенклатура, Период) Экспорт
	Возврат УчетНДСВызовСервера.СтавкаНДС(Номенклатура, Период);		
КонецФункции

// Функция ПолучитьОсновнуюСтавкуНДСПоСНО.
//
// Параметры:
//  СНО - ПеречислениеСсылка.СистемыНалогообложения - СНО.
//  НастройкаПечатиЧеков - Структура - настройки чеков этой клиники.
//  Период - Дата - дата получения ставки.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция ПолучитьОсновнуюСтавкуНДСПоСНО(СНО, НастройкаПечатиЧеков, Период) Экспорт
	
	Если ЗначениеЗаполнено(СНО) И Не НастройкаПечатиЧеков.ПустаяСсылка Тогда
		Если НастройкаПечатиЧеков.ОсновнаяСистемаНалогообложения = СНО Тогда
			Возврат НастройкаПечатиЧеков.СтавкаНДСДляОсновнойСистемыНалогообложения;
		ИначеЕсли СНО = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ЕдиныйНалогНаВмененныйДоход")
			Или	СНО = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ПатентнаяСистемаНалогообложения") Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
		Иначе
			Возврат ОбщаяСтавкаНДС(Период);
		КонецЕсли;
	Иначе
		Возврат ОбщаяСтавкаНДС(Период);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

