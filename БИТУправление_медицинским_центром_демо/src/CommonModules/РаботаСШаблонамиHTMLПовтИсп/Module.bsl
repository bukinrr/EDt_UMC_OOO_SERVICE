#Область ПрограммныйИнтерфейс

// Возвращает структуру настроек шаблонов по филиалу на указанную дату
//
// Параметры:
//  Филиал			 - СправочникСсылка.Филиалы	- филиал медицинского документа.
//  ДатаДокумента	 - Дата						- дата медицинского документа.
// 
// Возвращаемое значение:
//  Структура - Настройки шаблонов.
//
Функция ПолучитьНастройкиШаблоновHTML(ДатаДокумента, Филиал) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НастройкиШаблонов = Новый Структура("ТекстМедицинскогоЗаголовка, ТекстКлиентскогоЗаголовка, ШаблонМедицинскогоЗаголовка, ШаблонКлиентскогоЗаголовка, КешИзображений");
	СрезПоследних = РегистрыСведений.НастройкиШаблоновHTML.СрезПоследних(ДатаДокумента, Новый Структура("Филиал", Филиал));
	
	Если СрезПоследних.Количество() > 0 Тогда
		НастройкиШаблонов.ШаблонМедицинскогоЗаголовка	= СрезПоследних[0].МедицинскаяШапка;
		НастройкиШаблонов.ШаблонКлиентскогоЗаголовка	= СрезПоследних[0].КлиентскаяШапка;
		
		Шаблоны = Новый Массив;
		Шаблоны.Добавить(СрезПоследних[0].МедицинскаяШапка);
		Шаблоны.Добавить(СрезПоследних[0].КлиентскаяШапка);
		
		
		ВерсииШаблонов = ВерсионированиеШаблонов.ПолучитьТаблицуВерсийШаблонов(Шаблоны, ДатаДокумента);
		Для Каждого СтрокаВерсииШаблонов Из ВерсииШаблонов Цикл
			Если ЗначениеЗаполнено(СтрокаВерсииШаблонов.ВерсияШаблона) Тогда
				Шаблоны.Добавить(СтрокаВерсииШаблонов.ВерсияШаблона);
			КонецЕсли;
		КонецЦикла;
	    
		КешИзображений = РаботаСШаблонамиHTML.ПолучитьКешИзображенийПриема(Шаблоны,, РаботаСШаблонамиHTMLКлиентСервер.ЭтоWindowsXP());
		
		НастройкиШаблонов.ТекстМедицинскогоЗаголовка = ПолучитьТекстТелаHTML(ПолучитьТекстВерсииHTML(ВерсииШаблонов, СрезПоследних[0].МедицинскаяШапка), КешИзображений);
		НастройкиШаблонов.ТекстКлиентскогоЗаголовка	 = ПолучитьТекстТелаHTML(ПолучитьТекстВерсииHTML(ВерсииШаблонов, СрезПоследних[0].КлиентскаяШапка), КешИзображений);
	КонецЕсли;
	
	Возврат НастройкиШаблонов;
	
КонецФункции

Функция ПолучитьТекстТелаHTML(ТекстHTML, КешИзображений)
	
	ДокументHTML = РаботаСDOMКлиентСервер.СоздатьДокументDOM(ТекстHTML);
	РаботаСШаблонамиHTMLКлиентСервер.ЗагрузитьИзображенияПриема(ДокументHTML, КешИзображений, Неопределено);
	Возврат РаботаСDOMКлиентСервер.ПолучитьКодТелаHTMLИзДокументаDOM(ДокументHTML);
	
КонецФункции

Функция ПолучитьТекстВерсииHTML(ВерсииШаблонов, ШаблонПриема)
	
	СтрокиВерсийМедицинскойШапки = ВерсииШаблонов.НайтиСтроки(Новый Структура("ШаблонПриема", ШаблонПриема));
	Если СтрокиВерсийМедицинскойШапки.Количество() = 0 Тогда
		ТекстВерсииHTML = ШаблонПриема.ТекстHTML;
	Иначе
		ТекстВерсииHTML = СтрокиВерсийМедицинскойШапки[0].ВерсияШаблона.ТекстHTML;
	КонецЕсли;
	
	Возврат ТекстВерсииHTML;
	
КонецФункции

#КонецОбласти