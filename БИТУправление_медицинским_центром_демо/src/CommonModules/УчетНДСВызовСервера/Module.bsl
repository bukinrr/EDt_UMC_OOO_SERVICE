#Область ПрограммныйИнтерфейс

// Получает ставку НДС номенклатуры
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура.
//  Период		 - Дата							 - Дата получения.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
функция СтавкаНДС(Номенклатура, Период) Экспорт
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ВидСтавки = Номенклатура.ВидСтавкиНДС;
	Иначе
		ВидСтавки = Перечисления.ВидыСтавокНДС.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавки, Период);		
КонецФункции

// Перезаполняет ставки НДС в филиалах
//
Процедура ПроверитьПерезаполнитьСтавкиНДСВФилиалах() Экспорт
	
	Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Если Период >= '20190101' Тогда 
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПечатиЧеков.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НастройкиПечатиЧеков КАК НастройкиПечатиЧеков
		|ГДЕ
		|	(НастройкиПечатиЧеков.СтавкаНДСДляОсновнойСистемыНалогообложения = &НДС18
		|			ИЛИ НастройкиПечатиЧеков.СтавкаНДСДляОсновнойСистемыНалогообложения = &НДС118)";
		
		Запрос.УстановитьПараметр("НДС118", Перечисления.СтавкиНДС.НДС18_118);
		Запрос.УстановитьПараметр("НДС18", Перечисления.СтавкиНДС.НДС18);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Настройка = Выборка.Ссылка.ПолучитьОбъект();
			Если Настройка.СтавкаНДСДляОсновнойСистемыНалогообложения = Перечисления.СтавкиНДС.НДС18 Тогда
				Настройка.СтавкаНДСДляОсновнойСистемыНалогообложения = Перечисления.СтавкиНДС.НДС20;				
			ИначеЕсли Настройка.СтавкаНДСДляОсновнойСистемыНалогообложения = Перечисления.СтавкиНДС.НДС18_118 Тогда
				Настройка.СтавкаНДСДляОсновнойСистемыНалогообложения = Перечисления.СтавкиНДС.НДС20_120;
			КонецЕсли;
			Настройка.Записать();
		КонецЦикла;
		
		Если Выборка.Количество() <> 0 Тогда		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Был выполнен автоматический перевод основных ставок НДС с 18% на 20% в настройка чеков ККТ."); 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
