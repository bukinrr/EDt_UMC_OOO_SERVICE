
#Область ПрограммныйИнтерфейс

// Получает массив правил забора для номенклатуры анализа лаборатории Ситилаб.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура анализа, для которой получаем правила забора.
//  Лаборатория	 - СправочникСсылка.Лаборатории	 - лаборатория, для которой получаем правила забора.
// 
// Возвращаемое значение:
//   - Массив из Структура:
//  	* Биоматериал 					- Строка
//  	* ВыборНесколькихПравилЗабора 	- Булево
//  	* ИДБиоматериала 				- Строка
//  	* ИДКонтейнера 					- Строка
//  	* ИдПравила 					- УникальныйИдентификатор
//  	* Контейнер 					- Строка
//  	* Обязательное 					- Булево
//  	* Пометка 						- Булево
//  	* Представление 				- Неопределено
//
Функция ПолучитьПравилаЗабораАнализа(Номенклатура, Лаборатория) Экспорт
	
	Перем ДанныеКешаНоменклатуры;
	
	ПравилаАнализа = Новый Массив;
	
	Если Не глКешНСИЛаборатории.Свойство("СитиЛаб") Тогда
		Возврат ПравилаАнализа;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	
	Для Каждого Анализ Из глКешНСИЛаборатории.СитиЛаб.НоменклатураАнализов Цикл
		Если Анализ.Номенклатура = Номенклатура
			И Анализ.Лаборатория = Лаборатория Тогда
			ДанныеКешаНоменклатуры = Анализ;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ДанныеКешаНоменклатуры) Тогда
		Возврат ПравилаАнализа;
	КонецЕсли;
	
	ПравилаТекущегоАнализа = ПолучитьПравилаАнализаИзКешаПоАнализу(ДанныеКешаНоменклатуры.ИД, Лаборатория);
	
	Для Каждого ПравилоАнализа Из ПравилаТекущегоАнализа Цикл
		
		ДанныеПравила = Новый Структура("ИдПравила, Представление, Пометка, Обязательное, Биоматериал, Контейнер, ИДКонтейнера, ИДБиоматериала, ВыборНесколькихПравилЗабора");
		
		ДанныеПравила.Вставить("ИдПравила", Новый УникальныйИдентификатор);
		ДанныеПравила.Вставить("ИДБиоматериала", ПравилоАнализа.ИдентификаторБиоматериала);
		ДанныеПравила.Вставить("Биоматериал", ПравилоАнализа.НаименованиеБиоматериала);
		
		Если ПравилаТекущегоАнализа.Количество() > 1 И ПравилоАнализа.ТипИсследования = 1 Тогда
			ДанныеПравила.Вставить("Пометка", Ложь);
			ДанныеПравила.Вставить("Обязательное", Ложь);
		Иначе 	
			ДанныеПравила.Вставить("Пометка", Истина);
			ДанныеПравила.Вставить("Обязательное", Истина);
		КонецЕсли;
		ДанныеПравила.Вставить("Контейнер", ПравилоАнализа.НаименованиеБиоматериала);
		ДанныеПравила.Вставить("ИДКонтейнера", ПравилоАнализа.ИдентификаторБиоматериала);
		ДанныеПравила.Вставить("ВыборНесколькихПравилЗабора", Ложь);
		
		ПравилаАнализа.Добавить(ДанныеПравила);
	КонецЦикла;
	
	Возврат ПравилаАнализа;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПравилаАнализаИзКешаПоАнализу(ИдентификаторИсследования, Лаборатория)
	
	мсПравилаАнализа = Новый Массив;
	
	Если глКешНСИЛаборатории.СитиЛаб.ПравилаАнализов <> Неопределено
		И глКешНСИЛаборатории.СитиЛаб.ПравилаАнализов.Количество() > 0 Тогда
		
		Для Каждого ПравилоАнализов Из глКешНСИЛаборатории.СитиЛаб.ПравилаАнализов Цикл
			Если ПравилоАнализов.ИдентификаторИсследования = ИдентификаторИсследования
				И ПравилоАнализов.Лаборатория = Лаборатория Тогда
				мсПравилаАнализа.Добавить(ПравилоАнализов);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат мсПравилаАнализа;
	
КонецФункции 

#КонецОбласти
