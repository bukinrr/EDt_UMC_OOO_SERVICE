#Область ПрограммныйИнтерфейс

// Стандартная маска ввода СНИЛС
// 
// Возвращаемое значение:
//  Строка. 
//
Функция МаскаСНИЛС() Экспорт
	Возврат "999-999-999 99";
КонецФункции

Функция СНИЛС_Корректен(Номер) Экспорт
	
	НомерТолькоЦифры = СокрЛП(СтрЗаменить(Номер,"-",""));
	НомерТолькоЦифры = СтрЗаменить(НомерТолькоЦифры," ","");
	
	Если СтрДлина(НомерТолькоЦифры) = 11 Тогда
		
		ПроверяемоеЧисло = Лев(НомерТолькоЦифры,9);
		КонтрольноеЧисло = Прав(НомерТолькоЦифры,2);
		
		Сумма = 0;
		
		Для Счетчик = 1 По СтрДлина(ПроверяемоеЧисло) Цикл
			Цифра = Число(Сред(ПроверяемоеЧисло, Счетчик, 1));
			Сумма = Сумма + Цифра * (СтрДлина(ПроверяемоеЧисло) - Счетчик +1);
		КонецЦикла;
		
		Если Сумма < 100 Тогда
			КонтрольнаяСумма = Сумма;
		ИначеЕсли Сумма = 100 ИЛИ Сумма = 101 Тогда
			КонтрольнаяСумма = 0;
		Иначе
			Сумма = Сумма % 101;
			
			Если (Сумма < 100) Тогда
				КонтрольнаяСумма = Сумма;
			Иначе
				КонтрольнаяСумма = 0;
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат КонтрольнаяСумма = Число(КонтрольноеЧисло);
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Определяет, может ли строка с цифрами быть СНИЛС.
//
// Параметры:
//  Текст	 - Строка - строка с цифрами СНИЛС и, возможно, разделителями.
// 
// Возвращаемое значение:
//  Булево. 
//
Функция ЭтоСНИЛС(Текст) Экспорт
	
	ЕстьБуквы = Ложь;
	ЕстьЦифры = Ложь;
	Для Сч = 1 По СтрДлина(Текст) Цикл
		Символ = Сред(Текст, Сч, 1);
		
		ЕстьБуквы = ЕстьБуквы Или СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Символ) Или СтроковыеФункцииКлиентСервер.ТолькоЛатиницаВСтроке(Символ);
		ЕстьЦифры = ЕстьЦифры Или СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Символ);
	КонецЦикла;
	
	Если Не ЕстьБуквы И ЕстьЦифры Тогда
		
		ТекстЦифры = СтроковыеФункцииКлиентСервер.ПолучитьЦифрыСтроки(Текст);
	
		Если СтрДлина(ТекстЦифры) = 11
			И СНИЛС_Корректен(ТекстЦифры)
		Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
