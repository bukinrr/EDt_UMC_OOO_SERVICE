#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьПоУмолчанию(ЭтаФорма) Экспорт
	
	МассивТаблиц = Новый Массив;
	Структуры = ЭтаФорма.Параметры.ЗначенияПоУмолчанию;
	Для Каждого Соотнесение Из Структуры Цикл
		Если ЗначениеЗаполнено(Соотнесение.Таблица) Тогда
			ТаблицаФормы = ЭтаФорма[Соотнесение.Таблица];
			Если МассивТаблиц.Найти(Соотнесение.Таблица) = Неопределено Тогда
				ТаблицаФормы.Очистить();
				ТаблицаФормы.Добавить();
				МассивТаблиц.Добавить(Соотнесение.Таблица);
			КонецЕсли;
			СтрокаТаблицыФормы = ТаблицаФормы[0];
			СтрокаТаблицыФормы[Соотнесение.Имя] = Соотнесение.Значение;
		Иначе
			ЭтаФорма[Соотнесение.Имя] = Соотнесение.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьВыборЗаполнения(Форма,Команда) Экспорт
	
	Ссылка = СтрЗаменить(СтрЗаменить(Команда.Имя,"Ссылка",""),"_","-"); 
	СтруктураЗаполнения = УправлениеОбработкамиШаблоновСервер.ОбработатьПоУИШаблонЗаполнения(Ссылка);
	
	МассивПараметров = СтруктураЗаполнения.МассивПараметров;
	Для Каждого СтруктураЗначения Из МассивПараметров Цикл
		Форма[СтруктураЗначения.Имя] = СтруктураЗначения.Значение;
	КонецЦикла;
	
	МассивПараметровТаблиц = СтруктураЗаполнения.МассивПараметровТаблиц;
	
	МассивТаблицОбработки = Новый Массив;
	Для Каждого СтруктураЗначения из МассивПараметровТаблиц Цикл
		Если МассивТаблицОбработки.Найти(СтруктураЗначения.ТаблицаОбработки) = Неопределено Тогда
			ТаблицаФормы = Форма[СтруктураЗначения.ТаблицаОбработки];
			ТаблицаФормы.Очистить();
			ТаблицаФормы.Добавить();
			МассивТаблицОбработки.Добавить(СтруктураЗначения.ТаблицаОбработки);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтруктураЗначения из МассивПараметровТаблиц Цикл
		ТаблицаФормы = Форма[СтруктураЗначения.ТаблицаОбработки];
		ИндексСтрокиТаблицы = СтруктураЗначения.НомерСтрокиТаблицы - 1;
		Пока СтруктураЗначения.НомерСтрокиТаблицы > ТаблицаФормы.Количество() Цикл
			ТаблицаФормы.Добавить();
		КонецЦикла;
		СтрокаТаблицыФормы = ТаблицаФормы[ИндексСтрокиТаблицы];
		СтрокаТаблицыФормы[СтруктураЗначения.Имя] = СтруктураЗначения.Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти