
#Область ПрограммныйИнтерфейс

// Ппроверяет подписи объекта
//
// Параметры:
//  Ссылка	 - ЛюбаяСсылка	 - подписанный объект.
//
Процедура ПроверитьВсеПодписиОбъекта(Ссылка) Экспорт
	
	//Если ЭлектроннаяПодписьКлиент.ПроверятьЭлектронныеПодписиНаСервере() Тогда
	//	ЭЦП_УМЦ_Сервер.ПроверитьВсеПодписиОбъекта(Ссылка);	
	//Иначе
	//	ТаблицаПодписей = ЭЦП_УМЦ_Сервер.ПолучитьМассивДанныхПодписейОбъекта(Ссылка);
	//	РаботаСЭЦПКлиент.Проверить(ТаблицаПодписей);
	//	ЭЦП_УМЦ_Сервер.УстановитьСтатусПроверкиЭЦПОбъекта(Ссылка);
	//КонецЕсли;
	
КонецПроцедуры

Процедура ПерейтиКПодписаниюЭМД(Форма, ОтобразитьФормуРаботыСЭМД = Ложь, ТребуетсяПроведениеДокумента = Истина, ПроверкаРЭМД = Истина, СсылкаНаДокумент = Неопределено, РежимОткрытияОкна = Неопределено) Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ОтобразитьФормуРаботыСЭМД",	ОтобразитьФормуРаботыСЭМД);
	ДопПараметры.Вставить("ИдентификаторФормы",			Форма.УникальныйИдентификатор);
	ДопПараметры.Вставить("Форма",						Форма);
	ДопПараметры.Вставить("ПроверкаРЭМД",				ПроверкаРЭМД);
	ДопПараметры.Вставить("СсылкаНаДокумент",			СсылкаНаДокумент);
	ДопПараметры.Вставить("РежимОткрытияОкна",			РежимОткрытияОкна);
	
	Если СсылкаНаДокумент = Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПерейтиКПодписаниюЭМДПродолжение", ЭЦП_УМЦ_Клиент, ДопПараметры);
		РаботаСДиалогамиКлиент.ПроверитьМодифицированностьВФорме(Форма, ТребуетсяПроведениеДокумента, ОписаниеОповещения);
	Иначе
		ПерейтиКПодписаниюЭМДПродолжение(Истина, ДопПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПерейтиКПодписаниюЭМДПродолжение(Результат, Параметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СсылкаНаДокумент")
		И ЗначениеЗаполнено(Параметры.СсылкаНаДокумент)
	Тогда
		Параметры.Вставить("МедицинскийДокумент", Параметры.СсылкаНаДокумент);
	Иначе
		Параметры.Вставить("МедицинскийДокумент", Параметры.Форма.Объект.Ссылка);
	КонецЕсли;
	
	Если Параметры.ПроверкаРЭМД
		И ИнтеграцияЕГИСЗСерверПовтИсп.ИспользуетсяИнтеграцияРЭМД()
	Тогда
		
		ТекстПредупрежденияПриОткрытии = "";
		Проблемы = ИнтеграцияЕГИСЗВызовСервера.ПолучитьПроблемыСЭМДПередПодписанием(Параметры.МедицинскийДокумент);
		
		Если Проблемы.Количество() > 0 Тогда
			ТекстПредупрежденияПриОткрытии = НСтр("ru='Имеются проблемы, не позволяющие сформировать ЭМД.'");
		ИначеЕсли Не Параметры.Форма.Объект.ПодписанЭП И ИнтеграцияЕГИСЗВызовСервера.ИмеютсяОшибкиВалидацииПоДокументу(Параметры.МедицинскийДокумент) Тогда
			ТекстПредупрежденияПриОткрытии = НСтр("ru='Имеются ошибки валидации ЭМД по схематрону. ЭМД может не пройти регистрацию в РЭМД.'");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстПредупрежденияПриОткрытии) Тогда
			ПоказатьПредупреждение(Новый ОписаниеОповещения("ПослеЗакрытияПредупрежденияОПроблемах", ЭЦП_УМЦ_Клиент, Параметры), ТекстПредупрежденияПриОткрытии);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПерейтиВФормуРаботыСЭМД(Параметры.МедицинскийДокумент, Параметры.ИдентификаторФормы, Параметры.ОтобразитьФормуРаботыСЭМД, Параметры.Форма, Параметры.РежимОткрытияОкна);
	
КонецПроцедуры

Процедура ПослеЗакрытияПредупрежденияОПроблемах(Параметры) Экспорт
	
	ПерейтиВФормуРаботыСЭМД(Параметры.МедицинскийДокумент, Параметры.ИдентификаторФормы, Параметры.ОтобразитьФормуРаботыСЭМД, Параметры.Форма, Параметры.РежимОткрытияОкна);
	
КонецПроцедуры

Процедура ПерейтиВФормуРаботыСЭМД(МедицинскийДокумент, ИдентификаторФормы, ОтобразитьФормуРаботыСЭМД, Владелец, РежимОткрытияОкна)
	
	ПараметрыЭМД = Новый Структура;
	ПараметрыЭМД.Вставить("МедицинскийДокумент",			МедицинскийДокумент);
	ПараметрыЭМД.Вставить("ИдентификаторФормы",				ИдентификаторФормы);
	ПараметрыЭМД.Вставить("ОтобразитьФормуРаботыСЭМД",		ОтобразитьФормуРаботыСЭМД);
	
	ОткрытьФорму("ОбщаяФорма.РаботаСЭМД", ПараметрыЭМД, Владелец,,,,, РежимОткрытияОкна);
	
КонецПроцедуры

Процедура ПодписатьФайлыЗавершение(Результат, Параметры) Экспорт
	
	Если Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПослеПодписаний = Новый Структура("УИДФормы, РезультатСохраненияРЭМД, РезультатСохраненияНеРЭМД", Параметры.УИДФормыРаботыСЭМД);
	
	бит_СертификатыЭЦПКлиент.ПослеПодписанияОбъекта(Результат, Параметры);
	
	ОбработатьРезультатПодписанияФайлов(Параметры, ПараметрыПослеПодписаний);
	
	// РЭМД
	ИнтеграцияЕГИСЗ_РЭМДКлиент.ОбработатьРезультатПодписанияПоРЭМД(Параметры, ПараметрыПослеПодписаний);
	
	Оповестить("ОбновитьСпискиПослеПодписаний", ПараметрыПослеПодписаний);
	Оповестить("ИзменилисьПодписи", Параметры.Объект);
	
КонецПроцедуры

Процедура ОбработатьРезультатПодписанияФайлов(Параметры, ПараметрыПослеПодписаний)
	
	СтрокиЭЦПНеРЭМД = Новый Массив;
	Для Каждого СтрокаУИД_ЭЦП Из Параметры.УИДыЭЦП Цикл
		Если СтрокаУИД_ЭЦП.ТипПодписания = 2 Тогда
			СтрокиЭЦПНеРЭМД.Добавить(СтрокаУИД_ЭЦП);
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиЭЦПНеРЭМД.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РезультатСохранения = ЭЦП_УМЦ_Сервер.СохранитьИзмененияВРолиИПодписиЭМД(Параметры.Объект, СтрокиЭЦПНеРЭМД);
	Если Не РезультатСохранения.ЗаписьУспешна Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПослеПодписаний.РезультатСохраненияНеРЭМД = РезультатСохранения;
	
КонецПроцедуры

#КонецОбласти