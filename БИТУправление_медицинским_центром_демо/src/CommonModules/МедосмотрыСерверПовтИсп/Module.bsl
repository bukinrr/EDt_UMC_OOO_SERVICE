#Область ПрограммныйИнтерфейс

// Позволяет получить состав медкомиссии на требуемую дату.
//
// Параметры:
//  Дата						 - Дата		- Дата, на которую нужен состав медкомиссии.
//  ТолькоЗаполненныеСотрудники	 - Булево	- Возвращать ли те должности медкомиссии, для которых врач не определен.
//  Филиал						 - СправочникСсылка.Филиалы	 - Филиал, для которого получаем медкомиссиию.
// 
// Возвращаемое значение:
//   ТаблицаЗначений.
//
Функция ПолучитьСоставМедкомиссии(Дата = Неопределено, ТолькоЗаполненныеСотрудники = Истина, Филиал = Неопределено) Экспорт
	
	Если Дата = Неопределено Тогда 
		Дата = ТекущаяДата();
	КонецЕсли;
	
	Если Филиал = Неопределено Тогда 
		УсловиеФилиал = "";
	Иначе 
		УсловиеФилиал = "Филиал = &Филиал"
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоставМедкомиссииСрезПоследних.Период,
	|	СоставМедкомиссииСрезПоследних.Специализация,
	|	СоставМедкомиссииСрезПоследних.Роль,
	|	СоставМедкомиссииСрезПоследних.Сотрудник,
	|	ВЫБОР
	|		КОГДА СоставМедкомиссииСрезПоследних.Роль = ЗНАЧЕНИЕ(Справочник.РолиЧленовКомиссии.Председатель)
	|			ТОГДА 0
	|		КОГДА СоставМедкомиссииСрезПоследних.Роль = ЗНАЧЕНИЕ(Справочник.РолиЧленовКомиссии.ЧленКомиссии)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	РегистрСведений.СоставМедкомиссии.СрезПоследних(&Дата," +УсловиеФилиал+") КАК СоставМедкомиссииСрезПоследних
	|
	|//%УсловияОтбора%
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Филиал",Филиал);
	
	Если ТолькоЗаполненныеСотрудники Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"//%УсловияОтбора%",
												"ГДЕ СоставМедкомиссииСрезПоследних.Сотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Позволяет получить сотрудника из состава медкомиссии на требуемую дату.
//
// Параметры:
//  Дата			 - Дата			 - Дата, на которую нужен состав медкомиссии.
//  			ТолькоЗаполненныеСотрудники	 - Булево	- Возвращать ли те должности медкомиссии, для которых врач не определен.
//  Филиал			 - СправочникСсылка.Филиалы	 - Филиал, для которого получаем медкомиссиию.
//  Специализация	 - СправочникСсылка.КатегорииВыработки - Если заполнено, ищется врач этой специализации в комиссии.
//  Роль			 - СправочникСсылка.РолиЧленовКомиссии - Если не заполнена специализация, то ищется сотудник с этой ролью. Не применяется для роли "Член комиссии".
// 
// Возвращаемое значение:
//   СправочникСсылка.Сотрудники.
//
Функция ПолучитьСотрудникаМедкомиссии(Дата = Неопределено, Филиал = Неопределено, Специализация = Неопределено, Роль = Неопределено) Экспорт
	
	Результат = Справочники.Сотрудники.ПустаяСсылка(); // Значение по умолчанию, если сотрудник не будет найден.
	
	СоставМедкомиссии = ПолучитьСоставМедкомиссии(Дата, Истина, Филиал);
	
	Если ЗначениеЗаполнено(Специализация) Тогда
		Отбор = Новый Структура("Специализация", Специализация);
	ИначеЕсли ЗначениеЗаполнено(Роль) Тогда
		Отбор = Новый Структура("Роль", Роль);
	Иначе
		Возврат Результат;
	КонецЕсли;
	
	СтрокиСостава = СоставМедкомиссии.НайтиСтроки(Отбор);
	Для Каждого СтрокаСостава Из СтрокиСостава Цикл
		Если ЗначениеЗаполнено(СтрокаСостава.Сотрудник) Тогда
			Результат = СтрокаСостава.Сотрудник;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСписокНоменклатурМедосмотровТребующихПересчетСтоимости() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.МедосмотрСправка = ИСТИНА
		|	И Номенклатура.СпособФомированияЦеныМедосмотра <> Значение(Перечисление.СпособФормированияЦеныМедосмотра.ЦенаНоменклатурыПоПрайсу)");
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти