#Область ПрограммныйИнтерфейс

// Возвращает представление для кнопки отправки анализа в лабораторию из документа забора (интерактивно).
//
// Параметры:
//  ДокументОбъект	 - ДокументОбъект.ДействияНадАнализами	 - документ забора анализов.
//  Лаборатория	 - СправочникСсылка.Лаборатории	 - Лаборатория.
// 
// Возвращаемое значение:
//  Строка - представление кнопки отправки заказа.
//
Функция ОтправитьЗаказИнтерактивно_ПредставлениеКоманды(ДокументОбъект, Лаборатория) Экспорт
	
	// Если есть контейнер Гемотест с заполненным УсловияХранения, значит, заказ уже отправлен.
	ЗаказОтправлен = Ложь;
	
	НастройкиВзаимодействия = Гемотест_Сервер.ПолучитьНастройкиВзаимодействия(Лаборатория);
	Если НастройкиВзаимодействия.Используется Тогда
		
		Для Каждого СтрокаКонтейнер Из ДокументОбъект.Контейнеры Цикл
			Если СтрокаКонтейнер.Лаборатория = Лаборатория
				И ДопСерверныеФункции.ПолучитьРеквизит(СтрокаКонтейнер.Лаборатория, "ВнешняяЛаборатория") = ПредопределенноеЗначение("Перечисление.ВнешниеЛаборатории.Гемотест")
				И ЗначениеЗаполнено(СтрокаКонтейнер.УсловияХранения)
			Тогда
				ЗаказОтправлен = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Возврат НСтр("ru='Обмен выключен (Гемотест)'");
	КонецЕсли;
	
	Если ЗаказОтправлен Тогда
		Возврат НСтр("ru='Заказ отправлен (Гемотест)'");
	Иначе
		Возврат НСтр("ru='Отправить заказ (Гемотест)'");
	КонецЕсли;
	
КонецФункции

// Возвращает для параметра забора массив зависимых параметров.
//
// Параметры:
//  ДокументОбъект	 - ДокументОбъект.ДействияНадАнализами	 - документ забора анализов.
//  Лаборатория	 - СправочникСсылка.Лаборатории	 - Лаборатория.
// 
// Возвращаемое значение:
//  Строка - представление кнопки отправки заказа.
//
Функция ОтправитьЗаказИнтерактивно(ДокументОбъект, Лаборатория) Экспорт
	
	мКонтейнеровЛаборатории = ДокументОбъект.Контейнеры.НайтиСтроки(Новый Структура("Лаборатория", Лаборатория));
	
	Если мКонтейнеровЛаборатории.Количество() > 0 Тогда
		// Открытие отправленной заявки
		ПоказатьПредупреждение(, НСтр("ru='Заказ уже отправлен в лабораторию '") + Лаборатория); 
	Иначе
		Результат = Гемотест_Сервер.ОтправитьЗаказ(ДокументОбъект, Лаборатория);
		
		Если ТипЗнч(Результат) = Тип("Строка") Тогда
			// Ошибка
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
			//Добавление контейнеров в документ
			Счетчик = 1;
			Для Каждого Элемент Из Результат.мШтрихКодов Цикл
				НоваяСтрока = ДокументОбъект.Контейнеры.Добавить();
				НоваяСтрока.НомерКонтейнера = Счетчик;
				НоваяСтрока.КодПробы = Элемент.barcode;
				НоваяСтрока.Контейнер = Строка(Элемент.transport_name);
				НоваяСтрока.КонтейнерПредставление = Элемент.transport_name;
				НоваяСтрока.Биоматериал = Элемент.sample_description;
				НоваяСтрока.БиоматериалПредставление = Элемент.sample_description;
				НоваяСтрока.Лаборатория = Лаборатория;
				НоваяСтрока.УсловияХранения = Результат.НомерЗаказаВнешнейЛаборатории;
				Счетчик = Счетчик + 1;
			КонецЦикла;
			
			СделанныеИзменения = Новый Структура;
			СделанныеИзменения.Вставить("Изменен_Объект");
			СделанныеИзменения.Вставить("Изменен_Контейнеры");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заказ успешно отправлен в " + Лаборатория); 
			
			Возврат СделанныеИзменения;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Произошла неизвестная ошибка");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено
	
КонецФункции

#КонецОбласти