#Область ПрограммныйИнтерфейс

Процедура СформироватьФайлыЭМДПередПодписанием(Результат, ДополнительныеПараметры) Экспорт
	
	ТекстыОшибок = Новый Массив;
	ДанныеФайловЭМД = Неопределено;
	ДДФайловРЭМДПоШаблонам = Новый Соответствие;
	ДанныеРЭМДПоШаблонам = Новый Соответствие;
	
	ФормированиеФайловЭМД = Ложь;
	ФормированиеФайловРЭМД = Ложь;
	
	Для Каждого СтрокаНабораДанных Из Результат.ОписаниеДанных.НаборДанных Цикл
		Если СтрокаНабораДанных.ТипПодписания = 2 Тогда
			ФормированиеФайловЭМД = Истина;
		ИначеЕсли СтрокаНабораДанных.ТипПодписания = 3 Тогда
			ФормированиеФайловРЭМД = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ФормированиеФайловЭМД Тогда
		ТекстОшибкиФормированияЭМД = "";
		ДанныеФайловЭМД = МедицинскаяДеятельность.СформироватьЭМДМедицинскогоДокумента(ДополнительныеПараметры.МедицинскийДокумент, ТекстОшибкиФормированияЭМД);
		
		Если ЗначениеЗаполнено(ТекстОшибкиФормированияЭМД) Тогда
			ТекстыОшибок.Добавить(ТекстОшибкиФормированияЭМД);
		КонецЕсли;
	КонецЕсли;
	
	Если ФормированиеФайловРЭМД Тогда
		ИнтеграцияЕГИСЗ_РЭМД.СформироватьФайлыДляПодписания(ДополнительныеПараметры.МедицинскийДокумент, ДополнительныеПараметры.ДолжностиМЗ, Результат.ОписаниеДанных.ВыбранныйСертификат.Ссылка, ТекстыОшибок);
		ДДФайловРЭМДПоШаблонам = ИнтеграцияЕГИСЗ_РЭМД.ПолучитьДДФайловПоДокументу(ДополнительныеПараметры.МедицинскийДокумент, ДополнительныеПараметры.УИДы_Шаблонов, ДанныеРЭМДПоШаблонам);
	КонецЕсли;
	
	Если ДанныеФайловЭМД = Неопределено
		И ДДФайловРЭМДПоШаблонам.Количество() = 0
	Тогда
		// Ни один файл не сформировался
		Результат.Оповещение.Модуль.Закрыть();
	ИначеЕсли ТекстыОшибок.Количество() > 0 Тогда
		// Часть файлов не сформировалась
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ДополнительныеПараметры",	ДополнительныеПараметры);
		ПараметрыОповещения.Вставить("ДанныеРЭМДПоШаблонам",	ДанныеРЭМДПоШаблонам);
		ПараметрыОповещения.Вставить("ТекстыОшибок",			ТекстыОшибок);
		ПараметрыОповещения.Вставить("Результат",				Результат);
		ПараметрыОповещения.Вставить("ДДФайловРЭМДПоШаблонам",	ДДФайловРЭМДПоШаблонам);
		ПараметрыОповещения.Вставить("ДанныеФайловЭМД",			ДанныеФайловЭМД);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВопросаПродолженияЧастичногоПодписания", бит_РаботаСФайламиКлиент, ПараметрыОповещения);
		
		ШаблонТекстаВопроса = НСтр("ru='Формирование некоторых файлов завершилось ошибкой, они не будут подписаны.%1Продолжить подписание успешно сформированных файлов?'");
		ТекстВопроса = СтрШаблон(ШаблонТекстаВопроса, Символы.ПС);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	Иначе
		// Все файлы сформировались
		СформироватьФайлыЭМДПродолжение(Результат, ДополнительныеПараметры, ДДФайловРЭМДПоШаблонам, ДанныеРЭМДПоШаблонам, ДанныеФайловЭМД);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Результат.Оповещение, Результат.Оповещение.ДополнительныеПараметры);
	
	Для Каждого ТекстОшибки Из ТекстыОшибок Цикл
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Сообщить(ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаВопросаПродолженияЧастичногоПодписания(ОтветНаВопрос, Параметры) Экспорт
	
	ДополнительныеПараметры = Параметры.ДополнительныеПараметры;
	ДанныеРЭМДПоШаблонам = Параметры.ДанныеРЭМДПоШаблонам;
	ТекстыОшибок = Параметры.ТекстыОшибок;
	Результат = Параметры.Результат;
	ДДФайловРЭМДПоШаблонам = Параметры.ДДФайловРЭМДПоШаблонам;
	ДанныеФайловЭМД = Параметры.ДанныеФайловЭМД;
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		СформироватьФайлыЭМДПродолжение(Результат, ДополнительныеПараметры, ДДФайловРЭМДПоШаблонам, ДанныеРЭМДПоШаблонам, ДанныеФайловЭМД);
	Иначе
		Результат.Оповещение.Модуль.Закрыть();
		Если ДанныеФайловЭМД <> Неопределено Тогда
			бит_РаботаСФайлами.УдалитьФайлЭМД(ДополнительныеПараметры.МедицинскийДокумент);
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Результат.Оповещение, Результат.Оповещение.ДополнительныеПараметры);
	
	Для Каждого ТекстОшибки Из ТекстыОшибок Цикл
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Сообщить(ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьФайлыЭМДПродолжение(Результат, ДополнительныеПараметры, ДДФайловРЭМДПоШаблонам, ДанныеРЭМДПоШаблонам, ДанныеФайловЭМД) Экспорт
	
	НаборДанных = Результат.ОписаниеДанных.НаборДанных;
	
	Если ДанныеФайловЭМД <> Неопределено Тогда
		Для Каждого СтрокаНабораДанных Из НаборДанных Цикл
			Если СтрокаНабораДанных.ТипПодписания = 2 Тогда
				СтрокаНабораДанных.Данные = ДанныеФайловЭМД.Получить(СтрокаНабораДанных.УИД_ЭМД).ДД;
				СтрокаНабораДанных.Объект.ДополнительныеПараметры.ПодписываемыеДанные = ДанныеФайловЭМД.Получить(СтрокаНабораДанных.УИД_ЭМД).ДД;
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтрокаНабораДанных Из ДополнительныеПараметры.ОбработчикПродолжения.ДополнительныеПараметры.МассивДанныхОбъектов Цикл
			Если СтрокаНабораДанных.ТипПодписания = 2 Тогда
				СтрокаНабораДанных.ПодписываемыеДанные = ДанныеФайловЭМД.Получить(СтрокаНабораДанных.УИД_ЭМД).ДД;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ДДФайловРЭМДПоШаблонам.Количество() <> 0 Тогда
		Для Каждого УИД_Шаблона Из ДополнительныеПараметры.УИДы_Шаблонов Цикл
			
			ОбновленныеДанныеЭМД = ДанныеРЭМДПоШаблонам.Получить(УИД_Шаблона);
			
			Если ОбновленныеДанныеЭМД = Неопределено Тогда
				УдалёнЭлементНабораДанных = Ложь;
				Для Каждого ЭлементНабораДанных Из НаборДанных Цикл
					Если ЭлементНабораДанных.ТипПодписания = 3
						И ЭлементНабораДанных.УИД_Шаблона = УИД_Шаблона
					Тогда
						НаборДанных.Удалить(НаборДанных.Найти(ЭлементНабораДанных));
						УдалёнЭлементНабораДанных = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если УдалёнЭлементНабораДанных Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаНабораДанных = СтрокаМассиваПоУИД_Шаблона(НаборДанных, УИД_Шаблона, 3);
			Если СтрокаНабораДанных <> Неопределено Тогда
				СтрокаНабораДанных.Данные = ДДФайловРЭМДПоШаблонам.Получить(УИД_Шаблона);
				СтрокаНабораДанных.Объект.ДополнительныеПараметры.ПодписываемыеДанные = ДДФайловРЭМДПоШаблонам.Получить(УИД_Шаблона);
			КонецЕсли;
			
			СтрокаДанныеОбъекта = СтрокаМассиваПоУИД_Шаблона(ДополнительныеПараметры.ОбработчикПродолжения.ДополнительныеПараметры.МассивДанныхОбъектов, УИД_Шаблона, 3);
			Если СтрокаНабораДанных <> Неопределено Тогда
				СтрокаДанныеОбъекта.ПодписываемыеДанные = ДДФайловРЭМДПоШаблонам.Получить(УИД_Шаблона);
			КонецЕсли;
			
			ПараметрыОбработчикаЗавершения = ДополнительныеПараметры.ОбработчикПродолжения.ДополнительныеПараметры.ОбработчикЗавершения.ДополнительныеПараметры;
			
			СтрокаСтруктурыДанных = СтрокаМассиваПоУИД_Шаблона(ПараметрыОбработчикаЗавершения.СтруктурыДанных, УИД_Шаблона);
			Если СтрокаНабораДанных <> Неопределено И ОбновленныеДанныеЭМД <> Неопределено Тогда
				СтрокаСтруктурыДанных.ПутьКФайлу = ОбновленныеДанныеЭМД.ПутьКФайлуВАрхиве;
				СтрокаСтруктурыДанных.УИД = ОбновленныеДанныеЭМД.УИД;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ФайлСуществует(Знач ПутьКФайлу) Экспорт
	
	Файл = Новый Файл(ПутьКФайлу);
	Возврат Файл.Существует();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтрокаМассиваПоУИД_Шаблона(МассивСтруктур, УИД_Шаблона, ТипПодписания = Неопределено)
	
	Для Каждого Структура Из МассивСтруктур Цикл
		Если (ТипПодписания = Неопределено
				Или Структура.ТипПодписания = ТипПодписания)
			И Структура.УИД_Шаблона = УИД_Шаблона
		Тогда
			Возврат Структура;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти