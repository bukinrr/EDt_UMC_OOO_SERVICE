#Область ПрограммныйИнтерфейс

// Открывает форму для начала выполнения операции СБП.
//
// Параметры:
//  ОповещениеОЗавершении		 - ОписаниеОповещения				 - оповещение о завершении операции
//  ФормаДокумента				 - ФормаКлиентскогоПриложения		 - форма документа платежной операции.
//  Документ					 - ДокументСсылка					 - документ учета оплаты.
//  ЭквайринговыйТерминал		 - СправочникСсылка.ЭквайринговыеТерминалы	 - терминал с отметкой СБП.
//  ДокументОснованиеДляВозврата - ДокументСсылка							 - документ оплаты, которую надо вернуть.
//  ЭтоВозврат                   - Булево                                    - открывает форму возврата.
//
Процедура ОткрытьФормуОперацииСБП(ОповещениеОЗавершении, ФормаДокумента, Документ, ЭквайринговыйТерминал, ДокументОснованиеДляВозврата = Неопределено, ЭтоВозврат = Ложь) Экспорт
	
	ПараметрыФормы = Новый Структура("ЭквайринговыйТерминал", ЭквайринговыйТерминал);
	
	Если Не ЭтоВозврат Тогда
		// Прием оплаты
		ПараметрыФормы.Вставить("ДокументОплаты", Документ);
		ОткрытьФорму("Обработка.ПодключениеКСБП.Форма.ФормаОплатаСБП", ПараметрыФормы, ФормаДокумента,,,,ОповещениеОЗавершении);
	Иначе
		// Возврат оплаты
		ПараметрыФормы.Вставить("ДокументОплаты", ДокументОснованиеДляВозврата);
		ПараметрыФормы.Вставить("ДокументВозврата", Документ);
		Если ЗначениеЗаполнено(ДокументОснованиеДляВозврата) Тогда
			ОткрытьФорму("Обработка.ПодключениеКСБП.Форма.ФормаВозвратаСБП", ПараметрыФормы, ФормаДокумента,,,,ОповещениеОЗавершении);
		Иначе
			ДопПараметры = Новый Структура;
			ДопПараметры.Вставить("ПараметрыФормы", ПараметрыФормы);
			ДопПараметры.Вставить("ФормаДокумента", ФормаДокумента);
			ДопПараметры.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
			Оповещение = Новый ОписаниеОповещения("ПродолжениеОткрытияФормыОперацииСБП", ИнтеграцияСБПКлиент, ДопПараметры);
			ТекстВопроса = "Документ основания возврата не указан, продолжить ли операцию возврата через СБП?";
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПродолжениеОткрытияФормыОперацииСБП(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Обработка.ПодключениеКСБП.Форма.ФормаВозвратаСБП", Параметры.ПараметрыФормы, Параметры.ФормаДокумента,,,,Параметры.ОповещениеОЗавершении);	
	КонецЕсли;
	
КонецПроцедуры

Функция ОплатаВыполненаРанееПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если Параметры.ВидДокумента = "Возврат" Тогда 
			ВозвратСсылка = ИнтеграцияСБП.СоздатьДокументВозвратаПоПлатежнойКарте(Параметры.ДокументОплаты, Параметры.СуммаОплачено);
			ОплатаСсылка = ИнтеграцияСБП.СоздатьДокументОплатаПлатежнойКартой(Параметры.ДокументОплаты, Параметры.СуммаДокумента);
			ПоказатьЗначение(,ОплатаСсылка);
			ПоказатьЗначение(,ВозвратСсылка);
		ИначеЕсли Параметры.ВидДокумента = "Поступление" Тогда
			ОплатаСсылка = ИнтеграцияСБП.СоздатьДокументОплатаПлатежнойКартой(Параметры.ДокументОплаты, Параметры.РазницаСумм); 
			ПоказатьЗначение(,ОплатаСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

// Текст ошибки недопустимой операции СБП перед открытием формы оплаты.
// 
// Возвращаемое значение:
//  Строка.
//
Функция ТекстОшибкиНедопустимаяОперацияСБПНачалоОплаты() Экспорт
	
	Возврат "Не допустимая операция в документе для оплаты в системе быстрых платежей";
	
КонецФункции

#КонецОбласти
