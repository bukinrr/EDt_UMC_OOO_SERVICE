#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьДанныеПоШтрихкодамУпаковок(Штрихкоды, КэшированныеЗначения, ПараметрыДействия) Экспорт
	
	ДанныеШтрихкодов = Новый ТаблицаЗначений;
	ДанныеШтрихкодов.Колонки.Добавить("Штрихкод"     , ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ДанныеШтрихкодов.Колонки.Добавить("НомерУпаковки", ОбщегоНазначения.ОписаниеТипаСтрока(27));
	ДанныеШтрихкодов.Колонки.Добавить("GTIN"         , ОбщегоНазначения.ОписаниеТипаСтрока(14));
	ДанныеШтрихкодов.Колонки.Добавить("НомерСерии"   , ОбщегоНазначения.ОписаниеТипаСтрока(200));
	ДанныеШтрихкодов.Колонки.Добавить("ГоденДо"      , ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	
	Для Каждого ДанныеШтрихкода Из Штрихкоды Цикл
		
		НоваяСтрока = ДанныеШтрихкодов.Добавить();
		НоваяСтрока.Штрихкод      = ДанныеШтрихкода.Штрихкод;
		НоваяСтрока.НомерУпаковки = ДанныеШтрихкода.SGTIN;
		НоваяСтрока.GTIN          = ДанныеШтрихкода.GTIN;
		НоваяСтрока.НомерСерии    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеШтрихкода, "Серия");
		НоваяСтрока.ГоденДо       = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеШтрихкода, "ГоденДо");
		
	КонецЦикла;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Штрихкоды.Штрихкод КАК Штрихкод,
	                      |	Штрихкоды.НомерУпаковки КАК НомерУпаковки,
	                      |	Штрихкоды.GTIN КАК GTIN,
	                      |	Штрихкоды.НомерСерии КАК НомерСерии,
	                      |	Штрихкоды.ГоденДо КАК ГоденДо
	                      |ПОМЕСТИТЬ ДанныеШтрихкодов
	                      |ИЗ
	                      |	&Штрихкоды КАК Штрихкоды
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	НомерУпаковки
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЛОЖЬ КАК НеизвестныйШтрихкод,
	                      |	ДанныеШтрихкодов.Штрихкод КАК Штрихкод,
	                      |	УпаковкиТоваров.Ссылка КАК УпаковкаТовара,
	                      |	УпаковкиТоваров.Номенклатура КАК Номенклатура,
	                      |	УпаковкиТоваров.СерияНоменклатуры КАК СерияНоменклатуры,
	                      |	УпаковкиТоваров.Партия КАК Партия,
	                      |	УпаковкиТоваров.Номенклатура.Упаковка КАК ЕдиницаИзмерения
	                      |ИЗ
	                      |	ДанныеШтрихкодов КАК ДанныеШтрихкодов
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиМаркируемогоТовара КАК УпаковкиТоваров
	                      |		ПО (УпаковкиТоваров.НомерУпаковки = ДанныеШтрихкодов.НомерУпаковки)
	                      |			И (УпаковкиТоваров.ГоденДо = ДанныеШтрихкодов.ГоденДо
	                      |				ИЛИ ДанныеШтрихкодов.ГоденДо = ДАТАВРЕМЯ(1, 1, 1)
	                      |					И УпаковкиТоваров.ГоденДо > &ТекущаяДата)
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ИСТИНА,
	                      |	ДанныеШтрихкодов.Штрихкод,
	                      |	ЗНАЧЕНИЕ(Справочник.УпаковкиМаркируемогоТовара.ПустаяСсылка),
	                      |	ШтрихкодыНоменклатуры.Владелец,
	                      |	СправочникСерии.Ссылка,
	                      |	"""",
	                      |	ШтрихкодыНоменклатуры.ЕдиницаИзмерения
	                      |ИЗ
	                      |	ДанныеШтрихкодов КАК ДанныеШтрихкодов
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	                      |		ПО (ШтрихкодыНоменклатуры.Штрихкод = ДанныеШтрихкодов.GTIN)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СправочникСерии
	                      |		ПО (СправочникСерии.Владелец = ШтрихкодыНоменклатуры.Владелец)
	                      |			И (СправочникСерии.НомерСерии = ДанныеШтрихкодов.НомерСерии)
	                      |			И (СправочникСерии.ГоденДо = ДанныеШтрихкодов.ГоденДо
	                      |				ИЛИ ДанныеШтрихкодов.ГоденДо = ДАТАВРЕМЯ(1, 1, 1)
	                      |					И СправочникСерии.ГоденДо > &ТекущаяДата)
	                      |			И (СправочникСерии.СерияПромаркированаДляЦелейМДЛП)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиМаркируемогоТовара КАК УпаковкиТоваров
	                      |		ПО (УпаковкиТоваров.НомерУпаковки = ДанныеШтрихкодов.НомерУпаковки)
	                      |			И (УпаковкиТоваров.ГоденДо = ДанныеШтрихкодов.ГоденДо
	                      |				ИЛИ ДанныеШтрихкодов.ГоденДо = ДАТАВРЕМЯ(1, 1, 1)
	                      |					И УпаковкиТоваров.ГоденДо > &ТекущаяДата)
	                      |ГДЕ
	                      |	НЕ &ПропускатьНенайденные
	                      |	И УпаковкиТоваров.Ссылка ЕСТЬ NULL
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ИСТИНА,
	                      |	ДанныеШтрихкодов.Штрихкод,
	                      |	ЗНАЧЕНИЕ(Справочник.УпаковкиМаркируемогоТовара.ПустаяСсылка),
	                      |	СправочникСерии.Владелец,
	                      |	СправочникСерии.Ссылка,
	                      |	"""",
	                      |	СправочникСерии.Владелец.Упаковка
	                      |ИЗ
	                      |	ДанныеШтрихкодов КАК ДанныеШтрихкодов
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	                      |		ПО (ШтрихкодыНоменклатуры.Штрихкод = ДанныеШтрихкодов.GTIN)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СправочникСерии
	                      |		ПО (СправочникСерии.НомерСерии = ДанныеШтрихкодов.НомерСерии)
	                      |			И (СправочникСерии.ГоденДо = ДанныеШтрихкодов.ГоденДо
	                      |				ИЛИ ДанныеШтрихкодов.ГоденДо = ДАТАВРЕМЯ(1, 1, 1)
	                      |					И СправочникСерии.ГоденДо > &ТекущаяДата)
	                      |			И (СправочникСерии.СерияПромаркированаДляЦелейМДЛП)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиМаркируемогоТовара КАК УпаковкиТоваров
	                      |		ПО (УпаковкиТоваров.НомерУпаковки = ДанныеШтрихкодов.НомерУпаковки)
	                      |			И (УпаковкиТоваров.ГоденДо = ДанныеШтрихкодов.ГоденДо
	                      |				ИЛИ ДанныеШтрихкодов.ГоденДо = ДАТАВРЕМЯ(1, 1, 1)
	                      |					И УпаковкиТоваров.ГоденДо > &ТекущаяДата)
	                      |ГДЕ
	                      |	НЕ &ПропускатьНенайденные
	                      |	И УпаковкиТоваров.Ссылка ЕСТЬ NULL
	                      |	И ШтрихкодыНоменклатуры.Владелец ЕСТЬ NULL");
	
	Запрос.УстановитьПараметр("Штрихкоды", ДанныеШтрихкодов);
	Запрос.УстановитьПараметр("ТекущаяДата", ДобавитьМесяц(НачалоДня(ТекущаяДатаСеанса()), -6));
	Запрос.УстановитьПараметр("ПропускатьНенайденные", ПараметрыДействия.ПропускатьНенайденныеШтрихкоды);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИнформацияПоШтрихкоду = ОбработкаТабличнойЧастиКлиентСервер.СтуктураКэшируемойИнформацииПоШтрихкоду(Выборка.Штрихкод);
		ИнформацияПоШтрихкоду.ШтрихкодУпаковки				= Истина;
		ИнформацияПоШтрихкоду.НеизвестныйШтрихкод			= Выборка.НеизвестныйШтрихкод;
		ИнформацияПоШтрихкоду.УпаковкаМаркированногоТовара	= Выборка.УпаковкаТовара;
		ИнформацияПоШтрихкоду.Номенклатура					= Выборка.Номенклатура;
		ИнформацияПоШтрихкоду.СерияНоменклатуры				= Выборка.СерияНоменклатуры;
		ИнформацияПоШтрихкоду.Партия						= Выборка.Партия;
		ИнформацияПоШтрихкоду.ЕдиницаИзмерения				= Выборка.ЕдиницаИзмерения;
		
		Если ЗначениеЗаполнено(ИнформацияПоШтрихкоду.Номенклатура) И Не ЗначениеЗаполнено(ИнформацияПоШтрихкоду.ЕдиницаИзмерения) Тогда
			ИнформацияПоШтрихкоду.ЕдиницаИзмерения = ОсновнаяЕдиницаИзмерения(ИнформацияПоШтрихкоду.Номенклатура);
		КонецЕсли;
		
		КэшированныеЗначения.Штрихкоды.Вставить(Выборка.Штрихкод, ИнформацияПоШтрихкоду);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает основную единицу измерения номенклатуры.
Функция ОсновнаяЕдиницаИзмерения(Знач Номенклатура) Экспорт
	ЗапрашиваемыеПараметры = Новый Структура;
	ЗапрашиваемыеПараметры.Вставить("Упаковка");
	ЗапрашиваемыеПараметры.Вставить("ЕдиницаХраненияОстатков"); 
	ЗапрашиваемыеПараметры.Вставить("ЕдиницаТоваров");
	
	ЕдиницаИзмерения = Неопределено;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, ЗапрашиваемыеПараметры);
	Если ЗначениеЗаполнено(Реквизиты.Упаковка) Тогда
		ЕдиницаИзмерения = Реквизиты.Упаковка;		
	ИначеЕсли ЗначениеЗаполнено(Реквизиты.ЕдиницаТоваров) Тогда
		ЕдиницаИзмерения = Реквизиты.ЕдиницаТоваров;
	Иначе
		ЕдиницаИзмерения = Реквизиты.ЕдиницаХраненияОстатков;	
	КонецЕсли;
		
	Возврат ЕдиницаИзмерения;
	
КонецФункции

#КонецОбласти