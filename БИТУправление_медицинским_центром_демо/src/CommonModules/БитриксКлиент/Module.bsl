#Область ПрограммныйИнтерфейс

Процедура ПодключитьОбработчикиНовыхСообщений() Экспорт
	
	КлючСлужебногоОбсуждения = Битрикс.КлючСозданногоСлужебногоОбсуждения();
	
	Если КлючСлужебногоОбсуждения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаСообщенийСервера = Новый ОписаниеОповещения("ОбработкаСообщенийСервера", БитриксКлиент);
	СистемаВзаимодействия.НачатьПодключениеОбработчикаНовыхСообщений(, КлючСлужебногоОбсуждения, ОбработкаСообщенийСервера);
	
КонецПроцедуры

Процедура ОбработкаСообщенийСервера(Сообщение, ДопПараметры) Экспорт
	
	Данные = Сообщение.Данные;
	
	Если Данные <> Неопределено
		И ТипЗнч(Данные) = Тип("Структура")
		И Данные.Свойство("Пользователи")
		И Данные.Свойство("Ссылка")
	Тогда
		Если Данные.Свойство("ЗаписьКлиента") И Данные.ЗаписьКлиента Тогда
			ОбработатьЗапросЗаписиКлиента(Данные);
		Иначе
			ОткрытьЭлемент(Данные);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСобытиеЖЗ(ЭтотОбъект, Параметр) Экспорт
	
	ЭтотОбъект.Клиент = Параметр.Клиент;
	ЭтотОбъект.мКлиентВзятИзДанныхЗаявки = Ложь;
	ЭтотОбъект.Открыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьЭлемент(Данные)
	
	Если Данные.Пользователи.Найти(ОбщегоНазначения.ТекущийПользователь()) <> Неопределено Тогда
		ПоказатьЗначение(, Данные.Ссылка);
		ПанельЗадачОС.Сигнализировать(1);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьЗапросЗаписиКлиента(Данные)
	
	Если ТипЗнч(Данные.Ссылка) <> Тип("СправочникСсылка.Клиенты") Тогда
		Возврат;
	КонецЕсли;
	
	КалендарьПланированияКлиент.ОткрытьАктивизироватьКалендарьПланирования();
	ПараметрыЗаписи = Новый Структура("Клиент", Данные.Ссылка);
	Оповестить("СобытиеИзБ24", ПараметрыЗаписи);
	ПанельЗадачОС.Сигнализировать(1);
	
КонецПроцедуры

#КонецОбласти