#Область ПрограммныйИнтерфейс

// Округляет время в дате до периодичности планирования из учетной политики.
//
// Параметры:
//  ДатаВх	 - Дата - Дата с временем.
//
Процедура ОкруглитьВремяДоПериодаПланирования(ДатаВх) Экспорт
	
	КвантПланирования = ОбщегоНазначенияКлиентСервер.ВремяВСекунды(УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ПериодичностьПланирования",ТекущаяДата()));
	Если ЗначениеЗаполнено(КвантПланирования) Тогда
		
		КвантовВДате = (ДатаВх-НачалоДня(ДатаВх))/КвантПланирования;
		ДатаВх       = НачалоДня(ДатаВх) +  КвантПланирования*(Цел(КвантовВДате)+?(Цел(КвантовВДате)=КвантовВДате,0,1));
		
	КонецЕсли;
	                       
КонецПроцедуры

// Открывает диалог ввода причины отмены заявки
//
// Параметры:
//  ЗаявкаСсылка	 - ДокументСсылка.Заявка - отменяемая заявка.
//  СостояниеОтмены	 - Булево				 - состояние отмены заявки
//
Процедура ОткрытьДиалогПричиныОтменыЗаявки(ЗаявкаСсылка, СостояниеОтмены = Неопределено) Экспорт
	
	ФормаЗаписи = ПолучитьФорму("РегистрСведений.ПричиныОтменыЗаявок.Форма.ФормаЗаписиУпр", Новый Структура("СостояниеОтмены", СостояниеОтмены));
	ФормаЗаписи.Параметры.ДляВводаПричины	= Истина;
	ФормаЗаписи.Запись.Заявка				= ЗаявкаСсылка;
	ФормаЗаписи.РежимОткрытияОкна			= РежимОткрытияОкнаФормы.Независимый; 
	ФормаЗаписи.Открыть();	
	
КонецПроцедуры

// Делает форму журнала записи активным окном
//
Процедура ОткрытьАктивизироватьКалендарьПланирования() Экспорт 
	
	// Ищем журнал записи в открытых формах, на начальной странице приоритетней
	ФормаЖЗ = Неопределено;
	ОкноНачальнойСтраницы = Неопределено;
	ЖурналЗаписиНаНачальнойСтранице = Ложь;
	ОткрытыеОкна = ПолучитьОкна();
	Для Каждого Окно Из ОткрытыеОкна Цикл
		
		Для Каждого Форма Из Окно.Содержимое Цикл 
			Если ТипЗнч(Форма) = Тип("ФормаКлиентскогоПриложения") И
				Форма.ИмяФормы = "Отчет.КалендарьПланирования.Форма.ФормаОтчетаУпр"	
			Тогда
				ФормаЖЗ = Форма;
			КонецЕсли;
		КонецЦикла;
			
		Если Окно.НачальнаяСтраница Тогда
			ОкноНачальнойСтраницы = Окно;
			Если ФормаЖЗ <> Неопределено Тогда
				ЖурналЗаписиНаНачальнойСтранице = Истина;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ФормаЖЗ <> Неопределено Тогда // Журнал записи на открытом рабочем столе или отдельно - переходим на него
		Если ЖурналЗаписиНаНачальнойСтранице Тогда // Для Такси
			ОкноНачальнойСтраницы.Активизировать();
		Иначе
			ФормаЖЗ.Активизировать();
		КонецЕсли;
	ИначеЕсли ОкноНачальнойСтраницы <> Неопределено Тогда // Рабочий стол открыт, но журнала записи там нет, открываем отдельно
		ОткрытьФорму("Отчет.КалендарьПланирования.Форма.ФормаОтчетаУпр");
	Иначе
		// Пробуем открыть начальную страницу и найти журнал там
		НавигационнаяСсылкаРабочегоСтола = "e1cib/navigationpoint/startpage";
		ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаРабочегоСтола);
		ОткрытьАктивизироватьКалендарьПланирования();
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
	