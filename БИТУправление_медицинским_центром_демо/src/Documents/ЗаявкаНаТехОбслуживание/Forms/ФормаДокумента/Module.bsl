#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма, Ложь);
	ТекущийПользователь = ОбщегоНазначения.ТекущийПользователь();
	CRMСервер.ЗаполнитьПерепискуПоДокументу(СписокСообщений, Объект.Ссылка, ТекущийПользователь);
	
	ДоступноИзменениеДатыПриемки = ПравоДоступа("ОтменаПроведения", Метаданные.Документы.ЗаявкаНаТехОбслуживание);
	ДоступноИзменениеФактическойДаты = ПравоДоступа("ИнтерактивноеИзменениеПроведенных", Метаданные.Документы.ЗаявкаНаТехОбслуживание);
	
	Если Не ДоступноИзменениеДатыПриемки Тогда
		Элементы.ДатаПриемки.ТолькоПросмотр = Истина;
		Элементы.ДатаПриемкиУстановитьСегодня.Доступность = Ложь;
		Элементы.ПлановаяДата.ТолькоПросмотр = Не ДоступноИзменениеФактическойДаты;
		Элементы.ПлановаяДатаУстановитьСегодня.Доступность = ДоступноИзменениеФактическойДаты;
		Элементы.ФактическаяДата.ТолькоПросмотр = Не ДоступноИзменениеФактическойДаты;
		Элементы.ФактическаяДатаУстановитьСегодня.Доступность = ДоступноИзменениеФактическойДаты;
		
		Если Объект.Ссылка.Пустая() Тогда
			Элементы.Принимающий.ТолькоПросмотр = ЗначениеЗаполнено(Объект.Принимающий);
		Иначе
			// Открытие старой заявки
			Элементы.Дата.ТолькоПросмотр = Истина;
			Элементы.Описание.ТолькоПросмотр = Истина;
			ТолькоПросмотр = Не ДоступноИзменениеФактическойДаты;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Описание.УстановитьHTML(Объект.ОписаниеФорматированное, Новый Структура);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьСтатусЗаявки();
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Описание.ПолучитьHTML(Объект.ОписаниеФорматированное, Новый Структура);
	Объект.Описание = Описание.ПолучитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "НовоеСообщениеПоЛО" Тогда 
		
		Если Параметр.Документ = Объект.Ссылка Тогда 
			CRMКлиентСервер.ДобавитьСообщениеВДокумент(СписокСообщений, Параметр.Сообщение, Параметр.Период, Параметр.Пользователь, ТекущийПользователь);
		КонецЕсли;
				
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(ТекущийОбъект, Ложь, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипОбращенияПриИзменении(Элемент)

	ПриИзмененииТипОбращения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТипОбращения()
	
	РеквизитыТипаОбращения = ПолучитьРеквизитыТипаОбращения(Объект.ТипОбращения, Объект.Филиал);
	
	Если РеквизитыТипаОбращения.Исполнитель <> Неопределено Тогда
		Объект.Исполнитель = РеквизитыТипаОбращения.Исполнитель;
	КонецЕсли;
	
	Если РеквизитыТипаОбращения.Принимающий <> Неопределено Тогда
		Объект.Принимающий = РеквизитыТипаОбращения.Принимающий;
		
		Если Не ДоступноИзменениеДатыПриемки
			И Не ДоступноИзменениеФактическойДаты
			И ЗначениеЗаполнено(Объект.Принимающий)
		Тогда
			Элементы.Принимающий.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыТипаОбращения(ТипОбращения, Филиал)
	
	Результат = Новый Структура("Исполнитель, Принимающий", Неопределено, Неопределено);
	
	СтрокаПринимающий = ТипОбращения.Ответственные.Найти(Филиал, "Филиал");
	Если СтрокаПринимающий <> Неопределено Тогда
		Результат.Принимающий = СтрокаПринимающий.Ответственный;
	Иначе
		СтрокаПринимающий = ТипОбращения.Ответственные.Найти(Справочники.Филиалы.ПустаяСсылка(), "Филиал");
		Если СтрокаПринимающий <> Неопределено Тогда
			Результат.Принимающий = СтрокаПринимающий.Ответственный;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаИсполнитель = ТипОбращения.Исполнители.Найти(Филиал, "Филиал");
	Если СтрокаИсполнитель <> Неопределено Тогда
		Результат.Исполнитель = СтрокаИсполнитель.Исполнитель;
	Иначе
		СтрокаИсполнитель = ТипОбращения.Исполнители.Найти(Справочники.Филиалы.ПустаяСсылка(), "Филиал");
		Если СтрокаИсполнитель <> Неопределено Тогда
			Результат.Исполнитель = СтрокаИсполнитель.Исполнитель;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПлановаяДатаПриИзменении(Элемент)

	УстановитьСтатусЗаявки();
	
КонецПроцедуры

&НаКлиенте
Процедура ФактическаяДатаПриИзменении(Элемент)

	УстановитьСтатусЗаявки();

КонецПроцедуры

&НаКлиенте
Процедура ДатаПриемкиПриИзменении(Элемент)

	УстановитьСтатусЗаявки();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусЗаявки()

	Статус = ПолучитьСтатусЗаявки(Объект.ПлановаяДата, Объект.ФактическаяДата, Объект.ДатаПриемки); 
	
КонецПроцедуры
	
&НаСервереБезКонтекста
Функция ПолучитьСтатусЗаявки(ПлановаяДата, ФактическаяДата, ДатаПриемки)
	
	Возврат Документы.ЗаявкаНаТехОбслуживание.ПолучитьСтатусЗаявки(ПлановаяДата, ФактическаяДата, ДатаПриемки); 
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановаяДатаУстановитьСегодня(Команда)
	
	Объект.ПлановаяДата = ТекущаяДата();
	ПлановаяДатаПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ФактическаяДатаУстановитьСегодня(Команда)

	Объект.ФактическаяДата = ТекущаяДата();
	ФактическаяДатаПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриемкиУстановитьСегодня(Команда)
	
	Объект.ДатаПриемки = ТекущаяДата();
	ДатаПриемкиПриИзменении(Неопределено);
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьТекПользователяКакИсполнителя(Команда)

	Объект.Исполнитель = ТекущийПользователь;
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьТекПользователяКакПринимающего(Команда)

	Объект.Принимающий = ТекущийПользователь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСообщение(Команда)
	
	ТекущееСообщение = СокрЛП(ТекущееСообщение);
	Если ПустаяСтрока(ТекущееСообщение) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не РаботаСДиалогамиКлиент.ЗаписатьНовыйОбъектВФорме(ЭтаФорма) Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураОповещения = CRMСервер.ДобавитьСообщениеПоЛистуОжидания(Объект.Ссылка, ТекущееСообщение);
	
	Оповестить("НовоеСообщениеПоЛО", СтруктураОповещения, Объект.Ссылка);
	
	ТекущееСообщение = "";
	
	ЭтаФорма.ТекущийЭлемент = Элементы.ТекущееСообщение;
	
КонецПроцедуры

#КонецОбласти
