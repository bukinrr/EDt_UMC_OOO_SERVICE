#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ПоРазделу Тогда
		Элементы.НМУИзСтандартаЛеченияВидНазначения.Видимость = Ложь;
	КонецЕсли;
	
	Для Каждого СтрокаСвязи Из Параметры.НМУДляПлановЛечения Цикл
		НоваяСтрокаСвязи = НМУДляПлановЛечения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСвязи, СтрокаСвязи);
	КонецЦикла;
	
	Для Каждого СтрокаПлана Из Параметры.ПланЛеченияИзПриема Цикл
		НоваяСтрокаПлана = ПланЛеченияИзПриема.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПлана, СтрокаПлана);
	КонецЦикла;
	
	Для Каждого СтрокаНМУ Из Параметры.НМУИзСтандартаЛечения Цикл
		НоваяСтрокаНМУ = НМУИзСтандартаЛечения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаНМУ, СтрокаНМУ);
		НоваяСтрокаНМУ.КодНМУ = СтрокаНМУ.НМУ.Код;
	КонецЦикла;
	
	Для Каждого СтрокаНоменклатурыДляДанныхВыбора Из Параметры.НоменклатурыДляДанныхВыбора Цикл
		СоответствиеНоменклатурыНМУ = СоответствиеНоменклатурНМУ.Добавить();
		СоответствиеНоменклатурыНМУ.Номенклатура = СтрокаНоменклатурыДляДанныхВыбора.Номенклатура;
		СоответствиеНоменклатурыНМУ.НМУ = СтрокаНоменклатурыДляДанныхВыбора.НМУ;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Для Каждого НоваяСтрокаНМУ Из НМУИзСтандартаЛечения Цикл
		
		// Добавляем номенклатуру
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("НМУ", НоваяСтрокаНМУ.НМУ);
		СтруктураПоиска.Вставить("ВидНазначения", НоваяСтрокаНМУ.ВидНазначения);
		
		СвязиПоТекущейНМУ = НМУДляПлановЛечения.НайтиСтроки(СтруктураПоиска);
		
		Если СвязиПоТекущейНМУ.Количество() > 0 Тогда
			
			// Сначала поиск в связях
			СтрокаПлана = ПланЛеченияИзПриема.НайтиСтроки(Новый Структура("ИдентификаторНоменклатуры", СвязиПоТекущейНМУ[0].ИдентификаторПланаЛечения))[0];
			НоваяСтрокаНМУ.Номенклатура = СтрокаПлана.Номенклатура;
			НоваяСтрокаНМУ.Количество = СтрокаПлана.Количество;
			
		ИначеЕсли ЗначениеЗаполнено(НоваяСтрокаНМУ.ФиксированнаяНоменклатура) Тогда
			
			// Если нет в связях ставим фиксированную, если задана
			НоваяСтрокаНМУ.Номенклатура = НоваяСтрокаНМУ.ФиксированнаяНоменклатура;
			
			// Добавляем связь последнего плана с этой номенклатурой
			// ПланЛечения = ПолучитьПоследнийПланЛечения(Новый Структура("Номенклатура", НоваяСтрокаНМУ.Номенклатура)); // Для объединения строк плана лечения
			ПланЛечения = ПолучитьПоследнийПланЛечения(Новый Структура("Номенклатура, ВидНазначения", НоваяСтрокаНМУ.Номенклатура, НоваяСтрокаНМУ.ВидНазначения));
			
			// Проверяем, есть ли эта номенклатура в плане
			Если ПланЛечения = Неопределено Тогда
				
				// Если нет — создаем строку с планом
				НоваяСтрокаПланаЛечения = ПланЛеченияИзПриема.Добавить();
				НоваяСтрокаПланаЛечения.Номенклатура = НоваяСтрокаНМУ.Номенклатура;
				НоваяСтрокаПланаЛечения.ИдентификаторНоменклатуры = Новый УникальныйИдентификатор();
				НоваяСтрокаПланаЛечения.ДобавленВЭтойФорме = Истина;
				НоваяСтрокаПланаЛечения.Количество = НоваяСтрокаНМУ.Количество;
				НоваяСтрокаПланаЛечения.ВидНазначения = НоваяСтрокаНМУ.ВидНазначения;
				
				ПланЛечения = НоваяСтрокаПланаЛечения;
				
			КонецЕсли;
			
			Если ПланЛечения.ДобавленВЭтойФорме Тогда
				НоваяСтрокаНМУ.НоваяНоменклатура = Истина;
				НоваяСтрокаНМУ.НоваяСтрока = Истина;
			КонецЕсли;
				
			НоваяСвязь = НМУДляПлановЛечения.Добавить();
			НоваяСвязь.НМУ						 = НоваяСтрокаНМУ.НМУ;
			НоваяСвязь.ИдентификаторПланаЛечения = ПланЛечения.ИдентификаторНоменклатуры;
			НоваяСвязь.ВидНазначения			 = НоваяСтрокаНМУ.ВидНазначения;
			
			НоваяСтрокаНМУ.Количество			 = ПланЛечения.Количество;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	МассивСвязей = Новый Массив();
	МассивПланов = Новый Массив();
	Для Каждого СтрокаСвязи Из НМУДляПлановЛечения Цикл
		СтруктураСтрокиСвязи = Новый Структура;
		
		СтруктураСтрокиСвязи.Вставить("НМУ",						СтрокаСвязи.НМУ);
		СтруктураСтрокиСвязи.Вставить("ВидНазначения",				СтрокаСвязи.ВидНазначения);
		СтруктураСтрокиСвязи.Вставить("ИдентификаторПланаЛечения",	СтрокаСвязи.ИдентификаторПланаЛечения);
		
		МассивСвязей.Добавить(СтруктураСтрокиСвязи);
	КонецЦикла;
	
	Для Каждого СтрокаПланаЛечения Из ПланЛеченияИзПриема Цикл
		СтруктураСтрокиПланаЛечения = Новый Структура;
		
		СтруктураСтрокиПланаЛечения.Вставить("Номенклатура",			 СтрокаПланаЛечения.Номенклатура);
		СтруктураСтрокиПланаЛечения.Вставить("ИдентификаторНоменклатуры",СтрокаПланаЛечения.ИдентификаторНоменклатуры);
		СтруктураСтрокиПланаЛечения.Вставить("Количество",				 СтрокаПланаЛечения.Количество);
		СтруктураСтрокиПланаЛечения.Вставить("ВидНазначения",			 СтрокаПланаЛечения.ВидНазначения);
		
		МассивПланов.Добавить(СтруктураСтрокиПланаЛечения);
	КонецЦикла;
	
	Закрыть(Новый Структура("Связи, Планы", МассивСвязей, МассивПланов));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НМУИзСтандартаЛеченияНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	МассивНоменклатур = Новый Массив;
	ТекущаяНМУ = Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.НМУ;
	НоменклатурыПоКодуНМУ = СоответствиеНоменклатурНМУ.НайтиСтроки(Новый Структура("НМУ", ТекущаяНМУ)); 
	ДанныеВыбора = Новый СписокЗначений;
	СтандартнаяОбработка = Ложь;
	
	Если НоменклатурыПоКодуНМУ.Количество() > 1 Тогда
		
		Для Каждого СтрокаТаблицы Из НоменклатурыПоКодуНМУ Цикл
			МассивНоменклатур.Добавить(СтрокаТаблицы.Номенклатура);
		КонецЦикла;
		
	Иначе
		
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("НМУ", ТекущаяНМУ);
		СтруктураПоиска.Вставить("ВидНазначения", Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.ВидНазначения);
		
		СтрокиИзПланаЛеченияПоНМУ = НМУДляПлановЛечения.НайтиСтроки(СтруктураПоиска);
		Для Каждого Строка Из СтрокиИзПланаЛеченияПоНМУ Цикл
			Номенклатура = ПланЛеченияИзПриема.НайтиСтроки(Новый Структура("ИдентификаторНоменклатуры", Строка.ИдентификаторПланаЛечения))[0].Номенклатура;
			Если МассивНоменклатур.Найти(Номенклатура) = Неопределено Тогда
				МассивНоменклатур.Добавить(Номенклатура);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивНоменклатур.Количество() < 2 Тогда
			
			Для Каждого Строка Из ПланЛеченияИзПриема Цикл
				Если МассивНоменклатур.Найти(Строка.Номенклатура) = Неопределено Тогда
					МассивНоменклатур.Добавить(Строка.Номенклатура);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеВыбора.ЗагрузитьЗначения(МассивНоменклатур);
	
КонецПроцедуры

&НаКлиенте
Процедура НМУИзСтандартаЛеченияНоменклатураПриИзменении(Элемент)
	
	НоваяНоменклатура = Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.Номенклатура;
	ВидНазначения = Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.ВидНазначения;
	
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("НМУ", Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.НМУ);
	СтруктураПоиска.Вставить("ВидНазначения", ВидНазначения);
	
	СтарыеСвязи = НМУДляПлановЛечения.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтараяСвязь Из СтарыеСвязи Цикл
		НМУДляПлановЛечения.Удалить(СтараяСвязь);
	КонецЦикла;
	
	ТекущаяСтрокаНМУ = НМУИзСтандартаЛечения.НайтиПоИдентификатору(Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.ПолучитьИдентификатор());
	
	Если ЗначениеЗаполнено(НоваяНоменклатура) Тогда
		
		// ПланЛечения = ПолучитьПоследнийПланЛечения(Новый Структура("Номенклатура", НоваяНоменклатура)); // Для объединения строк плана лечения
		ПланЛечения = ПолучитьПоследнийПланЛечения(Новый Структура("Номенклатура, ВидНазначения", НоваяНоменклатура, ВидНазначения));
		
		Если ПланЛечения = Неопределено Тогда
			
			// Планов с такой номенклатурой нет. Сначала создаем их
			Если ТекущаяСтрокаНМУ.Количество = 0 Тогда
				ТекущаяСтрокаНМУ.Количество = 1;
			КонецЕсли;
			НоваяСтрокаПланаЛечения = ПланЛеченияИзПриема.Добавить();
			НоваяСтрокаПланаЛечения.Номенклатура = НоваяНоменклатура;
			НоваяСтрокаПланаЛечения.ИдентификаторНоменклатуры = Новый УникальныйИдентификатор();
			НоваяСтрокаПланаЛечения.ДобавленВЭтойФорме = Истина;
			НоваяСтрокаПланаЛечения.ВидНазначения = ВидНазначения;
			
			ПланЛечения = НоваяСтрокаПланаЛечения;
			ПланЛечения.Количество = ТекущаяСтрокаНМУ.Количество;
			
		Иначе
			
			ТекущаяСтрокаНМУ.Количество = ПланЛечения.Количество;
			
		КонецЕсли;
		
		Если ПланЛечения.ДобавленВЭтойФорме Тогда
			ТекущаяСтрокаНМУ.НоваяНоменклатура = Истина;
			ТекущаяСтрокаНМУ.НоваяСтрока = Истина;
		Иначе
			ТекущаяСтрокаНМУ.НоваяНоменклатура = Ложь;
			ТекущаяСтрокаНМУ.НоваяСтрока = Ложь;
		КонецЕсли;
		
		// Привязываем к плану	
		НоваяСвязь = НМУДляПлановЛечения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСвязь, ТекущаяСтрокаНМУ);
		НоваяСвязь.ИдентификаторПланаЛечения = ПланЛечения.ИдентификаторНоменклатуры;
		
	Иначе
		ТекущаяСтрокаНМУ.НоваяНоменклатура = Ложь;
		ТекущаяСтрокаНМУ.НоваяСтрока = Ложь;
		ТекущаяСтрокаНМУ.Количество = 0;
	КонецЕсли;
	
	// Проверить планы лечения. Если план был добавлен в этой форме, но у него не осталось связей — его надо удалить
	ПланыЛеченияКУдалению = Новый Массив();
	Для Каждого СтрокаПланаЛечения Из ПланЛеченияИзПриема Цикл
		Если СтрокаПланаЛечения.ДобавленВЭтойФорме И НМУДляПлановЛечения.НайтиСтроки(Новый Структура("ИдентификаторПланаЛечения",СтрокаПланаЛечения.ИдентификаторНоменклатуры)).Количество() = 0 Тогда
			ПланыЛеченияКУдалению.Добавить(СтрокаПланаЛечения);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ПланЛеченияКУдалению Из ПланыЛеченияКУдалению Цикл
		ПланЛеченияИзПриема.Удалить(ПланЛеченияКУдалению);
	КонецЦикла;
	
	ИзменитьСтрокиПоНМУ();
	
КонецПроцедуры

&НаКлиенте
Процедура НМУИзСтандартаЛеченияКоличествоПриИзменении(Элемент)
	
	НоваяНоменклатура = Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.Номенклатура;
	СтрокиПланаЛеченияПоНоменклатуре = ПланЛеченияИзПриема.НайтиСтроки(Новый Структура("Номенклатура", НоваяНоменклатура));
	
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("НМУ", Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.НМУ);
	СтруктураПоиска.Вставить("ВидНазначения", Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.ВидНазначения);
	
	СтрокиСвязи = НМУДляПлановЛечения.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаСвязи Из СтрокиСвязи Цикл
		
		СтрокаПлана = ПланЛеченияИзПриема.НайтиСтроки(Новый Структура("ИдентификаторНоменклатуры", СтрокаСвязи.ИдентификаторПланаЛечения))[0];
		СтрокаПлана.Количество = Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.Количество;
		
	КонецЦикла;
	
	ИзменитьСтрокиПоНМУ();
	
КонецПроцедуры

&НаКлиенте
Процедура НМУИзСтандартаЛеченияНоваяСтрокаПриИзменении(Элемент)
	
	СтрокаНМУ = Элементы.НМУИзСтандартаЛечения.ТекущиеДанные;
	
	Если Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.НоваяСтрока Тогда
		
		// 1. Найти и удалить старую связь
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("НМУ", СтрокаНМУ.НМУ);
		СтруктураПоиска.Вставить("ВидНазначения", СтрокаНМУ.ВидНазначения);
		
			СтараяСвязь = НМУДляПлановЛечения.НайтиСтроки(СтруктураПоиска)[0];
		НМУДляПлановЛечения.Удалить(СтараяСвязь);
		
		// 2. Добавляем новый план лечения
		НоваяСтрокаПланаЛечения = ПланЛеченияИзПриема.Добавить();
		НоваяСтрокаПланаЛечения.Номенклатура = СтрокаНМУ.Номенклатура;
		НоваяСтрокаПланаЛечения.ИдентификаторНоменклатуры = Новый УникальныйИдентификатор();
		НоваяСтрокаПланаЛечения.ДобавленВЭтойФорме = Истина;
		НоваяСтрокаПланаЛечения.Количество = СтрокаНМУ.Количество;
		НоваяСтрокаПланаЛечения.ВидНазначения = СтрокаНМУ.ВидНазначения;
		
		// 3. Создаем новую связь
		НоваяСвязь = НМУДляПлановЛечения.Добавить();
		НоваяСвязь.НМУ						 = СтрокаНМУ.НМУ;
		НоваяСвязь.ВидНазначения			 = СтрокаНМУ.ВидНазначения;
		НоваяСвязь.ИдентификаторПланаЛечения = НоваяСтрокаПланаЛечения.ИдентификаторНоменклатуры;
	Иначе 
		
		// 1. Найти и удалить старую связь
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("НМУ", СтрокаНМУ.НМУ);
		СтруктураПоиска.Вставить("ВидНазначения", СтрокаНМУ.ВидНазначения);
		
		СтараяСвязь = НМУДляПлановЛечения.НайтиСтроки(СтруктураПоиска)[0];
		ИдентификаторПланаЛечения = СтараяСвязь.ИдентификаторПланаЛечения;
		НМУДляПлановЛечения.Удалить(СтараяСвязь);
		
		// 2. Удалить связанный план лечения если связей у него нет
		СвязиПоНоменклатуре = НМУДляПлановЛечения.НайтиСтроки(Новый Структура("ИдентификаторПланаЛечения", ИдентификаторПланаЛечения));
		
		Если СвязиПоНоменклатуре.Количество() = 0 Тогда
			ПланЛечения = ПланЛеченияИзПриема.НайтиСтроки(Новый Структура("ИдентификаторНоменклатуры", ИдентификаторПланаЛечения))[0];
			ПланЛеченияИзПриема.Удалить(ПланЛечения);
		КонецЕсли;
		
		// 3. Создаем новую связь с последним планом лечения
		ПоследнийПланЛечения = ПолучитьПоследнийПланЛечения(Новый Структура("Номенклатура", СтрокаНМУ.Номенклатура));
		
		НоваяСвязь = НМУДляПлановЛечения.Добавить();
		НоваяСвязь.НМУ						 = СтрокаНМУ.НМУ;
		НоваяСвязь.ВидНазначения			 = СтрокаНМУ.ВидНазначения;
		НоваяСвязь.ИдентификаторПланаЛечения = ПоследнийПланЛечения.ИдентификаторНоменклатуры;
		
		// 4. Устанавливается количество по существующему плану
		НМУИзСтандартаЛечения.НайтиПоИдентификатору(СтрокаНМУ.ПолучитьИдентификатор()).Количество = ПоследнийПланЛечения.Количество;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьПоследнийПланЛечения(СтруктураПоиска)
	
	ПланыЛеченияПоНоменклатуре = ПланЛеченияИзПриема.НайтиСтроки(СтруктураПоиска);
	
	Если ПланыЛеченияПоНоменклатуре.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПоследняяСтрока = ПланыЛеченияПоНоменклатуре[0];
	
	Для Каждого СтрокаПланаЛечения Из ПланыЛеченияПоНоменклатуре Цикл
		Если ПланЛеченияИзПриема.Индекс(СтрокаПланаЛечения) > ПланЛеченияИзПриема.Индекс(ПоследняяСтрока) Тогда
			ПоследняяСтрока = СтрокаПланаЛечения;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПоследняяСтрока;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьСтрокиПоНМУ()
	
	ТекущаяСтрокаНМУ = НМУИзСтандартаЛечения.НайтиПоИдентификатору(Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.ПолучитьИдентификатор());
	
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("НМУ", Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.НМУ);
	СтруктураПоиска.Вставить("ВидНазначения", Элементы.НМУИзСтандартаЛечения.ТекущиеДанные.ВидНазначения);
	
	СтрокиНМУ = НМУИзСтандартаЛечения.НайтиСтроки(СтруктураПоиска);
	Если СтрокиНМУ.Количество() > 1 Тогда
		Для Каждого СтрокаНМУ Из СтрокиНМУ Цикл
			Если Не СтрокаНМУ = ТекущаяСтрокаНМУ Тогда
				ЗаполнитьЗначенияСвойств(СтрокаНМУ, ТекущаяСтрокаНМУ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти