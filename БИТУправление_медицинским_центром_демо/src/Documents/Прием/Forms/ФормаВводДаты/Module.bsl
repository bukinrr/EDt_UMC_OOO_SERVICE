#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = Параметры.Дата;
	// Параметр ВозвращатьДату объединяет 2 опции: ожидаемый тип значения Дата и формат ввода "Дата без времени".
	Параметры.Свойство("ВозвращатьДату", ВозвращатьДату);
	Элементы.ВариантВвода.Доступность = Не ВозвращатьДату;
	
	Если ТипЗнч(Дата) = Тип("Дата") Тогда
		
		ВариантВвода = 1;
		ПолеДаты = Дата;
		Элементы.ПолеДаты.ФорматРедактирования = "ДЛФ=D";
		
	Иначе
		Если СтрДлина(Дата) = 6 Тогда
			
			Элементы.ПолеДаты.ФорматРедактирования = "ДЛФ=T";
			
			ВариантВвода = 3;
			
			ПолеДаты = Дата("00010101" + Дата);
			
		ИначеЕсли СтрДлина(Дата) = 8 Тогда
			
			ВариантВвода = 1;
			
			ПолеДаты = Дата(Дата + "000000");
			
			Элементы.ПолеДаты.ФорматРедактирования = "ДЛФ=D";
			
		ИначеЕсли СтрДлина(Дата) = 14 Тогда
			
			ВариантВвода = 2;
			
			ПолеДаты = Дата(Дата);
			
			Элементы.ПолеДаты.ФорматРедактирования = "ДЛФ=DT";
			
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ПолеДатыКалендарь.ВыделенныеДаты.Очистить();
	Элементы.ПолеДатыКалендарь.ВыделенныеДаты.Добавить(ПолеДаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если НЕ ЗначениеЗаполнено(ВариантВвода) Тогда
		
		ВариантВвода = 1;
		
		Элементы.ПолеДаты.ФорматРедактирования = "ДЛФ=D";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если ВозвращатьДату Тогда
		
		ЭтаФорма.Закрыть(ПолеДаты);	
		
	ИначеЕсли ЗначениеЗаполнено(ВариантВвода) Тогда
		
		Если ВариантВвода = 1 Тогда
			
			ЭтаФорма.Закрыть(Формат(ПолеДаты, "ДЛФ=D"));
			
		ИначеЕсли ВариантВвода = 2 Тогда
			
			ЭтаФорма.Закрыть(Формат(ПолеДаты, "ДЛФ=DT"));
			
		ИначеЕсли ВариантВвода = 3 Тогда
			                                            
			ЭтаФорма.Закрыть(Формат(ПолеДаты, "ДЛФ=T"));
			
		КонецЕсли;
		
	Иначе
		
		ЭтаФорма.Закрыть(Неопределено);
		
	КонецЕсли;
	  	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВариантВводаПриИзменении(Элемент)
	
	Если ВариантВвода = 1 Тогда 
		
		Элементы.ПолеДаты.ФорматРедактирования = "ДЛФ=D";
		
	ИначеЕсли ВариантВвода = 2 Тогда
		
	     Элементы.ПолеДаты.ФорматРедактирования = "ДЛФ=DT";
		
	ИначеЕсли ВариантВвода = 3 Тогда
		
	     Элементы.ПолеДаты.ФорматРедактирования = "ДЛФ=T";
		
	КонецЕсли;
	
	Элементы.ПолеДатыКалендарь.Доступность = ВариантВвода <> 3;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеДатыПриИзменении(Элемент)
	
	Элементы.ПолеДатыКалендарь.ВыделенныеДаты.Очистить();
	Элементы.ПолеДатыКалендарь.ВыделенныеДаты.Добавить(ПолеДаты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеДатыКалендарьВыбор(Элемент, ВыбраннаяДата)
	Сохранить(Неопределено);
КонецПроцедуры

#КонецОбласти
