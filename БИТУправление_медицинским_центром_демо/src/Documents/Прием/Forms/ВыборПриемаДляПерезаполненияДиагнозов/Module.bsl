#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПерезаполнитьДиагнозы = Истина;
	СписокПриемов.Параметры.УстановитьЗначениеПараметра("Клиент", Параметры.Клиент);
	ДиагнозыПриема.Параметры.УстановитьЗначениеПараметра("СсылкаНаПрием", Документы.Прием.ПустаяСсылка());
	
	Если Параметры.СсылкаНаПервичныйПрием = Параметры.СсылкаНаТекущийПрием
		Или Параметры.СсылкаНаПервичныйПрием = Документы.Прием.ПустаяСсылка() Тогда
		ОтборПоПервичномуПриему = Ложь;
		Элементы.ПервичныйПрием.Видимость = Ложь;
	Иначе
		ОтборПоПервичномуПриему = Истина;
		Элементы.ПервичныйПрием.Видимость = Истина;
		ПервичныйПрием = Параметры.СсылкаНаПервичныйПрием;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборСпискаПриемов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.СписокПриемов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПриВыбореПриема(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрием(Команда)
	
	ОткрытьЗначение(Элементы.СписокПриемов.ТекущиеДанные.Ссылка);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПриемовПриАктивизацииСтроки(Элемент)
	
	Если Элементы.СписокПриемов.ТекущиеДанные <> Неопределено Тогда
		ОбновитьПараметрыСпискаДиагнозовПриема(Элементы.СписокПриемов.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоПервичномуПриемуПриИзменении(Элемент)
	
	УстановитьОтборСпискаПриемов();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриемовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПриВыбореПриема(Элементы.СписокПриемов.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПараметрыСпискаДиагнозовПриема(Диагноз)
	
	ДиагнозыПриема.Параметры.УстановитьЗначениеПараметра("СсылкаНаПрием", Диагноз);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборСпискаПриемов()
	
	Если ОтборПоПервичномуПриему Тогда
		РаботаСФормамиКлиент.УстановитьОтборСписка("ПервичныйПрием", Параметры.СсылкаНаПервичныйПрием, СписокПриемов, ВидСравненияКомпоновкиДанных.Равно); 
	Иначе
		РаботаСФормамиКлиент.СнятьОтборСписка("ПервичныйПрием", СписокПриемов); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореПриема(Прием)
	
	ОповеститьОВыборе(Новый Структура("УдалятьСтарые, Прием", ПерезаполнитьДиагнозы, Прием));
	
КонецПроцедуры

#КонецОбласти
