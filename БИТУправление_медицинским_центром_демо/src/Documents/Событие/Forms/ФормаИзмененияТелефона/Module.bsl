#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Действие=Элементы.Действие.СписокВыбора[0].Значение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Действие <> "АктуализироватьСотовыйТелефон"
		И Не ЗначениеЗаполнено(ВидКИ)
	Тогда
		ПоказатьПредупреждение(,НСтр("ru='Не указан Вид контактной информации'"), 60);
		Возврат;
	КонецЕсли;
	
	СохранитьНаСервере(Действие, ВидКИ, Параметры.Клиент, Параметры.Номер);
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДействиеПриИзменении(Элемент)
	
	Если Действие="АктуализироватьСотовыйТелефон" Тогда
		Элементы.ВидКИ.Доступность=Ложь;
	Иначе
		Элементы.ВидКИ.Доступность=Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьНаСервере(Действие, ВидКИ, Клиент, Номер)
	
	Если Действие = "АктуализироватьСотовыйТелефон" Тогда
		КонтактнаяИнформацияСерверПереопределяемый.ЗаписатьНомерТелефона(Клиент, Номер);
	ИначеЕсли Действие = "ДобавитьНомерПользвателю" Тогда
		КонтактнаяИнформацияСерверПереопределяемый.ЗаписатьНомерТелефона(Клиент, Номер, ВидКИ);	
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	КонтактнаяИнформация.Представление КАК Представление
		             |ИЗ
		             |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		             |ГДЕ
		             |	КонтактнаяИнформация.Объект = &Объект
		             |	И КонтактнаяИнформация.Вид = &Вид";
		Запрос.УстановитьПараметр("Объект", Клиент);
		Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.ТелефонСотовый);
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() > 0 Тогда
			СтарыйНомер = Результат.ВыгрузитьКолонку(0).Получить(0);
			КонтактнаяИнформацияСерверПереопределяемый.ЗаписатьНомерТелефона(Клиент, СтарыйНомер, ВидКИ);
		КонецЕсли;
		КонтактнаяИнформацияСерверПереопределяемый.ЗаписатьНомерТелефона(Клиент, Номер);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
