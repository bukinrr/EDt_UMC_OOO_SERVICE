#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(Знач ДанныеЗаполнения, СтандартнаяОбработка)

	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда 
		Возврат;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Количество() = 1
		И ДанныеЗаполнения.Свойство("Основание")
	Тогда
		ДанныеЗаполнения = ДанныеЗаполнения.Основание;
	КонецЕсли;
	
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ДанныеЗаполнения)) Тогда 
		ИсключаемыеРеквизиты = "Дата, Номер";
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЛистОжидания") Тогда
			ОбщегоНазначенияКлиентСервер.КонкатенацияСтрок(ИсключаемыеРеквизиты, "ТипРекламы, ИсточникИнформации, РекламнаяКомпания, КлючевоеСловоРекламнойКомпании, СодержаниеОбъявления");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, , ИсключаемыеРеквизиты);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Клиенты") Тогда 
		Клиент = ДанныеЗаполнения;
	Иначе
		Попытка
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	ЭтотОбъект.ДатаФакт = Дата(1,1,1);
	ЭтотОбъект.ДлительностьЗвонка = 0;
	ЭтотОбъект.ИсходЗвонка = Неопределено;
	ЭтотОбъект.ЗвонокЗавершен = Ложь;
	ЭтотОбъект.ЗвонокНачат = Ложь;
	ЭтотОбъект.ЗаписьРазговора = "";
	
	// Если прошлое событие было создано на основании ЛО, это событие тоже
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		ЛистОжидания = CRMСервер.НайтиЛистОжиданияСвязанныйСДокументом(ДанныеЗаполнения);
		Если ЛистОжидания <> Неопределено Тогда 	
			ДанныеЗаполнения = ЛистОжидания;
		КонецЕсли;
	КонецЕсли;
	
	ЭтотОбъект.Основание = ДанныеЗаполнения;
	
	пчВидыСобытий = Перечисления.ВидыСобытий;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Событие") Тогда
		
		Если ДанныеЗаполнения.ВидСобытия = пчВидыСобытий.ВходящееПисьмо
			Или ДанныеЗаполнения.ВидСобытия = пчВидыСобытий.ИсходящееПисьмо
		Тогда
			ВидСобытия = пчВидыСобытий.ИсходящееПисьмо;
			
		ИначеЕсли ДанныеЗаполнения.ВидСобытия = пчВидыСобытий.ВходящийЗвонок
			Или ДанныеЗаполнения.ВидСобытия = пчВидыСобытий.ИсходящийЗвонок
		Тогда
			ВидСобытия = пчВидыСобытий.ИсходящийЗвонок;
		КонецЕсли;
			
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОказаниеУслуг") Тогда
		
		ВидСобытия = пчВидыСобытий.ИсходящийЗвонок;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЛистОжидания") Тогда 
		
		ВидСобытия = Перечисления.ВидыСобытий.ИсходящийЗвонок;
		ТипЗвонка = Справочники.ТипыЗвонков.ЗаписьНаПрием;
		Клиент = Основание.Клиент;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачиПользователя") Тогда
		
		ВидСобытия = Перечисления.ВидыСобытий.ИсходящийЗвонок;
		
		Если ЗначениеЗаполнено(Основание.КонтактноеЛицо) Тогда
			Клиент = Основание.КонтактноеЛицо;
		ИначеЕсли ТипЗнч(Основание.Объект) = Тип("СправочникСсылка.Клиенты") Тогда
			Клиент = Основание.Объект;
		КонецЕсли;
			
		Комментарий = Основание.Описание;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОбратнаяСвязь") Тогда
		
		ВидСобытия = пчВидыСобытий.ИсходящийЗвонок;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Клиент) Тогда 
		НомерТелефона = КонтактнаяИнформацияСерверПереопределяемый.НайтиКонтактнуюИнформацию(Клиент);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	CRMСервер.ОбращениеПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	CRMСервер.ОбращениеПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти