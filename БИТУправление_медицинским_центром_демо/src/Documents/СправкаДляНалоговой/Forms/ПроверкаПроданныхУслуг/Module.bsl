
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Продажи") Тогда
		
		Элементы.УслугиСКодом.Заголовок = "Услуги с кодом """ + Параметры.КодУслуги + """";
		
		ДеревоСКодомНоль = ДанныеФормыВЗначение(УслугиСКодомНоль, Тип("ДеревоЗначений"));
		ДеревоСИскомымКодом = ДанныеФормыВЗначение(УслугиСИскомымКодом, Тип("ДеревоЗначений"));
		
		Для Каждого Элемент Из Параметры.Продажи Цикл
			
			Если Элемент.Номенклатура.КодМедУслуги = 0 Тогда
				ДобавитьЭлементВДерево(ДеревоСКодомНоль, Элемент);	
			Иначе
				ДобавитьЭлементВДерево(ДеревоСИскомымКодом, Элемент);		
			КонецЕсли;
			
		КонецЦикла;
		
		ЗначениеВДанныеФормы(ДеревоСКодомНоль, УслугиСКодомНоль);
		ЗначениеВДанныеФормы(ДеревоСИскомымКодом, УслугиСИскомымКодом);
		
		УстановитьСнятьПометку(УслугиСИскомымКодом, Истина);
		Если Параметры.КодУслуги = 1 Тогда 
			УстановитьСнятьПометку(УслугиСКодомНоль, Истина);
		Иначе 
			УстановитьСнятьПометку(УслугиСКодомНоль, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УслугиСИскомымКодомПометкаПриИзменении(Элемент)

  ПометкаПриИзменении("УслугиСИскомымКодом")
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСКодомНольПометкаПриИзменении(Элемент)
	
	ПометкаПриИзменении("УслугиСКодомНоль");
	
КонецПроцедуры

&НаКлиенте
Процедура ПометкаПриИзменении(ИмяДерева)
	
	Родитель = Элементы[ИмяДерева].ТекущиеДанные.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		ПодчиненныеСтроки = Элементы[ИмяДерева].ТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка Из ПодчиненныеСтроки Цикл 
			Строка.Пометка = Элементы[ИмяДерева].ТекущиеДанные.Пометка;	
		КонецЦикла;
	ИначеЕсли Элементы[ИмяДерева].ТекущиеДанные.Пометка Тогда
		Родитель.Пометка = Истина;
	Иначе
		ЭлементыРодителя = Родитель.ПолучитьЭлементы();
		Для Каждого ПодчиненныйЭлемент Из ЭлементыРодителя Цикл
			Если ПодчиненныйЭлемент.Пометка Тогда
				Возврат;	
			КонецЕсли;
		КонецЦикла;
		Родитель.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомадФормы

&НаКлиенте
Процедура ИскомыйКодОтметитьВсе(Команда)
	
	УстановитьСнятьПометку(УслугиСИскомымКодом, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИскомыйКодСнятьВсе(Команда)
		
	УстановитьСнятьПометку(УслугиСИскомымКодом, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КодНольОтметитьВсе(Команда)
	
	УстановитьСнятьПометку(УслугиСКодомНоль, Истина);	
	
КонецПроцедуры

&НаКлиенте
Процедура КодНольСнятьВсе(Команда)
	
	УстановитьСнятьПометку(УслугиСКодомНоль, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗакрыть(Команда)
	
	МассивВозврата = Новый Массив;
	ЗаполнитьМассивВозврата("УслугиСИскомымКодом", МассивВозврата);
	ЗаполнитьМассивВозврата("УслугиСКодомНоль", МассивВозврата);
	ЭтаФорма.Закрыть(МассивВозврата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьЭлементВДерево(Дерево, Элемент)
	
	СтрокаШапки = Дерево.Строки.Найти(Элемент.Номенклатура, "Услуга");
	
	Если СтрокаШапки = Неопределено Тогда
		СтрокаШапки = Дерево.Строки.Добавить();
		СтрокаШапки.Услуга = Элемент.Номенклатура;
		СтрокаШапки.Количество = Элемент.Количество;
		СтрокаШапки.Сумма = Элемент.Сумма;
	Иначе
		СтрокаШапки.Количество = СтрокаШапки.Количество + Элемент.Количество;
		СтрокаШапки.Сумма = СтрокаШапки.Сумма + Элемент.Сумма;			
	КонецЕсли;
	
	ПодчиненнаяСтрока = СтрокаШапки.Строки.Добавить();
	ПодчиненнаяСтрока.Дата = Элемент.Период;
	ПодчиненнаяСтрока.Количество = Элемент.Количество;
	ПодчиненнаяСтрока.Сумма = Элемент.Сумма;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСнятьПометку(Дерево, Пометка)
	
	ЭлементыДерева = Дерево.ПолучитьЭлементы();
	
	Для Каждого Элемент Из ЭлементыДерева Цикл 
		
		Элемент.Пометка = Пометка;
		
		Для Каждого ПодчиненныйЭлемент Из Элемент.ПолучитьЭлементы() Цикл
			ПодчиненныйЭлемент.Пометка = Пометка;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМассивВозврата(ИмяДерева, МассивВозврата)
	
		Для Каждого Строка Из ЭтотОбъект[ИмяДерева].ПолучитьЭлементы() Цикл
		Если Строка.Пометка Тогда
			Для Каждого ПодчиненнаяСтрока Из Строка.ПолучитьЭлементы() Цикл
				Если ПодчиненнаяСтрока.Пометка Тогда
					СтруктураЭлемента = Новый Структура;
					СтруктураЭлемента.Вставить("Номенклатура", Строка.Услуга);
					СтруктураЭлемента.Вставить("Период", ПодчиненнаяСтрока.Дата);
					СтруктураЭлемента.Вставить("Количество", ПодчиненнаяСтрока.Количество);
					СтруктураЭлемента.Вставить("Сумма", ПодчиненнаяСтрока.Сумма);
					
					МассивВозврата.Добавить(СтруктураЭлемента);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
