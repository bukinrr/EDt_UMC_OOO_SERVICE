#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор();
	
	НесколькоОплат = ?(Параметры.Свойство("НесколькоОплат"), Параметры.НесколькоОплат, Ложь);	
	
	Элементы.Сумма.Видимость = Не НесколькоОплат;
	Элементы.Оплаты.Видимость = НесколькоОплат;
	
	Элементы.ФормаВычестьПоДатам.Доступность = НесколькоОплат;
	Элементы.ОплатыКонтрагентовВычестьВыбранныеПоДатам.Доступность = НесколькоОплат;
	
	Если НесколькоОплат Тогда
		Элементы.Переместить(Элементы.ФормаГруппаКнопки, Элементы.Оплаты.КоманднаяПанель);
		ЭтаФорма.КоманднаяПанель.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("Сумма") Тогда
		Сумма = Параметры.Сумма;
		СуммаИзначально = Сумма;
	КонецЕсли;
	
	Если Параметры.Свойство("Оплаты") Тогда
		Для Каждого Строка Из Параметры.Оплаты Цикл
			НоваяСтрока = Оплаты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.СуммаИзначально = НоваяСтрока.Сумма;
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("ОплатыКонтрагентов") Тогда
		Для Каждого Строка Из Параметры.ОплатыКонтрагентов Цикл
			НоваяСтрока = ОплатыКонтрагентов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.СуммаИзначально = НоваяСтрока.Сумма;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьДанные(Команда)
	
	Если НесколькоОплат Тогда
		МассивВозврата = Новый Массив;
		Для Каждого Строка Из Оплаты Цикл
			МассивВозврата.Добавить(Новый Структура("Дата, Сумма",Строка.Дата,Строка.Сумма));	
		КонецЦикла;
		ЭтотОбъект.Закрыть(МассивВозврата);
	Иначе
		ЭтотОбъект.Закрыть(Сумма);		
	КонецЕсли;		
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	ПроставитьОтметки();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	ПроставитьОтметки(Ложь);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьСуммы(Команда)

	Для Каждого Строка Из ОплатыКонтрагентов Цикл
		Строка.Сумма = Строка.СуммаИзначально;	
	КонецЦикла;
	
	Если НесколькоОплат Тогда
		Для Каждого Строка Из Оплаты Цикл
			Строка.Сумма = Строка.СуммаИзначально;	
		КонецЦикла;		
	Иначе
		Сумма = СуммаИзначально;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычестьВсе(Команда)
	
	Если НесколькоОплат Тогда
		ВычестьПоДатам(Истина);
		ВычестьНесколькоОплат(Истина);	
	Иначе
		ВычестьОднаОплата(Истина);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычестьВыбранные(Команда)
	
	Если НесколькоОплат Тогда
		ВычестьПоДатам();
		ВычестьНесколькоОплат();	
	Иначе
		ВычестьОднаОплата();		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВычестьВсеПоДатам(Команда)
	
	ВычестьПоДатам(Истина);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВычестьВыбранныеПоДатам(Команда)
	
	ВычестьПоДатам();			
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОплатыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Для Каждого СтрокаОплатыКонтрагента Из ПараметрыПеретаскивания.Значение Цикл
		СтрокаОплаты = Оплаты[Строка];
		СтрокаОплаты.Сумма = СтрокаОплаты.Сумма - СтрокаОплатыКонтрагента.Сумма;
		СтрокаОплатыКонтрагента.Сумма = ?(СтрокаОплаты.Сумма > 0, 0, -СтрокаОплаты.Сумма);
		Если СтрокаОплаты.Сумма < 0 Тогда
			СтрокаОплаты.Сумма = 0;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура ПроставитьОтметки(Флаг = Истина)
	
	Для Каждого Строка Из ОплатыКонтрагентов Цикл
		Строка.Вычесть = Флаг;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычестьОднаОплата(ВычестьВсе = Ложь)
	
	Для Каждого Строка Из ОплатыКонтрагентов Цикл
		Если ВычестьВсе Или Строка.Вычесть Тогда
			Сумма = Сумма - Строка.Сумма;
			Строка.Сумма = ?(Сумма > 0, 0, -Сумма);
			Если Сумма < 0 Тогда
				Сумма = 0;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычестьНесколькоОплат(ВычестьВсе = Ложь, ВычитаемыеЭлементы = Неопределено)
	
	ВычитаемыеЭлементы = ?(ВычитаемыеЭлементы = Неопределено, ОплатыКонтрагентов, ВычитаемыеЭлементы);
	
	Для Каждого Оплата Из Оплаты Цикл
		Для Каждого ОплатаКонтрагента Из ВычитаемыеЭлементы Цикл
			Если ВычестьВсе Или ОплатаКонтрагента.Вычесть Тогда
				Оплата.Сумма = Оплата.Сумма - ОплатаКонтрагента.Сумма;
				ОплатаКонтрагента.Сумма = ?(Оплата.Сумма > 0, 0, -Оплата.Сумма);
				Если Оплата.Сумма <= 0 Тогда
					Оплата.Сумма = 0;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычестьПоДатам(ВычестьВсе = Ложь)
	
	Для Каждого ОплатаКонтрагента Из ОплатыКонтрагентов Цикл
		Если ВычестьВсе Или ОплатаКонтрагента.Вычесть Тогда
			СтрокиОплаты = Оплаты.НайтиСтроки(Новый Структура("Дата", ОплатаКонтрагента.Дата));
			Для Каждого Оплата Из СтрокиОплаты Цикл
				ОплатаКонтрагента.Сумма = ОплатаКонтрагента.Сумма - Оплата.Сумма;
				Оплата.Сумма = ?(ОплатаКонтрагента.Сумма > 0, 0, -ОплатаКонтрагента.Сумма);
				Если ОплатаКонтрагента.Сумма <= 0 Тогда
					ОплатаКонтрагента.Сумма = 0;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
