#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДанныеРекламации = ПолучитьДанныеРекламацииДляОтраженияРабот(ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ДанныеРекламации);
	ОткрытьФорму("Документ.РаботыПоРекламации.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеРекламацииДляОтраженияРабот(Рекламация)
	
	Результат = Новый Структура("Основание, Рекламация, Клиент, Исполнитель, Работы");
	
	Результат.Основание = Рекламация;
	Результат.Рекламация = Рекламация;
	Результат.Клиент = Рекламация.Клиент;
	Результат.Исполнитель = ?(ЗначениеЗаполнено(Рекламация.ИсполнительНовый), Рекламация.ИсполнительНовый, Рекламация.Исполнитель);
		
	Работы = Новый Массив;
	Для Каждого СтрокаТЧ Из Рекламация.Работы Цикл
		ДанныеРаботы = Новый Структура;
		ДанныеРаботы.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
		ДанныеРаботы.Вставить("ХарактеристикаНоменклатуры", СтрокаТЧ.ХарактеристикаНоменклатуры);
		ДанныеРаботы.Вставить("Количество", СтрокаТЧ.Количество);
		ДанныеРаботы.Вставить("Цена", СтрокаТЧ.Цена);
		ДанныеРаботы.Вставить("Сумма", СтрокаТЧ.Сумма);
		ДанныеРаботы.Вставить("СуммаБезСкидок", СтрокаТЧ.СуммаБезСкидок);
		Работы.Добавить(ДанныеРаботы);
	КонецЦикла;
	
	Результат.Работы = Работы;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти