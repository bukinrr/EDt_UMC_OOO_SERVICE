#Область ПрограммныйИнтерфейс

// Перезаполняеть работы по предмету рекламации
//
// Параметры:
//  Объект	 - ДокументСсылка.Рекламация - заполняемый документ.
//
Процедура ПерезаполнитьРаботыПоПредметуРекламации(Объект) Экспорт
	
	Объект.Работы.Очистить();
	
	Если Не ЗначениеЗаполнено(Объект.ПредметРекламации) Тогда 
		Возврат;
	КонецЕсли;
	
	ЭтоКомплексныйРасчет = (ТипЗнч(Объект.ПредметРекламации) = Тип("ДокументСсылка.КомплексныйРасчетКлиента"));
	
	Запрос = Новый Запрос;
	
	Если ЭтоКомплексныйРасчет Тогда 
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	КомплексныйРасчетКлиентаСостав.Номенклатура,
		|	КомплексныйРасчетКлиентаСостав.ХарактеристикаНоменклатуры,
		|	КомплексныйРасчетКлиентаСостав.Количество,
		|	КомплексныйРасчетКлиентаСостав.Цена,
		|	КомплексныйРасчетКлиентаСостав.Сумма,
		|	КомплексныйРасчетКлиентаСостав.Сумма КАК СуммаБезСкидок
		|ИЗ
		|	Документ.КомплексныйРасчетКлиента.Состав КАК КомплексныйРасчетКлиентаСостав
		|ГДЕ
		|	КомплексныйРасчетКлиентаСостав.Ссылка = &Ссылка
		|	И КомплексныйРасчетКлиентаСостав.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ОказаниеУслугРаботы.Номенклатура,
		|	ОказаниеУслугРаботы.ХарактеристикаНоменклатуры,
		|	ОказаниеУслугРаботы.Количество,
		|	ОказаниеУслугРаботы.Цена,
		|	ОказаниеУслугРаботы.Сумма,
		|	ОказаниеУслугРаботы.Сумма + ОказаниеУслугРаботы.СуммаСкидки КАК СуммаБезСкидок
		|ИЗ
		|	Документ.ОказаниеУслуг.Работы КАК ОказаниеУслугРаботы
		|ГДЕ
		|	ОказаниеУслугРаботы.Ссылка = &Ссылка";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Объект.ПредметРекламации);
	
	Объект.Работы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти