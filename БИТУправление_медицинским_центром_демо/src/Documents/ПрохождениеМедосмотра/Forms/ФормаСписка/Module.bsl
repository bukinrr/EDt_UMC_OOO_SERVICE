
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ВидМедосмотра Из Метаданные.Перечисления.ВидыМедосмотров.ЗначенияПеречисления Цикл
		
		Команда = ЭтаФорма.Команды.Добавить("Создать" + ВидМедосмотра.Имя);
		Команда.Заголовок = ВидМедосмотра.Синоним;
		Команда.Действие = "Создать";
		
		Кнопка = Элементы.Добавить("Кнопка"+Команда.Имя, Тип("КнопкаФормы"), Элементы.ФормаГруппаСоздать);
		Кнопка.ИмяКоманды = Команда.Имя;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	ИмяВидаМедосмотра = СтрЗаменить(Команда.Имя, "Создать", "");
	ВидМедосмотра = ПредопределенноеЗначение("Перечисление.ВидыМедосмотров."+ИмяВидаМедосмотра);
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("ВидПрохожденияМО", ВидМедосмотра));
	
	ОткрытьФорму("Документ.ПрохождениеМедосмотра.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти