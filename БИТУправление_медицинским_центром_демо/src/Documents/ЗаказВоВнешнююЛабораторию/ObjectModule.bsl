#Область СлужебныеПроцедурыИФункции

Процедура ДвиженияПоРегиструЛабораторныеИсследования()
	
	НаборЗаписей = Движения.ЛабораторныеИсследования;
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Если Не ПометкаУдаления
		Или Состояние = Перечисления.СостоянияЗаказовЛаборатории.Отправлен
		Или Состояние = Перечисления.СостоянияЗаказовЛаборатории.ПолученРезультат
		Или Состояние = Перечисления.СостоянияЗаказовЛаборатории.Обработан
	Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ИсследованияСрезПервых.ИД,
		               |	ИсследованияСрезПервых.КлиникаИсходная
		               |ИЗ
		               |	РегистрСведений.ЛабораторныеИсследования.СрезПервых КАК ИсследованияСрезПервых
		               |ГДЕ
		               |	ИсследованияСрезПервых.ИД В(&ИД)";
		
		Запрос.УстановитьПараметр("ИД", Исследования.ВыгрузитьКолонку("ИД"));
		ВыборкаИсходныеКлиники = Запрос.Выполнить().Выбрать();
		
		ИДОбработанных = Новый Массив;
		Для Каждого СтрокаИсследования Из Исследования Цикл
			Если ИДОбработанных.Найти(СтрокаИсследования.ИД) = Неопределено Тогда
				
				Движение = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Ссылка);
				ЗаполнитьЗначенияСвойств(Движение, СтрокаИсследования);
				Движение.Период = Дата;
				Движение.ДатаНачала = Дата;
								
				Движение.Статус = Перечисления.СостоянияИсследований.Забран;
				Движение.Лаборатория = Лаборатория;
				Движение.Местонахождение = Лаборатория;
				Движение.Клиент = Клиент;
				
				ВыборкаИсходныеКлиники.Сбросить();
				Если ВыборкаИсходныеКлиники.НайтиСледующий(Новый Структура("ИД", СтрокаИсследования.ИД)) Тогда
					Движение.КлиникаИсходная = ВыборкаИсходныеКлиники.КлиникаИсходная;
				КонецЕсли;
				
				ИДОбработанных.Добавить(СтрокаИсследования.ИД);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	ДвиженияПоРегиструЛабораторныеИсследования();
	
КонецПроцедуры

#КонецОбласти