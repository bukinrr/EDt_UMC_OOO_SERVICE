#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма, Ложь);
	НастроитьТабличныеЧасти();
	Элементы.НомерЗаказаВнешнейЛаборатории.Видимость = ЗначениеЗаполнено(Объект.НомерЗаказаВнешнейЛаборатории);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Объект.Лаборатория)
		И ЛабораторияСервер.ИспользуетсяАнонимнаяОтправкаАнализов(Объект.Лаборатория) 
	Тогда
		Элементы.Анонимно.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	НастроитьТабличныеЧасти();	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьЗаборАнализа(Команда)
	
	ТекущиеДанные = Элементы.Исследования.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ДокументЗабора = ЛабораторияСервер.НайтиЗаборАнализа(ТекущиеДанные.ИД, Объект.Клиент, Объект.Лаборатория, ТекущиеДанные.КодПробы, ТекущиеДанные.Номенклатура);
		
		Если ЗначениеЗаполнено(ДокументЗабора) Тогда
			ОткрытьЗначение(ДокументЗабора);
		КонецЕсли;
	Иначе
		Предупреждение(НСтр("ru='Не выбрана строка пробы'"), 30);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиОпись(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Предупреждение(НСтр("ru='Документ еще не записан'"), 30);
	Иначе
		Опись = НайтиОписьЗаказа(Объект.Ссылка);
		Если ЗначениеЗаполнено(Опись) Тогда
			ОткрытьФорму("Документ.ОписьЗаказовЛаборатории.ФормаОбъекта", Новый Структура("Ключ, Заказ", Опись, Объект.Ссылка));
		Иначе
			ПоказатьПредупреждение(,НСтр("ru='Опись не найдена'"), 30);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКлиентуПоЭлектроннойПочте(Команда)
	
	УправлениеЭлектроннойПочтойКлиент.ОтправитьДокументПоЭлектроннойПочте(ЭтотОбъект);
	
КонецПроцедуры

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИсследованияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если Не ЗначениеЗаполнено(Элементы.Исследования.ТекущиеДанные.Состояние) Тогда 
		Элементы.Исследования.ТекущиеДанные.Состояние =
			ПредопределенноеЗначение("Перечисление.СостоянияЗаказовЛаборатории.Создан");
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ЛабораторияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Лаборатория)
		И ЛабораторияСервер.ИспользуетсяАнонимнаяОтправкаАнализов(Объект.Лаборатория) 
	Тогда
		Элементы.Анонимно.Видимость = Истина;
	Иначе
		Элементы.Анонимно.Видимость = Ложь;
		Объект.Анонимно = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьТабличныеЧасти()
	
	Если Объект.Ссылка.Пустая() Тогда
		Элементы.Приемы.Видимость = Ложь;
	Иначе
		Элементы.Приемы.Видимость = Истина;
		РаботаСФормамиСервер.УстановитьОтборСписка("Основание", Объект.Ссылка, Приемы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиОписьЗаказа(Заказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОписьЗаказовЛабораторииСписокЗаказов.Ссылка
	|ИЗ
	|	Документ.ОписьЗаказовЛаборатории.СписокЗаказов КАК ОписьЗаказовЛабораторииСписокЗаказов
	|ГДЕ
	|	ОписьЗаказовЛабораторииСписокЗаказов.ЗаказВЛабораторию = &Заказ
	|	И НЕ ОписьЗаказовЛабораторииСписокЗаказов.Ссылка.ПометкаУдаления"
	;
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат Рез.Выгрузить()[0].Ссылка;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
