/////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ 

&НаКлиенте
Перем мТекущаяДатаДокумента; // Хранит текущую дату документа - для проверки перехода документа в другой период установки номера.

&НаКлиенте
Перем РаспределитьНоменклатуруПоРазнымСНО Экспорт; // Флаг, отвечающий за перезапись документа с отбором номенклатур только под одной СНО

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаСервере
Процедура ДействияФормыДействиеУстановитьОперациюСервер()
	
	ОсновнаяПанель = Элементы.ГруппаОперации;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыСКонтрагентами Тогда
		ОсновнаяПанель.ТекущаяСтраница = ОсновнаяПанель.ПодчиненныеЭлементы.ГруппаРасчетыСКонтрагентами;
		ЭУКлиент = Элементы.Контрагент;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРКО.ВыдачаДенежныхСредствСотруднику Тогда
		ОсновнаяПанель.ТекущаяСтраница = ОсновнаяПанель.ПодчиненныеЭлементы.ГруппаРасчетыССотрудниками;
		ЭУКлиент = Элементы.Сотрудник;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыСКлиентами Тогда
		ОсновнаяПанель.ТекущаяСтраница = ОсновнаяПанель.ПодчиненныеЭлементы.ГруппаОплатаКлиенту;
		ЭУКлиент = Элементы.Клиент;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРКО.ПеремещениеВКассу Тогда
		ОсновнаяПанель.ТекущаяСтраница = ОсновнаяПанель.ПодчиненныеЭлементы.ГруппаМеждуКассами;
		ЭУКлиент = Элементы.КассаПолучатель;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРКО.ПрочийРасходДенежныхСредств Тогда
		ОсновнаяПанель.ТекущаяСтраница = ОсновнаяПанель.ПодчиненныеЭлементы.ГруппаПрочийРасход;
		ЭУКлиент = Неопределено;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРКО.ВзносНаличнымиВБанк Тогда
		ОсновнаяПанель.ТекущаяСтраница = ОсновнаяПанель.ПодчиненныеЭлементы.ГруппаВзносНаличнымиВБанк;
		ЭУКлиент = Неопределено;
	КонецЕсли;
	
	Если ЭУКлиент<>Неопределено Тогда
		мКлиент = ЭУКлиент.ОграничениеТипа.ПривестиЗначение(Объект.Контрагент);
		Если Объект.Контрагент <> мКлиент Тогда
			Объект.Контрагент = мКлиент;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ПерезаполнитьПоДокументу.Видимость = Объект.ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыСКлиентами;
	
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(Объект, Истина, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	РаботаСФормамиКлиент.ДокументПриИзмененииДаты(ЭтаФорма, мТекущаяДатаДокумента);
	
КонецПроцедуры

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДополнительныеСвойства = РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		Объект.ВидОперации = Перечисления.ВидыОперацийРКО.ВыдачаДенежныхСредствСотруднику;
	КонецЕсли;
	
	УстановитьОграниченияТиповЭУ();
	ДействияФормыДействиеУстановитьОперациюСервер();
	
	НастройкаПечатиЧеков = МенеджерОборудованияВызовСервераПереопределяемый.ПолучитьНастройкуПечатиЧековФилиала(Объект.Филиал, Истина);
	Если Объект.Ссылка.Пустая() Тогда
		ЗаполнитьДепозитнуюОперациюККМ();
	КонецЕсли;
	
	ДоступныПолныеПрава = Пользователи.ЭтоПолноправныйПользователь();
	
	ПоместитьРеквизитыККТИзХЗвРеквизитыФормы();
	
	Элементы.КредитныеДанныеДобавитьСертификат.Видимость = Объект.ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыСКлиентами;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не РаботаСДокументамиКлиент.ФормаДокументаПриОткрытии(ЭтотОбъект, ДополнительныеСвойства) Тогда
		ЭтотОбъект.Закрыть();
		Возврат;
	КонецЕсли;
	
	УстановитьПараметрыВыбораСделки();
	мТекущаяДатаДокумента = Объект.Дата;
	
	Элементы.НадписьВалюта.Заголовок = глКраткоеНаименованиеОсновнойВалюты;
	
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
	РаботаСДокументамиКлиент.ОбновитьВидимостьДоступностьСпособаРасчетаККМ(ЭтаФорма, Объект.ВидОперации, Объект.СпособРасчетаЧекаККМ, НастройкаПечатиЧеков);
	РаботаСДокументамиКлиент.ОбновитьВидимостьКредитныхДанныхККМ(ЭтаФорма, Объект.ВидОперации, Объект.КредитныеДанные, Объект.СпособРасчетаЧекаККМ);
	РаботаСДокументамиКлиент.СогласоватьЗначенияРеквизитовПоСпособуРасчета(ЭтаФорма, Объект.ВидОперации, Объект.КредитныеДанные, Объект.СпособРасчетаЧекаККМ);
	
	Если Объект.Ссылка.Пустая() Тогда
		РаботаСДокументамиКлиент.ПредложитьПерезаполнитьКредитныеДанныеПоСделкеДокумента(Объект, ЭтаФорма);
	КонецЕсли;
	
	РаботаСДокументамиКлиент.РассчитатьЗаполнитьПризнакиЕНВДКредитнойЧасти(Объект, НастройкаПечатиЧеков);
	РаботаСДокументамиКлиент.ПлатежныйДокументПриОткрытииПроверкаНоменклатурыДоплатыНаНесколькоСНО(ЭтаФорма);
	
	КоррекцияПриИзменении("");
	
	Если Истина // ИспользоватьПодключаемоеОборудование Проверка на включенную ФО "Использовать ВО"
	   И МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО.
		ОписаниеОшибки = "";
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("ДисплейПокупателя");
		
		ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);    
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, ПоддерживаемыеТипыВО);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	РаботаСДокументамиКлиент.ПлатежныйДокументПередЗаписьюНаФорме(ЭтотОбъект, Отказ, Объект, ПараметрыЗаписи, НастройкаПечатиЧеков)

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ЗаписатьРеквизитыККТИзРеквизитовФормыВХЗ(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(Объект.Ссылка, Истина, ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	РаботаСДокументамиКлиент.РассчитатьЗаполнитьПризнакиЕНВДКредитнойЧасти(Объект, НастройкаПечатиЧеков);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.ВыдачаДенежныхСредствСотруднику") Тогда
		ПараметрыОповещения = Новый Структура("Получатель", Объект.Контрагент);
		Оповестить("РКОЗаписан", ПараметрыОповещения, Объект.Ссылка);
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПКО.ОплатаОтКлиента")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "Сделка")
		И ЗначениеЗаполнено(Объект["Сделка"])
	Тогда
		Оповестить("ИзменениеДанныхОплатыКомплексногоРасчета", Новый Структура("Сделка", Объект["Сделка"]), Объект.Ссылка);
	КонецЕсли;
	
	РаботаСДокументамиКлиент.РассчитатьЗаполнитьПризнакиЕНВДКредитнойЧасти(Объект, НастройкаПечатиЧеков);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДокументОплатыЗаполнитьРасчетыПоДолгуПоКомплексномуРасчету"
		И Параметр = ЭтаФорма
	Тогда
		ДокументОплатыЗаполнитьРасчетыПоДолгуПоКомплексномуРасчетуСервер(РаботаСДокументамиКлиент.СделкаДокумента(Объект));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ОборудованиеДисплеиПокупателяКлиент.НачатьОчисткуДисплеяПокупателя(,УникальныйИдентификатор);
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ ПРОЦЕДУРЫ И ФУНКЦИИ.

&НаКлиенте
Процедура КредитныеДанныеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И Не Копирование Тогда
		Элемент.ТекущиеДанные.Количество = 1;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьОграниченияТиповЭУ()
	
	Элементы.Контрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	Элементы.Сотрудник.ОграничениеТипа  = Новый ОписаниеТипов("СправочникСсылка.Сотрудники");
	Элементы.Клиент.ОграничениеТипа     = Новый ОписаниеТипов("СправочникСсылка.Клиенты");
	Элементы.СтатьяЗатрат.ОграничениеТипа		= Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат");
	Элементы.КассаПолучатель.ОграничениеТипа	= Новый ОписаниеТипов("СправочникСсылка.Кассы");
	
КонецПроцедуры

&НаКлиенте
Процедура КассаПриИзменении(Элемент)
	
	СообщениеПользователю = КассаПриИзмененииСервер();
	Если ЗначениеЗаполнено(СообщениеПользователю) Тогда
		ПоказатьПредупреждение(,СообщениеПользователю, 30);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КассаПриИзмененииСервер()
	
	Если Не ПустаяСтрока(Объект.Номер) Тогда
		Об = ДанныеФормыВЗначение(Объект, Тип("ДокументОбъект.РасходныйКассовыйОрдер"));
		ОбщегоНазначенияСервер.СброситьУстановленныйНомерОбъекта(Об, Объект.Номер);
		ЗначениеВДанныеФормы(Об, Объект);
	КонецЕсли;
	
	Возврат ОграничениеДоступаНаУровнеЗаписей.ТребуетсяПредупреждениеНесоответствияКассыФилиала(Объект.Касса, Объект.Филиал)
	
КонецФункции

&НаКлиенте
Процедура КлиентПриИзменении(Элемент)
	
	РаботаСДокументамиСервер.ЗаполнитьУчастникаИИННДенежнойОперации(Объект.Контрагент, Объект.Выдать, Объект.ВыдатьИНН, Объект.ВыдатьАдрес);
	УстановитьПараметрыВыбораСделки();
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)

	РаботаСФормамиКлиент.ПолеВводаКлиентаАвтоПодбор(Текст, СтандартнаяОбработка, ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)

	РаботаСФормамиКлиент.ПолеВводаКлиентаАвтоПодбор(Текст, СтандартнаяОбработка, ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	РаботаСДокументамиСервер.ЗаполнитьУчастникаИИННДенежнойОперации(Объект.Контрагент, Объект.Выдать, Объект.ВыдатьИНН, Объект.ВыдатьАдрес);

КонецПроцедуры

&НаКлиенте
Процедура СделкаПриИзменении(Элемент)
	Если Не Объект.ДокументОснование.Пустая() Тогда
		Объект.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование,"Контрагент");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Чек(Команда)
	
	РаботаСДокументамиКлиент.ЧекДенежногоДокумента(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаSMS(Команда)
	ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон");
	Объект.ТелефонЧек = КонтактнаяИнформацияКлиент.ВыбратьСоздатьКИКлиента(Объект.Контрагент, ТипКИ, Объект.Филиал);
КонецПроцедуры

&НаКлиенте
Процедура КомандаEmail(Команда)
	ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты");
	Объект.АдресEmailЧек = КонтактнаяИнформацияКлиент.ВыбратьСоздатьКИКлиента(Объект.Контрагент, ТипКИ, Объект.Филиал);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыВыбораСделки()
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.РасчетыСКлиентами") Тогда
		
		ЭлементСделка = Элементы.Найти("СделкаКлиент");
		Если ЭлементСделка <> Неопределено Тогда
			Параметр = Новый ПараметрВыбора("Отбор.Клиент", Объект.Контрагент);
			ПараметрыВыбораЗначения = Новый Массив;
			ПараметрыВыбораЗначения.Добавить(Параметр);
			
			ЭлементСделка.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЗначения);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ДействияФормыДействиеУстановитьОперациюСервер();
	УстановитьПараметрыВыбораСделки();
	
	РаботаСДокументамиКлиент.ОбновитьВидимостьДоступностьСпособаРасчетаККМ(ЭтаФорма, Объект.ВидОперации, Объект.СпособРасчетаЧекаККМ, НастройкаПечатиЧеков);
	РаботаСДокументамиКлиент.ОбновитьВидимостьКредитныхДанныхККМ(ЭтаФорма, Объект.ВидОперации, Объект.КредитныеДанные, Объект.СпособРасчетаЧекаККМ);
	РаботаСДокументамиКлиент.СогласоватьЗначенияРеквизитовПоСпособуРасчета(ЭтаФорма, Объект.ВидОперации, Объект.КредитныеДанные, Объект.СпособРасчетаЧекаККМ);
	
	Элементы.КредитныеДанныеДобавитьСертификат.Видимость = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.РасчетыСКлиентами");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоКомплексномуРасчету(Команда)
	ДокументОплатыЗаполнитьРасчетыПоДолгуПоКомплексномуРасчетуСервер(РаботаСДокументамиКлиент.СделкаДокумента(Объект));
	РаботаСДокументамиКлиент.РассчитатьЗаполнитьПризнакиЕНВДКредитнойЧасти(Объект, НастройкаПечатиЧеков);
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьОплатуКредитныхДанныхККМ(Команда)
	РаботаСДокументамиКлиентСервер.РаспределитьОплатуКредитныхДанныхККМ(Объект.КредитныеДанные, Объект.СуммаДокумента);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификат(Команда)
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НаУслуги", Истина);
		ПараметрыФормы.Вставить("НаОплату", Ложь);
		ПараметрыФормы.Вставить("СертификатыТолькоВладельца", Истина);
		ПараметрыФормы.Вставить("ВладелецСертификата", Объект.Контрагент);
		ПараметрыФормы.Вставить("ПроданныеСертификаты", Истина);
		ПараметрыФормы.Вставить("Дата", Объект.Дата);
		
		Оповещение = Новый ОписаниеОповещения("ДобавитьСертификат_Завершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.Сертификаты.Форма.ФормаСпискаРасширеннаяУпр",ПараметрыФормы,ЭтотОбъект,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ПоказатьПредупреждение(,НСтр("ru='Нельзя выбрать сертификат, т.к. не указан клиент!'"), 10);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификат_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.Сертификаты") Тогда
		
		СтруктураДанныхСертификата = ПолучитьСтруктуДанныхСтрокиСертификата(Результат);
		ЗаполнитьЗначенияСвойств(Объект.КредитныеДанные.Добавить(), СтруктураДанныхСертификата);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРасчетаЧекаККМПриИзменении(Элемент)
	Если Объект.СпособРасчетаЧекаККМ = ПредопределенноеЗначение("Перечисление.СпособыРасчетаЧекаККМ.Аванс") Тогда
		Объект.КредитныеДанные.Очистить();
	КонецЕсли;
	
	РаботаСДокументамиКлиент.ОбновитьВидимостьКредитныхДанныхККМ(ЭтаФорма, Объект.ВидОперации, Объект.КредитныеДанные, Объект.СпособРасчетаЧекаККМ);
	РаботаСДокументамиКлиент.СогласоватьЗначенияРеквизитовПоСпособуРасчета(ЭтаФорма, Объект.ВидОперации, Объект.КредитныеДанные, Объект.СпособРасчетаЧекаККМ);
КонецПроцедуры

&НаКлиенте
Процедура КредитныеДанныеНоменклатураПриИзменении(Элемент)
	
	ТекущиеКредитныеДанные = Элементы.КредитныеДанные.ТекущиеДанные;
	Если ТекущиеКредитныеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеКредитныеДанные.Номенклатура) Тогда
		// Заполним Наименование
		ТекущиеКредитныеДанные.Наименование = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ТекущиеКредитныеДанные.Номенклатура, "НаименованиеПолное");
		СтавкаНДСНоменклатуры = УчетНДСВызовСервера.СтавкаНДС(ТекущиеКредитныеДанные.Номенклатура, ОбщегоНазначения.ТекущаяДатаПользователя());
		
		// Заполним Ставку НДС
		Если ЗначениеЗаполнено(СтавкаНДСНоменклатуры) Тогда
			ТекущиеКредитныеДанные.СтавкаНДС = СтавкаНДСНоменклатуры;
		Иначе
			Если ЗначениеЗаполнено(НастройкаПечатиЧеков) И ЗначениеЗаполнено(НастройкаПечатиЧеков.СтавкаНДСДляОсновнойСистемыНалогообложения) Тогда
				СтавкаНДСПоУмолчанию = НастройкаПечатиЧеков.СтавкаНДСДляОсновнойСистемыНалогообложения;
			Иначе
				СтавкаНДСПоУмолчанию = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0");
			КонецЕсли;
			
			ТекущиеКредитныеДанные.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	РаботаСДокументамиКлиент.РассчитатьЗаполнитьПризнакиЕНВДКредитнойЧасти(Объект, НастройкаПечатиЧеков);
	
КонецПроцедуры

&НаКлиенте
Процедура КредитныеДанныеСуммаПриИзменении(Элемент)
	ТД = Элементы.КредитныеДанные.ТекущиеДанные;
	Если ТД <> Неопределено Тогда
		РаботаСДокументамиКлиент.ЗаполнитьСуммуПриПокупкеСтрокиТабЧасти(ТД);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СделкаКлиентПриИзменении(Элемент)
	Объект.КредитныеДанные.Очистить();
	РаботаСДокументамиКлиент.ПредложитьПерезаполнитьКредитныеДанныеПоСделкеДокумента(Объект, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОказаниюУслуг(Команда)
	ЗаполнитьПоОказаниюУслугСервер();	
	РаботаСДокументамиКлиент.РассчитатьЗаполнитьПризнакиЕНВДКредитнойЧасти(Объект, НастройкаПечатиЧеков);
КонецПроцедуры

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("Подключаемый_КнопкаФилиалПриНажатииЗавершение", ЭтотОбъект);
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма, , ОповещениеОВыборе);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатииЗавершение(Результат, ДополнительныеЗначения) Экспорт
	
	НастройкаПечатиЧеков = МенеджерОборудованияВызовСервераПереопределяемый.ПолучитьНастройкуПечатиЧековФилиала(Объект.Филиал, Истина);
	ЗаполнитьДепозитнуюОперациюККМ();
	РаботаСДокументамиКлиент.ОбновитьВидимостьДоступностьСпособаРасчетаККМ(ЭтаФорма, Объект.ВидОперации, Объект.СпособРасчетаЧекаККМ, НастройкаПечатиЧеков);
	РаботаСДокументамиКлиент.ОбновитьВидимостьКредитныхДанныхККМ(ЭтаФорма, Объект.ВидОперации, Объект.КредитныеДанные, Объект.СпособРасчетаЧекаККМ);
	РаботаСДокументамиКлиент.СогласоватьЗначенияРеквизитовПоСпособуРасчета(ЭтаФорма, Объект.ВидОперации, Объект.КредитныеДанные, Объект.СпособРасчетаЧекаККМ);
	
	СообщениеПользователю = ОграничениеДоступаНаУровнеЗаписей.ТребуетсяПредупреждениеНесоответствияКассыФилиала(Объект.Касса, Объект.Филиал);
	Если ЗначениеЗаполнено(СообщениеПользователю) Тогда
		ПоказатьПредупреждение(,СообщениеПользователю, 30);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ДокументОплатыЗаполнитьРасчетыПоДолгуПоКомплексномуРасчетуСервер(Сделка)
	
	Если ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.Клиенты") Тогда
		Объект.СуммаДокумента = Макс(0, РаботаСКлиентамиПереопределяемый.ПолучитьВзаиморасчетыСКлиентом(Объект.Контрагент, ТекущаяДата(), Сделка));
	Иначе
		// Не будем изменять сумму документа	
	КонецЕсли;
	РаботаСДокументамиСервер.ДокументОплатыЗаполнитьРасчетыПоДолгуПоКомплексномуРасчетуСервер(Объект.Сделка, НастройкаПечатиЧеков, Объект.КредитныеДанные, Объект.СуммаДокумента);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДепозитнуюОперациюККМ()
	РаботаСДокументамиСервер.ЗаполнитьДепозитнуюОперациюККМ(Объект, НастройкаПечатиЧеков);
КонецФункции

&НаКлиенте
Процедура КредитныеДанныеПриИзменении(Элемент)
	ТекущиеКредитныеДанные = Элементы.КредитныеДанные.ТекущиеДанные;
	Если ТекущиеКредитныеДанные <> Неопределено И НЕ ЗначениеЗаполнено(ТекущиеКредитныеДанные.Номенклатура) Тогда
		// Заполним Ставку НДС
		Если ЗначениеЗаполнено(НастройкаПечатиЧеков) И ЗначениеЗаполнено(НастройкаПечатиЧеков.СтавкаНДСДляОсновнойСистемыНалогообложения) Тогда
			СтавкаНДСПоУмолчанию = НастройкаПечатиЧеков.СтавкаНДСДляОсновнойСистемыНалогообложения;
		Иначе
			СтавкаНДСПоУмолчанию = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0");
		КонецЕсли;
		ТекущиеКредитныеДанные.СтавкаНДС = СтавкаНДСПоУмолчанию;		
	КонецЕсли;
	РаботаСДокументамиКлиент.РассчитатьЗаполнитьПризнакиЕНВДКредитнойЧасти(Объект, НастройкаПечатиЧеков);
КонецПроцедуры

&НаКлиенте
Процедура КредитныеДанныеПриАктивизацииЯчейки(Элемент)
	Если Элемент.ТекущийЭлемент.Имя	= "КредитныеДанныеНДС" И НЕ ДоступныПолныеПрава Тогда
		Элемент.ТекущийЭлемент.Доступность = Ложь;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОказаниюУслугСервер()
	РаботаСДокументамиСервер.ДокументОплатыЗаполнитьРасчетыПоДолгуПоОказаниюУслуг(Объект.ДокументОснование, НастройкаПечатиЧеков, Объект.КредитныеДанные, Объект.СуммаДокумента, Объект.СпособРасчетаЧекаККМ);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПолучателя(Команда)
	РаботаСДокументамиСервер.ЗаполнитьУчастникаИИННДенежнойОперации(Объект.Контрагент, Объект.Выдать, Объект.ВыдатьИНН, Объект.ВыдатьАдрес, Истина);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КоррекцияПриИзменении(Элемент)
	РаботаСДокументамиКлиент.ОбновитьВидимостьЭлементовКоррекции(ЭтаФорма, Объект.Коррекция);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРедактированиеРеквизитаККТ(НазваниеРеквизитаККТ)
	
	РаботаСДокументамиКлиент.ВыполнитьРедактированиеРеквизитаККТ(ЭтотОбъект, НазваниеРеквизитаККТ, Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПерсональныеДанныеПокупателяНажатие(Элемент)
	
	ВыполнитьРедактированиеРеквизитаККТ("ПерсональныеДанныеПокупателя");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьОперационныйРеквизитНажатие(Элемент)
	
	ВыполнитьРедактированиеРеквизитаККТ("ОперационныйРеквизит");
	
КонецПроцедуры

&наСервере
Процедура ПоместитьРеквизитыККТИзХЗвРеквизитыФормы()
	
	ОУОбъект = РеквизитФормыВЗначение("Объект");
	ПерсональныеДанныеПокупателя	= ОУОбъект.ПерсональныеДанныеПокупателя.Получить();
	ОперационныйРеквизит			= ОУОбъект.ОперационныйРеквизит.Получить();
	
КонецПроцедуры

&наСервере
Процедура ЗаписатьРеквизитыККТИзРеквизитовФормыВХЗ(ОУОбъект)
	
	ОУОбъект.ПерсональныеДанныеПокупателя	= Новый ХранилищеЗначения(ПерсональныеДанныеПокупателя);
	ОУОбъект.ОперационныйРеквизит			= Новый ХранилищеЗначения(ОперационныйРеквизит);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыРедактированияРеквизитаККТ(Результат, ПараметрыРеквизитаККТ) Экспорт
	
		Если Результат <> Неопределено Тогда
		Если Результат.Свойство("ДетальнаяИнформация") Тогда
			РаботаСДокументамиКлиент.СохранитьЗначениеРеквизитаККТВОбъекте(ЭтаФорма, Результат.ДетальнаяИнформация, ПараметрыРеквизитаККТ.НазваниеРеквизитаККТ);
			Если ПараметрыРеквизитаККТ.НазваниеРеквизитаККТ = "ПерсональныеДанныеПокупателя" Тогда
				Объект.Выдать = Результат.Клиент;
				Объект.ВыдатьИНН = Результат.ДетальнаяИнформация.ИНН;
				Объект.ВыдатьАдрес = Результат.Адрес;
			КонецЕсли;
		ИначеЕсли Результат.Свойство("Удалить") Тогда
			РаботаСДокументамиКлиент.ОчиститьЗначениеРеквизитаККТВОбъекте(ЭтаФорма, ПараметрыРеквизитаККТ.НазваниеРеквизитаККТ);	
		КонецЕсли;
	КонецЕсли;

	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:
				|""%ОписаниеОшибки%"".'" );
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуДанныхСтрокиСертификата(Сертификат)
		
	Возврат Новый Структура("Номенклатура, Наименование", Сертификат.ВидСертификата.Номенклатура, Сертификат.ВидСертификата.Наименование);
	
КонецФункции

&НаКлиенте
Процедура ПерезаполнитьПоДокументу(Команда)
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.РасчетыСКлиентами") Тогда
		Объект.ПоДокументу = ПолучитьТекстПоДокументу(Объект.Контрагент, Объект.Дата);	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТекстПоДокументу(Клиент, Дата)
	
	Результат = "";
	
	ПаспортныеДанные = РегистрыСведений.ПаспортныеДанные.ПолучитьПоследнее(Дата, Новый Структура("ФизЛицо", Клиент));
	
	Если ЗначениеЗаполнено(ПаспортныеДанные.ДокументСерия) Или ЗначениеЗаполнено(ПаспортныеДанные.ДокументНомер) Тогда
		
		ОбщегоНазначенияБИТКлиентСервер.КонкатенацияСтрок(Результат, Строка(ПаспортныеДанные.ДокументВид),, Истина);
		
		Если ЗначениеЗаполнено(ПаспортныеДанные.ДокументСерия) Тогда
			ОбщегоНазначенияБИТКлиентСервер.КонкатенацияСтрок(Результат, "серия: " + ПаспортныеДанные.ДокументСерия, ", ");
		КонецЕсли;
		
		// Перевод первой буквы в верхний регистр.
		Если ЗначениеЗаполнено(Результат) Тогда
			Результат = ТРег(Сред(Результат, 1, 1)) + Прав(Результат, СтрДлина(Результат) - 1);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПаспортныеДанные.ДокументНомер) Тогда
			ОбщегоНазначенияБИТКлиентСервер.КонкатенацияСтрок(Результат, "№: " + ПаспортныеДанные.ДокументНомер, ", ");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПаспортныеДанные.ДокументДатаВыдачи) Тогда
			ОбщегоНазначенияБИТКлиентСервер.КонкатенацияСтрок(Результат, "выдан: " + Формат(ПаспортныеДанные.ДокументДатаВыдачи, "ДЛФ=DD"), ", ");
		КонецЕсли;
		
		ОбщегоНазначенияБИТКлиентСервер.КонкатенацияСтрок(Результат, ПаспортныеДанные.ДокументКемВыдан, " ", Истина);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
