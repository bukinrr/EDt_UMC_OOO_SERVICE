#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ПоВсемФилиалам = Истина;
	КонецЕсли;
	
	Элементы.ГруппаФилиалы.Видимость = УправлениеНастройками.ТребуетсяУчетПоФилиалам();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПоВсемФилиаламПриИзменении(Неопределено);
	ВидСкидкиПриИзменении(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоВсемФилиаламПриИзменении(Элемент)
	
	Если Объект.ПоВсемФилиалам Тогда
		Элементы.Филиалы.Доступность = Ложь;
		Элементы.Филиалы.КоманднаяПанель.Доступность = Ложь;
	Иначе
		Элементы.Филиалы.Доступность = Истина;
		Элементы.Филиалы.КоманднаяПанель.Доступность = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ФилиалыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	СвернутьФилиалы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСкидкиПриИзменении(Элемент)
	ОбработкаИзмененияВидаПолучателя(ДопСерверныеФункции.ПолучитьРеквизит(Объект.ВидСкидки, "ВидПолучателя"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СвернутьФилиалы()
	ТаблицаФилиалов = Объект.Филиалы.Выгрузить();
	ТаблицаФилиалов.Свернуть("Филиал");
	Объект.Филиалы.Загрузить(ТаблицаФилиалов);
КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаИзмененияВидаПолучателя(ВидПолучателя)
	
	Если ВидПолучателя = ПредопределенноеЗначение("Перечисление.ВидПолучателя.Клиенты") Тогда
		Массив = Новый Массив;
		Массив.Добавить(Тип("СправочникСсылка.Клиенты"));
		Элементы.Получатель.Видимость = Истина;
		Элементы.Получатель.ОграничениеТипа = Новый ОписаниеТипов(Массив);
		Элементы.Получатель.Заголовок = НСтр("ru='Клиент(ы)'");
		
	ИначеЕсли ВидПолучателя = ПредопределенноеЗначение("Перечисление.ВидПолучателя.КартаСкидок") Тогда	
		Массив = Новый Массив;
		Массив.Добавить(Тип("СправочникСсылка.КартыСкидок"));
		Элементы.Получатель.Видимость = Истина;
		Элементы.Получатель.ОграничениеТипа = Новый ОписаниеТипов(Массив);
		Элементы.Получатель.Заголовок = НСтр("ru='Карта скидок'");
		
	ИначеЕсли ВидПолучателя = ПредопределенноеЗначение("Перечисление.ВидПолучателя.ВидКартыСкидок") Тогда	
		Массив = Новый Массив;
		Массив.Добавить(Тип("СправочникСсылка.ВидыКартСкидок"));
		Элементы.Получатель.Видимость = Истина;
		Элементы.Получатель.ОграничениеТипа = Новый ОписаниеТипов(Массив);
		Элементы.Получатель.Заголовок = НСтр("ru='Вид карты скидок'");
		
	Иначе
		Объект.Получатель = ПредопределенноеЗначение("Справочник.Клиенты.ПустаяСсылка");
		Элементы.Получатель.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти