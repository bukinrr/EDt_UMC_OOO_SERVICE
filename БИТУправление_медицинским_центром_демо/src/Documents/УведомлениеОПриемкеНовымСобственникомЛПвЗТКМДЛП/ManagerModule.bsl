
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ИнтеграцияМДЛПВызовСервера.ПриПолученииФормыДокумента(
		ПустаяСсылка(), ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДействияПриОбмене

Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция) Экспорт
	
	Возврат ИнтеграцияМДЛП.ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция);
	
КонецФункции

Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки) Экспорт
	
	Возврат ИнтеграцияМДЛП.ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки);
	
КонецФункции

Функция ОбновитьСтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Операция = Перечисления.ОперацииОбменаМДЛП.Получение_ИнформацияОКиЗ
	 Или Операция = Перечисления.ОперацииОбменаМДЛП.Получение_КвитанцияОФиксации И ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИнформацииОКиЗ
	 Или Операция = Перечисления.ОперацииОбменаМДЛП.Получение_СоставУпаковки
	 Или Операция = Перечисления.ОперацииОбменаМДЛП.Получение_КвитанцияОФиксации И ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросСоставаУпаковки
	 Или Операция = Перечисления.ОперацииОбменаМДЛП.Получение_ИерархияВложенностиУпаковок
	 Или Операция = Перечисления.ОперацииОбменаМДЛП.Получение_КвитанцияОФиксации И ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИерархииВложенностиУпаковок Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДальнейшиеДействия = ПолучитьДальнейшиеДействияПоДокументу(ДокументСсылка);
	
	Статусы = РегистрыСведений.СтатусыИнформированияМДЛП.СтатусыОбработки();
	
	Если ДальнейшиеДействия.Количество() = 0 Тогда
		
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Принято;
		Статусы.Принят  = Перечисления.СтатусыИнформированияМДЛП.Закрыто;
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Статусы.ПринятДействия  , ДальнейшиеДействия);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Статусы.ОтклоненДействия, ДальнейшиеДействия);
		
		Если Операция = Перечисления.ОперацииОбменаМДЛП.Получение_КвитанцияОФиксации Тогда
			
			СтатусОбработки = ДополнительныеПараметры.СтатусОбработки;
			
			Статусы.Принят = Перечисления.СтатусыИнформированияМДЛП.Принято;
			
			Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.ПринятоЧастично Тогда
				Статусы.Отклонен = Перечисления.СтатусыИнформированияМДЛП.ЧастичноПринято;
			Иначе
				Статусы.Отклонен = Перечисления.СтатусыИнформированияМДЛП.Отклонено;
			КонецЕсли;
			
		Иначе // Входящее уведомление
			
			СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Принято;
			Статусы.Принят = Перечисления.СтатусыИнформированияМДЛП.ПринятоИзМДЛП;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыОбновления = РегистрыСведений.СтатусыИнформированияМДЛП.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, Статусы);
	
	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыОбновления.Вставить("Комментарий", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Комментарий"));
	НовыйСтатус = РегистрыСведений.СтатусыИнформированияМДЛП.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления);
	
	Возврат НовыйСтатус;
	
КонецФункции

Функция ПолучитьДальнейшиеДействияПоДокументу(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ссылка  КАК Ссылка,
	|	Ссылка.СтатусПроверкиИПодбора  КАК СтатусПроверкиИПодбора,
	
	|	МИНИМУМ(ВЫБОР
	|		КОГДА НомераУпаковок.СостояниеПодтверждения В (&КонечныеСостояния)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ)            КАК ВзаимодействиеЗавершено,
	
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА НомераУпаковок.СостояниеПодтверждения В (&МожноПодтвердитьПолучение)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ)            КАК МожноПодтвердитьПолучение,
	
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА НомераУпаковок.СостояниеПодтверждения В (&ОжиданиеОбмена)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ)            КАК ОжиданиеОбмена,
	
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА НомераУпаковок.СостояниеПодтверждения В (&МожноПолучитьКвитанциюОФиксации)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ)            КАК МожноПолучитьКвитанциюОФиксации,
	
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА НомераУпаковок.СостояниеПодтверждения В (&МожноОтказатьсяОтТоваров)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ)            КАК МожноОтказатьсяОтТоваров
	
	|ИЗ
	|	(ВЫБРАТЬ
	|		НомераУпаковок.Ссылка                  КАК Ссылка,
	|		НомераУпаковок.СостояниеПодтверждения  КАК СостояниеПодтверждения
	|	ИЗ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК НомераУпаковок
	|	ГДЕ
	|		НомераУпаковок.Ссылка = &Ссылка
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НомераУпаковок.Ссылка                  КАК Ссылка,
	|		НомераУпаковок.СостояниеПодтверждения  КАК СостояниеПодтверждения
	|	ИЗ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК НомераУпаковок
	|	ГДЕ
	|		НомераУпаковок.Ссылка = &Ссылка
	|	) КАК НомераУпаковок
	|СГРУППИРОВАТЬ ПО
	|	Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	КонечныеСостояния = Новый Массив;
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтозваноПоставщиком);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтклоненоПокупателем);
	Запрос.УстановитьПараметр("КонечныеСостояния", КонечныеСостояния);
	
	
	МожноПодтвердитьПолучение = Новый Массив;
	МожноПодтвердитьПолучение.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.Подтвердить);
	МожноПодтвердитьПолучение.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьОтклоненоГИСМ);
	МожноПодтвердитьПолучение.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяОтклоненоГИСМ);
	Запрос.УстановитьПараметр("МожноПодтвердитьПолучение", МожноПодтвердитьПолучение);
	
	
	ОжиданиеОбмена = Новый Массив;
	ОжиданиеОбмена.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьПодготовленоКПередаче);
	ОжиданиеОбмена.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьПередано);
	ОжиданиеОбмена.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяПодготовленоКПередаче);
	ОжиданиеОбмена.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяПередано);
	Запрос.УстановитьПараметр("ОжиданиеОбмена", ОжиданиеОбмена);
	
	
	МожноПолучитьКвитанциюОФиксации = Новый Массив;
	МожноПолучитьКвитанциюОФиксации.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьПередано);
	МожноПолучитьКвитанциюОФиксации.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяПередано);
	Запрос.УстановитьПараметр("МожноПолучитьКвитанциюОФиксации", МожноПолучитьКвитанциюОФиксации);
	
	
	МожноОтказатьсяОтТоваров = Новый Массив;
	МожноОтказатьсяОтТоваров.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ПринятоГИСМ);
	МожноОтказатьсяОтТоваров.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.Подтвердить);
	МожноОтказатьсяОтТоваров.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьОтклоненоГИСМ);
	МожноОтказатьсяОтТоваров.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.Отказаться);
	МожноОтказатьсяОтТоваров.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяОтклоненоГИСМ);
	Запрос.УстановитьПараметр("МожноОтказатьсяОтТоваров", МожноОтказатьсяОтТоваров);
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Статусы = РегистрыСведений.СтатусыИнформированияМДЛП.СтатусыОбработки();
	
	ДальнейшиеДействия = Новый Массив;
	Если Выборка.ВзаимодействиеЗавершено Тогда
		
	ИначеЕсли Выборка.ОжиданиеОбмена Тогда
		
		Если Выборка.МожноПолучитьКвитанциюОФиксации Тогда
			ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ПолучитеКвитанциюОФиксации);
		КонецЕсли;
		
		ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ОжидайтеПолучениеКвитанцииОФиксации);
		
	Иначе
		
		Если Выборка.МожноПодтвердитьПолучение Тогда
			Если ИнтеграцияМДЛПКлиентСервер.ЗавершеныПроверкаИПодбор(Выборка.СтатусПроверкиИПодбора) Тогда
				ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ПодтвердитеПолучение);
			Иначе
				ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ВыполнитеПроверку);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.МожноОтказатьсяОтТоваров Тогда
			ДальнейшиеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ОткажитесьОтПриемкиТовара);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДальнейшиеДействия;
	
КонецФункции

Процедура ОбновитьСостояниеПодтверждения(ДокументОбъект, Операция, Сообщение, СтатусОбработки, ОтклоненныеНомера = Неопределено) Экспорт
	
	НовоеСостояние = Неопределено;
	
	Если Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ПодтверждениеСведенийОПриемке
	 Или Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ОприходованиеИмпорт Тогда
		
		ИсходноеСообщение = Сообщение;
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.КПередаче Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьПодготовленоКПередаче;
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Ошибка Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.Подтвердить;
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Передано Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьПередано;
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Отклонено Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьОтклоненоГИСМ;
		Иначе
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено;
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ОтказОтПриемкиТовара Тогда
		
		ИсходноеСообщение = Сообщение;
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.КПередаче Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяПодготовленоКПередаче;
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Ошибка Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.Отказаться;
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Передано Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяПередано;
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Отклонено Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяОтклоненоГИСМ;
		Иначе
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.ОтклоненоПокупателем;
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИнформацииОКиЗ
			Или Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросСоставаУпаковки
			Или Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИерархииВложенностиУпаковок Тогда
		
		ИсходноеСообщение = Сообщение;
		Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.КПередаче Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.КПередаче;
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Передано Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.Передано;
		ИначеЕсли СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Отклонено
			  Или СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийМДЛП.Ошибка Тогда
			НовоеСостояние = Перечисления.СостоянияПодтвержденияМДЛП.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	Если НовоеСостояние = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = ИнтеграцияМДЛПВызовСервера.ТекстСообщенияИзПротокола(ИсходноеСообщение);
	ОбъектXDTO = ИнтеграцияМДЛП.ОбъектXDTOПоТекстуСообщенияXML(ТекстСообщения, "documents", ИнтеграцияМДЛП.ПространствоИмен());
	
	КонечныеСостояния = Новый Массив;
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтклоненоПокупателем);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтозваноПоставщиком);
	
	СостояниеОтклонено = Перечисления.СостоянияПодтвержденияМДЛП.ОтклоненоГИСМ;
	
	Если Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ПодтверждениеСведенийОПриемке Тогда
		ДанныеДокумента = ОбъектXDTO["accept"];
		СостояниеОтклонено = Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьОтклоненоГИСМ;
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ОприходованиеИмпорт Тогда
		ДанныеДокумента = ОбъектXDTO["posting_import"];
		СостояниеОтклонено = Перечисления.СостоянияПодтвержденияМДЛП.ПодтвердитьОтклоненоГИСМ;
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ОтказОтПриемкиТовара Тогда
		ДанныеДокумента = ОбъектXDTO["refusal_receiver"];
		СостояниеОтклонено = Перечисления.СостоянияПодтвержденияМДЛП.ОтказатьсяОтклоненоГИСМ;
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИнформацииОКиЗ Тогда
		ДанныеДокумента = ОбъектXDTO["query_kiz_info"];
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросСоставаУпаковки Тогда
		ДанныеДокумента = ОбъектXDTO["query_kiz_info"];
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИерархииВложенностиУпаковок Тогда
		ДанныеДокумента = ОбъектXDTO["query_hierarchy_info"];
	КонецЕсли;
	
	Если Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ПодтверждениеСведенийОПриемке
	 Или Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ОтказОтПриемкиТовара Тогда
		
		СписокТоваров = ДанныеДокумента.order_details;
		Для Каждого НомерУпаковки Из СписокТоваров.sgtin Цикл
			
			Строка = ДокументОбъект.НомераУпаковок.Найти(НомерУпаковки, "НомерКиЗ");
			
			Если КонечныеСостояния.Найти(Строка.СостояниеПодтверждения) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ОтклоненныеНомера = Неопределено Или ОтклоненныеНомера[НомерУпаковки] = Неопределено Тогда
				Строка.СостояниеПодтверждения = НовоеСостояние;
			Иначе
				Строка.СостояниеПодтверждения = СостояниеОтклонено;
				Строка.ПричинаОтказа = ИнтеграцияМДЛП.ПредставлениеПричиныОтклонения(ОтклоненныеНомера[НомерУпаковки]);
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого НомерУпаковки Из СписокТоваров.sscc Цикл
			
			Строка = ДокументОбъект.ТранспортныеУпаковки.Найти(НомерУпаковки, "НомерУпаковки");
			
			Если КонечныеСостояния.Найти(Строка.СостояниеПодтверждения) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ОтклоненныеНомера = Неопределено Или ОтклоненныеНомера[НомерУпаковки] = Неопределено Тогда
				Строка.СостояниеПодтверждения = НовоеСостояние;
			Иначе
				Строка.СостояниеПодтверждения = СостояниеОтклонено;
				Строка.ПричинаОтказа = ИнтеграцияМДЛП.ПредставлениеПричиныОтклонения(ОтклоненныеНомера[НомерУпаковки]);
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ОприходованиеИмпорт Тогда
		
		СписокТоваров = ДанныеДокумента.order_details.union;
		Для Каждого Данные Из СписокТоваров Цикл
			Если ЗначениеЗаполнено(Данные.sgtin) Тогда
				НомерУпаковки = Данные.sgtin;
				Строка = ДокументОбъект.НомераУпаковок.Найти(НомерУпаковки, "НомерКиЗ");
			Иначе
				НомерУпаковки = Данные.sscc_detail.sscc;
				Строка = ДокументОбъект.ТранспортныеУпаковки.Найти(НомерУпаковки, "НомерУпаковки");
			КонецЕсли;
			
			Если КонечныеСостояния.Найти(Строка.СостояниеПодтверждения) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ОтклоненныеНомера = Неопределено Или ОтклоненныеНомера[НомерУпаковки] = Неопределено Тогда
				Строка.СостояниеПодтверждения = НовоеСостояние;
			Иначе
				Строка.СостояниеПодтверждения = СостояниеОтклонено;
				Строка.ПричинаОтказа = ИнтеграцияМДЛП.ПредставлениеПричиныОтклонения(ОтклоненныеНомера[НомерУпаковки]);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИнформацииОКиЗ Тогда
		
		НомерУпаковки = ДанныеДокумента.sgtin;
		Строка = ДокументОбъект.НомераУпаковок.Найти(НомерУпаковки, "НомерКиЗ");
		Если Строка <> Неопределено Тогда
			Строка.СостояниеПолученияИнформации = НовоеСостояние;
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросСоставаУпаковки Тогда
		
		НомерУпаковки = ДанныеДокумента.sscc_down;
		Строка = ДокументОбъект.ТранспортныеУпаковки.Найти(НомерУпаковки, "НомерУпаковки");
		Если Строка <> Неопределено Тогда
			Строка.СостояниеПолученияИнформации = НовоеСостояние;
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИерархииВложенностиУпаковок Тогда
		
		НомерУпаковки = ДанныеДокумента.sscc;
		Строка = ДокументОбъект.ТранспортныеУпаковки.Найти(НомерУпаковки, "НомерУпаковки");
		Если Строка <> Неопределено Тогда
			Строка.СостояниеПолученияИнформации = НовоеСостояние;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Определить необходимость перерасчета статуса оформления документов.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.УведомлениеОРозничнойПродажеМДЛП - Документ, по которому требуется рассчитать статус оформления.
//  ПредыдущийСтатус - ПеречислениеСсылка.СтатусыОбработки - Предыдущий статус.
//  НовыйСтатус - ПеречислениеСсылка.СтатусыОбработки - Новый статус.
// 
// Возвращаемое значение:
//  Булево - Необходимость перерасчета статуса оформления.
//
Функция РассчитатьСтатусОформления(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус) Экспорт
	
	Основание = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Основание");
	Если ЗначениеЗаполнено(Основание) Тогда
		ИнтеграцияМДЛППереопределяемый.РассчитатьСтатусОформленияУведомленияОПриемкеНовымСобственникомЛПвЗТК(Основание);
	КонецЕсли;
	
КонецФункции

// Получить последовательность операций в течении жизненного цикла документа.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. функцию ИнтеграцияМДЛП.ПустаяТаблицаПоследовательностьОпераций().
//
Функция ПоследовательностьОпераций(ДокументСсылка) Экспорт
	
	Таблица = ИнтеграцияМДЛП.ПустаяТаблицаПоследовательностьОпераций();
	
	Исходящее = Перечисления.ТипыСообщенийМДЛП.Исходящее;
	Входящее  = Перечисления.ТипыСообщенийМДЛП.Входящее;
	
	ИнтеграцияМДЛП.ДобавитьОперациюВПоследовательность(Таблица, 0, Входящее, Перечисления.ОперацииОбменаМДЛП.Получение_СменаСобственникаНаТаможне);
	ИнтеграцияМДЛП.ДобавитьОперациюВПоследовательность(Таблица, 0, Исходящее, Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ПодтверждениеСведенийОПриемке);
	
	ИнтеграцияМДЛП.ДобавитьОперациюВПоследовательность(Таблица, 1, Исходящее, Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ОтказОтПриемкиТовара);
	ИнтеграцияМДЛП.ДобавитьОперациюВПоследовательность(Таблица, 1, Входящее , Перечисления.ОперацииОбменаМДЛП.Получение_ОтзывОтправителемПереданногоТовара);
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти

#Область Статусы

// Возвращает статус информирования по умолчанию.
// 
// Возвращаемое значение:
//  Перечисления.СтатусыИнформированияМДЛП - Статус по-умолчанию.
//
Функция СтатусИнформированияПоУмолчанию() Экспорт
	
	Возврат Перечисления.СтатусыИнформированияМДЛП.Черновик;
	
КонецФункции

// Возвращает дальнейшее действие по умолчанию.
// 
// Возвращаемое значение:
//  Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП - Дальнейшее действие по-умолчанию.
//
Функция ДальнейшееДействиеПоУмолчанию() Экспорт
	
	Возврат Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ВыполнитеПроверку;
	
КонецФункции

// Возвращает запрос для получения статуса оформления.
//
// Параметры:
//  ДокументОснование - ДокументСсылка - Документ основание.
// 
// Возвращаемое значение:
//  Запрос - Запрос для получения статуса оформления.
//
Функция ЗапросСтатусаОформления(ДокументОснование) Экспорт
	
	Запрос = ИнтеграцияМДЛППереопределяемый.ЗапросСтатусаОформленияУведомленияОПриемкеНовымСобственникомЛПвЗТК(ДокументОснование);
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ПанельМаркировкиМДЛП

Функция ВсеТребующиеДействия(Все = Ложь) Экспорт
	
	Действия = Новый Массив;
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ПодтвердитеПолучение);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ЗапроситеИнформациюОКиЗ);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ВыполнитеПроверку);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ВыполнитеОбмен);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ПолучитеКвитанциюОФиксации);
	
	Возврат Действия;
	
КонецФункции

Функция ВсеТребующиеОжидания(Все = Ложь) Экспорт
	
	Действия = Новый Массив;
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ОжидайтеПолучениеКвитанцииОФиксации);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ОжидайтеПолучениеИнформации);
	
	Возврат Действия;
	
КонецФункции

Процедура ПриЗаполненииДокументовПанелиМаркировкиМДЛП(ТаблицаДокументы) Экспорт
	
	Описание = ИнтеграцияМДЛП.ДобавитьДокументНаПанельМаркировки(
		ТаблицаДокументы,
		Метаданные.Документы.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП,
		НСтр("ru = 'Приемка новым собственником ЛП в ЗТК'"),
		ИнтеграцияМДЛПКлиентСервер.ПанельМаркировкаРазделИмпортЭкспорт());
	
	Описание.Оформите    = Истина;
	Описание.Отработайте = Истина;
	Описание.Ожидайте    = Истина;
	
	Описание.Порядок = 90;
	
КонецПроцедуры

// Возвращает текст запроса для получения количества документов для оформления
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ПанельМаркировкиМДЛПТекстЗапросаОформите() Экспорт
	
	Возврат ИнтеграцияМДЛППереопределяемый.УведомлениеОПриемкеНовымСобственникомЛПвЗТКТекстЗапросаОформите();
	
КонецФункции

// Возвращает текст запроса для получения количества документов для отработки
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ПанельМаркировкиМДЛПТекстЗапросаОтработайте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Статусы.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияМДЛП КАК Статусы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП КАК Уведомления
	|	ПО
	|		Статусы.Документ = Уведомления.Ссылка
	|ГДЕ
	|	НЕ Уведомления.ПометкаУдаления
	|	И Статусы.ДальнейшееДействие1 В(&ВсеТребующиеДействия)
	|	И (&Организация = НЕОПРЕДЕЛЕНО ИЛИ Уведомления.Организация = &Организация)
	|	И (&Ответственный = НЕОПРЕДЕЛЕНО ИЛИ Уведомления.Ответственный = &Ответственный)
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения количества документов, находящихся в состоянии ожидания
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ПанельМаркировкиМДЛПТекстЗапросаОжидайте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Статусы.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияМДЛП КАК Статусы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП КАК Уведомления
	|	ПО
	|		Статусы.Документ = Уведомления.Ссылка
	|ГДЕ
	|	НЕ Уведомления.ПометкаУдаления
	|	И Статусы.ДальнейшееДействие1 В(&ВсеТребующиеОжидания)
	|	И (&Организация = НЕОПРЕДЕЛЕНО ИЛИ Уведомления.Организация = &Организация)
	|	И (&Ответственный = НЕОПРЕДЕЛЕНО ИЛИ Уведомления.Ответственный = &Ответственный)
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для формирования данных динамического списка формы списка и формы выбора документов.
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ПанельМаркировкиМДЛПТекстЗапросаДинамическогоСписока() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Уведомления.Ссылка                                                              КАК Ссылка,
	|	Уведомления.ПометкаУдаления                                                     КАК ПометкаУдаления,
	|	Уведомления.Номер                                                               КАК Номер,
	|	Уведомления.Дата                                                                КАК Дата,
	|	Уведомления.Проведен                                                            КАК Проведен,
	|	Уведомления.Основание                                                           КАК Основание,
	|	Уведомления.Организация                                                         КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.МестаДеятельностиМДЛП.ПустаяСсылка)                         КАК МестоДеятельности,
	|	Уведомления.Ответственный                                                       КАК Ответственный,
	|	Уведомления.Комментарий                                                         КАК Комментарий,
	|	ЕСТЬNULL(Статусы.ДальнейшееДействие1,
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюМДЛП.НеТребуется))  КАК ДальнейшееДействие1,
	|	ЕСТЬNULL(Статусы.ДальнейшееДействие2,
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюМДЛП.НеТребуется))  КАК ДальнейшееДействие2,
	|	ЕСТЬNULL(Статусы.ДальнейшееДействие3,
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюМДЛП.НеТребуется))  КАК ДальнейшееДействие3,
	|	ЕСТЬNULL(Статусы.ДальнейшееДействие4,
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюМДЛП.НеТребуется))  КАК ДальнейшееДействие4,
	|	ЕСТЬNULL(Статусы.Статус,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияМДЛП.Отсутствует))               КАК Статус
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП КАК Уведомления
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СтатусыИнформированияМДЛП КАК Статусы
	|	ПО
	|		Статусы.Документ = Уведомления.Ссылка
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для формирования данных динамического Списка к оформлению формы списка и формы выбора документов.
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ПанельМаркировкиМДЛПТекстЗапросаДинамическогоСпискаКОформлению() Экспорт
	
	ТекстЗапроса = ИнтеграцияМДЛППереопределяемый.УведомлениеОПриемкеНовымСобственникомЛПвЗТКТекстЗапросаДинамическогоСпискаКОформлению();
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		ТекстЗапроса = Обработки.ПанельМаркировкиМДЛП.ТекстЗапросаДинамическогоСпискаКОформлениюФормДокументов();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СообщениеМДЛП

Функция СообщениеКПередаче(ДокументСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ЗапроситеИнформациюОКиЗ Тогда
		Возврат ЗапросИнформацииОКиЗ(ДокументСсылка);
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюМДЛП.ОткажитесьОтПриемкиТовара Тогда
		Возврат УведомлениеОбОтказеОтПриемкиНовымСобственникомЛПвЗТК(ДокументСсылка, ДополнительныеПараметры);
	Иначе
		Возврат УведомлениеОПриемкеНовымСобственникомЛПвЗТК(ДокументСсылка, ДальнейшееДействие);
	КонецЕсли;
	
КонецФункции

Функция ОпределитьДокументыДляЗагрузки(ДанныеДокумента, Операция) Экспорт
	
	Если Операция = Перечисления.ОперацииОбменаМДЛП.Получение_СменаСобственникаНаТаможне Тогда
		Возврат ОпределитьУведомлениеОПриемкеНовымСобственникомЛПвЗТК(ДанныеДокумента);
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.Получение_ОтзывОтправителемПереданногоТовара Тогда
		Возврат ОпределитьУведомленияОПриемкеНовымСобственникомЛПвЗТКПоОтзываемымТоварам(ДанныеДокумента);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура ЗагрузитьВходящееСообщение(ДанныеДокумента, Операция, ДокументСсылка) Экспорт
	
	Если Операция = Перечисления.ОперацииОбменаМДЛП.Получение_СменаСобственникаНаТаможне Тогда
		ЗагрузитьУведомлениеОПриемкеНовымСобственникомЛПвЗТК(ДанныеДокумента, ДокументСсылка);
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.Получение_ОтзывОтправителемПереданногоТовара Тогда
		ЗагрузитьУведомлениеОбОтзывеСменыСобственникаЛПвЗТК(ДанныеДокумента, ДокументСсылка);
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.Получение_ИнформацияОКиЗ Тогда
		ЗагрузитьИнформациюОКиЗ(ДанныеДокумента, ДокументСсылка);
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.Получение_СоставУпаковки Тогда
		ЗагрузитьСоставУпаковки(ДанныеДокумента, ДокументСсылка);
	ИначеЕсли Операция = Перечисления.ОперацииОбменаМДЛП.Получение_ИерархияВложенностиУпаковок Тогда
		ЗагрузитьИерархиюВложенностиУпаковок(ДанныеДокумента, ДокументСсылка);
	Иначе
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Неизвестная операция %1'"), Операция);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УпаковкиТовара

Функция ВходящееДеревоУпаковок(ДокументСсылка) Экспорт
	
	ДеревоУпаковок = Новый ДеревоЗначений;
	ДеревоУпаковок.Колонки.Добавить("ИдентификаторСтроки");
	ДеревоУпаковок.Колонки.Добавить("НомерУпаковки");
	ДеревоУпаковок.Колонки.Добавить("ТипУпаковки");
	ДеревоУпаковок.Колонки.Добавить("GTIN");
	ДеревоУпаковок.Колонки.Добавить("НомерСерии");
	ДеревоУпаковок.Колонки.Добавить("ГоденДо");
	ДеревоУпаковок.Колонки.Добавить("Номенклатура");
	ДеревоУпаковок.Колонки.Добавить("Характеристика");
	ДеревоУпаковок.Колонки.Добавить("Серия");
	ДеревоУпаковок.Колонки.Добавить("СостояниеПодтверждения");
	ДеревоУпаковок.Колонки.Добавить("СтатусПроверки");
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаТовары.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|	ТаблицаТовары.Номенклатура         КАК Номенклатура,
	|	ТаблицаТовары.Характеристика       КАК Характеристика,
	|	ТаблицаТовары.Серия                КАК Серия,
	|	ТаблицаТовары.GTIN                 КАК GTIN,
	|	ТаблицаТовары.НомерСерии           КАК НомерСерии,
	|	ТаблицаТовары.ГоденДо              КАК ГоденДо,
	|	ТаблицаУпаковки.НомерРодительскойУпаковки  КАК НомерРодительскойУпаковки,
	|	ТаблицаУпаковки.НомерКиЗ                   КАК НомерУпаковки,
	|	ТаблицаУпаковки.СостояниеПодтверждения     КАК СостояниеПодтверждения
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.Товары КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК ТаблицаУпаковки
	|	ПО
	|		ТаблицаУпаковки.Ссылка = ТаблицаТовары.Ссылка
	|		И ТаблицаУпаковки.ИдентификаторСтроки = ТаблицаТовары.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                  КАК НомерСтроки,
	|	ТаблицаТовары.ИдентификаторСтрокиУпаковки  КАК ИдентификаторСтроки,
	|	ТаблицаТовары.Номенклатура                 КАК Номенклатура,
	|	ТаблицаТовары.Характеристика               КАК Характеристика,
	|	ТаблицаТовары.Серия                        КАК Серия,
	|	ТаблицаТовары.GTIN                         КАК GTIN,
	|	ТаблицаТовары.НомерСерии                   КАК НомерСерии,
	|	ТаблицаТовары.ГоденДо                      КАК ГоденДо,
	|	ТаблицаУпаковки.НомерРодительскойУпаковки    КАК НомерРодительскойУпаковки,
	|	ТаблицаУпаковки.НомерКиЗ                     КАК НомерУпаковки,
	|	ТранспортныеУпаковки.СостояниеПодтверждения  КАК СостояниеПодтверждения
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.СоставТранспортныхУпаковок КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК ТаблицаУпаковки
	|	ПО
	|		ТаблицаУпаковки.Ссылка = ТаблицаТовары.Ссылка
	|		И ТаблицаУпаковки.ИдентификаторСтроки = ТаблицаТовары.ИдентификаторСтрокиУпаковки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК ТранспортныеУпаковки
	|	ПО
	|		ТранспортныеУпаковки.Ссылка = ТаблицаТовары.Ссылка
	|		И ТранспортныеУпаковки.ИдентификаторСтроки = ТаблицаТовары.ИдентификаторСтроки
	|ГДЕ
	|	ТранспортныеУпаковки.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерРодительскойУпаковки,
	|	НомерСтроки УБЫВ
	|
	|ИТОГИ ПО
	|	НомерРодительскойУпаковки,
	|	ИдентификаторСтроки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТранспортныеУпаковки.НомерУпаковки  КАК НомерУпаковки,
	|	""""                                КАК НомерРодительскойУпаковки,
	|	ТранспортныеУпаковки.СостояниеПодтверждения  КАК СостояниеПодтверждения
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК ТранспортныеУпаковки
	|ГДЕ
	|	ТранспортныеУпаковки.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИерархияУпаковок.НомерУпаковки              КАК НомерУпаковки,
	|	ИерархияУпаковок.НомерРодительскойУпаковки  КАК НомерРодительскойУпаковки,
	|	ТранспортныеУпаковки.СостояниеПодтверждения  КАК СостояниеПодтверждения
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК ТранспортныеУпаковки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ИерархияГрупповыхУпаковок КАК ИерархияУпаковок
	|	ПО
	|		ИерархияУпаковок.Ссылка = ТранспортныеУпаковки.Ссылка
	|		И ИерархияУпаковок.ИдентификаторСтроки = ТранспортныеУпаковки.ИдентификаторСтроки
	|ГДЕ
	|	ТранспортныеУпаковки.Ссылка = &Ссылка
	|
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ПотребительскиеУпаковки = РезультатЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ИерархияУпаковок = РезультатЗапроса[1].Выгрузить();
	
	СтатусПроверки = Неопределено;
	ДобавитьПотребительскиеУпаковкиВДерево(ДеревоУпаковок, ПотребительскиеУпаковки, "", СтатусПроверки);
	
	ТранспортныеУпаковки = ИерархияУпаковок.НайтиСтроки(Новый Структура("НомерРодительскойУпаковки", ""));
	Для Каждого ТранспортнаяУпаковка Из ТранспортныеУпаковки Цикл
		
		ДобавитьГрупповуюУпаковкуВДерево(ДеревоУпаковок, ТранспортнаяУпаковка, ПотребительскиеУпаковки, ИерархияУпаковок, СтатусПроверки);
		ИерархияУпаковок.Удалить(ТранспортнаяУпаковка);
		
	КонецЦикла;
	
	Возврат ДеревоУпаковок;
	
КонецФункции

Процедура ДобавитьГрупповуюУпаковкуВДерево(ДеревоУпаковок, ТранспортнаяУпаковка, ПотребительскиеУпаковки, ИерархияУпаковок, СтатусПроверки = Неопределено)
	
	Строка = ДеревоУпаковок.Строки.Добавить();
	Строка.НомерУпаковки          = ТранспортнаяУпаковка.НомерУпаковки;
	Строка.ТипУпаковки            = Перечисления.ТипыУпаковокМДЛП.ГрупповаяУпаковка;
	Строка.СостояниеПодтверждения = ТранспортнаяУпаковка.СостояниеПодтверждения;
	Строка.СтатусПроверки         = СтатусПроверки;
	
	ДобавитьПотребительскиеУпаковкиВДерево(Строка, ПотребительскиеУпаковки, Строка.НомерУпаковки);
	
	ВложенныеУпаковки = ИерархияУпаковок.НайтиСтроки(Новый Структура("НомерРодительскойУпаковки", ТранспортнаяУпаковка.НомерУпаковки));
	Для Каждого ВложеннаяУпаковка Из ВложенныеУпаковки Цикл
		ДобавитьГрупповуюУпаковкуВДерево(Строка, ВложеннаяУпаковка, ПотребительскиеУпаковки, ИерархияУпаковок);
		ИерархияУпаковок.Удалить(ВложеннаяУпаковка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПотребительскиеУпаковкиВДерево(Дерево, ПотребительскиеУпаковки, НомерРодительскойУпаковки, СтатусПроверки = Неопределено)
	
	ПотребительскиеУпаковки.Сбросить();
	Если ПотребительскиеУпаковки.НайтиСледующий(НомерРодительскойУпаковки, "НомерРодительскойУпаковки") Тогда
		ВыборкаПоНомеруСтроки = ПотребительскиеУпаковки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоНомеруСтроки.Следующий() Цикл
			
			ПерваяСтрока = Истина;
			
			Выборка = ВыборкаПоНомеруСтроки.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				Если ПерваяСтрока Тогда
					ПерваяСтрока = Ложь;
					СтрокаНоменклатураБезУпаковки = Дерево.Строки.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаНоменклатураБезУпаковки, Выборка,, "НомерУпаковки, СостояниеПодтверждения");
				КонецЕсли;
				
				Строка = СтрокаНоменклатураБезУпаковки.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(Строка, Выборка);
				Строка.ТипУпаковки    = Перечисления.ТипыУпаковокМДЛП.МаркированныйТовар;
				Строка.СтатусПроверки = СтатусПроверки;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Возвращает данные для заполнения представления документа.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//  * КомандаСоздать - Строка - Представление документа, если документ требуется создать.
//  * ИмяКомандыСоздать - Строка - Имя команды "Создать".
//  * ИмяКомандыОткрыть - Строка - Имя команды "Открыть".
//  * ДокументОтсутствуетНетПравНаСоздание - Строка - Представление документа, если документ не создан.
//  * Представление - Строка - Представление документа.
//  * НесколькоДокументовПредставление - Строка - Представление документа, если их несколько.
//
Функция ПредставлениеДокумента() Экспорт
	
	ВозвращаемоеЗначение = ИнтеграцияМДЛП.ПустоеПредставлениеДокумента();
	ВозвращаемоеЗначение.КомандаСоздать                       = НСтр("ru = 'Создать уведомление о приемке новым собственником ЛП в ЗТК МДЛП'");
	ВозвращаемоеЗначение.ИмяКомандыСоздать                    = "СоздатьУведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП";
	ВозвращаемоеЗначение.ИмяКомандыОткрыть                    = "ОткрытьУведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП";
	ВозвращаемоеЗначение.ДокументОтсутствуетНетПравНаСоздание = НСтр("ru = 'Уведомление о приемке новым собственником ЛП в ЗТК МДЛП не создано'");
	ВозвращаемоеЗначение.Представление                        = НСтр("ru = 'Уведомление о приемке новым собственником ЛП в ЗТК МДЛП: %1'");
	ВозвращаемоеЗначение.НесколькоДокументовПредставление     = НСтр("ru = 'Уведомление о приемке новым собственником ЛП в ЗТК МДЛП (%1)'");
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПоддерживаетЗагрузкуУведомлений() Экспорт
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаСообщенийМДЛП

Функция УведомлениеОПриемкеНовымСобственникомЛПвЗТК(ДокументСсылка, ДальнейшееДействие)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Сообщения = Новый Массив;
	
	СообщениеКПередаче = ИнтеграцияМДЛП.СтруктураСообщенияКПередаче();
	СообщениеКПередаче.Документ = ДокументСсылка;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Шапка.Организация.ВерсияСхемОбмена               КАК ВерсияСхемОбмена,
	|	Шапка.Ссылка                                     КАК Ссылка,
	|	Шапка.Дата                                       КАК Дата,
	|	Шапка.Основание                                  КАК Основание,
	|	Шапка.Организация.РегистрационныйНомерУчастника  КАК ИдентификаторОрганизации,
	|	Шапка.Контрагент.РегистрационныйНомерУчастника   КАК ИдентификаторКонтрагента,
	|	Шапка.НомерДокумента                             КАК НомерДокумента,
	|	Шапка.ДатаДокумента                              КАК ДатаДокумента
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП КАК Шапка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.GTIN              КАК GTIN,
	|	НомераУпаковок.НомерКиЗ  КАК НомерКиЗ
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК НомераУпаковок
	|	ПО
	|		НомераУпаковок.Ссылка = Товары.Ссылка
	|		И НомераУпаковок.ИдентификаторСтроки = Товары.ИдентификаторСтроки
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|	И НомераУпаковок.СостояниеПодтверждения В (ЗНАЧЕНИЕ(Перечисление.СостоянияПодтвержденияМДЛП.Подтвердить))
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераУпаковок.НомерУпаковки  КАК НомерКиЗ
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК НомераУпаковок
	|ГДЕ
	|	НомераУпаковок.Ссылка = &Ссылка
	|	И НомераУпаковок.СостояниеПодтверждения В (ЗНАЧЕНИЕ(Перечисление.СостоянияПодтвержденияМДЛП.Подтвердить))
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Результат = Запрос.ВыполнитьПакет();
	Шапка    = Результат[0].Выбрать();
	Товары   = Результат[1].Выгрузить();
	Упаковки = Результат[2].Выгрузить();
	
	Если Не Шапка.Следующий() Или (Товары.Количество() = 0 И Упаковки.Количество() = 0) Тогда
		
		ИнтеграцияМДЛПКлиентСервер.ДобавитьТекстОшибки(СообщениеКПередаче, НСтр("ru = 'Нет данных для выгрузки.'"));
		Сообщения.Добавить(СообщениеКПередаче);
		Возврат Сообщения;
		
	КонецЕсли;
	
	ПространствоИмен = ИнтеграцияМДЛП.ПространствоИмен(Шапка.ВерсияСхемОбмена);
	
	УстановленныеДаты = Новый Соответствие;
	
	ИмяТипа = "documents";
	ПередачаДанных = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа, ПространствоИмен);
	ИнтеграцияМДЛП.УстановитьВерсиюСхемОбменаПакета(ПередачаДанных);
	
	СообщениеКПередаче.Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ПодтверждениеСведенийОПриемке;
	
	ИмяПакета = "accept";
	
	Уведомление = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(ПередачаДанных, ИмяПакета);
	ПередачаДанных[ИмяПакета] = Уведомление;
	
	Уведомление.action_id = Уведомление.action_id;
	
	ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "subject_id" , Шапка.ИдентификаторОрганизации, СообщениеКПередаче);
	ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "counterparty_id", Шапка.ИдентификаторКонтрагента, СообщениеКПередаче);
	ИнтеграцияМДЛП.УстановитьДатуСЧасовымПоясом(Уведомление, "operation_date", Шапка.Дата, УстановленныеДаты, СообщениеКПередаче);
	
	ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "confirm_paused", Ложь, СообщениеКПередаче);
	
	Уведомление.order_details = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(Уведомление, "order_details");
	Для Каждого Строка Из Товары Цикл
		ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление.order_details, "sgtin", Строка.НомерКИЗ, СообщениеКПередаче);
	КонецЦикла;
	Для Каждого Строка Из Упаковки Цикл
		ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление.order_details, "sscc", Строка.НомерКИЗ, СообщениеКПередаче);
	КонецЦикла;
	
	ИнтеграцияМДЛП.ПроверитьОбъектXDTO(ПередачаДанных, СообщениеКПередаче);
	
	ТекстСообщения = ИнтеграцияМДЛП.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа, ПространствоИмен);
	ТекстСообщения = ИнтеграцияМДЛП.ПреобразоватьВременныеДаты(УстановленныеДаты, ТекстСообщения);
	
	СообщениеКПередаче.ТекстСообщения = ТекстСообщения;
	СообщениеКПередаче.ИдентификаторСубъектаОбращения = Шапка.ИдентификаторОрганизации;
	СообщениеКПередаче.Основание      = Шапка.Основание;
	СообщениеКПередаче.ТипСообщения   = Перечисления.ТипыСообщенийМДЛП.Исходящее;
	СообщениеКПередаче.ОбновитьСостояниеПодтверждения = Истина;
	
	Сообщения.Добавить(СообщениеКПередаче);
	Возврат Сообщения;
	
КонецФункции

Функция УведомлениеОбОтказеОтПриемкиНовымСобственникомЛПвЗТК(ДокументСсылка, ДополнительныеПараметры)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Сообщения = Новый Массив;
	
	СообщениеКПередаче = ИнтеграцияМДЛП.СтруктураСообщенияКПередаче();
	СообщениеКПередаче.Документ = ДокументСсылка;
	СообщениеКПередаче.Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ОтказОтПриемкиТовара;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Шапка.Организация.ВерсияСхемОбмена               КАК ВерсияСхемОбмена,
	|	Шапка.Ссылка                                     КАК Ссылка,
	|	Шапка.Дата                                       КАК Дата,
	|	Шапка.Основание                                  КАК Основание,
	|	Шапка.Организация.РегистрационныйНомерУчастника  КАК ИдентификаторОрганизации,
	|	Шапка.Контрагент.РегистрационныйНомерУчастника   КАК ИдентификаторКонтрагента
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП КАК Шапка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.GTIN              КАК GTIN,
	|	НомераУпаковок.НомерКиЗ  КАК НомерКиЗ
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК НомераУпаковок
	|	ПО
	|		НомераУпаковок.Ссылка = Товары.Ссылка
	|		И НомераУпаковок.ИдентификаторСтроки = Товары.ИдентификаторСтроки
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|	И НомераУпаковок.СостояниеПодтверждения = ЗНАЧЕНИЕ(Перечисление.СостоянияПодтвержденияМДЛП.Отказаться)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераУпаковок.НомерУпаковки  КАК НомерКиЗ
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК НомераУпаковок
	|ГДЕ
	|	НомераУпаковок.Ссылка = &Ссылка
	|	И НомераУпаковок.СостояниеПодтверждения = ЗНАЧЕНИЕ(Перечисление.СостоянияПодтвержденияМДЛП.Отказаться)
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Результат = Запрос.ВыполнитьПакет();
	Шапка    = Результат[0].Выбрать();
	Товары   = Результат[1].Выгрузить();
	Упаковки = Результат[2].Выгрузить();
	
	Если Не Шапка.Следующий() Или (Товары.Количество() = 0 И Упаковки.Количество() = 0) Тогда
		
		ИнтеграцияМДЛПКлиентСервер.ДобавитьТекстОшибки(СообщениеКПередаче, НСтр("ru = 'Нет данных для выгрузки.'"));
		Сообщения.Добавить(СообщениеКПередаче);
		Возврат Сообщения;
		
	КонецЕсли;
	
	ПространствоИмен = ИнтеграцияМДЛП.ПространствоИмен(Шапка.ВерсияСхемОбмена);
	
	УстановленныеДаты = Новый Соответствие;
	
	ИмяТипа   = "documents";
	ИмяПакета = "refusal_receiver";
	
	ПередачаДанных = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа, ПространствоИмен);
	ИнтеграцияМДЛП.УстановитьВерсиюСхемОбменаПакета(ПередачаДанных);
	
	Уведомление = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(ПередачаДанных, ИмяПакета);
	ПередачаДанных[ИмяПакета] = Уведомление;
	
	Уведомление.action_id = Уведомление.action_id;
	
	ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "subject_id" , Шапка.ИдентификаторОрганизации, СообщениеКПередаче);
	ИнтеграцияМДЛП.УстановитьДатуСЧасовымПоясом(Уведомление, "operation_date", Шапка.Дата, УстановленныеДаты, СообщениеКПередаче);
	ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "shipper_id" , Шапка.ИдентификаторКонтрагента, СообщениеКПередаче);
	
	ПричинаОтзыва = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Причина");
	ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "reason", ПричинаОтзыва, СообщениеКПередаче);
	
	ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "confirm_paused", Ложь, СообщениеКПередаче);
	
	Уведомление.order_details = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(Уведомление, "order_details");
	Для Каждого Строка Из Товары Цикл
		ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление.order_details, "sgtin", Строка.НомерКиЗ, СообщениеКПередаче);
	КонецЦикла;
	Для Каждого Строка Из Упаковки Цикл
		ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление.order_details, "sscc" , Строка.НомерКиЗ, СообщениеКПередаче);
	КонецЦикла;
	
	ИнтеграцияМДЛП.ПроверитьОбъектXDTO(ПередачаДанных, СообщениеКПередаче);
	
	ТекстСообщения = ИнтеграцияМДЛП.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа, ПространствоИмен);
	ТекстСообщения = ИнтеграцияМДЛП.ПреобразоватьВременныеДаты(УстановленныеДаты, ТекстСообщения);
	
	СообщениеКПередаче.ТекстСообщения = ТекстСообщения;
	СообщениеКПередаче.ИдентификаторСубъектаОбращения = Шапка.ИдентификаторОрганизации;
	СообщениеКПередаче.Основание      = Шапка.Основание;
	СообщениеКПередаче.ТипСообщения   = Перечисления.ТипыСообщенийМДЛП.Исходящее;
	СообщениеКПередаче.ОбновитьСостояниеПодтверждения = Истина;
	
	Сообщения.Добавить(СообщениеКПередаче);
	Возврат Сообщения;
	
КонецФункции

Функция ЗапросИнформацииОКиЗ(ДокументСсылка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользоватьЗапросИерархииВложенностиУпаковок = ИнтеграцияМДЛП.ИспользуемаяВерсияСхемОбмена() >= "1.35";
	
	Сообщения = Новый Массив;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Шапка.Организация.ВерсияСхемОбмена               КАК ВерсияСхемОбмена,
	|	Шапка.Ссылка                                     КАК Ссылка,
	|	Шапка.Основание                                  КАК Основание,
	|	Шапка.Дата                                       КАК Дата,
	|	Шапка.Организация.РегистрационныйНомерУчастника  КАК ИдентификаторОрганизации
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП КАК Шапка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераУпаковок.НомерКиЗ  КАК НомерУпаковки
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК НомераУпаковок
	|ГДЕ
	|	НомераУпаковок.Ссылка = &Ссылка
	|	И НомераУпаковок.НомерРодительскойУпаковки = &ПустойSSCC
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераУпаковок.НомерУпаковки  КАК НомерУпаковки
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК НомераУпаковок
	|ГДЕ
	|	НомераУпаковок.Ссылка = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПустойSSCC", Метаданные.ОпределяемыеТипы.SSCC.Тип.ПривестиЗначение());
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Шапка  = РезультатЗапроса[0].Выбрать();
	ПотребительскиеУпаковки = РезультатЗапроса[1].Выгрузить();
	ТранспортныеУпаковки = РезультатЗапроса[2].Выгрузить();
	
	Если Не Шапка.Следующий() Или (ПотребительскиеУпаковки.Количество() = 0 И ТранспортныеУпаковки.Количество() = 0) Тогда
		
		Если Сообщения.Количество() = 0 Тогда
			СообщениеКПередаче = ИнтеграцияМДЛП.СтруктураСообщенияКПередаче();
			СообщениеКПередаче.Документ = ДокументСсылка;
			
			Если Не ИспользоватьЗапросИерархииВложенностиУпаковок Или Не Шапка.Следующий() Или ПотребительскиеУпаковки.Количество() = 0 Тогда
				СообщениеКПередаче.Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИнформацииОКиЗ;
			Иначе
				СообщениеКПередаче.Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИерархииВложенностиУпаковок;
			КонецЕсли;
			
			ИнтеграцияМДЛПКлиентСервер.ДобавитьТекстОшибки(СообщениеКПередаче, НСтр("ru = 'Нет данных для выгрузки.'"));
			Сообщения.Добавить(СообщениеКПередаче);
		КонецЕсли;
		Возврат Сообщения;
		
	КонецЕсли;
	
	ПространствоИмен = ИнтеграцияМДЛП.ПространствоИмен(Шапка.ВерсияСхемОбмена);
	
	ИмяТипа   = "documents";
	ИмяПакета = "query_kiz_info";
	
	Для Каждого ТекущаяСтрока Из ПотребительскиеУпаковки Цикл
		
		СообщениеКПередаче = ИнтеграцияМДЛП.СтруктураСообщенияКПередаче();
		СообщениеКПередаче.Документ = ДокументСсылка;
		СообщениеКПередаче.Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИнформацииОКиЗ;
		
		ПередачаДанных = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа, ПространствоИмен);
		ИнтеграцияМДЛП.УстановитьВерсиюСхемОбменаПакета(ПередачаДанных);
		
		Уведомление = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(ПередачаДанных, ИмяПакета);
		ПередачаДанных[ИмяПакета] = Уведомление;
		
		Уведомление.action_id = Уведомление.action_id;
		
		ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "subject_id", Шапка.ИдентификаторОрганизации, СообщениеКПередаче);
		ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "sgtin"     , ТекущаяСтрока.НомерУпаковки, СообщениеКПередаче);
		
		ИнтеграцияМДЛП.ПроверитьОбъектXDTO(ПередачаДанных, СообщениеКПередаче);
		
		ТекстСообщения = ИнтеграцияМДЛП.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа, ПространствоИмен);
		
		СообщениеКПередаче.ТекстСообщения = ТекстСообщения;
		СообщениеКПередаче.ИдентификаторСубъектаОбращения = Шапка.ИдентификаторОрганизации;
		СообщениеКПередаче.Основание      = Шапка.Основание;
		СообщениеКПередаче.ТипСообщения   = Перечисления.ТипыСообщенийМДЛП.Исходящее;
		СообщениеКПередаче.ОбновитьСостояниеПодтверждения = Истина;
		
		Сообщения.Добавить(СообщениеКПередаче);
		
	КонецЦикла;
	
	Если Не ИспользоватьЗапросИерархииВложенностиУпаковок Тогда
		
		Для Каждого ТекущаяСтрока Из ТранспортныеУпаковки Цикл
			
			СообщениеКПередаче = ИнтеграцияМДЛП.СтруктураСообщенияКПередаче();
			СообщениеКПередаче.Документ = ДокументСсылка;
			СообщениеКПередаче.Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросСоставаУпаковки;
			
			ПередачаДанных = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа, ПространствоИмен);
			ИнтеграцияМДЛП.УстановитьВерсиюСхемОбменаПакета(ПередачаДанных);
			
			Уведомление = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(ПередачаДанных, ИмяПакета);
			ПередачаДанных[ИмяПакета] = Уведомление;
			
			Уведомление.action_id = Уведомление.action_id;
			
			ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "subject_id", Шапка.ИдентификаторОрганизации, СообщениеКПередаче);
			ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "sscc_down" , ТекущаяСтрока.НомерУпаковки, СообщениеКПередаче);
			
			ИнтеграцияМДЛП.ПроверитьОбъектXDTO(ПередачаДанных, СообщениеКПередаче);
			
			ТекстСообщения = ИнтеграцияМДЛП.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа, ПространствоИмен);
			
			СообщениеКПередаче.ТекстСообщения = ТекстСообщения;
			СообщениеКПередаче.ИдентификаторСубъектаОбращения = Шапка.ИдентификаторОрганизации;
			СообщениеКПередаче.Основание      = Шапка.Основание;
			СообщениеКПередаче.ТипСообщения   = Перечисления.ТипыСообщенийМДЛП.Исходящее;
			СообщениеКПередаче.ОбновитьСостояниеПодтверждения = Истина;
			
			Сообщения.Добавить(СообщениеКПередаче);
			
		КонецЦикла;
		
	Иначе
		
		ИмяПакета = "query_hierarchy_info";
		
		Для Каждого ТекущаяСтрока Из ТранспортныеУпаковки Цикл
			
			СообщениеКПередаче = ИнтеграцияМДЛП.СтруктураСообщенияКПередаче();
			СообщениеКПередаче.Документ = ДокументСсылка;
			СообщениеКПередаче.Операция = Перечисления.ОперацииОбменаМДЛП.ПередачаДанных_ЗапросИерархииВложенностиУпаковок;
			
			ПередачаДанных = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(Неопределено, ИмяТипа, ПространствоИмен);
			ИнтеграцияМДЛП.УстановитьВерсиюСхемОбменаПакета(ПередачаДанных);
			
			Уведомление = ИнтеграцияМДЛП.ОбъектXDTOПоИмениСвойства(ПередачаДанных, ИмяПакета);
			ПередачаДанных[ИмяПакета] = Уведомление;
			
			Уведомление.action_id = Уведомление.action_id;
			
			ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "subject_id", Шапка.ИдентификаторОрганизации, СообщениеКПередаче);
			ИнтеграцияМДЛП.ЗаполнитьСвойствоXDTO(Уведомление, "sscc"      , ТекущаяСтрока.НомерУпаковки, СообщениеКПередаче);
			
			ИнтеграцияМДЛП.ПроверитьОбъектXDTO(ПередачаДанных, СообщениеКПередаче);
			
			ТекстСообщения = ИнтеграцияМДЛП.ОбъектXDTOВXML(ПередачаДанных, ИмяТипа, ПространствоИмен);
			
			СообщениеКПередаче.ТекстСообщения = ТекстСообщения;
			СообщениеКПередаче.ИдентификаторСубъектаОбращения = Шапка.ИдентификаторОрганизации;
			СообщениеКПередаче.Основание      = Шапка.Основание;
			СообщениеКПередаче.ТипСообщения   = Перечисления.ТипыСообщенийМДЛП.Исходящее;
			СообщениеКПередаче.ОбновитьСостояниеПодтверждения = Истина;
			
			Сообщения.Добавить(СообщениеКПередаче);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Сообщения;
	
КонецФункции

Функция ОпределитьУведомлениеОПриемкеНовымСобственникомЛПвЗТК(ДанныеДокумента)
	
	ДокументСсылка = ПолучитьСсылку();
	Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументСсылка);
	
КонецФункции

Функция ОпределитьУведомленияОПриемкеНовымСобственникомЛПвЗТКПоОтзываемымТоварам(ДанныеДокумента)
	
	ДатаОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "operation_date", ТекущаяДатаСеанса());
	ИдентификаторОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "receiver_id");
	ИдентификаторКонтрагента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "subject_id");
	
	НомераПотребительскихУпаковок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента.order_details, "sgtin", Новый Массив);
	НомераТранспортныхУпаковок    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента.order_details, "sscc" , Новый Массив);
	
	Возврат ОпределитьДокументыПриемкиНовымСобственникомЛПвЗТК(ДатаОперации, ИдентификаторОрганизации, ИдентификаторКонтрагента, НомераПотребительскихУпаковок, НомераТранспортныхУпаковок);
	
КонецФункции

Функция ОпределитьДокументыПриемкиНовымСобственникомЛПвЗТК(ДатаОперации, ИдентификаторОрганизации, ИдентификаторКонтрагента, НомераПотребительскихУпаковок, НомераТранспортныхУпаковок)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ссылка  КАК Документ
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК Упаковки
	|ГДЕ
	|	Упаковки.НомерКиЗ В (&НомераПотребительскихУпаковок)
	|	И НЕ Упаковки.СостояниеПодтверждения В (&КонечныеСостояния)
	|	И Ссылка.Организация.РегистрационныйНомерУчастника = &ИдентификаторОрганизации
	|	И Ссылка.Контрагент.РегистрационныйНомерУчастника = &ИдентификаторКонтрагента
	|	И НЕ Ссылка.ПометкаУдаления
	|	И Ссылка.Дата <= &ДатаОперации
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Ссылка  КАК Документ
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК Упаковки
	|ГДЕ
	|	Упаковки.НомерУпаковки В (&НомераТранспортныхУпаковок)
	|	И НЕ Упаковки.СостояниеПодтверждения В (&КонечныеСостояния)
	|	И Ссылка.Организация.РегистрационныйНомерУчастника = &ИдентификаторОрганизации
	|	И Ссылка.Контрагент.РегистрационныйНомерУчастника = &ИдентификаторКонтрагента
	|	И НЕ Ссылка.ПометкаУдаления
	|	И Ссылка.Дата <= &ДатаОперации
	|");
	
	КонечныеСостояния = Новый Массив;
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтклоненоПокупателем);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтозваноПоставщиком);
	
	Запрос.УстановитьПараметр("КонечныеСостояния"            , КонечныеСостояния);
	Запрос.УстановитьПараметр("НомераПотребительскихУпаковок", НомераПотребительскихУпаковок);
	Запрос.УстановитьПараметр("НомераТранспортныхУпаковок"   , НомераТранспортныхУпаковок);
	Запрос.УстановитьПараметр("ИдентификаторОрганизации"     , ИдентификаторОрганизации);
	Запрос.УстановитьПараметр("ИдентификаторКонтрагента"     , ИдентификаторКонтрагента);
	Запрос.УстановитьПараметр("ДатаОперации"                 , ДатаОперации);
	
	ДокументыСУпаковками = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ");
	Возврат ДокументыСУпаковками;
	
КонецФункции

Процедура ЗагрузитьУведомлениеОПриемкеНовымСобственникомЛПвЗТК(ДанныеДокумента, ДокументСсылка)
	
	Документ = ДокументСсылка.ПолучитьОбъект();
	Если Документ = Неопределено Тогда
		
		ИдентификаторОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "receiver_id");
		ИдентификаторКонтрагента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "subject_id");
		
		НомерДокумента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "doc_num");
		ДатаДокумента = СтроковыеФункцииКлиентСервер.СтрокаВДату(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "doc_date"));
		
		Документ = СоздатьДокумент();
		Документ.УстановитьСсылкуНового(ДокументСсылка);
		
		Документ.Дата = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента, "operation_date", ТекущаяДатаСеанса());
		
		Документ.ИдентификаторОрганизации = ИдентификаторОрганизации;
		ИнтеграцияМДЛП.ПолучитьОрганизациюМестоДеятельностиПоИдентификатору(ИдентификаторОрганизации, Документ.Организация);
		Документ.ИдентификаторКонтрагента = ИдентификаторКонтрагента;
		ИнтеграцияМДЛП.ПолучитьОрганизациюМестоДеятельностиПоИдентификатору(ИдентификаторКонтрагента, Документ.Контрагент);
		
		Документ.НомерДокумента    = НомерДокумента;
		Документ.ДатаДокумента     = ДатаДокумента;
		
	Иначе
		Документ.Заблокировать();
	КонецЕсли;
	
	НомераПотребительскихУпаковок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента.order_details, "sgtin", Новый Массив);
	НомераТранспортныхУпаковок    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента.order_details, "sscc" , Новый Массив);
	
	Для Каждого НомерУпаковки Из НомераПотребительскихУпаковок Цикл
		
		GTIN = Лев(НомерУпаковки, 14);
		НайденныеСтроки = Документ.Товары.НайтиСтроки(Новый Структура("GTIN", GTIN));
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаТовара = Документ.Товары.Добавить();
			СтрокаТовара.GTIN = GTIN;
			СтрокаТовара.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		Иначе
			СтрокаТовара = НайденныеСтроки[0];
		КонецЕсли;
		
		СтрокаТовара.Количество = СтрокаТовара.Количество + 1;
		
		СтрокаУпаковки = Документ.НомераУпаковок.Добавить();
		СтрокаУпаковки.ИдентификаторСтроки = СтрокаТовара.ИдентификаторСтроки;
		СтрокаУпаковки.НомерКИЗ = НомерУпаковки;
		СтрокаУпаковки.СостояниеПодтверждения = Перечисления.СостоянияПодтвержденияМДЛП.Подтвердить;
		
	КонецЦикла;
	
	Для Каждого НомерУпаковки Из НомераТранспортныхУпаковок Цикл
		
		СтрокаУпаковки = Документ.ТранспортныеУпаковки.Добавить();
		СтрокаУпаковки.НомерУпаковки = НомерУпаковки;
		СтрокаУпаковки.СостояниеПодтверждения = Перечисления.СостоянияПодтвержденияМДЛП.Подтвердить;
		СтрокаУпаковки.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		
	КонецЦикла;
	
	ИнтеграцияМДЛППереопределяемый.ПриЗагрузкеУведомленияОСменеСобственникаВЗТК(Документ);
	
	ИнтеграцияМДЛП.ЗаписатьДокумент(Документ);
	Документ.Разблокировать();
	
КонецПроцедуры

Процедура ЗагрузитьУведомлениеОбОтзывеСменыСобственникаЛПвЗТК(ДанныеДокумента, ДокументСсылка)
	
	НомераПотребительскихУпаковок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента.order_details, "sgtin", Новый Массив);
	НомераТранспортныхУпаковок    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента.order_details, "sscc" , Новый Массив);
	
	Документ = ДокументСсылка.ПолучитьОбъект();
	Документ.Заблокировать();
	
	Для Каждого НомерУпаковки Из НомераПотребительскихУпаковок Цикл
		Строка = Документ.НомераУпаковок.Найти(НомерУпаковки, "НомерКиЗ");
		Если Строка <> Неопределено Тогда
			Строка.СостояниеПодтверждения = Перечисления.СостоянияПодтвержденияМДЛП.ОтозваноПоставщиком;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого НомерУпаковки Из НомераТранспортныхУпаковок Цикл
		Строка = Документ.ТранспортныеУпаковки.Найти(НомерУпаковки, "НомерУпаковки");
		Если Строка <> Неопределено Тогда
			Строка.СостояниеПодтверждения = Перечисления.СостоянияПодтвержденияМДЛП.ОтозваноПоставщиком;
		КонецЕсли;
	КонецЦикла;
	
	ИнтеграцияМДЛП.ЗаписатьДокумент(Документ);
	Документ.Разблокировать();
	
КонецПроцедуры

Процедура ЗагрузитьИнформациюОКиЗ(ДанныеДокумента, ДокументСсылка)
	
	Документ = ДокументСсылка.ПолучитьОбъект();
	Документ.Заблокировать();
	
	СтрокаУпаковки = Документ.НомераУпаковок.Найти(ДанныеДокумента.sgtin, "НомерКиЗ");
	Если СтрокаУпаковки <> Неопределено Тогда
		СтрокаУпаковки.СостояниеПолученияИнформации = Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено;
		Если ДанныеДокумента.ЕстьИнформация Тогда
			
			ДанныеУпаковки = ДанныеДокумента.ДанныеУпаковки;
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеУпаковки, "info_sgtin") Тогда
				ДанныеУпаковки = ДанныеУпаковки.info_sgtin;
			КонецЕсли;
			СтрокаУпаковки.Статус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеУпаковки, "status");
			
			СтрокаТовараСтарая = Документ.Товары.Найти(СтрокаУпаковки.ИдентификаторСтроки, "ИдентификаторСтроки");
			Если СтрокаТовараСтарая <> Неопределено Тогда
				
				ПараметрыЗаполнения = ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
				ПараметрыЗаполнения.ПересчитатьКоличествоУпаковок = Истина;
				
				Отбор = Новый Структура;
				Отбор.Вставить("GTIN"      , ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеУпаковки, "GTIN"));
				Отбор.Вставить("НомерСерии", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеУпаковки, "series_number"));
				Отбор.Вставить("ГоденДо"   , СтроковыеФункцииКлиентСервер.СтрокаВДату(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеУпаковки, "expiration_date")));
				
				НайденныеСтроки = Документ.Товары.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() > 0 Тогда
					
					Если НайденныеСтроки.Найти(СтрокаТовараСтарая) = Неопределено Тогда
					
						СтрокаТовараСтарая.Количество = СтрокаТовараСтарая.Количество - 1;
						Если СтрокаТовараСтарая.Количество = 0 Тогда
							Документ.Товары.Удалить(СтрокаТовараСтарая);
						Иначе
							ИнтеграцияМДЛППереопределяемый.ПриИзмененииКоличества(Документ, СтрокаТовараСтарая, ПараметрыЗаполнения);
						КонецЕсли;
						
						НоваяСтрокаТовара = НайденныеСтроки[0];
						СтрокаУпаковки.ИдентификаторСтроки = НоваяСтрокаТовара.ИдентификаторСтроки;
						НоваяСтрокаТовара.Количество = НоваяСтрокаТовара.Количество + 1;
						ИнтеграцияМДЛППереопределяемый.ПриИзмененииКоличества(Документ, НоваяСтрокаТовара, ПараметрыЗаполнения);
						
					КонецЕсли;
					
				Иначе
					
					ОтборПоИдентификатору = Новый Структура("ИдентификаторСтроки", СтрокаУпаковки.ИдентификаторСтроки);
					НайденныеСтроки = Документ.НомераУпаковок.НайтиСтроки(ОтборПоИдентификатору);
					Если НайденныеСтроки.Количество() > 1 Тогда
						СтрокаТовараСтарая.Количество = СтрокаТовараСтарая.Количество - 1;
						ИнтеграцияМДЛППереопределяемый.ПриИзмененииКоличества(Документ, СтрокаТовараСтарая, ПараметрыЗаполнения);
						
						НоваяСтрокаТовара = Документ.Товары.Вставить(СтрокаТовараСтарая.НомерСтроки);
						ЗаполнитьЗначенияСвойств(НоваяСтрокаТовара, СтрокаТовараСтарая,, "Серия, Количество, КоличествоУпаковок, ИдентификаторСтроки");
						НоваяСтрокаТовара.Количество = 1;
						НоваяСтрокаТовара.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
						СтрокаУпаковки.ИдентификаторСтроки = НоваяСтрокаТовара.ИдентификаторСтроки;
						
						ИнтеграцияМДЛППереопределяемый.ПриИзмененииКоличества(Документ, НоваяСтрокаТовара, ПараметрыЗаполнения);
					Иначе
						НоваяСтрокаТовара = СтрокаТовараСтарая;
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТовара, Отбор);
					
					Параметры = ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
					Параметры.ОбработатьУпаковки = Ложь;
					Параметры.ПроверитьСериюРассчитатьСтатус = Истина;
					Параметры.ПараметрыУказанияСерий = ГосударственныеИнформационныеСистемыПереопределяемый.ПараметрыУказанияСерийФормыОбъекта(Документ, ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументСсылка));
					ИнтеграцияМДЛППереопределяемый.ПриИзмененииПараметровНоменклатуры(Документ, НоваяСтрокаТовара, Параметры);
					
				КонецЕсли;
			КонецЕсли;
		Иначе
			СтрокаУпаковки.Статус = ДанныеДокумента.Статус;
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияМДЛП.ЗаписатьДокумент(Документ);
	Документ.Разблокировать();
	
КонецПроцедуры

Процедура ЗагрузитьСоставУпаковки(ДанныеДокумента, ДокументСсылка)
	
	Документ = ДокументСсылка.ПолучитьОбъект();
	Документ.Заблокировать();
	
	СтрокаУпаковки = Документ.ТранспортныеУпаковки.Найти(ДанныеДокумента.sscc, "НомерУпаковки");
	Если СтрокаУпаковки <> Неопределено Тогда
		СтрокаУпаковки.СостояниеПолученияИнформации = Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено;
		СтрокаУпаковки.Статус = ДанныеДокумента.Статус;
		
		ТекущийСостав = Документ.СоставТранспортныхУпаковок.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаУпаковки.ИдентификаторСтроки));
		Для Каждого СтрокаСостава Из ТекущийСостав Цикл
			СтрокаСостава.Количество = 0;
			УпаковкиВСоставе = Документ.НомераУпаковок.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаСостава.ИдентификаторСтрокиУпаковки));
			Для Каждого УдаляемаяСтрока Из УпаковкиВСоставе Цикл
				Документ.НомераУпаковок.Удалить(УдаляемаяСтрока);
			КонецЦикла;
		КонецЦикла;
		ГрупповыеУпаковкиВСоставе = Документ.ИерархияГрупповыхУпаковок.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаУпаковки.ИдентификаторСтроки));
		Для Каждого УдаляемаяСтрока Из ГрупповыеУпаковкиВСоставе Цикл
			Документ.ИерархияГрупповыхУпаковок.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
		Если ДанныеДокумента.ЕстьИнформация Тогда
			
			ПараметрыЗаполнения = ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
			ПараметрыЗаполнения.ОбработатьУпаковки = Ложь;
			ПараметрыЗаполнения.ПроверитьСериюРассчитатьСтатус = Истина;
			ПараметрыЗаполнения.ПараметрыУказанияСерий = ГосударственныеИнформационныеСистемыПереопределяемый.ПараметрыУказанияСерийФормыОбъекта(Документ, ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументСсылка));
			
			Состав = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеДокумента.ДанныеУпаковки, "tree", Новый Массив);
			Для Каждого ЭлементСостава Из Состав Цикл
				ИнформацияОКиЗ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементСостава, "sgtin");
				Если ИнформацияОКиЗ <> Неопределено Тогда
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИнформацияОКиЗ, "info_sgtin") Тогда
						ИнформацияОКиЗ = ИнформацияОКиЗ.info_sgtin;
					КонецЕсли;
					
					НомерКиЗ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "sgtin");
					Отбор = Новый Структура;
					Отбор.Вставить("ИдентификаторСтроки", СтрокаУпаковки.ИдентификаторСтроки);
					Отбор.Вставить("GTIN"      , ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "GTIN"));
					Отбор.Вставить("НомерСерии", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "series_number"));
					
					ИзменилисьДанныеВСтроке = Ложь;
					
					НайденныеСтроки = Документ.СоставТранспортныхУпаковок.НайтиСтроки(Отбор);
					Если НайденныеСтроки.Количество() > 0 Тогда
						СтрокаТовара = НайденныеСтроки[0];
					Иначе
						СтрокаТовара = Документ.СоставТранспортныхУпаковок.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТовара, Отбор);
						ИзменилисьДанныеВСтроке = Истина;
					КонецЕсли;
					
					Если ПустаяСтрока(СтрокаТовара.ИдентификаторСтрокиУпаковки) Тогда
						СтрокаТовара.ИдентификаторСтрокиУпаковки = Строка(Новый УникальныйИдентификатор);
					КонецЕсли;
					Если Не ЗначениеЗаполнено(СтрокаТовара.ГоденДо) Тогда
						СтрокаТовара.ГоденДо = СтроковыеФункцииКлиентСервер.СтрокаВДату(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "expiration_date"));
						ИзменилисьДанныеВСтроке = Истина;
					КонецЕсли;
					
					СтрокаТовара.Количество = СтрокаТовара.Количество + 1;
					
					Если ИзменилисьДанныеВСтроке Тогда
						ИнтеграцияМДЛППереопределяемый.ПриИзмененииПараметровНоменклатуры(Документ, СтрокаТовара, ПараметрыЗаполнения);
					КонецЕсли;
					
					НоваяСтрока = Документ.НомераУпаковок.Добавить();
					НоваяСтрока.ИдентификаторСтроки = СтрокаТовара.ИдентификаторСтрокиУпаковки;
					НоваяСтрока.НомерКИЗ = НомерКиЗ;
					НоваяСтрока.Статус    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "status");
					НоваяСтрока.СостояниеПолученияИнформации = Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено;
					НоваяСтрока.СостояниеПодтверждения = Перечисления.СостоянияПодтвержденияМДЛП.НеТребуется;
					
				Иначе
					НоваяСтрока = Документ.ИерархияГрупповыхУпаковок.Добавить();
					НоваяСтрока.НомерУпаковки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементСостава, "sscc");
					НоваяСтрока.ИдентификаторСтроки = СтрокаУпаковки.ИдентификаторСтроки;
				КонецЕсли;
				НоваяСтрока.НомерРодительскойУпаковки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементСостава, "parent_sscc");
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияМДЛП.ЗаписатьДокумент(Документ);
	Документ.Разблокировать();
	
КонецПроцедуры

Процедура ЗагрузитьИерархиюВложенностиУпаковок(ДанныеДокумента, ДокументСсылка)
	
	Документ = ДокументСсылка.ПолучитьОбъект();
	Документ.Заблокировать();
	
	СтрокаУпаковки = Документ.ТранспортныеУпаковки.Найти(ДанныеДокумента.sscc, "НомерУпаковки");
	Если СтрокаУпаковки <> Неопределено Тогда
		СтрокаУпаковки.СостояниеПолученияИнформации = Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено;
		СтрокаУпаковки.Статус = ДанныеДокумента.Статус;
		
		ТекущийСостав = Документ.СоставТранспортныхУпаковок.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаУпаковки.ИдентификаторСтроки));
		Для Каждого СтрокаСостава Из ТекущийСостав Цикл
			СтрокаСостава.Количество = 0;
			УпаковкиВСоставе = Документ.НомераУпаковок.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаСостава.ИдентификаторСтрокиУпаковки));
			Для Каждого УдаляемаяСтрока Из УпаковкиВСоставе Цикл
				Документ.НомераУпаковок.Удалить(УдаляемаяСтрока);
			КонецЦикла;
		КонецЦикла;
		ГрупповыеУпаковкиВСоставе = Документ.ИерархияГрупповыхУпаковок.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаУпаковки.ИдентификаторСтроки));
		Для Каждого УдаляемаяСтрока Из ГрупповыеУпаковкиВСоставе Цикл
			Документ.ИерархияГрупповыхУпаковок.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
		Если ДанныеДокумента.ЕстьИнформация Тогда
			
			ПараметрыЗаполнения = ИнтеграцияМДЛПКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
			ПараметрыЗаполнения.ОбработатьУпаковки = Ложь;
			ПараметрыЗаполнения.ПроверитьСериюРассчитатьСтатус = Истина;
			ПараметрыЗаполнения.ПараметрыУказанияСерий = ГосударственныеИнформационныеСистемыПереопределяемый.ПараметрыУказанияСерийФормыОбъекта(Документ, ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументСсылка));
			
			СтекУпаковок = Новый Массив;
			СтекУпаковок.Добавить(ДанныеДокумента.ДанныеУпаковки);
			
			Пока СтекУпаковок.Количество() > 0 Цикл
				
				ТекущаяУпаковка = СтекУпаковок[0];
				СтекУпаковок.Удалить(0);
				
				Состав = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущаяУпаковка, "childs", Новый Массив);
				Для Каждого ЭлементСостава Из Состав Цикл
					
					НомераУпаковок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементСостава, "sgtin_info", Новый Массив);
					Если НомераУпаковок.Количество() > 0 Тогда
						
						НомерРодительскойУпаковки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущаяУпаковка, "sscc");
						Для Каждого ИнформацияОКиЗ Из НомераУпаковок Цикл
							
							НомерКиЗ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "sgtin");
							Отбор = Новый Структура;
							Отбор.Вставить("ИдентификаторСтроки", СтрокаУпаковки.ИдентификаторСтроки);
							Отбор.Вставить("gtin"      , ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "gtin"));
							Отбор.Вставить("НомерСерии", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "series_number"));
							
							ИзменилисьДанныеВСтроке = Ложь;
							
							НайденныеСтроки = Документ.СоставТранспортныхУпаковок.НайтиСтроки(Отбор);
							Если НайденныеСтроки.Количество() > 0 Тогда
								СтрокаТовара = НайденныеСтроки[0];
							Иначе
								СтрокаТовара = Документ.СоставТранспортныхУпаковок.Добавить();
								ЗаполнитьЗначенияСвойств(СтрокаТовара, Отбор);
								ИзменилисьДанныеВСтроке = Истина;
							КонецЕсли;
							
							Если ПустаяСтрока(СтрокаТовара.ИдентификаторСтрокиУпаковки) Тогда
								СтрокаТовара.ИдентификаторСтрокиУпаковки = Строка(Новый УникальныйИдентификатор);
							КонецЕсли;
							Если Не ЗначениеЗаполнено(СтрокаТовара.ГоденДо) Тогда
								СтрокаТовара.ГоденДо = СтроковыеФункцииКлиентСервер.СтрокаВДату(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "expiration_date"));
								ИзменилисьДанныеВСтроке = Истина;
							КонецЕсли;
							
							СтрокаТовара.Количество = СтрокаТовара.Количество + 1;
							
							Если ИзменилисьДанныеВСтроке Тогда
								ИнтеграцияМДЛППереопределяемый.ПриИзмененииПараметровНоменклатуры(Документ, СтрокаТовара, ПараметрыЗаполнения);
							КонецЕсли;
							
							НоваяСтрока = Документ.НомераУпаковок.Добавить();
							НоваяСтрока.ИдентификаторСтроки          = СтрокаТовара.ИдентификаторСтрокиУпаковки;
							НоваяСтрока.НомерКИЗ                     = НомерКиЗ;
							НоваяСтрока.Статус                       = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКиЗ, "status");
							НоваяСтрока.СостояниеПолученияИнформации = Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено;
							НоваяСтрока.СостояниеПодтверждения       = Перечисления.СостоянияПодтвержденияМДЛП.НеТребуется;
							НоваяСтрока.НомерРодительскойУпаковки    = НомерРодительскойУпаковки;
							
						КонецЦикла;
					КонецЕсли;
					
					ГрупповыеУпаковки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементСостава, "sscc_info", Новый Массив);
					Если ГрупповыеУпаковки.Количество() > 0 Тогда
						
						НомерРодительскойУпаковки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущаяУпаковка, "sscc");
						Для Каждого ИнформацияОГрупповойУпаковке Из ГрупповыеУпаковки Цикл
							НоваяСтрока = Документ.ИерархияГрупповыхУпаковок.Добавить();
							НоваяСтрока.НомерУпаковки             = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОГрупповойУпаковке, "sscc");
							НоваяСтрока.ИдентификаторСтроки       = СтрокаУпаковки.ИдентификаторСтроки;
							НоваяСтрока.НомерРодительскойУпаковки = НомерРодительскойУпаковки;
							СтекУпаковок.Добавить(ИнформацияОГрупповойУпаковке);
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	ИнтеграцияМДЛП.ЗаписатьДокумент(Документ);
	Документ.Разблокировать();
	
КонецПроцедуры

#КонецОбласти

#Область Серии

// Имена реквизитов, от значений которых зависят параметры указания серий
//
// Возвращаемое значение:
//  Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ИменаРеквизитовДляЗаполненияПараметровУказанияСерий(ПустаяСсылка().Метаданные());
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//  Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ПараметрыУказанияСерий(ПустаяСсылка().Метаданные(), Объект);
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
//
// Параметры:
//  ПараметрыУказанияСерий - Структура
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПустаяСсылка().Метаданные(), ПараметрыУказанияСерий);
	
КонецФункции

#КонецОбласти

#Область Отчеты

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции Подключаемые.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, НастройкиФормы) Экспорт
	
	ГосударственныеИнформационныеСистемыПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
