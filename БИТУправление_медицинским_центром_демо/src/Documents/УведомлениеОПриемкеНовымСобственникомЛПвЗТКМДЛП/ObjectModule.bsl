#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнтеграцияМДЛППереопределяемый.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ВызватьИсключение НСтр("ru = 'Обратный акцепт для операции смены собственника ЛП в ЗТК не предусмотрен.'");
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ИнтеграцияМДЛП.ИспользоватьХарактеристикиНоменклатуры() Тогда
		НепроверяемыеРеквизиты.Добавить("Товары.Характеристика");
		НепроверяемыеРеквизиты.Добавить("СоставТранспортныхУпаковок.Характеристика");
	КонецЕсли;
	
	Если Не ИнтеграцияМДЛП.ИспользоватьСерииНоменклатуры() Тогда
		НепроверяемыеРеквизиты.Добавить("Товары.Серия");
		НепроверяемыеРеквизиты.Добавить("СоставТранспортныхУпаковок.Серия");
	КонецЕсли;
	
	ИнтеграцияМДЛППереопределяемый.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
	ИнтеграцияМДЛП.ПроверитьВозможностьЗаписиУведомления(ЭтотОбъект, РежимЗаписи);
	
	ИнтеграцияМДЛП.УбратьНезначащиеСимволы(ЭтотОбъект, "НомерДокумента");
	
	ИнтеграцияМДЛППереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияМДЛП.ЗаписатьСтатусДокументаПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СтандартнаяОбработка = Истина;
	ИнтеграцияМДЛППереопределяемый.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияМДЛП.ПараметрыОперацииИзмененияСтатусаУпаковок();
	ПараметрыОперации.ДатаОперации   = Дата;
	
	ПараметрыОперации.ИсходныйСтатус     = Перечисления.СтатусыУпаковокМДЛП.ПустаяСсылка();
	ПараметрыОперации.НовыйСтатус        = Перечисления.СтатусыУпаковокМДЛП.ВвезенНаТерриториюРФ;
	ПараметрыОперации.СтатусВРезерве     = Перечисления.СтатусыУпаковокМДЛП.ОжидаетПодтвержденияСменыСобственника;
	ПараметрыОперации.Владелец           = Справочники.ОрганизацииМДЛП.ПустаяСсылка();
	ПараметрыОперации.МестоДеятельности  = Организация;
	ПараметрыОперации.ДокументРезерва    = Ссылка;
	ПараметрыОперации.ИспользуетсяЗонаТаможенногоКонтроля = Истина;
	ПараметрыОперации.НоваяЗонаТаможенногоКонтроля = Истина;
	ПараметрыОперации.ИсходнаяЗонаТаможенногоКонтроля = Справочники.ЗоныТаможенногоКонтроляМДЛП.ПустаяСсылка();
	ПараметрыОперации.НоваяЗонаТаможенногоКонтроляДляКаждойУпаковки = Истина;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ПодтвержденныеУпаковки.НомерКиЗ                 КАК НомерУпаковки,
	|	ПодтвержденныеУпаковки.ЗонаТаможенногоКонтроля  КАК НоваяЗонаТаможенногоКонтроля
	|ПОМЕСТИТЬ ПодтвержденныеУпаковки
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК ПодтвержденныеУпаковки
	|ГДЕ
	|	ПодтвержденныеУпаковки.Ссылка = &Ссылка
	|	И ПодтвержденныеУпаковки.СостояниеПодтверждения = ЗНАЧЕНИЕ(Перечисление.СостоянияПодтвержденияМДЛП.Подтверждено)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтвержденныеУпаковки.НомерУпаковки            КАК НомерУпаковки,
	|	ПодтвержденныеУпаковки.ЗонаТаможенногоКонтроля  КАК НоваяЗонаТаможенногоКонтроля
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК ПодтвержденныеУпаковки
	|ГДЕ
	|	ПодтвержденныеУпаковки.Ссылка = &Ссылка
	|	И ПодтвержденныеУпаковки.СостояниеПодтверждения = ЗНАЧЕНИЕ(Перечисление.СостоянияПодтвержденияМДЛП.Подтверждено)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераУпаковок.НомерСтроки  КАК НомерСтроки,
	|	НомераУпаковок.НомерКиЗ     КАК НомерУпаковки,
	|	НомераУпаковок.ЗонаТаможенногоКонтроля  КАК НоваяЗонаТаможенногоКонтроля,
	|	1                           КАК ДоляУпаковки,
	|	ЛОЖЬ                        КАК ГрупповаяУпаковка,
	|	""НомераУпаковок""          КАК ИмяТабличнойЧасти,
	|	""НомерКиЗ""                КАК ИмяПоля
	|ПОМЕСТИТЬ НомераУпаковок
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК НомераУпаковок
	|ГДЕ
	|	НомераУпаковок.Ссылка = &Ссылка
	|	И НЕ НомераУпаковок.СостояниеПодтверждения В (&КонечныеСостояния)
	|	И НомераУпаковок.НомерРодительскойУпаковки = """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НомераУпаковок.НомерСтроки    КАК НомерСтроки,
	|	НомераУпаковок.НомерУпаковки  КАК НомерУпаковки,
	|	НомераУпаковок.ЗонаТаможенногоКонтроля  КАК НоваяЗонаТаможенногоКонтроля,
	|	1                             КАК ДоляУпаковки,
	|	ИСТИНА                        КАК ГрупповаяУпаковка,
	|	""ТранспортныеУпаковки""      КАК ИмяТабличнойЧасти,
	|	""НомерУпаковки""             КАК ИмяПоля
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК НомераУпаковок
	|ГДЕ
	|	НомераУпаковок.Ссылка = &Ссылка
	|	И НЕ НомераУпаковок.СостояниеПодтверждения В (&КонечныеСостояния)
	|");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДокументРезерва", ПараметрыОперации.ДокументРезерва);
	Запрос.УстановитьПараметр("МестоДеятельности", ПараметрыОперации.МестоДеятельности);
	Запрос.УстановитьПараметр("ИспользуетсяЗонаТаможенногоКонтроля", ПараметрыОперации.ИспользуетсяЗонаТаможенногоКонтроля);
	
	КонечныеСостояния = Новый Массив;
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ПустаяСсылка());
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.Подтверждено);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтклоненоГИСМ);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтозваноПоставщиком);
	КонечныеСостояния.Добавить(Перечисления.СостоянияПодтвержденияМДЛП.ОтклоненоПокупателем);
	Запрос.УстановитьПараметр("КонечныеСостояния", КонечныеСостояния);
	
	Запрос.УстановитьПараметр("НовыйСтатус", ПараметрыОперации.НовыйСтатус);
	Запрос.УстановитьПараметр("ДатаСтатуса", ПараметрыОперации.ДатаОперации);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	ИнтеграцияМДЛП.ДобавитьКлючиУпаковок(Запрос.МенеджерВременныхТаблиц, "ПодтвержденныеУпаковки");
	ИнтеграцияМДЛП.ДобавитьКлючиУпаковок(Запрос.МенеджерВременныхТаблиц);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеУпаковок.НомерУпаковки                    КАК НомерУпаковки,
	|	ДанныеУпаковок.МестоДеятельности                КАК МестоДеятельности,
	|	ДанныеУпаковок.ДокументРезерва                  КАК ДокументРезерва,
	|	ДанныеУпаковок.ЗонаТаможенногоКонтроля          КАК ЗонаТаможенногоКонтроля,
	|	ДанныеУпаковок.Статус                           КАК Статус,
	|	ДанныеУпаковок.ДатаСтатуса                      КАК ДатаСтатуса,
	|	ДанныеУпаковок.ИсходныйСтатус                   КАК ИсходныйСтатус,
	|	ДанныеУпаковок.Владелец                         КАК Владелец,
	|	ДанныеУпаковок.НомерГрупповойУпаковки           КАК НомерГрупповойУпаковки,
	|	ДанныеУпаковок.ГрупповаяУпаковка                КАК ГрупповаяУпаковка,
	|	ДанныеУпаковок.ВложеныПотребительскиеУпаковки   КАК ВложеныПотребительскиеУпаковки,
	|	ДанныеУпаковок.ИсходнаяЗонаТаможенногоКонтроля  КАК ИсходнаяЗонаТаможенногоКонтроля
	|ИЗ
	|	РегистрСведений.УпаковкиМДЛП КАК ДанныеУпаковок
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПодтвержденныеУпаковки КАК ПодтвержденныеУпаковки
	|	ПО
	|		ПодтвержденныеУпаковки.НомерУпаковки = ДанныеУпаковок.ДокументРезерва
	|		И ПодтвержденныеУпаковки.КлючУпаковки = ДанныеУпаковок.КлючУпаковки
	|ГДЕ
	|	ДанныеУпаковок.ДокументРезерва = &ДокументРезерва
	|	И ПодтвержденныеУпаковки.НомерУпаковки ЕСТЬ NULL
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеУпаковок.НомерУпаковки                   КАК НомерУпаковки,
	|	&МестоДеятельности                             КАК МестоДеятельности,
	|	НЕОПРЕДЕЛЕНО                                   КАК ДокументРезерва,
	|	ВЫБОР
	|		КОГДА &ИспользуетсяЗонаТаможенногоКонтроля
	|			ТОГДА ПодтвержденныеУпаковки.НоваяЗонаТаможенногоКонтроля
	|		ИНАЧЕ ДанныеУпаковок.ЗонаТаможенногоКонтроля
	|	КОНЕЦ                                          КАК ЗонаТаможенногоКонтроля,
	|	&НовыйСтатус                                   КАК Статус,
	|	&ДатаСтатуса                                   КАК ДатаСтатуса,
	|	ДанныеУпаковок.Владелец                        КАК Владелец,
	|	ДанныеУпаковок.НомерГрупповойУпаковки          КАК НомерГрупповойУпаковки,
	|	ДанныеУпаковок.ГрупповаяУпаковка               КАК ГрупповаяУпаковка,
	|	ДанныеУпаковок.ВложеныПотребительскиеУпаковки  КАК ВложеныПотребительскиеУпаковки
	|ИЗ
	|	ПодтвержденныеУпаковки КАК ПодтвержденныеУпаковки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.УпаковкиМДЛП КАК ДанныеУпаковок
	|	ПО
	|		ПодтвержденныеУпаковки.НомерУпаковки = ДанныеУпаковок.НомерУпаковки
	|		И ПодтвержденныеУпаковки.КлючУпаковки = ДанныеУпаковок.КлючУпаковки
	|		И ДанныеУпаковок.ДокументРезерва = &ДокументРезерва
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НомераУпаковок.НомерКиЗ                        КАК НомерУпаковки,
	|	&МестоДеятельности                             КАК МестоДеятельности,
	|	ВерхнеуровневыеУпаковки.НомерУпаковки          КАК ДокументРезерва,
	|	ВЫБОР
	|		КОГДА &ИспользуетсяЗонаТаможенногоКонтроля
	|			ТОГДА ВерхнеуровневыеУпаковки.НоваяЗонаТаможенногоКонтроля
	|		ИНАЧЕ ДанныеУпаковок.ЗонаТаможенногоКонтроля
	|	КОНЕЦ                                          КАК ЗонаТаможенногоКонтроля,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУпаковокМДЛП.ВУпаковке)  КАК Статус,
	|	&ДатаСтатуса                                   КАК ДатаСтатуса,
	|	ДанныеУпаковок.Владелец                        КАК Владелец,
	|	НомераУпаковок.НомерРодительскойУпаковки       КАК НомерГрупповойУпаковки,
	|	ЛОЖЬ                                           КАК ГрупповаяУпаковка,
	|	НЕОПРЕДЕЛЕНО                                   КАК ВложеныПотребительскиеУпаковки
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК ТранспортныеУпаковки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.СоставТранспортныхУпаковок КАК СоставТранспортныхУпаковок
	|	ПО
	|		СоставТранспортныхУпаковок.Ссылка = ТранспортныеУпаковки.Ссылка
	|		И СоставТранспортныхУпаковок.ИдентификаторСтроки = ТранспортныеУпаковки.ИдентификаторСтроки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК НомераУпаковок
	|	ПО
	|		НомераУпаковок.Ссылка = ТранспортныеУпаковки.Ссылка
	|		И НомераУпаковок.ИдентификаторСтроки = СоставТранспортныхУпаковок.ИдентификаторСтрокиУпаковки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПодтвержденныеУпаковки КАК ВерхнеуровневыеУпаковки
	|	ПО
	|		ВерхнеуровневыеУпаковки.НомерУпаковки = ТранспортныеУпаковки.НомерУпаковки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.УпаковкиМДЛП КАК ДанныеУпаковок
	|		ПО
	|			ВерхнеуровневыеУпаковки.НомерУпаковки = ДанныеУпаковок.НомерУпаковки
	|			И ВерхнеуровневыеУпаковки.КлючУпаковки = ДанныеУпаковок.КлючУпаковки
	|			И ДанныеУпаковок.ДокументРезерва = &ДокументРезерва
	|ГДЕ
	|	ТранспортныеУпаковки.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НомераУпаковок.НомерУпаковки                   КАК НомерУпаковки,
	|	&МестоДеятельности                             КАК МестоДеятельности,
	|	ВерхнеуровневыеУпаковки.НомерУпаковки          КАК ДокументРезерва,
	|	ВЫБОР
	|		КОГДА &ИспользуетсяЗонаТаможенногоКонтроля
	|			ТОГДА ВерхнеуровневыеУпаковки.НоваяЗонаТаможенногоКонтроля
	|		ИНАЧЕ ДанныеУпаковок.ЗонаТаможенногоКонтроля
	|	КОНЕЦ                                          КАК ЗонаТаможенногоКонтроля,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУпаковокМДЛП.ВУпаковке)  КАК Статус,
	|	&ДатаСтатуса                                   КАК ДатаСтатуса,
	|	ДанныеУпаковок.Владелец                        КАК Владелец,
	|	НомераУпаковок.НомерРодительскойУпаковки       КАК НомерГрупповойУпаковки,
	|	ИСТИНА                                         КАК ГрупповаяУпаковка,
	|	НЕ ПервыйУровеньУпаковок.НомерУпаковки ЕСТЬ NULL  КАК ВложеныПотребительскиеУпаковки
	|ИЗ
	|	Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ТранспортныеУпаковки КАК ТранспортныеУпаковки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.ИерархияГрупповыхУпаковок КАК НомераУпаковок
	|	ПО
	|		НомераУпаковок.Ссылка = ТранспортныеУпаковки.Ссылка
	|		И НомераУпаковок.ИдентификаторСтроки = ТранспортныеУпаковки.ИдентификаторСтроки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПодтвержденныеУпаковки КАК ВерхнеуровневыеУпаковки
	|	ПО
	|		ВерхнеуровневыеУпаковки.НомерУпаковки = ТранспортныеУпаковки.НомерУпаковки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.УпаковкиМДЛП КАК ДанныеУпаковок
	|		ПО
	|			ВерхнеуровневыеУпаковки.НомерУпаковки = ДанныеУпаковок.НомерУпаковки
	|			И ВерхнеуровневыеУпаковки.КлючУпаковки = ДанныеУпаковок.КлючУпаковки
	|			И ДанныеУпаковок.ДокументРезерва = &ДокументРезерва
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Т.НомерРодительскойУпаковки КАК НомерУпаковки
	|		ИЗ
	|			Документ.УведомлениеОПриемкеНовымСобственникомЛПвЗТКМДЛП.НомераУпаковок КАК Т
	|		ГДЕ
	|			Т.Ссылка = &Ссылка) КАК ПервыйУровеньУпаковок
	|	ПО
	|		ПервыйУровеньУпаковок.НомерУпаковки = НомераУпаковок.НомерУпаковки
	|ГДЕ
	|	НомераУпаковок.Ссылка = &Ссылка
	|";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ОтразитьПоступление = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выбрать();
	
	Набор = РегистрыСведений.УпаковкиМДЛП.СоздатьНаборЗаписей();
	Пока ОтразитьПоступление.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Набор.Добавить(), ОтразитьПоступление);
	КонецЦикла;
	Если Набор.Количество() > 0 Тогда
		Набор.Записать(Ложь);
	КонецЕсли;
	
	Если ОтразитьПоступление.Количество() > 0 Тогда
		ОставитьВРезерве = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выгрузить();
		Набор = РегистрыСведений.УпаковкиМДЛП.СоздатьНаборЗаписей();
		Набор.Отбор.ДокументРезерва.Установить(ПараметрыОперации.ДокументРезерва);
		Набор.Загрузить(ОставитьВРезерве);
		Набор.Записать();
	КонецЕсли;
	
	ПроверкаПройдена = ИнтеграцияМДЛП.ПроверитьДоступностьУпаковок(Запрос.МенеджерВременныхТаблиц, ПараметрыОперации, Отказ);
	Если ПроверкаПройдена Тогда
		ИнтеграцияМДЛП.ЗарезервироватьУпаковки(Запрос.МенеджерВременныхТаблиц, ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	СтандартнаяОбработка = Истина;
	ИнтеграцияМДЛППереопределяемый.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияМДЛП.ОтменитьРезерв(Ссылка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли