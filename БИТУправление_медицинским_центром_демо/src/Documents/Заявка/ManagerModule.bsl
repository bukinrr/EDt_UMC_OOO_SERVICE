#Область ПрограммныйИнтерфейс

// Возвращает доступные варианты печати документа.
//  Результатом является Струткура, каждая строка которой соответствует одному из вариантов печати.
// 
// Возвращаемое значение:
//  Структура.
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура;
	СтруктураМакетов.Вставить("Заявка"	,"Заявка");
	СтруктураМакетов.Вставить("ЗаявкаА5","Заявка А5");
	СтруктураМакетов.Вставить("Талон","Талон");
	СтруктураМакетов.Вставить("СтоимостьУслуг", "Предварительная стоимость услуг");
	СтруктураМакетов.Вставить("ПодготовкаКИсследованию"	,"Подготовка к сдаче анализов");
	
	Возврат СтруктураМакетов;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

// Процедура формирует печатную форму документа
//  Название макета печати передается в качестве параметра,
//  по переданному имени макета определяется соответствующая функция печати.
//
// Параметры:
//  СсылкаНаОбъект	 - ДокументСсылка	 - ссылка на распечатываемый документ.
//  ИмяМакета		 - Строка			 - имя макета из структуры печатных форм.
// 
// Возвращаемое значение:
//  ТабличныйДокумент.
//
Функция Печать(СсылкаНаОбъект, ИмяМакета) Экспорт
	
	Перем ТабДокумент;
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "Заявка" Тогда
		ТабДокумент = ПечатьЗаявка(СсылкаНаОбъект);
	ИначеЕсли ИмяМакета = "ЗаявкаА5" Тогда
		ТабДокумент = ПечатьЗаявкаА5(СсылкаНаОбъект);
	ИначеЕсли ИмяМакета = "Талон" Тогда
		ТабДокумент = ПечатьТалон(СсылкаНаОбъект);
	ИначеЕсли ИмяМакета = "СтоимостьУслуг" Тогда
		ТабДокумент = ПечатьСтоимостьУслуг(СсылкаНаОбъект);
	ИначеЕсли ИмяМакета = "ПодготовкаКИсследованию" Тогда
		ТабДокумент = ПечатьПодготовкаКИсследованию(СсылкаНаОбъект);
	КонецЕсли;
	
	Возврат ТабДокумент;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьШапкуТелоПечатиЗаявки(СсылкаНаОбъект,ТабДокумент, Макет, ЧислоДопСтрокРабот, ЧислоСтрокМатериалов, Выборка)
	
	Шапка = "ШапкаТаблицы";
	Строка = "Строка";
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(СсылкаНаОбъект, "Заявка");
	ТабДокумент.Вывести(ОбластьМакета); 		
	ОбластьМакета = Макет.ПолучитьОбласть("Даты");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаРаботы.Номенклатура КАК Работа,
	|	ЗаявкаРаботы.Продолжительность КАК Длительность,
	|	ВЫБОР
	|		КОГДА ЗаявкаРаботы.Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|			ТОГДА ЗаявкаРаботы.Ссылка.Сотрудник
	|		ИНАЧЕ ЗаявкаРаботы.Сотрудник
	|	КОНЕЦ КАК Сотрудник
	|ИЗ
	|	Документ.Заявка.Работы КАК ЗаявкаРаботы
	|ГДЕ
	|	ЗаявкаРаботы.Ссылка = &ТекущийДокумент";
	Выборка = Запрос.Выполнить().Выгрузить();
	
	Если СсылкаНаОбъект.НесколькоСотрудников Тогда
		Шапка = "ШапкаТаблицыСотрудника" ;
		Строка = "СтрокаСотрудника" ;
		Сотрудника = СсылкаНаОбъект.Сотрудник;
		ОбластьМакета.Параметры.ПредставлениеПоставщика = СсылкаНаОбъект.Сотрудник;
		Для каждого СтрокаТабличнойЧасти из Выборка Цикл
			Если НЕ СТрокаТабличнойЧасти.Сотрудник = СсылкаНаОбъект.Сотрудник Тогда
				Если НЕ СтрокаТабличнойЧасти.Сотрудник = Справочники.Сотрудники.ПустаяСсылка() Тогда
					Сотрудника = "" + Сотрудника + ", "  + СтрокаТабличнойЧасти.Сотрудник ; 
					ОбластьМакета.Параметры.ПредставлениеПоставщика = Сотрудника;
				КонецЕсли;
			КонецЕсли; 
		КонецЦикла;
	Иначе
		ОбластьМакета.Параметры.ПредставлениеПоставщика = СсылкаНаОбъект.Сотрудник;
	КонецЕсли;
	ОбластьМакета.Параметры.ПредставлениеПолучателя = СсылкаНаОбъект.Клиент;
	ОбластьМакета.Параметры.Дата = СсылкаНаОбъект.ДатаНачала;
	ОбластьМакета.Параметры.ДатаНачала	= СсылкаНаОбъект.ВремяНачала;
	ОбластьМакета.Параметры.ДатаКонца	= СсылкаНаОбъект.ВремяОкончания;
	ОбластьМакета.Параметры.ДатаРегистрации = СсылкаНаОбъект.Дата;
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Работы");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьШапка = Макет.ПолучитьОбласть(Шапка);
	ОбластьСтрока = Макет.ПолучитьОбласть(Строка);
	ТабДокумент.Вывести(ОбластьШапка);
	НомерСтроки = 0;
	Для Каждого СтрокаТабличнойЧасти Из Выборка Цикл
		НомерСтроки = НомерСТроки +1;
		ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ТабДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;	
	Для ит = 1 по ЧислоДопСтрокРабот Цикл
		НомерСтроки = НомерСтроки +1;	 
		ОбластьМакета = Макет.ПолучитьОбласть(Строка);
		ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
		ТабДокумент.Вывести(ОбластьМакета);	
	КонецЦикла;	
	ОбластьМакета = Макет.ПолучитьОбласть("ГраницаСотрудник");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Материалы");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицыМатериалы");
	ТабДокумент.Вывести(ОбластьМакета);
	Для ит = 1 По ЧислоСтрокМатериалов Цикл
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаМатериалы");
		ОбластьМакета.Параметры.НомерСтроки = Ит+1;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	ОбластьМакета = Макет.ПолучитьОбласть("ГраницаСотрудник");
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("ОтметкиСотрудника");
	ТабДокумент.Вывести(ОбластьМакета);
	
КонецПроцедуры

Функция ПечатьЗаявка(СсылкаНаОбъект)
	
	Перем Выборка;
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет       = Документы.Заявка.ПолучитьМакет("Заявка");
	СформироватьШапкуТелоПечатиЗаявки(СсылкаНаОбъект, ТабДокумент, Макет, 6, 12, Выборка);
	
	Если СсылкаНаОбъект.НесколькоСотрудников Тогда
		Для Каждого СтрокаТабличнойЧасти из Выборка Цикл
			ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
			ОбластьПодпись.Параметры.Сотрудник = СтрокаТабличнойЧасти.Сотрудник;
			ТабДокумент.Вывести(ОбластьПодпись);
		КонецЦикла
	Иначе
		ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
		ОбластьПодпись.Параметры.Сотрудник = СсылкаНаОбъект.Сотрудник;
		ТабДокумент.Вывести(ОбластьПодпись);
	КонецЕсли;	
	
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Заявка_Заявка";
	
	Возврат ТабДокумент;
	
КонецФункции // ПечатьАктаОбОказанииУслуг()

Функция ПечатьЗаявкаА5(СсылкаНаОбъект)
	
	Перем Выборка;
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет       = Документы.Заявка.ПолучитьМакет("ЗаявкаА5");
	СформироватьШапкуТелоПечатиЗаявки(СсылкаНаОбъект, ТабДокумент, Макет, 2, 5, Выборка);
	
	Подпись = Истина;
	Если СсылкаНаОбъект.НесколькоСотрудников Тогда
		Для каждого СтрокаТабличнойЧасти из Выборка Цикл
			Если Подпись Тогда
				ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
				ОбластьПодпись.Параметры.Сотрудник = СтрокаТабличнойЧасти.Сотрудник;
				ТабДокумент.Вывести(ОбластьПодпись);
				Подпись=Ложь;
			Иначе
				ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
				ОбластьПодпись.Параметры.Сотрудник = СтрокаТабличнойЧасти.Сотрудник;
				ТабДокумент.Присоединить(ОбластьПодпись);
				Подпись=Истина;
			КонецЕсли;
		КонецЦикла
	Иначе
		ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
		ОбластьПодпись.Параметры.Сотрудник = СсылкаНаОбъект.Сотрудник;
		ТабДокумент.Вывести(ОбластьПодпись);
	КонецЕсли;	
	
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Заявка_ЗаявкаА5";
	
	Возврат ТабДокумент;
	
КонецФункции 

Функция ПечатьТалон(СсылкаНаОбъект)
	ТабДокумент = новый ТабличныйДокумент;
	Макет=ПолучитьМакет("Талон");
	
	ОбластьШапки = Макет.ПолучитьОбласть("Шапка");
    ОбластьШапки.Параметры.НазваниеОрганизации = ПроцедурыСпециализацииПоставки.ПолучитьРеквизитыОрганизации(СсылкаНаОбъект).НазваниеОрганизации;
	ОбластьШапки.Параметры.ФИО = СсылкаНаОбъект.Клиент.Фамилия + " " + СсылкаНаОбъект.Клиент.Имя + " " + СсылкаНаОбъект.Клиент.Отчество; 	
	ОбластьШапки.Параметры.ДатаРождения = СсылкаНаОбъект.Клиент.ДатаРождения;
	ОбластьШапки.Параметры.Пол = ?(СсылкаНаОбъект.Клиент.Пол=Перечисления.ПолФизическихЛиц.НеУказан,"",СсылкаНаОбъект.Клиент.Пол);
	ОбластьШапки.Параметры.ДатаПриема = Формат(СсылкаНаОбъект.ДатаНачала,"ДФ='dd.MM.yyyy HH:mm'");
	ТабДокумент.Вывести(ОбластьШапки);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаРаботы.Номенклатура КАК Работа,
	|	ЗаявкаРаботы.Продолжительность КАК Длительность,
	|	ВЫБОР
	|		КОГДА ЗаявкаРаботы.Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|			ТОГДА ЗаявкаРаботы.Ссылка.Сотрудник
	|		ИНАЧЕ ЗаявкаРаботы.Сотрудник
	|	КОНЕЦ КАК Врач
	|ИЗ
	|	Документ.Заявка.Работы КАК ЗаявкаРаботы
	|ГДЕ
	|	ЗаявкаРаботы.Ссылка = &ТекущийДокумент";
	Выборка = Запрос.Выполнить().Выгрузить();
	
	ОбластьШапка = Макет.ПолучитьОбласть("ШапкаУслуг");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаУслуг");
	ТабДокумент.Вывести(ОбластьШапка);
	НомерСтроки = 0;
	Для Каждого СтрокаТабличнойЧасти Из Выборка Цикл
		НомерСтроки = НомерСТроки +1;
		ОбластьСтрока.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ТабДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;		

    ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.Врач = СсылкаНаОбъект.Сотрудник;	
	ТабДокумент.Вывести(ОбластьПодвал);
	
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Заявка_Талон";
	
	Возврат ТабДокумент;
КонецФункции

Функция ПечатьСтоимостьУслуг(СсылкаНаОбъект)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОтображатьСетку = Ложь;
	ТабДокумент.АвтоМасштаб = Истина;
	
	Макет = ПолучитьМакет("СтоимостьУслуг");
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	
	РеквизитыОрганизации = ПроцедурыСпециализацииПоставки.ПолучитьРеквизитыОрганизации(СсылкаНаОбъект);	
	ОбластьШапка.Параметры.НазваниеОрганизации = РеквизитыОрганизации.НазваниеОрганизации;
	ОбластьШапка.Параметры.АдресОрганизации = РеквизитыОрганизации.Адрес;
	ОбластьШапка.Параметры.ТелефонОрганизации = РеквизитыОрганизации.Телефон;
	
	ОбластьШапка.Параметры.КлиентФИО = СсылкаНаОбъект.Клиент.Фамилия + " " + СсылкаНаОбъект.Клиент.Имя + " " + СсылкаНаОбъект.Клиент.Отчество;
	Адрес = КонтактнаяИнформацияСервер.ПолучитьКИОбъекта(СсылкаНаОбъект.Клиент, Перечисления.ТипыКонтактнойИнформации.Адрес);
	Телефон = КонтактнаяИнформацияСервер.ПолучитьКИОбъекта(СсылкаНаОбъект.Клиент, Перечисления.ТипыКонтактнойИнформации.Телефон);
	ОбластьШапка.Параметры.КлиентАдрес = Адрес;
	ОбластьШапка.Параметры.КлиентТелефон = Телефон;
	ТабДокумент.Вывести(ОбластьШапка);
		
	ОбластьПланШапка = Макет.ПолучитьОбласть("ОбластьПланШапка");
	ТабДокумент.Вывести(ОбластьПланШапка);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаРаботы.Номенклатура,
	|	ЗаявкаРаботы.Ссылка.ДатаНачала КАК Назначено,
	|	ЗаявкаРаботы.Продолжительность,
	|	ЗаявкаРаботы.ДатаНачала,
	|	ЗаявкаРаботы.ДатаОкончания
	|ПОМЕСТИТЬ УслугиПоЗаявке
	|ИЗ
	|	Документ.Заявка.Работы КАК ЗаявкаРаботы
	|ГДЕ
	|	ЗаявкаРаботы.Ссылка = &Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланыЛеченияПоПриему.Номенклатура,
	|	ПланыЛеченияПоПриему.Продолжительность,
	|	ПланыЛеченияПоПриему.Назначено,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена,
	|	ПланыЛеченияПоПриему.ДатаНачала,
	|	ПланыЛеченияПоПриему.ДатаОкончания
	|ИЗ
	|	УслугиПоЗаявке КАК ПланыЛеченияПоПриему
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, Прейскурант = &Прейскурант) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ПланыЛеченияПоПриему.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура";
	Запрос.УстановитьПараметр("Заявка", СсылкаНаОбъект);
	Запрос.УстановитьПараметр("Дата", СсылкаНаОбъект.Дата);
	Запрос.УстановитьПараметр("Прейскурант", УправлениеНастройками.ПолучитьПрейскурантФилиала(СсылкаНаОбъект.Филиал));
	Выборка = Запрос.Выполнить().Выбрать();
	СуммаИтого = 0;
	Пока Выборка.Следующий() Цикл
		ОбластьПланСтрока = Макет.ПолучитьОбласть("ОбластьПланСтрока");
		ОбластьПланСтрока.Параметры.Заполнить(Выборка);
		
		КоличествоУслуги = 1;
		Если Выборка.Номенклатура.ПродолжительностьЕдиницыУслуги <> 0 Тогда
			ПродолжительностьВМин = (Выборка.ДатаОкончания - Выборка.ДатаНачала)/60;
			КоличествоУслуги = Окр((ПродолжительностьВМин/Выборка.Номенклатура.ПродолжительностьЕдиницыУслуги)+0.5,0,0);
		КонецЕсли;
		СуммаУслуги = Выборка.Цена * КоличествоУслуги;
		ОбластьПланСтрока.Параметры.Количество = КоличествоУслуги;
		ОбластьПланСтрока.Параметры.Сумма = СуммаУслуги;
		
		СуммаИтого = СуммаИтого + СуммаУслуги;
			
		ОбластьПланСтрока.Параметры.Назначено = Формат(Выборка.Назначено,"ДФ=dd.MM.yyyy");
		ТабДокумент.Вывести(ОбластьПланСтрока);
	КонецЦикла;
	ОбластьПланИтог = Макет.ПолучитьОбласть("ОбластьПланИтог");
	ОбластьПланИтог.Параметры.СуммаИтого = ОбщегоНазначенияКлиентСервер.ФорматСумм(СуммаИтого);
	ОбластьПланИтог.Параметры.Валюта = ОбщегоНазначения.ПолучитьКраткоеНаименованиеОсновнойВалюты();
	ТабДокумент.Вывести(ОбластьПланИтог);
	
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Заявка_СтоимостьУслуг";
	
	Возврат ТабДокумент;
	
КонецФункции

Функция ПечатьПодготовкаКИсследованию(СсылкаНаОбъект)
	
	мНоменклатура = СсылкаНаОбъект.Работы.ВыгрузитьКолонку("Номенклатура");
	ТабличныйДокумент = Документы.ДействияНадАнализами.ПолучитьТабДокПодготовкаКИсследованию(мНоменклатура);
	
	Возврат ТабличныйДокумент;

КонецФункции

#КонецОбласти
