#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПериода = КонецДня(ТекущаяДата()) + 1;
	КонецПериода  = НачалоПериода;
		
	ВключаяКлиентовБезТелефона = Истина;
	
	РежимИскомыхСостоянийЗаявок = Элементы.РежимИскомыхСостоянийЗаявок.СписокВыбора[0].Значение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ПроизвестиВыборкуАбонентов(Команда)
	
	Если ЗначениеЗаполнено(НачалоПериода)
		И ЗначениеЗаполнено(КонецПериода)
	Тогда
		Закрыть(ПолучитьКлиентовРасписания());
	Иначе
		Предупреждение("Не указан период заполнения!");
	КонецЕсли;                               	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция ПолучитьКлиентовРасписания()
	
	Если РежимИскомыхСостоянийЗаявок = "ДоПрихода" Тогда
		РазрешенныеСостояния = УправлениеЗаявками.СостоянияЗаявкиДоПриходаКлиента();
	ИначеЕсли РежимИскомыхСостоянийЗаявок = "ПослеПрихода" Тогда
		РазрешенныеСостояния = УправлениеЗаявками.СостоянияЗаявкиПослеНачалаВыполнения();
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Заявка.Клиент КАК Клиент
	|ИЗ
	|	Документ.Заявка КАК Заявка
	|ГДЕ
	|	НЕ Заявка.ПометкаУдаления
	|	И Заявка.ДатаНачала >= НАЧАЛОПЕРИОДА(&ДатаНачала, ДЕНЬ)
	|	И Заявка.ДатаНачала <= КОНЕЦПЕРИОДА(&ДатаОкончания, ДЕНЬ)
	|	И Заявка.Состояние В (&РазрешенныеСостояния)
	|";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
	Запрос.УстановитьПараметр("РазрешенныеСостояния", РазрешенныеСостояния);
	
	Если ЗначениеЗаполнено(Филиал) Тогда
		Запрос.УстановитьПараметр("Филиал", Филиал);
		Запрос.Текст = Запрос.Текст + "
			|	И Заявка.Филиал = &Филиал";
	КонецЕсли;
	
	СписокКонтрагентов = Новый Массив;
	Выгрузка = Запрос.Выполнить().Выгрузить();
	Для Каждого Строка Из Выгрузка Цикл
		
		НомерТелефона = КонтактнаяИнформацияСерверПереопределяемый.НайтиКонтактнуюИнформацию(Строка.Клиент);
		
		Если ВключаяКлиентовБезТелефона
			Или ЗначениеЗаполнено(НомерТелефона)
		Тогда
			СтрокаСписка = Новый Структура;
			СтрокаСписка.Вставить("Контрагент", Строка.Клиент);
			СтрокаСписка.Вставить("НомерТелефона", НомерТелефона);
		
			СписокКонтрагентов.Добавить(СтрокаСписка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокКонтрагентов;
	
КонецФункции

#КонецОбласти