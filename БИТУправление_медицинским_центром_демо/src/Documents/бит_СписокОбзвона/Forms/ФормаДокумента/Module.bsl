#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	Если Объект.Ссылка.Пустая() И Параметры.Свойство("СписокКлиентов") Тогда
		ЗаполнитьСписок(Параметры.СписокКлиентов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура СписокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		строка = Объект.Список.Добавить();
		строка.Контрагент = ВыбранноеЗначение;
		строка.НомерТелефона = КонтактнаяИнформацияСерверПереопределяемый.НайтиКонтактнуюИнформацию(строка.Контрагент);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокКонтрагентПриИзменении(Элемент)
	Элементы.Список.ТекущиеДанные.НомерТелефона = КонтактнаяИнформацияСерверПереопределяемый.НайтиКонтактнуюИнформацию(Элементы.Список.ТекущиеДанные.Контрагент);
	Элементы.Список.ТекущиеДанные.Обработан = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура СписокНомерТелефонаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Элементы.Список.ТекущиеДанные.Контрагент) Тогда
		массивНомераТелефонов = КонтактнаяИнформацияСерверПереопределяемый.НайтиКонтактнуюИнформацию(Элементы.Список.ТекущиеДанные.Контрагент, , , Ложь);
		Элемент.СписокВыбора.ЗагрузитьЗначения(массивНомераТелефонов);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОчиститьФлагОбработки(Команда)
	Для Каждого строкаСписка Из Объект.Список Цикл
		Если строкаСписка.Обработан Тогда
			строкаСписка.Обработан = Ложь;
			Модифицированность = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРасписанию(Команда)
	
	СписокКлиентов = ОткрытьФормуМодально("Документ.бит_СписокОбзвона.Форма.ЗаполнениеПоРасписанию",Новый Структура("Филиал", Объект.Филиал));
	Если СписокКлиентов <> Неопределено Тогда
		Для Каждого СтрокаСписка Из СписокКлиентов Цикл
			
			СуществующиеЗначения = Объект.Список.НайтиСтроки(Новый Структура("Контрагент", СтрокаСписка.Контрагент));
			Если СуществующиеЗначения.Количество() = 0 Тогда
			
				НоваяСтрока = Объект.Список.Добавить();
				НоваяСтрока.Контрагент	  = СтрокаСписка.Контрагент;
				НоваяСтрока.НомерТелефона = КонтактнаяИнформацияКлиентСерверПереопределяемый.НормализоватьСотовыйТелефон(СтрокаСписка.НомерТелефона, Ложь);
				
			КонецЕсли;
				
		КонецЦикла;
	КонецЕсли;			
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписок(СписокКлиентов)
	
	Для Каждого Клиент Из СписокКлиентов Цикл
		Строка = Объект.Список.Добавить();
		Строка.Контрагент = Клиент.Значение;
		Строка.НомерТелефона = КонтактнаяИнформацияСерверПереопределяемый.НайтиКонтактнуюИнформацию(Клиент.Значение);
		Строка.Обработан = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти