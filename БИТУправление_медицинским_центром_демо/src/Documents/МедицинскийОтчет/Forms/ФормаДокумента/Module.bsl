
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	мСкопированаФорма	= Неопределено;
	мСохраненныйДок		= Неопределено;
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НеОтображатьПредупреждение	= Параметры.НеОтображатьПредупреждение;
	мСохраненныйДок				= Параметры.ЗначениеКопирования;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		И ЗначениеЗаполнено(Объект.ВыбраннаяФорма)
	Тогда
		мСкопированаФорма = Объект.ВыбраннаяФорма;
	КонецЕсли;
	
	ИсточникОтчета = "";
	Объект.Свойство("ИсточникОтчета", ИсточникОтчета);
	
	Если Не(РеквизитФормыВЗначение("Объект").ЭтоНовый())
		Или (мСкопированаФорма <> Неопределено)
	Тогда
		
		Если Объект.ИсточникОтчета = "МедицинскийОтчетСведенияОДолеДоходовОтОбразовательнойИлиМедицинскойДеятельности" Тогда
			Объект.ИсточникОтчета = "МедицинскийОтчетСведенияОДолеДоходовОтОбразовательнойИлиМедДеятельности";
		КонецЕсли;
		
		ПравоДоступаКОтчету = МедицинскаяОтчетностьВызовСервера.ПравоДоступаКМедицинскомуОтчету(Объект.ИсточникОтчета);
		
		Если ПравоДоступаКОтчету = Ложь Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Недостаточно прав.'");
			Сообщение.Сообщить();
			Возврат;
			
		ИначеЕсли ПравоДоступаКОтчету = Неопределено Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не удалось открыть сохраненные данные. Отчет не найден.'");
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		
		// Возвращает типы ВнешнийОтчетОбъект.<Имя> и ОтчетМенеджер.<Имя>
		ВариантФормыОтчета = Неопределено;
		ОбъектОтчет = МедицинскаяОтчетность.МедОтчеты(Объект.ИсточникОтчета);
		
		Если ОбъектОтчет = Неопределено Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не удалось открыть сохраненные данные. Отчет не найден.'");
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		
		ОткрытьФормуОтчета = Истина;
		ВариантФормыОтчета = ?(СтрНайти(ОбъектОтчет, "ОтчетМенеджер") > 0, "Отчет.", "ВнешнийОтчет.")
		
	ИначеЕсли РеквизитФормыВЗначение("Объект").ЭтоНовый() Тогда
		
		ОткрытьФормуОтчета = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Отказ = Истина;
	
	Если ОткрытьФормуОтчета Тогда
		
		СтандартнаяОбработка = Истина;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета",		НачалоДня(Объект.ДатаНачала));
		ПараметрыФормы.Вставить("мСкопированаФорма",			мСкопированаФорма);
		ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета",		КонецДня(Объект.ДатаОкончания));
		ПараметрыФормы.Вставить("мПериодичность",				Объект.Периодичность);
		ПараметрыФормы.Вставить("Организация",					Объект.Организация);
		ПараметрыФормы.Вставить("мВыбраннаяФорма",				Объект.ВыбраннаяФорма);
		ПараметрыФормы.Вставить("ВидДокумента",					Объект.Вид);
		ПараметрыФормы.Вставить("НеОтображатьПредупреждение",	НеОтображатьПредупреждение);
		ПараметрыФормы.Вставить("ПредставлениеВидаОтчета",		Объект.НаименованиеОтчета);
		ПараметрыФормы.Вставить("мСохраненныйДок",				мСохраненныйДок);
		
		ВыбраннаяФорма = Объект.ВыбраннаяФорма;
		
		СтартоваяФорма = "ОсновнаяФорма";
		ЭтоСтартоваяФорма = ВыбраннаяФорма = СтартоваяФорма;
		
		Если мСкопированаФорма <> Неопределено Тогда
			Если (ВладелецФормы.ИмяФормы = "Документ.МедицинскийОтчет.Форма.МедицинскаяОтчетность")
				Или (ВладелецФормы.ИмяФормы = "Документ.МедицинскийОтчет.Форма.ФормаСписка")
			Тогда
				ЭтоСтартоваяФорма = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			
			КлючУникальности = Объект.Ссылка;
			
			Если мСкопированаФорма = Неопределено Тогда
				
				ПараметрыФормы.Вставить("мСохраненныйДок", Объект.Ссылка);
				
				Попытка
					ОтчетМетаданные = МедицинскаяОтчетностьВызовСервера.ОтчетМетаданные(Объект.ИсточникОтчета);
					ИмяОтчета = ОтчетМетаданные.Имя;
				Исключение
					ИмяОтчета = Объект.ИсточникОтчета;
				КонецПопытки;
				
				ИмяФормыОтчета = "Документ.МедицинскийОтчет.Форма.ФормаОтчета";
				
				Если ЭтоСтартоваяФорма Тогда
					КлючУникальностиФормы = ИмяФормыОтчета;
				Иначе
					КлючУникальностиФормы = КлючУникальности;
				КонецЕсли;
				
				ФормаОтчета = ПолучитьФорму(ИмяФормыОтчета, ПараметрыФормы, , КлючУникальностиФормы);
				ФормаОтчета.СтруктураРеквизитовФормы.Филиал = Объект.Филиал;
				
			Иначе
				ИмяФормыОтчета = ВариантФормыОтчета + Объект.ИсточникОтчета + ".Форма.ОсновнаяФорма";
				ФормаОтчета = ПолучитьФорму(ИмяФормыОтчета, ПараметрыФормы, , ИмяФормыОтчета);
				ФормаОтчета.Организация = Объект.Организация;
			КонецЕсли;
			
		Исключение
			
			Сообщение = Новый СообщениеПользователю;
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			
			Если ИнформацияОбОшибке.Причина = Неопределено Тогда
				Сообщение.Текст = НСтр("ru='При открытии формы Медицинского отчета произошла ошибка.'");
			ИначеЕсли СтрНайти(ИнформацияОбОшибке.Причина.Описание, ".Форма.") > 0 Тогда
				Сообщение.Текст = НСтр("ru='Отчет не может быть открыт. Устаревшая редакция формы отчета не поддерживается текущей версией конфигурации.'");
			Иначе
				Сообщение.Текст = ИнформацияОбОшибке.Причина.Описание;
			КонецЕсли;
			
			Сообщение.Сообщить();
			
			Возврат;
			
		КонецПопытки;
		
		Если СтандартнаяОбработка Тогда
			
			Если ЭтоСтартоваяФорма Тогда
				
				// Сначала попробуем найти его среди открытых стартовых форм.
				// Необходимо для предотвращения открытия нескольких стартовых форм одного отчета.
				НайденоОкно = Ложь;
				
				МедицинскаяОтчетностьКлиент.ВебКлиентНайтиАктивизироватьОкно(ИмяФормыОтчета, ЭтаФорма, НайденоОкно);
				
				Если НайденоОкно <> Неопределено Тогда
					Если НайденоОкно Тогда
						Возврат;
					КонецЕсли;
				КонецЕсли;
				
				Если ВладелецФормы <> Неопределено Тогда
					ФормаОтчета.ВладелецФормы = ВладелецФормы;
					ВладелецФормы.Активизировать();
				КонецЕсли;
				
				ФормаОтчета.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			КонецЕсли;
			
			ФормаОтчета.ЗакрыватьПриЗакрытииВладельца = Ложь;
			ФормаОтчета.Открыть();
		КонецЕсли;
	Иначе
		ОткрытьФорму("Справочник.МедицинскиеОтчеты.Форма.ФормаСписка");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти