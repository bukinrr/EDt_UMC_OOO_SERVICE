#Область ПрограммныйИнтерфейс

// Возвращает доступные варианты печати документа.
//  Результатом является Струткура, каждая строка которой соответствует одному из вариантов печати.
// 
// Возвращаемое значение:
//  Структура.
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура;
	СтруктураМакетов.Вставить("ПКО", НСтр("ru='Приходный кассовый ордер'"));
	
	Возврат СтруктураМакетов;

КонецФункции // ПолучитьТаблицуПечатныхФорм()

// Процедура формирует печатную форму документа
//  Название макета печати передается в качестве параметра,
//  по переданному имени макета определяется соответствующая функция печати.
//
// Параметры:
//  СсылкаНаОбъект	 - ДокументСсылка	 - ссылка на распечатываемый документ.
//  ИмяМакета		 - Строка			 - имя макета из структуры печатных форм.
// 
// Возвращаемое значение:
//  ТабличныйДокумент.
//
Функция Печать(СсылкаНаОбъект, ИмяМакета) Экспорт
	
	Перем ТабДокумент;
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "ПКО" Тогда
		
		ТабДокумент = ПечатьПКО(СсылкаНаОбъект);
		
	КонецЕсли;
	
	Возврат ТабДокумент;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция формирует табличный документ с печатной формой накладной,
//  разработанной методистами
//
// Параметры:
//  СсылкаНаОбъект	 - ДокументСсылка.ПриходныйКассовыйОрдер - печатаемый документ.
// 
// Возвращаемое значение:
//  ТабличныйДокумент - печатная форма накладной.
//
Функция ПечатьПКО(СсылкаНаОбъект)

	ВалютаДокумента = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ВалютаУчета");
	Если Не ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ВалютаДокумента = Справочники.Валюты.Рубль;
	КонецЕсли;
	
	ОтветственныеЛица = ПроцедурыСпециализацииПоставки.ОтветственныеЛицаФилиала(СсылкаНаОбъект.Филиал);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.Номер,
	|	ПриходныйКассовыйОрдер.Дата КАК ДатаДокумента,
	|	ПриходныйКассовыйОрдер.Клиент,
	|	ПриходныйКассовыйОрдер.Клиент.Представление КАК Клиент,
	|	ПриходныйКассовыйОрдер.ПринятоОт КАК ПринятоОт,
	|	ПриходныйКассовыйОрдер.Основание КАК Основание,
	|	ПриходныйКассовыйОрдер.Приложение КАК Приложение,
	|	ПриходныйКассовыйОрдер.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.Ссылка = &ТекущийДокумент";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриходныйКассовыйОрдер_КО1";
	Макет       = Документы.ПриходныйКассовыйОрдер.ПолучитьМакет("КО1");
	
	// Выводим шапку ПКО

	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	
	РеквизитыОрганизации = ПроцедурыСпециализацииПоставки.ПолучитьРеквизитыОрганизации(СсылкаНаОбъект);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = РеквизитыОрганизации.НазваниеОрганизации;
	ОбластьМакета.Параметры.ОрганизацияПоОКПО		 = РеквизитыОрганизации.ОКПО;
	
	ОбластьМакета.Параметры.СуммаРубКоп       = Формат(Шапка.Сумма,"ЧЦ=15; ЧДЦ=2");
	ОбластьМакета.Параметры.Сумма		      = ОбластьМакета.Параметры.СуммаРубКоп ;
	ОбластьМакета.Параметры.СуммаПрописью     = ОбщегоНазначения.СформироватьСуммуПрописью(Шапка.Сумма, ВалютаДокумента);
	ОбластьМакета.Параметры.ДатаДокумента     = Шапка.ДатаДокумента;
	ОбластьМакета.Параметры.НомерДокумента    = ОбщегоНазначения.ПолучитьНомерНаПечать(Шапка);
		
	ОбластьМакета.Параметры.ПринятоОт  = Шапка.ПринятоОт;
	ОбластьМакета.Параметры.Основание  = СсылкаНаОбъект.Основание;
	ОбластьМакета.Параметры.Приложение = СсылкаНаОбъект.Приложение;
	
	ОбластьМакета.Параметры.ФИОГлавногоБухгалтера = ОбщегоНазначения.ФамилияИнициалыФизЛица(ОтветственныеЛица.ГлавныйБухгалтер);
	ОбластьМакета.Параметры.ФИОКассира = ОбщегоНазначения.ФамилияИнициалыФизЛица(ОтветственныеЛица.Кассир);
	
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьПКО()

#КонецОбласти