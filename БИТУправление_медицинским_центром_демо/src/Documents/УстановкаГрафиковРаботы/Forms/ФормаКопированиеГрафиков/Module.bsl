#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("МесяцГрафика") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НачалоМесяца(Параметры.МесяцГрафика)) Тогда
		ПрошлыйМесяц = НачалоМесяца(ДобавитьМесяц(Параметры.МесяцГрафика, -1));
	Иначе
		ПрошлыйМесяц = НачалоМесяца(ДобавитьМесяц(ТекущаяДата(), -1));
	КонецЕсли;
	
	ПериодПо = НачалоДня(КонецМесяца(ПрошлыйМесяц));
	ПериодОт = ПериодПо - 7 * 86400;
		
	Для Каждого Сотрудник Из Параметры.Сотрудники Цикл
		НоваяСтрока = СписокСотрудников.Добавить();
		НоваяСтрока.Сотрудник = Сотрудник;
		НоваяСтрока.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкопироватьГрафик(Команда)
	
	УдаляемыеСтроки = Новый Массив;
	Для Каждого СтрокаСписка Из СписокСотрудников Цикл
		Если Не СтрокаСписка.Выбран Тогда
			УдаляемыеСтроки.Добавить(СтрокаСписка);
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаСписка Из УдаляемыеСтроки Цикл
		СписокСотрудников.Удалить(СтрокаСписка);
	КонецЦикла;
	
	ПараметрыКопирования = Новый Структура("СписокСотрудников, ПериодОт,ПериодПо", СписокСотрудников, ПериодОт, ПериодПо);
	Оповестить("ВыполнитьУстановкуГрафиковРаботы",ПараметрыКопирования,ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	ИзменитьЗначениеВыбранСпискаСотрудников(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыборУВсех(Команда)
	ИзменитьЗначениеВыбранСпискаСотрудников(Ложь);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодОтПриИзменении(Элемент)
	ПериодПо = ПериодОт + 86400 * 6;
КонецПроцедуры

&НаКлиенте
Процедура СписокСотрудниковПериодОтПриИзменении(Элемент)
	
	ТекДанные = Элементы.СписокСотрудников.ТекущиеДанные;
	ТекДанные.ПериодПо = ТекДанные.ПериодОт + 86400 * 6;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьЗначениеВыбранСпискаСотрудников(Значение)
	Для Каждого Стр Из СписокСотрудников Цикл
		Стр.Выбран = Значение;	
	КонецЦикла;
КонецПроцедуры

#КонецОбласти