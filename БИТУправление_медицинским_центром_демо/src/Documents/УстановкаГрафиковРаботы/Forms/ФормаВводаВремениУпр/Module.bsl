#Область РазделОписанияПеременных

&НаКлиенте
Перем мНаименованиеТекущегоВидаВремени;

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.НадписьЗаголовок.Заголовок = НСтр("ru='Установить время на'") + " " + Формат(Параметры.Дата,"ДФ=dd.MM.yyyy");
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры,	"ВремяНачала, ВремяОкончания, Смена, Кабинет, Сотрудник, 
													|РежимВремяСмена, ВидВремени, Название, Комментарий");
													
	Если Параметры.ЗапретитьСмены Тогда
		РежимВремяСмена = 1;
		Элементы.РежимВремяСмена.ТолькоПросмотр = Истина;
	Иначе
		РежимВремяСмена = ?(ЗначениеЗаполнено(РежимВремяСмена), РежимВремяСмена, 1);
	КонецЕсли;
	
	Если РежимВремяСмена = 1 Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.ВремяНачала;
	Иначе
		ЭтаФорма.ТекущийЭлемент = Элементы.Смена;
	КонецЕсли;
	
	Элементы.Кабинет.Видимость	 = Параметры.ВыбиратьКабинет;
	Элементы.Сотрудник.Видимость = Параметры.ВыбиратьСотрудника;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РежимВремяСменаПриИзменении(Неопределено);
	УстановитьДоступностьЭлементовОписанияСобытия();
	
	мНаименованиеТекущегоВидаВремени = Строка(ВидВремени);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	// Проверка
	СоощениеОбОшибке = "";
	Если РежимВремяСмена = 2 Тогда
		Если Не ЗначениеЗаполнено(Смена) Тогда
			СоощениеОбОшибке = НСтр("ru='Смена не указана!'");
		КонецЕсли;
	Иначе
		Если (ВремяНачала >= ВремяОкончания И ВремяОкончания <> '00010101000000') Тогда
			СоощениеОбОшибке = НСтр("ru='Время начала должно быть меньше времени окончания!'");
		КонецЕсли;			
	КонецЕсли;
	Если ЗначениеЗаполнено(СоощениеОбОшибке) Тогда
		Предупреждение(СоощениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если ГрафикиСотрудников.ЭтоОбычноеРабочееВремя(ВидВремени) Тогда
		Название = "";
		Комментарий = "";
	КонецЕсли;	
	
	// Фомирование результата
	Результат = Новый Структура("Сотрудник, Кабинет, ВидВремени, Название, Комментарий, ВремяНачала,ВремяОкончания,Смена", 
								 Сотрудник, Кабинет, ВидВремени, Название, Комментарий);
	Если РежимВремяСмена = 2 Тогда
		Результат.Смена = Смена;
	Иначе 
		Результат.ВремяНачала = ВремяНачала;
		Результат.ВремяОкончания = ВремяОкончания;
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РежимВремяСменаПриИзменении(Элемент)
	
	Элементы.Смена.Доступность = РежимВремяСмена = 2;
	Элементы.ВремяНачала.Доступность = РежимВремяСмена <> 2;
	Элементы.ВремяОкончания.Доступность = РежимВремяСмена <> 2;
	
	Элементы.ГруппаДанныеВидаСобытия.Доступность = РежимВремяСмена <> 2;
	
КонецПроцедуры

&НаКлиенте
Процедура СобытиеВидВремениПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ВидВремени) Тогда
		ВидВремени = ПредопределенноеЗначение("Справочник.ВидыВремениГрафика.РабочееВремя");
	КонецЕсли;
	
	УстановитьДоступностьЭлементовОписанияСобытия();
	
	Если ВидВремени = ПредопределенноеЗначение("Справочник.ВидыВремениГрафика.РабочееВремя") Тогда
		Название = "";
		мНаименованиеТекущегоВидаВремени = "";
	Иначе
		НаименованиеВидаВремени = Строка(ВидВремени);
		Если СокрЛП(Название) = мНаименованиеТекущегоВидаВремени
			Или ПустаяСтрока(Название)
		Тогда
			Название = НаименованиеВидаВремени;
		КонецЕсли;
		мНаименованиеТекущегоВидаВремени = НаименованиеВидаВремени;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементовОписанияСобытия()
	
	ЭтоПростоеРабочееВремя = ГрафикиСотрудников.ЭтоОбычноеРабочееВремя(ВидВремени);
	Элементы.СобытиеНазвание.Доступность	= Не ЭтоПростоеРабочееВремя;
	Элементы.СобытиеКомментарий.Доступность	= Не ЭтоПростоеРабочееВремя;
	
КонецПроцедуры

#КонецОбласти
