#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	ПодписанЭП = Ложь;
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	// Новый алгоритм хранения вредных факторов клиента в ПМО
	Если ЗначениеЗаполнено(Ссылка) Тогда
		СсылкаСПМО = Ссылка;
	Иначе
		СсылкаСПМО = ПолучитьСсылкуНового();
		Если Не ЗначениеЗаполнено(СсылкаСПМО) Тогда
			УстановитьСсылкуНового(Документы.СписокНаПрохождениеМедосмотра.ПолучитьСсылку(Новый УникальныйИдентификатор));
			СсылкаСПМО = ПолучитьСсылкуНового();
		КонецЕсли;
	КонецЕсли;	
	Если Не (ДополнительныеСвойства.Свойство("НеВыполнятьСозданиеПМО") И ДополнительныеСвойства.НеВыполнятьСозданиеПМО) Тогда
		СоздатьИзменитьПрохожденияМОПередЗаписью(Отказ, СсылкаСПМО);
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьИзменитьПрохожденияМОПередЗаписью(Отказ, СсылкаСПМО)
	
	// Получаем выборку клиент-документ ПМО. Если был изменен приказ изменяем его и в ПМО
	// если не было ПМО, создаем новый
	
	// Перед записью изменяем уже имеющиеся ПМО, при записи создаем новые если требуется
	Запрос = Новый Запрос;
	Запрос.Текст =  
		"ВЫБРАТЬ
		|	&СсылкаСПМО КАК Ссылка,
		|	СписокНаПрохождениеМедосмотраКлиенты.НомерСтроки КАК НомерСтроки,
		|	СписокНаПрохождениеМедосмотраКлиенты.Клиент КАК Клиент,
		|	СписокНаПрохождениеМедосмотраКлиенты.ТипМедосмотра КАК ТипМедосмотра,
		|	СписокНаПрохождениеМедосмотраКлиенты.ПричинаНеявки КАК ПричинаНеявки,
		|	СписокНаПрохождениеМедосмотраКлиенты.ФИОКлиента КАК ФИОКлиента,
		|	СписокНаПрохождениеМедосмотраКлиенты.Профессия КАК Профессия,
		|	СписокНаПрохождениеМедосмотраКлиенты.ЦехУчасток КАК ЦехУчасток
		|ПОМЕСТИТЬ СписокНаПрохождениеМедосмотраКлиенты
		|ИЗ
		|	&Клиенты КАК СписокНаПрохождениеМедосмотраКлиенты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокНаПрохождениеМедосмотраКлиенты.Клиент КАК Клиент,
		|	СписокНаПрохождениеМедосмотраКлиенты.Ссылка КАК СПМО,
		|	&Филиал КАК Филиал,
		|	СписокНаПрохождениеМедосмотраКлиенты.Профессия КАК Профессия,
		|	СписокНаПрохождениеМедосмотраКлиенты.ЦехУчасток КАК ЦехУчасток,
		|	СписокНаПрохождениеМедосмотраКлиенты.ФИОКлиента КАК ФИОКлиента,
		|	СписокНаПрохождениеМедосмотраКлиенты.ТипМедосмотра КАК ТипМедосмотра,
		|	&ВидМО КАК ВидМО,
		|	ПрохождениеМедосмотра.Ссылка КАК СсылкаПМО,
		|	СписокНаПрохождениеМедосмотраКлиенты.ПричинаНеявки КАК ПричинаНеявки
		|ИЗ
		|	СписокНаПрохождениеМедосмотраКлиенты КАК СписокНаПрохождениеМедосмотраКлиенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПрохождениеМедосмотра КАК ПрохождениеМедосмотра
		|		ПО СписокНаПрохождениеМедосмотраКлиенты.Ссылка = ПрохождениеМедосмотра.СписокНаПрохождениеМедосмотра
		|			И СписокНаПрохождениеМедосмотраКлиенты.Клиент = ПрохождениеМедосмотра.Клиент";
	
	Запрос.УстановитьПараметр("Клиенты", Клиенты.Выгрузить());
	Запрос.УстановитьПараметр("ВидМО", ВидМО);
	Запрос.УстановитьПараметр("СсылкаСПМО", СсылкаСПМО);
	Запрос.УстановитьПараметр("Филиал", Филиал);
	Выгрузка = Запрос.Выполнить().Выгрузить();
	ОбработанныеДокументы = Новый Массив;
	ИзмененныеДокументы = Новый Массив;
	НеобходимоСоздать = Новый Массив;
	
	Для Каждого Эл Из Выгрузка Цикл
		СсылкаПМО = Эл.СсылкаПМО;
		Если ЗначениеЗаполнено(Эл.СсылкаПМО) Тогда 
			ОбъектПМО = СсылкаПМО.ПолучитьОбъект();
			ТребуетсяЗапись = Ложь;
			
			Если ОбъектПМО.ПометкаУдаления Тогда
				ОбъектПМО.ПометкаУдаления = Ложь;
				Если ЗначениеЗаполнено(ОбъектПМО.ДатаЗавершения) Тогда
					// Фактически не возможно, но гепотетически может быть все
					Продолжить;
				КонецЕсли;
				ТребуетсяЗапись = Истина;
			КонецЕсли;
			
			Если ОбъектПМО.ВидМО <> ВидМО Тогда 
				ОбъектПМО.ВидМО = ВидМО;
				ТребуетсяЗапись = Истина;
				Если ЗначениеЗаполнено(ОбъектПМО.ДатаЗавершения) Тогда
					Отказ = Истина;
					ТекстСообщения = "Невозможно изменить вид медосмотра, т.к. " 
									+ Строка(ОбъектПМО.Ссылка)
									+ " завершено с другим видом медосмотра";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если ТребуетсяЗапись Тогда
				Документы.ПрохождениеМедосмотра.УбратьЛишниеВредности(ОбъектПМО.ФакторыИУсловияРаботы, ОбъектПМО.ВидМО);
				ОбъектПМО.Записать();
				ИзмененныеДокументы.Добавить(ОбъектПМО.Ссылка);
			КонецЕсли;
			
			ОбработанныеДокументы.Добавить(СсылкаПМО);
		Иначе
			НеобходимоСоздать.Добавить(Эл);
		КонецЕсли;
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("НеобходимоСоздать", НеобходимоСоздать);
	ДополнительныеСвойства.Вставить("ОбработанныеДокументы", ОбработанныеДокументы);
	ДополнительныеСвойства.Вставить("ИзмененныеДокументы", ИзмененныеДокументы);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Не (ДополнительныеСвойства.Свойство("НеВыполнятьСозданиеПМО") И ДополнительныеСвойства.НеВыполнятьСозданиеПМО) Тогда
		СоздатьИзменитьПрохожденияМОПриЗаписи();
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьИзменитьПрохожденияМОПриЗаписи()
	
	Для Каждого Эл Из ДополнительныеСвойства.НеобходимоСоздать Цикл
		Если ЗначениеЗаполнено(Эл.ПричинаНеявки) Тогда
			Продолжить;
		КонецЕсли;
		
		ПоляДокумента = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Эл);
		ПоляДокумента.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
		Если ДополнительныеСвойства.Свойство("НеВыполнятьЗаполнениеВФ") Тогда
			ПоляДокумента.Вставить("НеВыполнятьЗаполнениеВФ", ДополнительныеСвойства.НеВыполнятьЗаполнениеВФ);
		КонецЕсли;
		
		СсылкаПМО = Документы.ПрохождениеМедосмотра.СоздатьПрохождениеМедосмотра(ПоляДокумента);
		ДополнительныеСвойства.ОбработанныеДокументы.Добавить(СсылкаПМО);
	КонецЦикла;
	
	// Не обработанные на предыдущем шаге ПМО, пометим на удаление, т.к. их уже не имеется в данном списке 
	// (Например удалили Клиента из ПМО)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрохождениеМедосмотра.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПрохождениеМедосмотра КАК ПрохождениеМедосмотра
	|ГДЕ
	|	НЕ ПрохождениеМедосмотра.Ссылка В (&СписокОбработанныхПМО)
	|	И ПрохождениеМедосмотра.ПометкаУдаления = ЛОЖЬ
	|	И ПрохождениеМедосмотра.Проведен = ЛОЖЬ
	|	И ПрохождениеМедосмотра.СписокНаПрохождениеМедосмотра = &СписокНаПрохождениеМедосмотра";
	
	Запрос.УстановитьПараметр("СписокОбработанныхПМО", ДополнительныеСвойства.ОбработанныеДокументы);
	Запрос.УстановитьПараметр("СписокНаПрохождениеМедосмотра", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбъектПМО = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектПМО.УстановитьПометкуУдаления(Истина);
		ДополнительныеСвойства.ИзмененныеДокументы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	ДополнительныеСвойства.Удалить("НеобходимоСоздать");
	ДополнительныеСвойства.Удалить("ОбработанныеДокументы");
	
КонецПроцедуры

#КонецОбласти