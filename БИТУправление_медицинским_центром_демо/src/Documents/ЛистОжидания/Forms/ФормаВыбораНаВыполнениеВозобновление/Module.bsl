#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Режим) Тогда 
		Отказ = Истина;
	КонецЕсли;
	
	Если Параметры.Режим = "ОтменаЗаявки" Тогда
		Элементы.ДекорацияСуществующиеЛисты.Заголовок = НСтр("ru='По отмененной заявке есть листы ожидания, если их нужно возобновить, выберите их из списка.'");
	ИначеЕсли Параметры.Режим = "ЗаписьЗаявки" Тогда 
		Элементы.ДекорацияСуществующиеЛисты.Заголовок = НСтр("ru='У клиента есть актуальные листы ожидания, если их нужно закрыть, выберите их из списка.'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Параметры.СписокЛистов) Тогда 
		РаботаСФормамиКлиент.УстановитьОтборСписка("Ссылка", Параметры.СписокЛистов, Список, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СписокВыборНаСервере(ВыбраннаяСтрока, Параметры.Режим, Параметры.Заявка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Выбрать(Команда)
	
	СписокВыборНаСервере(Элементы.Список.ВыделенныеСтроки, Параметры.Режим, Параметры.Заявка);
	Оповестить("ЗаявкаПоДокументуЗапись", Новый Структура("ДокументЗаписи, Заявка", Неопределено, Параметры.Заявка), Параметры.Заявка);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СписокВыборНаСервере(Знач ВыбранныеСтроки, Режим, Заявка)
	
	мсВыбранныеСтроки = Новый Массив;
	Если ТипЗнч(ВыбранныеСтроки) = Тип("Массив") Тогда 
		мсВыбранныеСтроки = ВыбранныеСтроки;
	ИначеЕсли ТипЗнч(ВыбранныеСтроки) = Тип("ДокументСсылка.ЛистОжидания") Тогда 
		мсВыбранныеСтроки.Добавить(ВыбранныеСтроки);
	Иначе
		Возврат;
	КонецЕсли;
	
	Для Каждого элЛист Из мсВыбранныеСтроки Цикл
		ЛистОбъект = элЛист.ПолучитьОбъект();
		Если Режим = "ОтменаЗаявки" Тогда 
			ЛистОбъект.ДокументВыполнения = Неопределено;
			ЛистОбъект.Выполнен = Ложь;
		ИначеЕсли Режим = "ЗаписьЗаявки" Тогда 
			ЛистОбъект.ДокументВыполнения = Заявка;
			ЛистОбъект.Выполнен = Истина;
		Иначе
			Возврат;
		КонецЕсли;
		ЛистОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти