#Область ПрограммныйИнтерфейс

// Возвращает доступные варианты печати документа.
//  Результатом является Струткура, каждая строка которой соответствует одному из вариантов печати.
// 
// Возвращаемое значение:
//  Структура.
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура;
	СтруктураМакетов.Вставить("ОписьЗаказов", "Опись заказов");

	Возврат СтруктураМакетов;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

// Процедура формирует печатную форму документа
//  Название макета печати передается в качестве параметра,
//  по переданному имени макета определяется соответствующая функция печати.
//
// Параметры:
//  СсылкаНаОбъект	 - ДокументСсылка	 - ссылка на распечатываемый документ.
//  ИмяМакета		 - Строка			 - имя макета из структуры печатных форм.
// 
// Возвращаемое значение:
//  ТабличныйДокумент.
//
Функция Печать(СсылкаНаОбъект, ИмяМакета) Экспорт
	
	Перем ТабДокумент;
	
	Если ИмяМакета = "ОписьЗаказов" Тогда
		ТабДокумент = ПечатьОписьЗаказов(СсылкаНаОбъект);
	КонецЕсли;
	
	Возврат ТабДокумент;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьОписьЗаказов(СсылкаНаОбъект)
	
	// Получение формы лаборатории
	ТабДокумент = ЛабораторияСервер.ПечатьОписиЗаказовДляЛаборатории(СсылкаНаОбъект);
	Если ТабДокумент <> Неопределено Тогда
		Возврат ТабДокумент;
	КонецЕсли;
	
	// Печать общей формы
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.ОтображатьЗаголовки = Истина;
	ТабДокумент.ОтображатьСетку = Ложь;
	
	Лаборатория = СсылкаНаОбъект.Лаборатория;
	
	Макет = ПолучитьМакет("ОписьЗаказов");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.МестоЗабора = ЛабораторияСервер.ПолучитьМестоЗабораИсследований(Лаборатория, СсылкаНаОбъект.Филиал);
	ОбластьМакета.Параметры.ДатаФормирования = Формат(ТекущаяДата(), "ДЛФ=DD");
	ТабДокумент.Вывести(ОбластьМакета);
	
	мсОписьЗаказов = СсылкаНаОбъект.СписокЗаказов.ВыгрузитьКолонку("ЗаказВЛабораторию");
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЗаказВоВнешнююЛабораториюИсследования.ИД КАК ИД,
	                      |	ЗаказВоВнешнююЛабораториюИсследования.КодПробы КАК КодПробы,
	                      |	ЗаказВоВнешнююЛабораториюИсследования.Ссылка.Лаборатория КАК Лаборатория
	                      |ПОМЕСТИТЬ Заказ
	                      |ИЗ
	                      |	Документ.ЗаказВоВнешнююЛабораторию.Исследования КАК ЗаказВоВнешнююЛабораториюИсследования
	                      |ГДЕ
	                      |	ЗаказВоВнешнююЛабораториюИсследования.Ссылка В(&мсОписьЗаказов)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ДействияНадАнализамиКонтейнеры.КодПробы КАК КодПробы,
	                      |	ДействияНадАнализамиКонтейнеры.БиоматериалПредставление КАК Биоматериал,
	                      |	ДействияНадАнализамиКонтейнеры.КонтейнерПредставление КАК Контейнер,
	                      |	ДействияНадАнализамиКонтейнеры.УсловияТранспортировки КАК Температура
	                      |ИЗ
	                      |	Документ.ДействияНадАнализами.Контейнеры КАК ДействияНадАнализамиКонтейнеры
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Заказ КАК Заказ
	                      |		ПО ДействияНадАнализамиКонтейнеры.ИдИсследования = Заказ.ИД
	                      |			И ДействияНадАнализамиКонтейнеры.КодПробы = Заказ.КодПробы
	                      |			И ДействияНадАнализамиКонтейнеры.Лаборатория = Заказ.Лаборатория
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	КодПробы");
	
	Запрос.УстановитьПараметр("мсОписьЗаказов",мсОписьЗаказов);
	Выборка = Запрос.Выполнить().Выбрать();	
	
	
	ОбластьШапка = Макет.ПолучитьОбласть("ШапкаТаблицы");
	Область = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДокумент.Вывести(ОбластьШапка);
	
	МассивПроверкаВывода = Новый Массив;
	МассивПроверкаВывода.Добавить(Область);
	МассивПроверкаВывода.Добавить(ОбластьПодвал);
	
	
	НомерПункта = 1;
	Пока Выборка.Следующий() Цикл 
		
		Область.Параметры.Пункт	 		  = НомерПункта;
		Область.Параметры.ОбразецНомер 	  = Выборка.КодПробы;
		Область.Параметры.ТипБиоматериала = Выборка.Биоматериал + ", " + Выборка.Контейнер;
		Область.Параметры.Температура	  = Выборка.Температура;
		
		Если Не ТабДокумент.ПроверитьВывод(МассивПроверкаВывода) Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ОбластьШапка);
		КонецЕсли;
		
		ТабДокумент.Вывести(Область);
		НомерПункта = НомерПункта + 1;
		
	КонецЦикла;
	
	ОбластьПодвал.Параметры.Количество = Выборка.Количество();
	ТабДокумент.Вывести(ОбластьПодвал);
	
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОписьЗаказовЛаборатории_ОписьЗаказов";
		
	Возврат ТабДокумент;
	
КонецФункции

#КонецОбласти
