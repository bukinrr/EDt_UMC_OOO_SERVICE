#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НаУслуги
	Тогда
		ИсключаемаяНоменклатура = МедицинскоеСтрахование.НоменклатураИсключаемаяОтборомПолиса(Полис, Услуги.ВыгрузитьКолонку("Номенклатура"));
		Если ИсключаемаяНоменклатура.Количество() <> 0 Тогда
			
			ТекстОшибки = Документы.ГарантийноеПисьмоПоПолису.ТекстОшибкиУслугаИсключенаОтбромПолиса();
			
			Для Каждого Номенклатура Из ИсключаемаяНоменклатура Цикл
				ТекстОшибки = ТекстОшибки + Символы.ПС + " - " + Строка(Номенклатура);
			КонецЦикла;
			
			ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке(ТекстОшибки, Отказ);
		КонецЕсли;
	КонецЕсли;

	Если Не НаУслуги
		И Услуги.Количество() <> 0
	Тогда
		Услуги.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//Движения.ОстаткиПоГарантийнымПисьмам.Записывать = Истина;
	//Движения.ОстаткиПоГарантийнымПисьмамНаУслуги.Записывать = Истина;
	//
	Если НаУслуги Тогда // Гарантийное письмо на конкретные услуги.
		
		УдалитьДвиженияПриПроведенииЕслиЕсть(Движения.ОстаткиПоГарантийнымПисьмам);
		
		//Движения.ОстаткиПоГарантийнымПисьмам.Очистить();
		
		Движения.ОстаткиПоГарантийнымПисьмамНаУслуги.Записывать = Истина;
		Для Каждого СтрокаУслуги Из Услуги Цикл
			
			Движение = Движения.ОстаткиПоГарантийнымПисьмамНаУслуги.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = НачалоДня(Дата);
			Движение.СтраховойПолис = Полис;
			Движение.ГарантийноеПисьмо = ЭтотОбъект.Ссылка;
			
			Движение.Номенклатура = СтрокаУслуги.Номенклатура;
			Движение.Количество = СтрокаУслуги.Количество;
		КонецЦикла;
		
	Иначе // Гарантийное письмо на сумму.
		УдалитьДвиженияПриПроведенииЕслиЕсть(Движения.ОстаткиПоГарантийнымПисьмамНаУслуги);
		//Движения.ОстаткиПоГарантийнымПисьмамНаУслуги.Очистить();

		Движения.ОстаткиПоГарантийнымПисьмам.Записывать = Истина;
		Движение = Движения.ОстаткиПоГарантийнымПисьмам.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = НачалоДня(Дата);
		Движение.СтраховойПолис = Полис;
		Движение.ГарантийноеПисьмо = ЭтотОбъект.Ссылка;
		
		Движение.Сумма = Сумма;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьДвиженияПриПроведенииЕслиЕсть(ДвиженияПоРегистру)
	
	ДвиженияПоРегистру.Прочитать();
	Если ДвиженияПоРегистру.Количество() <> 0 Тогда
		ДвиженияПоРегистру.Очистить();
		ДвиженияПоРегистру.Записывать = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти