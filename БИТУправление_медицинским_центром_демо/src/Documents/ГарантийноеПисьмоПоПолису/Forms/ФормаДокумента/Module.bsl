#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Объект.Полис) Тогда
		Клиент = Объект.Полис.Владелец;
	КонецЕсли;
	
	Вид = ?(Объект.НаУслуги, "НаУслуги", "НаСумму");
	ПриИзмененииВидаПисьма(Объект.НаУслуги, Элементы.Сумма, Элементы.СтраницыУслуг);
	
	ОбновитьИндикациюЗапрещенныхУслуг(Объект.Полис, Объект.Услуги);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(Объект.Ссылка, Ложь, ЭтаФорма);
	
	ОбновитьИндикациюЗапрещенныхУслуг(Объект.Полис, Объект.Услуги);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КлиентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСФормамиКлиент.ПолеВводаКлиентаАвтоПодбор(Текст, СтандартнаяОбработка, ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСФормамиКлиент.ПолеВводаКлиентаАвтоПодбор(Текст, СтандартнаяОбработка, ДанныеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолисПриИзменении(Элемент)
	
	Клиент = ?(ЗначениеЗаполнено(Объект.Полис), ДопСерверныеФункции.ПолучитьРеквизит(Объект.Полис, "Владелец"), Неопределено);
	ОбновитьИндикациюЗапрещенныхУслуг(Объект.Полис, Объект.Услуги);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолисНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый НастройкиКомпоновкиДанных;
	ЭлементОтбора = Отбор.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование	 = Истина;
	ЭлементОтбора.ВидСравнения	 = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ЛевоеЗначение	 = Новый ПолеКомпоновкиДанных("ВидПолиса.УчетОстаткаПоГарантийнымПисьмам");
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	Если ЗначениеЗаполнено(Клиент) Тогда
		ЭлементОтбора = Отбор.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование	 = Истина;
		ЭлементОтбора.ВидСравнения	 = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ЛевоеЗначение	 = Новый ПолеКомпоновкиДанных("Владелец");
		ЭлементОтбора.ПравоеЗначение = Клиент;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФиксированныеНастройки", Отбор);
	ПараметрыФормы.Вставить("ТекущееЗначение", Объект.Полис);
	
	ОткрытьФорму("Справочник.СтраховыеПолисы.ФормаВыбора", ПараметрыФормы, Элементы.Полис);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПриИзменении(Элемент)
	
	Объект.НаУслуги = (Вид = "НаУслуги");
	
	ПриИзмененииВидаПисьма(Объект.НаУслуги, Элементы.Сумма, Элементы.СтраницыУслуг);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииВидаПисьма(НаУслуги, ЭлементСумма, ЭлементУслуги)
	
	ЭлементСумма.Видимость = Не НаУслуги;
	ЭлементУслуги.ТекущаяСтраница = ЭлементУслуги.ПодчиненныеЭлементы[?(НаУслуги, 0, 1)];
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		Элемент.ТекущиеДанные.Количество = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Не ОтменаРедактирования Тогда
		ОбновитьИндикациюЗапрещенныхУслуг(Объект.Полис, Объект.Услуги);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент = Элементы.УслугиВнимание
		И Элемент.ТекущиеДанные.Внимание
	Тогда
		ТекстОшибки = ОбщегоНазначенияСервер.ФункцияМенеджера("Документы.ГарантийноеПисьмоПоПолису", "ТекстОшибкиУслугаИсключенаОтбромПолиса");
		ПоказатьПредупреждение(, ТекстОшибки, 15);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИндикациюЗапрещенныхУслуг(Полис, Услуги)
	
	Если ЗначениеЗаполнено(Полис) Тогда
		
		ПроверяемаяНоменклатура = Новый Массив;
		
		Для Каждого СтрокаУслуги Из Услуги Цикл
			ПроверяемаяНоменклатура.Добавить(СтрокаУслуги.Номенклатура);
		КонецЦикла;
		
		ИсключаемаяНоменклатура = МедицинскоеСтрахование.НоменклатураИсключаемаяОтборомПолиса(Полис, ПроверяемаяНоменклатура);
	Иначе
		ИсключаемаяНоменклатура = Новый Массив;
	КонецЕсли;
	
	Для Каждого СтрокаУслуги Из Услуги Цикл
		СтрокаУслуги.Внимание = ИсключаемаяНоменклатура.Найти(СтрокаУслуги.Номенклатура) <> Неопределено;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

