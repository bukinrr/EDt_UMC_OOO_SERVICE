#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	Организация = Объект.Филиал.Организация;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(Объект.Ссылка, Ложь, ЭтаФорма);	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьСоставУслуг(Команда)
	
	Заполнено = Истина;
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Заполнено = Ложь;
		ПоказатьПредупреждение(, НСтр("ru='Укажите контрагента'"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Соглашение) Тогда
		Заполнено = Ложь;
		ПоказатьПредупреждение(, НСтр("ru='Укажите договор с контрагентом'"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.НачалоПериода)
		Или Не ЗначениеЗаполнено(Объект.КонецПериода)
	Тогда
		Заполнено = Ложь;
		ПоказатьПредупреждение(, НСтр("ru='Укажите период, за который формируется реестр'"));
		
	ИначеЕсли Заполнено Тогда
		ДоговорДатаНачала	 = ДопСерверныеФункции.ПолучитьРеквизит(Объект.Соглашение, "ДатаНачала");
		ДоговорДатаКонца	 = ДопСерверныеФункции.ПолучитьРеквизит(Объект.Соглашение, "ДатаОкончания");
		
		Если	 (ЗначениеЗаполнено(ДоговорДатаНачала)	 И ДоговорДатаНачала > Объект.НачалоПериода)
			Или  (ЗначениеЗаполнено(ДоговорДатаКонца)	 И ДоговорДатаКонца	 < Объект.КонецПериода)
		Тогда
			ПоказатьПредупреждение(, НСтр("ru='Период документа не вписывается в период действия договора. Заполнение может быть некорректным.'"));
		КонецЕсли;
	КонецЕсли;
	
	Если Заполнено Тогда
		Если Объект.Услуги.Количество() <> 0 Тогда
			Если Вопрос(НСтр("ru='Табличная часть будет очищена. Продолжить?'"), РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				Объект.Услуги.Очистить();
				ЗаполнитьСоставУслугСервер();
			КонецЕсли;
		Иначе
			ЗаполнитьСоставУслугСервер();
			Состояние(НСтр("ru='Операция завершена'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПериод(Команда)
	
	Период = Новый СтандартныйПериод;
	Период.ДатаНачала = Объект.НачалоПериода;
	Период.ДатаОкончания = ?(ЗначениеЗаполнено(Объект.КонецПериода), КонецДня(Объект.КонецПериода), Дата(1,1,1));
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период = Период;
	Если Диалог.Редактировать() Тогда
		Объект.НачалоПериода = Диалог.Период.ДатаНачала;
		Объект.КонецПериода = Диалог.Период.ДатаОкончания;
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("Подключаемый_КнопкаФилиалПриНажатииЗавершение", ЭтотОбъект);
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтотОбъект, , ОповещениеОВыборе);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатииЗавершение(Результат, ДополнительныеЗначения) Экспорт
	
	Организация = ДопСерверныеФункции.ПолучитьРеквизит(Объект.Филиал, "Организация");
	
	// Контроль организации договора
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		ОрганизацияДоговора = ДопСерверныеФункции.ПолучитьРеквизит(Объект.Соглашение, "Организация");
		Если ОрганизацияДоговора <> Организация Тогда
			Объект.Соглашение = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	ПодставитьСоглашениеСервер();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСоставУслугСервер()
	
	Документы.РеестрОказанныхУслугПоСоглашениям.ЗаполнитьСоставУслугДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПодставитьСоглашениеСервер()
	Если Объект.Контрагент <> Объект.Соглашение.Владелец Тогда
		Объект.Соглашение = Справочники.СоглашенияСтрахования.ПустаяСсылка();
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СоглашенияСтрахования.Ссылка
	|ИЗ
	|	Справочник.СоглашенияСтрахования КАК СоглашенияСтрахования
	|ГДЕ
	|	СоглашенияСтрахования.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Объект.Контрагент);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выгрузить();
	Если Выборка.Количество() = 1 Тогда
		Объект.Соглашение = Выборка[0].Ссылка; 
	КонецЕсли;
КонецПроцедуры

#КонецОбласти