#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	
	мУчетнаяПолитика = УправлениеНастройками.ПолучитьУчетнуюПолитику();
	
	Если Объект.Ссылка.Пустая()
		И ЗначениеЗаполнено(Объект.ВидДоговора)
	Тогда
		УстановитьДатуОкончанияПоВидуДоговора(Объект.ДатаОкончания, Объект.ВидДоговора, Объект.Дата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(ТекущийОбъект,,ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Клиенты") Тогда
		
		Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			
			Объект.Клиент = ВыбранноеЗначение;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПоискКлиента(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ФормаПоискаКлиентаУпр",,Элементы.Клиент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПоШаблону(Команда)
	
	СодержаниеОшибки = "";
	Если РаботаСДиалогамиКлиент.ПроверитьМодифицированностьВФорме(ЭтаФорма) Тогда
		Результат = РаботаСШаблонамиПечатиКлиент.ВыполнитьПечатьДокументаПоШаблонуПечати(Объект.Ссылка,ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидДоговора,"ШаблонПечати") , СодержаниеОшибки, мУчетнаяПолитика.РежимПечатиПоШаблонуWORD);
		Если Не Результат Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СодержаниеОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КлиентАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПодбора = Новый Структура("Отбор, СтрокаПоиска, ВыборГруппИЭлементов");
	
	ПараметрыПодбора.СтрокаПоиска = Текст;
	
	ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.Клиенты"), ПараметрыПодбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда

		СтандартнаяОбработка = Ложь;
		
		ПараметрыПодбора = Новый Структура("Отбор, СтрокаПоиска, ВыборГруппИЭлементов");
		
		ПараметрыПодбора.СтрокаПоиска = Текст;
		
		ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.Клиенты"), ПараметрыПодбора);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	
	УстановитьДатуОкончанияПоВидуДоговора(Объект.ДатаОкончания, Объект.ВидДоговора, Объект.Дата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДатуОкончанияПоВидуДоговора(ДатаОкончания, ВидДоговора, ДатаДокумента)
	
	Если ЗначениеЗаполнено(ВидДоговора) Тогда
		СрокДействия = ДопСерверныеФункции.ПолучитьРеквизит(ВидДоговора, "СрокДействия");
		Если ЗначениеЗаполнено(СрокДействия) Тогда
			ДатаОкончания = ДобавитьМесяц(ДатаДокумента, СрокДействия);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
