#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если Не Отказ И Не ПометкаУдаления Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЛабораторныеИсследованияСрезПоследних.ИД КАК ИД,
		|	ЛабораторныеИсследованияСрезПоследних.КодПробы КАК НомерПробы,
		|	ЛабораторныеИсследованияСрезПоследних.Регистратор КАК Регистратор,
		|	ЛабораторныеИсследованияСрезПоследних.Лаборатория КАК Лаборатория,
		|	ЛабораторныеИсследованияСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЛабораторныеИсследованияСрезПоследних.КлиникаИсходная КАК КлиникаИсходная
		|ИЗ
		|	РегистрСведений.ЛабораторныеИсследования.СрезПоследних(
		|			,
		|			КодПробы = &НайденныйИНЗ
		|				И Статус <> ЗНАЧЕНИЕ(Перечисление.СостоянияИсследований.Обработан)) КАК ЛабораторныеИсследованияСрезПоследних"
		;
		
		Запрос.УстановитьПараметр("НайденныйИНЗ", ИНЗ);
		
		ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
		ТаблицаРегистраторы = ТаблицаРезультат.Скопировать(, "Регистратор"); 
		ТаблицаРегистраторы.Свернуть("Регистратор");
		
		СостояниеПолученРезультат = Перечисления.СостоянияЗаказовЛаборатории.ПолученРезультат;
		
		// Складываем данные по полученным результатам в эту таблицу, потом передаем эту таблицу в общий модуль
		// для обработки отметок медосмотра
		ТЗДляПростановкиОтметокПМО = МедосмотрыСервер.ПолучитьПустуюТЗДляПростановкиОтметокПМО();
		
		Для Каждого СтрокаТаблицаРегистраторы Из ТаблицаРегистраторы Цикл
			Если ТипЗнч(СтрокаТаблицаРегистраторы.Регистратор) = Тип("ДокументСсылка.ЗаказВоВнешнююЛабораторию") Тогда
				ЗаказОбъект = СтрокаТаблицаРегистраторы.Регистратор.ПолучитьОбъект();
				ИсследованияОбработанные = ЗаказОбъект.Исследования.НайтиСтроки(Новый Структура("КодПробы", ИНЗ));	
				Если ИсследованияОбработанные.Количество() > 0 Тогда
					РезультатыСтроки = ЗаказОбъект.Результаты.НайтиСтроки(Новый Структура("КодПробы", ИНЗ));
					Если РезультатыСтроки.Количество() = 0 Тогда
						стрРезультат = ЗаказОбъект.Результаты.Добавить();
						стрРезультат.КодПробы = ИНЗ;
					КонецЕсли;	
					
					Для Каждого Исследование Из ИсследованияОбработанные Цикл
						
						// Добавляем в ТЗ ТЗДляПростановкиОтметокПМО данные обработанных анализов
						НоваяСтрокаТЗПМО = ТЗДляПростановкиОтметокПМО.Добавить();
						НоваяСтрокаТЗПМО.КодПробы = Исследование.КодПробы;
						НоваяСтрокаТЗПМО.ЗаказВоВнешююЛабораторию = ЗаказОбъект.Ссылка;
						
						Если Исследование.Состояние <> СостояниеПолученРезультат Тогда 
							Исследование.Состояние = СостояниеПолученРезультат;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				// Проверим, все ли заказы обработаны.
				Если ЗаказОбъект.Состояние <> СостояниеПолученРезультат 
					И ЗаказОбъект.Исследования.НайтиСтроки(Новый Структура("Состояние", СостояниеПолученРезультат)).Количество() = ЗаказОбъект.Исследования.Количество() 
				Тогда
					ЗаказОбъект.Состояние = СостояниеПолученРезультат;
				КонецЕсли;
				
				ЗаказОбъект.Записать(РежимЗаписиДокумента.Запись);		
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	Прием.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.Прием КАК Прием
			|ГДЕ
			|	Прием.Основание = &Основание
			|	И НЕ Прием.ПометкаУдаления"
			;
			
			Запрос.УстановитьПараметр("Основание", ДокументОснование);
			
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() Тогда
				ПриемИнвитро = Документы.Прием.СоздатьДокумент();
			Иначе
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					ПриемИнвитро = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
				КонецЦикла;		
			КонецЕсли;
			ЗаполнитьЗаписатьПриемСРезультатами(ПриемИнвитро, ДокументОснование, ТаблицаРезультат);
			
			// Обработка отметок ПМО дополнение ссылки на прием
			Если ТЗДляПростановкиОтметокПМО.Количество() > 0 Тогда
				Для Каждого Эл Из ТЗДляПростановкиОтметокПМО Цикл
					Эл.Прием = ПриемИнвитро.Ссылка; 
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		// Передаем ТЗДляПростановкиОтметокПМО для обработки
		МедосмотрыСервер.УстановитьОтметкиДляДействийПМОПриСозданииДокументаИзВнЛаборатории(ТЗДляПростановкиОтметокПМО);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьФайл(ПутьКФайлу, СсылкаНаДокумент)
	
	Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
		ВыбФайл = Новый Файл(ПутьКФайлу);
		Если ВыбФайл.Существует() Тогда
			РаботаСФайлами.ДобавитьНовыйФайл(СсылкаНаДокумент, ПутьКФайлу);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЗаписатьПриемСРезультатами(ПриемИнвитро, invЗаявка, ТаблицаРезультат)
	
	// Заполнение реквизитов по заявке.
	Если ПриемИнвитро.Ссылка.Пустая() Тогда
		ПриемИнвитро.Дата				= Дата;
		ПриемИнвитро.Клиент				= Пациент;
		ПриемИнвитро.Врач				= invИнвитроСервер.ВернутьЗаписьНастроекПодразделения(Подразделение).Лаборант;
		ПриемИнвитро.Основание			= invЗаявка;	
		ПриемИнвитро.МедицинскаяКарта	= ОтраслевыеДополнения.ПолучитьОсновнуюМедКартуКлиента(Пациент);
	КонецЕсли;
	
	Если ТаблицаРезультат.Количество() <> 0 Тогда 
		
		НоменклатураИсследований = ТаблицаРезультат.НайтиСтроки(Новый Структура("Лаборатория", invЗаявка.Лаборатория));
		
		Для Каждого СтрокаНоменклатураИсследований Из НоменклатураИсследований Цикл
			
			Если Не ЗначениеЗаполнено(ПриемИнвитро.Филиал) Тогда
				ПриемИнвитро.Филиал = СтрокаНоменклатураИсследований.КлиникаИсходная;
			КонецЕсли;
			
			Отбор = Новый Структура("Номенклатура, НомерПробы, ИД");
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаНоменклатураИсследований);
			СтрокиРаботы = ПриемИнвитро.Работы.НайтиСтроки(Отбор);
			Если СтрокиРаботы.Количество() = 0 Тогда
				НоваяСтрока = ПриемИнвитро.Работы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНоменклатураИсследований);
				НоваяСтрока.Количество = 1;
				НоваяСтрока.КлючСтроки = НоваяСтрока.НомерСтроки;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТекстИсследование = НСтр("ru='Исследование Инвитро'");
	ТекстОсмотра = ТекстИсследование + ":<br><br>";
	
	Если Тесты.Количество() > 0 Тогда
		ТекстОсмотра = ТекстОсмотра + НСтр("ru='Тесты'") + ":<br>" + РаботаСHTMLКлиентСервер.ТаблицаЗначенийВТаблицуHTML(Тесты.Выгрузить()) + "<br>";
	КонецЕсли;
	
	Если Посев.Количество() > 0 Тогда
		ТекстОсмотра = ТекстОсмотра + НСтр("ru='Посевы'") + ":<br>" + РаботаСHTMLКлиентСервер.ТаблицаЗначенийВТаблицуHTML(Посев.Выгрузить()) + "<br>";
	КонецЕсли;
	
	Если Комментарии.Количество() > 0 Тогда
		ТекстОсмотра = ТекстОсмотра + НСтр("ru='Комментарии'") + ":<br>" + РаботаСHTMLКлиентСервер.ТаблицаЗначенийВТаблицуHTML(Комментарии.Выгрузить()) + "<br>";
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", invЗаявка);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	invРезультатыИсследования.Ссылка КАК РезультатИсследования,
	|	invРезультатыИсследования.Тесты.(
	|		НомерСтроки КАК N,
	|		Тест КАК Тест,
	|		Результат КАК Результат,
	|		ЕдИзм КАК Ед_изм,
	|		РефМин КАК Реф_мин,
	|		РефМакс КАК Реф_макс,
	|		КомментарийКАнализу КАК Комментарий_к_анализу,
	|		ТипКомментария КАК Тип_комментария
	|	) КАК Тесты,
	|	invРезультатыИсследования.Посев.(
	|		НомерСтроки КАК N,
	|		Культура КАК Культура,
	|		Антибиотик КАК Антибиотик,
	|		КодАнтибиотика КАК Код_антибиотика,
	|		ТипСопротивляемости КАК Тип_сопротивляемости,
	|		SIR КАК SIR,
	|		ТипКомментарияAna КАК Тип_комментария_ana,
	|		ТекстКомментарияAna КАК Текст_комментария_ana,
	|		Рост КАК Рост
	|	) КАК Посев,
	|	invРезультатыИсследования.Комментарии.(
	|		НомерСтроки КАК N,
	|		Текст КАК Текст,
	|		Тип КАК Тип
	|	) КАК Комментарии
	|ИЗ
	|	Документ.invРезультатыИсследования КАК invРезультатыИсследования
	|ГДЕ
	|	invРезультатыИсследования.ДокументОснование = &ДокументОснование
	|	И НЕ invРезультатыИсследования.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	invРезультатыИсследования.Дата";
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстОсмотра = ТекстОсмотра + "<br>" + ТекстИсследование + ":<br><br>";
		
		РезультатТесты = ВыборкаДетальныеЗаписи.Тесты.Выгрузить();
		РезультатПосев = ВыборкаДетальныеЗаписи.Посев.Выгрузить();
		РезультатКомментарии = ВыборкаДетальныеЗаписи.Комментарии.Выгрузить(); 
		
		Если РезультатТесты.Количество() > 0 Тогда
			ПреобразоватьЗаголовкиКолонокТаблицыЗначений(РезультатТесты);
			ТекстОсмотра = ТекстОсмотра + НСтр("ru='Тесты'") + ":<br>" + РаботаСHTMLКлиентСервер.ТаблицаЗначенийВТаблицуHTML(РезультатТесты) + "<br>";
		КонецЕсли;
		
		Если РезультатПосев.Количество() > 0 Тогда
			ПреобразоватьЗаголовкиКолонокТаблицыЗначений(РезультатПосев);
			ТекстОсмотра = ТекстОсмотра + НСтр("ru='Посевы'") + ":<br>" + РаботаСHTMLКлиентСервер.ТаблицаЗначенийВТаблицуHTML(РезультатПосев) + "<br>";
		КонецЕсли;
		
		Если РезультатКомментарии.Количество() > 0 Тогда
			ПреобразоватьЗаголовкиКолонокТаблицыЗначений(РезультатКомментарии);
			ТекстОсмотра = ТекстОсмотра + НСтр("ru='Комментарии'") + ":<br>" + РаботаСHTMLКлиентСервер.ТаблицаЗначенийВТаблицуHTML(РезультатКомментарии) + "<br>";
		КонецЕсли;
	КонецЦикла;
	
	ТекстОсмотра = "<!DOCTYPE html PUBLIC ""-//W3C//DTD HTML 4.0 Transitional//EN""><html><body><div style=""HEIGHT: 6.79cm; WIDTH: 21cm"">"
					+ ТекстОсмотра + "</div></body></html>";

	ПриемИнвитро.Осмотр = ТекстОсмотра;
	
	// Запись приема.
	Попытка
		ПриемИнвитро.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ПриемИнвитро.Записать(РежимЗаписиДокумента.Запись);
	КонецПопытки;
	// Заполнение ссылки на первичный прием.
	Если Не ЗначениеЗаполнено(ПриемИнвитро.ПервичныйПрием) Тогда 
		ПриемИнвитро.ПервичныйПрием = ПриемИнвитро.Ссылка;
		ПриемИнвитро.ЭтоПервичный = Истина;
		Попытка
			ПриемИнвитро.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ПриемИнвитро.Записать(РежимЗаписиДокумента.Запись);
		КонецПопытки;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПреобразоватьЗаголовкиКолонокТаблицыЗначений(ТаблицаЗначений)
	
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		Колонка.Заголовок = СтрЗаменить(Колонка.Заголовок, "_", " ");		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
