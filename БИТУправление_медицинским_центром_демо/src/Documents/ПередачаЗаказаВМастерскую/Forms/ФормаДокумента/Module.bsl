#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	мУчетнаяПолитика = УправлениеНастройками.ПолучитьУчетнуюПолитику();
	Если Объект.Ссылка.Пустая() Тогда 
		Если Не ЗначениеЗаполнено(Объект.Склад) Тогда
			ЗаполнитьДокументНастройкамиМастераПоУмолчаниюСервер();
		КонецЕсли;
	КонецЕсли;
	НастроитьВидимостьЭлементов();
	
	ЗаполнитьЭтапыРаботСОтметкамиВыполнения();
	УправлениеЗаказами.УстановитьУсловноеОформлениеЭтапамРабот(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
	Если Объект.Ссылка.Пустая()
		И ЗначениеЗаполнено(Объект.Заказ)
	Тогда
		ЗаполнитьПоНормам(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Формирование списка выполенных этапов работ
	ВыполненныеЭтапы = КомплексныеРасчетыКлиентовСервер.ПолучитьВыполненныхЭтаповРаботИзТаблицы(ЭтапыРабот);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ВыполненныеЭтапыРабот", ВыполненныеЭтапы);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(Объект.Ссылка, Ложь, ЭтаФорма);
	ЗаполнитьЭтапыРаботСОтметкамиВыполнения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененСтатусЗаказа", Объект.Заказ);
	Оповестить("ИзмененыЭтапыРаботЗаказа", Объект.Заказ, Объект.Ссылка);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыЭтапыРаботЗаказа" И 
		Параметр = Объект.Заказ И Источник <> Объект.Ссылка 
	Тогда
		ЗаполнитьЭтапыРаботСОтметкамиВыполнения();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьДокументНастройкамиМастераПоУмолчаниюСервер()
	док = РеквизитФормыВЗначение("Объект");
	док.ЗаполнитьДокументНастройкамиМастераПоУмолчанию();
	ЗначениеВРеквизитФормы(док, "Объект");
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьЭлементов()
	
	Элементы.МатериалыХарактеристикаНоменклатуры.Видимость = мУчетнаяПолитика.ВестиУчетПоХарактеристикам;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоНормам(Команда)
	Если Не ЗначениеЗаполнено(Объект.Заказ) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Материалы.Количество() <> 0 Тогда
		ТекстВопроса = НСтр("ru='Перед заполнением табличная часть будет очищена. Заполнить?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, );
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьПоНормамСервер();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоНормамСервер()
	
	Документы.ПередачаЗаказаВМастерскую.ЗаполнитьМатериалыПоНормам(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыЕдиницаИзмеренияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Материалы.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) И ЗначениеЗаполнено(ТекущиеДанные.ЕдиницаИзмерения)
		Тогда
		ТекущиеДанные.Коэффициент = ДопСерверныеФункции.ПолучитьРеквизит(ТекущиеДанные.ЕдиницаИзмерения, "Коэффициент");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура МатериалыКоличествоПриИзменении(Элемент)
	ТД = Элементы.Материалы.ТекущиеДанные;
	ТД.Сумма = ТД.Количество * ТД.Цена;
КонецПроцедуры

&НаКлиенте
Процедура МатериалыЦенаПриИзменении(Элемент)
	ТД = Элементы.Материалы.ТекущиеДанные;
	ТД.Сумма = ТД.Количество * ТД.Цена;
КонецПроцедуры

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ЗаполнитьЭтапыРаботСОтметкамиВыполнения();
	
КонецПроцедуры

#Область ЭтапыРабот

&НаСервере
Процедура ЗаполнитьЭтапыРаботСОтметкамиВыполнения()
	
	КомплексныеРасчетыКлиентовСервер.ЗаполнитьЭтапыРаботСОтметкамиВыполнения(Объект.Заказ, ЭтапыРабот, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыРаботВыполненПриИзменении(Элемент)
	
	ТекДанные = Элементы.ЭтапыРабот.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	КомплексныеРасчетыКлиентовКлиент.ЭтапыРаботВыполненПриИзменении(ТекДанные);
	Если ТекДанные.ОтметкаВыполнения И ТекДанные.ВнешнийИсполнитель И ЗначениеЗаполнено(Объект.Контрагент) Тогда 
		ТекДанные.ИсполнительФакт = Объект.Контрагент;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыРаботОтметкаВыполненияПриИзменении(Элемент)
	
	ТекДанные = Элементы.ЭтапыРабот.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	КомплексныеРасчетыКлиентовКлиент.ЭтапыРаботОтметкаВыполненияПриИзменении(ТекДанные);
	Если ТекДанные.ОтметкаВыполнения И ТекДанные.ВнешнийИсполнитель И ЗначениеЗаполнено(Объект.Контрагент) Тогда 
		ТекДанные.ИсполнительФакт = Объект.Контрагент;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыРаботВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = ЭтапыРабот.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Поле = Элементы.ЭтапыРаботДокументРегистратор Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ТекДанные.ДокументРегистратор) Тогда 
			ОткрытьЗначение(ТекДанные.ДокументРегистратор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

