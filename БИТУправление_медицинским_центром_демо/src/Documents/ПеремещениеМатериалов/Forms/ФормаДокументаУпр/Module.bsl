#Область РазделОписанияПеременных

&НаКлиенте
Перем мТекущаяДатаДокумента Экспорт; // Хранит текущую дату документа - для проверки перехода документа в другой период установки номера.

&НаКлиенте
Перем мИзмененыНастройкиПодбора; // По-умолчанию = Неопределено.

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	
	Элементы.ТоварыАртикул.Видимость = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ПоказыватьАртикул");
		
	Элементы.ТоварыХарактеристикаНоменклатуры.Видимость = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ВестиУчетПоХарактеристикам");
	
	РаботаСФормамиСервер.НастройкаПодбораПриСоздании(ЭтаФорма, Ложь, "Товары", "Материал");	
	
	РаботаСФормамиСервер.УстановитьВидимостьКнопокЗагрузитьИзТСД(Элементы.ТоварыЗагрузитьДанныеИзТСД);
	
	Если Объект.Ссылка.Пустая() ТОгда
		// ИнтеграцияМДЛП
		СформироватьТекстУведомленияМДЛП();
		// Конец ИнтеграцияМДЛП
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// ИнтеграцияМДЛП
	СформироватьТекстУведомленияМДЛП();
	// Конец ИнтеграцияМДЛП
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	мТекущаяДатаДокумента = Объект.Дата;
	
	Если Параметры.Ключ.Пустая() Тогда
		ВхДокДата = ТекущаяДата();
		Модифицированность = ЛОЖЬ;
	КонецЕсли;
	
	Если Истина // ИспользоватьПодключаемоеОборудование Проверка на включенную ФО "Использовать ВО"
	   И МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО.
		ОписаниеОшибки = "";
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
		ПоддерживаемыеТипыВО.Добавить("ТерминалСбораДанных");
		
		ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);    
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	
	КонецЕсли;
	
	РаботаСФормамиКлиент.ОчиститьЛишниеКомандыПобор(ЭтаФорма);
    ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	// МеханизмВнешнегоОборудования
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
	ПоддерживаемыеТипыВО.Добавить("ТерминалСбораДанных");
	
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(Неопределено, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	// Конец МеханизмВнешнегоОборудования
	Если НЕ ЗавершениеРаботы Тогда
		СохранитьНастройкиПодбора();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(ТекущийОбъект,,ЭтаФорма);
	
	РаботаСДокументамиСервер.ПроверитьПринадлежностьСкладаФилиалу(Объект.Ссылка);
	
	// ИнтеграцияМДЛП
	СформироватьТекстУведомленияМДЛП();
	// Конец ИнтеграцияМДЛП
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ScanData" Тогда
		Если ВводДоступен() Тогда
			ТипШК = Неопределено;
			РаботаСТорговымОборудованиемКлиент.ОбработатьСобытиеСШКФормы(ЭтаФорма, Параметр, ТипШК);
		КонецЕсли;
	КонецЕсли;
	
	// ИнтеграцияМДЛП
	Если (ИмяСобытия = "ИзменениеСостоянияМДЛП"
		Или ИмяСобытия = "Запись_УведомлениеОПриемкеМДЛП"
		Или ИмяСобытия = "Запись_УведомлениеОПеремещенииМДЛП")
		И Параметр.Основание = Объект.Ссылка Тогда
		СформироватьТекстУведомленияМДЛП();
	КонецЕсли;
	Если ИмяСобытия = "ВыполненОбменМДЛП" Тогда
		СформироватьТекстУведомленияМДЛП();
	КонецЕсли;
	// Конец ИнтеграцияМДЛП
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	
	Перем Действие;
	
	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		ЗначениеВыбора.Свойство("Действие", Действие);
		
		Если Действие = "ПодборМатериала" Тогда			
			ОбработкаВыбораНоменклатуры(ЗначениеВыбора.Номенклатура,ЗначениеВыбора.ЕдиницаИзмерения, ЗначениеВыбора.Количество, ЗначениеВыбора.ХарактеристикаНоменклатуры, ЗначениеВыбора.СерияНоменклатуры, ЗначениеВыбора.Цена);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ.

&НаКлиенте
Процедура КоманднаяПанельТоварыКнопкаПодбор(Команда)
	
	РаботаСФормамиКлиент.КнопкаПодборПриНажатии(ЭтаФорма, "Товары");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ.

// Изменение даты документа
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ВызватьОбщийМодульПроверитьНомерДокумента(мТекущаяДатаДокумента);
	мТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура ВызватьОбщийМодульПроверитьНомерДокумента(ДатаДок)
	
	РаботаСДиалогамиСервер.ПроверитьНомерДокумента(ЭтаФорма, ДатаДок);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ "ТОВАРЫ" И ЕГО РЕКВИЗИТОВ.

// Изменение номенклатуры.
&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	ТоварыРеквизитПриИзменении("Товары.Номенклатура");	
КонецПроцедуры

// Изменение характеристики номенклатуры.
&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	ТоварыРеквизитПриИзменении("Товары.ХарактеристикаНоменклатуры");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	ТоварыРеквизитПриИзменении("Товары.ЕдиницаИзмерения");
КонецПроцедуры

// Изменение количества.
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	ТоварыРеквизитПриИзменении("Товары.Количество");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыРеквизитПриИзменении(ИмяРеквизита)
	ОбработкаРеквизитаУпр(ИмяРеквизита,Элементы.Товары.ТекущиеДанные,ЭтаФорма);
КонецПроцедуры

// Обработка изменения реквизитов документа.
//
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– ФормаКлиентскогоПриложения				– Ссылка на форму документа. 
// 									  Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
// 										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
&НаКлиенте
Функция ОбработкаРеквизитаУпр(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено)
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА.
	Если Имя="Склад" Тогда
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ".
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		
		Рез=РаботаСДокументамиКлиент.дкОбработкаРеквизитаКлиент(Объект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.Коэффициент = ДопСерверныеФункции.ПолучитьРеквизит(ТекСтрока.ЕдиницаИзмерения, "Коэффициент");
		
	ИначеЕсли Имя="Товары.ХарактеристикаНоменклатуры" Тогда
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		
		ТекСтрока.Коэффициент = ДопСерверныеФункции.ПолучитьРеквизит(ТекСтрока.ЕдиницаИзмерения, "Коэффициент");

	ИначеЕсли Имя="Товары.Количество" Тогда
		
		рез=РаботаСДокументамиКлиент.дкОбработкаРеквизитаКлиент(Объект,Имя,ТекСтрока,ЭтаФорма,допПараметры);
		
	Иначе 
		Возврат РаботаСДокументамиКлиент.
		РаботаСДокументамиКлиент.дкОбработкаРеквизитаКлиент(Объект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
	
КонецФункции // ОбработкаРеквизита().

&НаКлиенте
Процедура ОбработкаВыбораНоменклатуры(Номенклатура, Единица, Количество, ХарактеристикаНоменклатуры = Неопределено, СерияНоменклатуры = Неопределено, Цена = 0)

	Результат = ОбработкаВыбораНоменклатурыСервер(Номенклатура, Единица, Количество, ХарактеристикаНоменклатуры, СерияНоменклатуры, Цена);
	
	Если Результат.ОК Тогда
		Элементы.Товары.Обновить();
		Элементы.Товары.ТекущаяСтрока = Объект.Товары[Результат.ИндексСтроки].ИсходныйНомерСтроки-1;
		Если Результат.НоваяСтрока Тогда
			ТоварыРеквизитПриИзменении("Товары.Номенклатура");
		Иначе
			ТоварыРеквизитПриИзменении("Товары.Количество");
		КонецЕсли;
	Иначе
		РаботаСДокументамиКлиент.ЧтениеШтрихкода_ПредупреждениеОжидаетсяМатериал(Номенклатура);
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Функция ОбработкаВыбораНоменклатурыСервер(Номенклатура, Единица, Количество, ХарактеристикаНоменклатуры = Неопределено, СерияНоменклатуры = Неопределено, Цена = 0)
	
	Результат = Новый Структура("ОК,НоваяСтрока, ИндексСтроки", Истина, Ложь);
	
	Если ХарактеристикаНоменклатуры = Неопределено Тогда
		ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(); 
	КонецЕсли;
	

	Если СерияНоменклатуры = Неопределено Тогда
		СерияНоменклатуры = Справочники.СерииНоменклатуры.ПустаяСсылка(); 
	КонецЕсли;
	
	Если Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Материал Тогда
		
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
		
		ТабличнаяЧасть = ДокументОбъект.Товары;
		мсСтрокиТовары = ТабличнаяЧасть.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры, ЕдиницаИзмерения", Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры, Единица));
		
		Если мсСтрокиТовары.Количество() = 0 Тогда
			
			// Добавление новой строки
			СтрокаТовары = ТабличнаяЧасть.Добавить();
			СтрокаТовары.Номенклатура = Номенклатура;
			СтрокаТовары.Количество = Количество;      
			СтрокаТовары.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;      
			СтрокаТовары.СерияНоменклатуры = СерияНоменклатуры;      
			СтрокаТовары.ЕдиницаИзмерения = Единица;
			СтрокаТовары.Коэффициент = Единица.Коэффициент;                          
			
			Результат.НоваяСтрока = Истина;
			
		Иначе
			// Увеличивается количество в существующей строке
			СтрокаТовары = мсСтрокиТовары[0];
			СтрокаТовары.Количество = СтрокаТовары.Количество + Количество;      
			
			Результат.НоваяСтрока = Ложь;
		КонецЕсли;
		Результат.ИндексСтроки = СтрокаТовары.НомерСтроки-1;
		ЗначениеВРеквизитФормы(ДокументОбъект,"Объект");		
	Иначе
		Результат.ОК = Ложь;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ ПРОЦЕДУРЫ И ФУНКЦИИ.

// Функция осуществляет обработку считывания штрихкода номенклатуры.
//
// Параметры:
//  ДанныеНоменклатуры	 - Старуктура	 - сведения о считанной позиции.
//  СШК					 - Строка		 - Идентификатор сканера штрих-кода, с которым связано данное событие.
// 
// Возвращаемое значение:
//  Булево - Событие обработано.
//
&НаКлиенте
Функция СШКНоменклатура(ДанныеНоменклатуры, СШК) Экспорт
	Номенклатура				= ДанныеНоменклатуры.Номенклатура;
	Количество					= ДанныеНоменклатуры.Количество;
	ХарактеристикаНоменклатуры	= ДанныеНоменклатуры.ХарактеристикаНоменклатуры;
	Единица						= ДанныеНоменклатуры.ЕдиницаИзмерения;
	Цена						= ДанныеНоменклатуры.Цена;
	СерияНоменклатуры			= ДанныеНоменклатуры.СерияНоменклатуры;
	Штрихкод					= ДанныеНоменклатуры.Штрихкод;
	
	РеквизитыНоменклатуры = РаботаСДокументамиСервер.ДанныеНоменклатурыДляОбработкиЧтенияШК(Номенклатура, Ложь);
	Если РеквизитыНоменклатуры.ЭтоМатериал Тогда
	
		ТабличнаяЧасть = Объект.Товары;
		мсСтрокиТовары = РаботаСДокументамиКлиент.ЧтениеШтрихкода_НайтиСтрокиДокументаПоДаннымШтрихкода(ТабличнаяЧасть, ДанныеНоменклатуры, РеквизитыНоменклатуры);
		
		Если мсСтрокиТовары.Количество() = 0 Тогда
			
			// Добавление новой строки
			СтрокаТовары = РаботаСДокументамиКлиент.ЧтениеШтрихкода_ДобавитьСтрокуТовараПоДаннымШтрихкода(ТабличнаяЧасть, ДанныеНоменклатуры, РеквизитыНоменклатуры);
			
			Элементы.Товары.ТекущаяСтрока = ТабличнаяЧасть[СтрокаТовары.НомерСтроки-1];
			НоваяСтрока = Истина;
		Иначе
			// Увеличивается количество в существующей строке
			СтрокаТовары = мсСтрокиТовары[0];
			СтрокаТовары.Количество = СтрокаТовары.Количество + Количество;      
			Элементы.Товары.ТекущаяСтрока = ТабличнаяЧасть[СтрокаТовары.НомерСтроки-1];
			ТоварыКоличествоПриИзменении(Неопределено);
			
			НоваяСтрока = Ложь;
		КонецЕсли;
	Иначе
		РаботаСДокументамиКлиент.ЧтениеШтрихкода_ПредупреждениеОжидаетсяМатериал(Номенклатура);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // СШКНоменклатура()

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:
				|""%ОписаниеОшибки%"".'" );
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////
// МЕТОДЫ ПОДБОРА.

&НаКлиенте
Процедура Подключаемый_ВыборПодбор(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если Ложь Тогда ЗапрашиватьКоличество = Ложь; ЗапрашиватьЦену = Ложь; ЗапрашиватьХарактеристику = Ложь; ЗапрашиватьСерию = Ложь; КонецЕсли; 
	
	СтандартнаяОбработка = Ложь;
													  
	Если Не ДопСерверныеФункции.ПолучитьРеквизит(ВыбранноеЗначение, "ЭтоГруппа") тогда
		Действие = "ПодборМатериала";
	    Результат = РаботаСФормамиКлиент.ВыборПодборОбработка(ВыбранноеЗначение, ЗапрашиватьКоличество, ЗапрашиватьЦену, ЗапрашиватьХарактеристику, ЗапрашиватьСерию, Действие, ЭтаФорма); 
		Если Результат <> Неопределено Тогда
			ОбработкаВыбора(Результат, Неопределено);
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПодбораПриИзменении(Элемент)
	мИзмененыНастройкиПодбора = Истина;
	СохранитьНастройкиПодбора();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиПодбора()
	
	Если ЗначениеЗаполнено(мИзмененыНастройкиПодбора) Тогда 
		РаботаСФормамиСервер.СохранитьНастройкиПодбора(ЭтаФорма.ЗапрашиватьКоличество, ЭтаФорма.ЗапрашиватьЦену, ЭтаФорма.ЗапрашиватьХарактеристику, ЭтаФорма.ЗапрашиватьСерию, ТипЗнч(Объект.Ссылка));		
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#Область ТерминалыСбораДанных // ТЕРМИНАЛЫ СБОРА ДАННЫХ

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	ОповещенияПриПодключении = Новый ОписаниеОповещения("ЗагрузитьДанныеИзТСДЗавершение", ЭтотОбъект);    
	МенеджерОборудованияКлиент.НачатьЗагрузкуДанныеИзТСД(ОповещенияПриПодключении, УникальныйИдентификатор, Ложь)			
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСДЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		мсШтрихкоды = РезультатВыполнения.ТаблицаТоваров;
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ДобавлятьБезВладельца", Истина);
		РаботаСТорговымОборудованиемКлиент.ОбработатьШтрихкоды(ЭтаФорма, Объект, мсШтрихкоды, "Товары", ДопПараметры);
	Иначе
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:
		|""%ОписаниеОшибки%"".'" );
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ТекстУведомленияМДЛПОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИнтеграцияМДЛПКлиент.ТекстУведомленияМДЛПОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

// ИнтеграцияМДЛП

&НаСервере
Процедура СформироватьТекстУведомленияМДЛП()
	
	ИнтеграцияМДЛП.СформироватьТекстУведомленияМДЛП(ЭтотОбъект);
	
КонецПроцедуры

// Конец ИнтеграцияМДЛП
