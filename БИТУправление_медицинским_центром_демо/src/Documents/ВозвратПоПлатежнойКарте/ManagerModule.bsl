#Область ПрограммныйИнтерфейс

// Заполнить документ возврата по оплате платежной картой.
//
// Параметры:
//  ДокументОбъект	 - ДокументОбъект.ВозвратПоПлатежнойКарте - заполняемый документ.
//
Процедура ЗаполнитьВозвратПоОплатеПлатежнойКартой(ДокументОбъект) Экспорт
	
	СписокЗаполняемыхРеквизитов =	"Клиент,
									|СуммаДокумента,
									|Основание,
									|СпособРасчетаЧекаККМ,
									|СсылочныйНомер,
									|НомерКарты";
									
	Если ДокументОбъект.Метаданные().Реквизиты.Найти("Сделка") <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.КонкатенацияСтрок(СписокЗаполняемыхРеквизитов,"Сделка",",");
	КонецЕсли;								
									
	ЗаполнитьЗначенияСвойств(ДокументОбъект, ДокументОбъект.ДокументОснование, СписокЗаполняемыхРеквизитов);
	ДокументОбъект.Выдать = ДокументОбъект.ДокументОснование.ПринятоОт;
	ДокументОбъект.КредитныеДанные.Загрузить(ДокументОбъект.ДокументОснование.КредитныеДанные.Выгрузить());
	
	// Эквайринговый терминал заполняется из основания только если он - это настройка обмена с Системой быстрых платежей.
	ЭТОснования = ОбщегоНазначенияКлиентСервер.СвойствоОбъекта(ДокументОбъект.ДокументОснование, "ЭквайринговыйТерминал", Неопределено);
	Если ЗначениеЗаполнено(ЭТОснования)
		И ИнтеграцияСБП.ЭтоЭквайринговыйТерминалСБП(ЭТОснования)
	Тогда
		ДокументОбъект.ЭквайринговыйТерминал = ЭТОснования; 		
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти