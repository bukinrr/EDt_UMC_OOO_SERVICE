#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
			
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(Объект, Истина, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	             
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("БанковскаяВедомостьЗПЗаполнена",, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоРасчетамССотрудниками(Команда)
	
	Если Объект.РасчетыССотрудниками.Количество() <> 0 Тогда
		
		ТекстВопроса = НСтр("ru='Таблица с сотрудниками будет очищена. Продолжить?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30 , КодВозвратаДиалога.Да);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;	
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьТЧРасчетовССотрудниками();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТЧРасчетовССотрудниками()
	
	Объект.РасчетыССотрудниками.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыССотрудникамиОстатки.Сотрудник,
		|	ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыССотрудниками.Остатки(&Дата, ) КАК ВзаиморасчетыССотрудникамиОстатки
		|ГДЕ
		|	ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток > 0";
	
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	
	Объект.РасчетыССотрудниками.Загрузить(Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры

#КонецОбласти