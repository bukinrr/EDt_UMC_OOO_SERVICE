#Область ОписаниеПеременных
&НаКлиенте
Перем старыйВидСкидки;

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	РаботаСФормамиСервер.СоздатьПодменюДвиженияДокумента(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВидСкидкиПриИзменении(Неопределено);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ВидСкидкиПриИзменении(Элемент)
	
	типПолучателя = ОпределитьТипПолучателя();
	Элементы.НакопленияПолучатель.ОграничениеТипа = типПолучателя;
	
	Если старыйВидСкидки <> Неопределено Тогда
		Если НЕ ОдинаковыеВидыПолучателей(старыйВидСкидки, Объект.ВидСкидки) Тогда
			Для Каждого стр Из Объект.Накопления Цикл 
				стр.Получатель = Неопределено;
			КонецЦикла;	
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСкидкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	старыйВидСкидки = Объект.ВидСкидки;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция ОпределитьТипПолучателя()
	
	Если Объект.ВидСкидки.ВидПолучателя = Перечисления.ВидПолучателя.ВидКартыСкидок Или
		 Объект.ВидСкидки.ВидПолучателя = Перечисления.ВидПолучателя.КартаСкидок 
	Тогда
		Возврат ОбщегоНазначенияСервер.ПолучитьОписаниеТипаПоНаименованию("СправочникСсылка.КартыСкидок");
	ИначеЕсли Объект.ВидСкидки.ВидПолучателя = Перечисления.ВидПолучателя.Клиенты ИЛИ 
			  Объект.ВидСкидки.ВидПолучателя = Перечисления.ВидПолучателя.ВсеКлиенты 
	Тогда
		Возврат ОбщегоНазначенияСервер.ПолучитьОписаниеТипаПоНаименованию("СправочникСсылка.Клиенты");		
	Иначе
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Вид получателя в виде скидки не заполнен. По умолчанию будет установлен вид получателя ""Клиент""");
		КонецЕсли;
		Возврат ОбщегоНазначенияСервер.ПолучитьОписаниеТипаПоНаименованию("СправочникСсылка.Клиенты");		
	КонецЕсли;	
	
КонецФункции	

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(ТекущийОбъект,,ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ОдинаковыеВидыПолучателей(Скидка1, Скидка2)
	
	Если Скидка1 <> Неопределено И Скидка2 <> Неопределено Тогда
		Если Скидка1.ВидПолучателя = Скидка2.ВидПолучателя Тогда
			Возврат Истина;
		ИначеЕсли (Скидка1.ВидПолучателя = Перечисления.ВидПолучателя.Клиенты
			И Скидка2.ВидПолучателя = Перечисления.ВидПолучателя.ВсеКлиенты) ИЛИ
			(Скидка1.ВидПолучателя = Перечисления.ВидПолучателя.ВсеКлиенты
			И Скидка2.ВидПолучателя = Перечисления.ВидПолучателя.Клиенты) Тогда
			Возврат Истина;
		Иначе 	
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	Возврат Ложь;
КонецФункции	

#КонецОбласти