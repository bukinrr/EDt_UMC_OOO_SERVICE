#Область ОбработчикиСобытий
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДанныеОУ = ПолучитьДанныеОказанияУслугДляВводаПриема(ПараметрКоманды);
	
	Если ДанныеОУ.СписокСотрудников.Количество() = 0 Тогда
		Сотрудник = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
	ИначеЕсли ДанныеОУ.СписокСотрудников.Количество() > 1 Тогда
		ЭлементСписка = ДанныеОУ.СписокСотрудников.ВыбратьЭлемент("Выберите врача нового приёма");
		Если ЭлементСписка = Неопределено Тогда
			Возврат;
		Иначе
			Сотрудник = ЭлементСписка.Значение;
		КонецЕсли;
	Иначе
		Сотрудник = ДанныеОУ.СписокСотрудников[0].Значение;
	КонецЕсли;
	
	мОбрИнд = ДанныеОУ.Работы.Количество()-1;
	Для сч = 0 По мОбрИнд Цикл
		
		ДанныеРаботы = ДанныеОУ.Работы[мОбрИнд-сч];
		Если ДанныеРаботы.Сотрудник <> Сотрудник Тогда
			ДанныеОУ.Работы.Удалить(мОбрИнд-сч);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗначенияЗаполнения = Новый Структура("Клиент, Врач, Заявка, Филиал", ДанныеОУ.Клиент, Сотрудник, ДанныеОУ.ДокументОснование, ДанныеОУ.Филиал);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения, Работы", ЗначенияЗаполнения, ДанныеОУ.Работы);
	ОткрытьФорму("Документ.Прием.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеОказанияУслугДляВводаПриема(ОУСсылка)
	
	Результат = Новый Структура("Клиент, ДокументОснование, Филиал, СписокСотрудников, Работы");
	
	Результат.Клиент = ОУСсылка.Клиент;
	Результат.Филиал = ОУСсылка.Филиал;
	
	Если ТипЗнч(ОУСсылка.ДокументОснование) = Тип("ДокументСсылка.Заявка") Тогда
		Результат.ДокументОснование = ОУСсылка.ДокументОснование;
	КонецЕсли;
	
	СписокСотрудников = Новый СписокЗначений;
	мсТЧ = Новый Массив;
	мсТЧ.Добавить("Работы");
	мсТЧ.Добавить("Товары");
	СотрудникШапки = ОУСсылка.Сотрудник;
	
	Для Каждого ИмяТЧ Из мсТЧ Цикл
		ИмяРеквизитаСотрудник = ?(ИмяТЧ = "ПополнениеСертификатов", "Сотрудник", "Сотрудник");
		Для Каждого СтрокаТЧ Из ОУСсылка[ИмяТЧ] Цикл
			Сотрудник = ?(ЗначениеЗаполнено(СтрокаТЧ[ИмяРеквизитаСотрудник]), СтрокаТЧ[ИмяРеквизитаСотрудник], СотрудникШапки);
			Если СписокСотрудников.НайтиПоЗначению(Сотрудник) = Неопределено Тогда
				СписокСотрудников.Добавить(Сотрудник, Сотрудник.Наименование);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СотрудникШапки)
		И СписокСотрудников.НайтиПоЗначению(СотрудникШапки) = Неопределено 
	Тогда
		СписокСотрудников.Вставить(0, СотрудникШапки, СотрудникШапки.Наименование);
	КонецЕсли;
	
	Результат.СписокСотрудников = СписокСотрудников;
	
	Работы = Новый Массив;
	Для Каждого СтрокаТЧ Из ОУСсылка.Работы Цикл
		ДанныеРаботы = Новый Структура;
		ДанныеРаботы.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
		ДанныеРаботы.Вставить("ХарактеристикаНоменклатуры", СтрокаТЧ.ХарактеристикаНоменклатуры);
		ДанныеРаботы.Вставить("Количество", СтрокаТЧ.Количество);
		ДанныеРаботы.Вставить("Сотрудник", ?(ЗначениеЗаполнено(СтрокаТЧ.Сотрудник), СтрокаТЧ.Сотрудник, СотрудникШапки));
		Работы.Добавить(ДанныеРаботы);
	КонецЦикла;
	
	Результат.Работы = Работы;
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти
