#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДанныеОУ = ПолучитьДанныеОказанияУслугДляРекламации(ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ДанныеОУ);
	ОткрытьФорму("Документ.Рекламация.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеОказанияУслугДляРекламации(ОУСсылка)
	
	Результат = Новый Структура("ПредметРекламации, Клиент, Исполнитель, Прейскурант, Работы");
	
	Результат.ПредметРекламации = ОУСсылка;
	Результат.Клиент = ОУСсылка.Клиент;
	Результат.Исполнитель = ОУСсылка.Сотрудник;
	Результат.Прейскурант = ОУСсылка.Прейскурант;
	
	Работы = Новый Массив;
	Для Каждого СтрокаТЧ Из ОУСсылка.Работы Цикл
		ДанныеРаботы = Новый Структура;
		ДанныеРаботы.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
		ДанныеРаботы.Вставить("ХарактеристикаНоменклатуры", СтрокаТЧ.ХарактеристикаНоменклатуры);
		ДанныеРаботы.Вставить("Количество", СтрокаТЧ.Количество);
		ДанныеРаботы.Вставить("Цена", СтрокаТЧ.Цена);
		ДанныеРаботы.Вставить("Сумма", СтрокаТЧ.Сумма);
		ДанныеРаботы.Вставить("СуммаБезСкидок", СтрокаТЧ.Сумма + СтрокаТЧ.СуммаСкидки);
		Работы.Добавить(ДанныеРаботы);
	КонецЦикла;
	
	Результат.Работы = Работы;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти