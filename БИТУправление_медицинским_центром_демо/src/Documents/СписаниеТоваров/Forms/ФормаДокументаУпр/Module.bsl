/////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ. 

&НаКлиенте
Перем мТекущаяДатаДокумента Экспорт; // Хранит текущую дату документа - для проверки перехода документа в другой период установки номера.

&НаКлиенте
Перем мИзмененыНастройкиПодбора; // По-умолчанию = Неопределено.

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ.

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ВызватьОбщийМодульПроверитьНомерДокумента(мТекущаяДатаДокумента);
	мТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура ВызватьОбщийМодульПроверитьНомерДокумента(ДатаДок)
	
	РаботаСДиалогамиСервер.ПроверитьНомерДокумента(ЭтаФорма, ДатаДок);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ "ТОВАРЫ" И ЕГО РЕКВИЗИТОВ.

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент, НомерСтроки = Неопределено)
	ТоварыРеквизитПриИзменении("Товары.Номенклатура",НомерСтроки);	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	ТоварыРеквизитПриИзменении("Товары.ХарактеристикаНоменклатуры");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	ТоварыРеквизитПриИзменении("Товары.ЕдиницаИзмерения");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент, НомерСтроки)
	ТоварыРеквизитПриИзменении("Товары.Количество", НомерСтроки);
КонецПроцедуры

&НаКлиенте
ПРоцедура ТоварыРеквизитПриИзменении(ИмяРеквизита, НомерСтроки = Неопределено)
	ОбработкаРеквизитаУпр(ИмяРеквизита,?(НомерСтроки = Неопределено, Элементы.Товары.ТекущиеДанные, Объект.Товары[НомерСтроки]),ЭтаФорма);
КонецПроцедуры

// Обработка изменения реквизитов документа.
//
// Параметры
//  Имя			– Строка			– Имя реквизита документа с полным путем (например Тавары.Номенклатура).
//  ЭтаФорма	– Форма				– Ссылка на форму документа. 
//									  Если значение неопределено, производится программная обработка реквизитов.
//  ТекСтрока	– СтрокаТабличнойЧасти – Ссылка на строку табличной части документа, реквизит которой обрабатывается.
//										 Имеет смысл только для табличных частей документов.
//  ДопПараметры– Стркутура			– Структура, содержащая дополнительные параметры обработки реквизита.
// Возвращаемое значение:
//   Булево   – Результат выполнения обработки.
&НаКлиенте
Функция ОбработкаРеквизитаУпр(Имя,ТекСтрока=Неопределено,ЭтаФорма=Неопределено,ДопПараметры=Неопределено)
	
	// ОБРАБОТКА РЕКВИЗИТОВ ДОКУМЕНТА .
	Если Имя="Склад" Тогда
		
	// ОБРАБОТКА РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ "ТОВАРЫ".
	ИначеЕсли Имя="Товары.Номенклатура" Тогда
		Рез=РаботаСДокументамиКлиент.дкОбработкаРеквизитаКлиент(Объект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
		ТекСтрока.Коэффициент = ДопСерверныеФункции.ПолучитьРеквизит(ТекСтрока.ЕдиницаИзмерения, "Коэффициент");
		
	ИначеЕсли Имя="Товары.ЕдиницаИзмерения" Тогда
		
		ТекСтрока.Коэффициент = ДопСерверныеФункции.ПолучитьРеквизит(ТекСтрока.ЕдиницаИзмерения, "Коэффициент");
		
	ИначеЕсли Имя="Товары.Количество" Тогда
		
		рез=РаботаСДокументамиКлиент.дкОбработкаРеквизитаКлиент(Объект,Имя,ТекСтрока,ЭтаФорма,допПараметры);
		
	Иначе 
		Возврат РаботаСДокументамиКлиент.дкОбработкаРеквизитаКлиент(Объект,Имя,ТекСтрока,ЭтаФорма,ДопПараметры);
	КонецЕсли;
КонецФункции // ОбработкаРеквизита().

&НаКлиенте
Процедура ИнвентаризацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Основание) Тогда
		Если ТипЗнч(Объект.Основание) = Тип("ДокументСсылка.ПеремещениеМатериалов") Тогда
			Объект.Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Основание, "СкладПолучатель");
		Иначе
			Объект.Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Основание, "Склад");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ.

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);	
	
	Элементы.ТоварыАртикул.Видимость = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ПоказыватьАртикул" , ТекущаяДата());
	
	Элементы.ТоварыХарактеристикаНоменклатуры.Видимость = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ВестиУчетПоХарактеристикам");
	
	РаботаСФормамиСервер.НастройкаПодбораПриСоздании(ЭтаФорма, Ложь, "Товары", "Материал");	
	
	// ИнтеграцияМДЛП
	СформироватьТекстУведомленияМДЛП();
	// Конец ИнтеграцияМДЛП
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// ИнтеграцияМДЛП
	СформироватьТекстУведомленияМДЛП();
	// Конец ИнтеграцияМДЛП
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	мТекущаяДатаДокумента = Объект.Дата;
	
	Если Параметры.Ключ.Пустая() Тогда
		ВхДокДата = ТекущаяДата();
		Модифицированность = Ложь;
	КонецЕсли;
	
	ПриИзмененииОтнестиНаЗатраты();
	
	Если Истина
	   И МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО
		ОписаниеОшибки = "";
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
		ПоддерживаемыеТипыВО.Добавить("ТерминалСбораДанных");
		
		ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);    
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, ПоддерживаемыеТипыВО);

	КонецЕсли;
	
	РаботаСФормамиКлиент.ОчиститьЛишниеКомандыПобор(ЭтаФорма);
    ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	// МеханизмВнешнегоОборудования.
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
	ПоддерживаемыеТипыВО.Добавить("ТерминалСбораДанных");
	
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(Неопределено, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	// Конец МеханизмВнешнегоОборудования.
	Если Не ЗавершениеРаботы Тогда	
		СохранитьНастройкиПодбора();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(ТекущийОбъект,,ЭтаФорма);
	
	РаботаСДокументамиСервер.ПроверитьПринадлежностьСкладаФилиалу(Объект.Ссылка);
	
	// ИнтеграцияМДЛП
	СформироватьТекстУведомленияМДЛП();
	// Конец ИнтеграцияМДЛП
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ScanData" Тогда
		Если ВводДоступен() Тогда
			ТипШК = Неопределено;
			РаботаСТорговымОборудованиемКлиент.ОбработатьСобытиеСШКФормы(ЭтаФорма, Параметр, ТипШК);
		КонецЕсли;
	КонецЕсли;
	
	// ИнтеграцияМДЛП
	Если (ИмяСобытия = "ИзменениеСостоянияМДЛП" Или ИмяСобытия = "Запись_УведомлениеОбОтгрузкеМДЛП")
		И Параметр.Основание = Объект.Ссылка Тогда
		СформироватьТекстУведомленияМДЛП();
	КонецЕсли;
	Если ИмяСобытия = "ВыполненОбменМДЛП" Тогда
		СформироватьТекстУведомленияМДЛП();
	КонецЕсли;
	// Конец ИнтеграцияМДЛП
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник) 
	
	Перем Действие;
	
	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		ЗначениеВыбора.Свойство("Действие", Действие);
		
		Если Действие = "ПодборМатериала" Тогда
			
			ДанныеНоменклатуры = РаботаСТорговымОборудованиемКлиент.ДанныеНоменклатуры();
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, ЗначениеВыбора);
			СШКНоменклатура(ДанныеНоменклатуры, Неопределено);
			
			Если Элементы.Товары.ТекущиеДанные = Неопределено И Объект.Товары.Количество() Тогда
				Объект.Товары[0].Склад = Объект.Склад;
			Иначе
				Объект.Товары[Объект.Товары.Количество() - 1].Склад= Объект.Склад;
			КонецЕсли;
			Элементы.Товары.Обновить();
			
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Функция осуществляет обработку считывания штрихкода номенклатуры.
//
// Параметры:
//  ДанныеНоменклатуры	 - Старуктура	 - сведения о считанной позиции.
//  СШК					 - Строка		 - Идентификатор сканера штрих-кода, с которым связано данное событие.
// 
// Возвращаемое значение:
//  Булево - Событие обработано.
//
&НаКлиенте
Функция СШКНоменклатура(ДанныеНоменклатуры, СШК) Экспорт
	Номенклатура				= ДанныеНоменклатуры.Номенклатура;
	Количество					= ДанныеНоменклатуры.Количество;
	ХарактеристикаНоменклатуры	= ДанныеНоменклатуры.ХарактеристикаНоменклатуры;
	Единица						= ДанныеНоменклатуры.ЕдиницаИзмерения;
	Цена						= ДанныеНоменклатуры.Цена;
	СерияНоменклатуры			= ДанныеНоменклатуры.СерияНоменклатуры;
	Штрихкод					= ДанныеНоменклатуры.Штрихкод;
	
	РеквизитыНоменклатуры = РаботаСДокументамиСервер.ДанныеНоменклатурыДляОбработкиЧтенияШК(Номенклатура, Ложь);
	Если РеквизитыНоменклатуры.ЭтоМатериал Тогда
		
		ТабличнаяЧасть = Объект.Товары;
		мсСтрокиТовары = РаботаСДокументамиКлиент.ЧтениеШтрихкода_НайтиСтрокиДокументаПоДаннымШтрихкода(ТабличнаяЧасть, ДанныеНоменклатуры, РеквизитыНоменклатуры);
		
		Если мсСтрокиТовары.Количество() = 0 Тогда
			
			// Добавление новой строки.
			СтрокаТовары = РаботаСДокументамиКлиент.ЧтениеШтрихкода_ДобавитьСтрокуТовараПоДаннымШтрихкода(ТабличнаяЧасть, ДанныеНоменклатуры, РеквизитыНоменклатуры);
			
			Если СтрокаТовары.Свойство("Цена") Тогда
				Если Цена <> Неопределено  И Цена > 0 Тогда
					СтрокаТовары.Цена = Цена;
				КонецЕсли;
			КонецЕсли;
			
			ТоварыНоменклатураПриИзменении(Неопределено, СтрокаТовары.НомерСтроки-1);
			НоваяСтрока = Истина;
		Иначе
			// Увеличивается количество в существующей строке.
			СтрокаТовары = мсСтрокиТовары[0];
			СтрокаТовары.Количество = СтрокаТовары.Количество + Количество;      
			ТоварыКоличествоПриИзменении(Неопределено, СтрокаТовары.НомерСтроки-1);
			
			НоваяСтрока = Ложь;
		КонецЕсли;
		
	Иначе
		РаботаСДокументамиКлиент.ЧтениеШтрихкода_ПредупреждениеОжидаетсяМатериал(Номенклатура);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // СШКНоменклатура().

&НаКлиенте
Процедура КоманднаяПанельТоварыЗаполнитьПоИнвентаризации(Команда)
	
	ДопольнительныеПараметры = Новый Структура;
	Если Объект.Товары.Количество()<>0 Тогда
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ТоварыЗаполнитьПоИнвентаризацииЗавершение", ЭтотОбъект, ДопольнительныеПараметры);
		ТекстВопроса = НСтр("ru='Перед заполнением табличная часть будет очищена. Заполнить?'");
		ПоказатьВопрос(ОповещениеОЗакрытии,ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, );
	Иначе
		ТоварыЗаполнитьПоИнвентаризацииЗавершение(КодВозвратаДиалога.Да, ДопольнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЗаполнитьПоИнвентаризацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоИнвентаризацииСервер();		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоИнвентаризацииСервер()
	об = ДанныеФормыВЗначение(Объект, ТИп("ДокументОбъект.СписаниеТоваров"));
	об.ЗаполнитьПоИнвентаризации();
	ЗначениеВДанныеФормы(об, Объект);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОтнесеноНаЗатратыПриИзменении(Элемент)
	
	ПриИзмененииОтнестиНаЗатраты();
	
	// Если не отнесено на затраты, тогда заполнение статьи затрат, категории выработки (специализации), номенклатуры-получателя пустыми ссылками.
	Если Не Объект.ОтнесеноНаЗатраты Тогда
		Для Каждого СтрокаТовары Из Объект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовары.СтатьяЗатрат) Тогда 
				СтрокаТовары.СтатьяЗатрат = ПредопределенноеЗначение("Справочник.СтатьиЗатрат.ПустаяСсылка");	
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТовары.КатегорияВыработки) Тогда 
				СтрокаТовары.КатегорияВыработки = ПредопределенноеЗначение("Справочник.КатегорииВыработки.ПустаяСсылка");	
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТовары.НоменклатураПолучатель) Тогда 
				СтрокаТовары.НоменклатураПолучатель = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОтнестиНаЗатраты()
	
	Элементы.Товары.ПодчиненныеЭлементы.ТоварыСтатьяЗатрат.Видимость			= Объект.ОтнесеноНаЗатраты;
	Элементы.Товары.ПодчиненныеЭлементы.ТоварыКатегорияВыработки.Видимость		= Объект.ОтнесеноНаЗатраты;
	Элементы.Товары.ПодчиненныеЭлементы.ТоварыНоменклатураПолучатель.Видимость	= Объект.ОтнесеноНаЗатраты;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтатьяЗатратПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ПолучательЗатраты = ПолучитьПолучателяЗатраты(ТекущиеДанные.СтатьяЗатрат);
	Если ЗначениеЗаполнено(ПолучательЗатраты) Тогда
		ТекущиеДанные.КатегорияВыработки = ПолучательЗатраты;
	КонецЕсли;
	                                                 
КонецПроцедуры                                              

&НаСервереБезКонтекста
Функция ПолучитьПолучателяЗатраты(СтатьяЗатрат)
	
	Если ЗначениеЗаполнено(СтатьяЗатрат) Тогда
		Возврат СтатьяЗатрат.ОсновнойПолучательЗатрат;
	Иначе 
		Возврат Справочники.КатегорииВыработки.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:
				|""%1"".'" );
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, РезультатВыполнения.ОписаниеОшибки); 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////
// МЕТОДЫ ПОДБОРА.

&НаКлиенте
Процедура Подбор(Команда)
	
	РаботаСФормамиКлиент.КнопкаПодборПриНажатии(ЭтаФорма, "Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыборПодбор(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)  
	
	Если Ложь Тогда ЗапрашиватьКоличество = Ложь; ЗапрашиватьЦену = Ложь; ЗапрашиватьХарактеристику = Ложь; ЗапрашиватьСерию = Ложь; КонецЕсли; 
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ДопСерверныеФункции.ПолучитьРеквизит(ВыбранноеЗначение, "ЭтоГруппа") тогда
		Действие = "ПодборМатериала";
	    Результат = РаботаСФормамиКлиент.ВыборПодборОбработка(ВыбранноеЗначение, ЗапрашиватьКоличество, ЗапрашиватьЦену, ЗапрашиватьХарактеристику, ЗапрашиватьСерию, Действие, ЭтаФорма); 
		Если Результат <> Неопределено Тогда
			ОбработкаВыбора(Результат, Неопределено);
			Модифицированность = Истина;
		КонецЕсли;

	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура Подключаемый_ПолеПодбораПриИзменении(Элемент)
	мИзмененыНастройкиПодбора = Истина;
	СохранитьНастройкиПодбора();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиПодбора()
	
	Если ЗначениеЗаполнено(мИзмененыНастройкиПодбора) Тогда 
		РаботаСФормамиСервер.СохранитьНастройкиПодбора(ЭтаФорма.ЗапрашиватьКоличество, ЭтаФорма.ЗапрашиватьЦену, ЭтаФорма.ЗапрашиватьХарактеристику, ЭтаФорма.ЗапрашиватьСерию, ТипЗнч(Объект.Ссылка));		
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#Область ТерминалыСбораДанных // ТЕРМИНАЛЫ СБОРА ДАННЫХ

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	ОповещенияПриПодключении = Новый ОписаниеОповещения("ЗагрузитьДанныеИзТСДЗавершение", ЭтотОбъект);    
	МенеджерОборудованияКлиент.НачатьЗагрузкуДанныеИзТСД(ОповещенияПриПодключении, УникальныйИдентификатор, Ложь)			
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСДЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		мсШтрихкоды = РезультатВыполнения.ТаблицаТоваров;
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ДобавлятьБезВладельца", Истина);
		РаботаСТорговымОборудованиемКлиент.ОбработатьШтрихкоды(ЭтаФорма, Объект, мсШтрихкоды, "Товары", ДопПараметры);
	Иначе
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:
		|""%1"".'" );
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, РезультатВыполнения.ОписаниеОшибки); 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ТекстУведомленияМДЛПОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИнтеграцияМДЛПКлиент.ТекстУведомленияМДЛПОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

// ИнтеграцияМДЛП

&НаСервере
Процедура СформироватьТекстУведомленияМДЛП()
	
	ИнтеграцияМДЛП.СформироватьТекстУведомленияМДЛП(ЭтотОбъект);
	
КонецПроцедуры

// Конец ИнтеграцияМДЛП
