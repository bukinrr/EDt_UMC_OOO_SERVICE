#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Действие = Параметры.Действие;
	Документ = Параметры.Документ;
	
	ОГРН = ?(ЗначениеЗаполнено(Параметры.ОГРН), Параметры.ОГРН, ОбменФСС.ПолучитьОГРНМедОрганизацииДляОбменаСФСС());
	
	Если ЗначениеЗаполнено(Документ) Тогда
		СНИЛС = ?(ЗначениеЗаполнено(Параметры.СНИЛС), Параметры.СНИЛС, Документ.СНИЛС);
		СНИЛС = СтрЗаменить(СНИЛС, "-","");
		СНИЛС = СтрЗаменить(СНИЛС, " ","");
		Номер = Формат(Документ.Номер,"ЧГ=");	
	Иначе
		Номер = ?(ЗначениеЗаполнено(Параметры.Номер),Параметры.Номер,""); 
		СНИЛС = ?(ЗначениеЗаполнено(Параметры.СНИЛС),Параметры.СНИЛС,"");
		СНИЛС = СтрЗаменить(СНИЛС, "-","");
		СНИЛС = СтрЗаменить(СНИЛС, " ","");
		Номер = Формат(Номер,"ЧГ=");
	КонецЕсли; 
		
	НоваяСтрока = ТаблицаПараметров.Добавить();
	НоваяСтрока.Параметр = "ОГРН";
	
	Если Действие = "ПолучитьСписокНомеров" Тогда
		ЭтаФорма.Заголовок = нСтр("ru='Получение списка номеров для ЭЛН из ФСС'", "ru");
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Параметр = "Количество";
		Количество = 100;
	ИначеЕсли Действие = "ОтправитьЭЛН" Тогда 
		ЭтаФорма.Заголовок = нСтр("ru='Отправка ЭЛН в ФСС'", "ru");
		
	ИначеЕсли Действие = "ОбновитьЭЛН" Тогда
		ЭтаФорма.Заголовок = нСтр("ru='Запрос данных ЭЛН из ФСС'", "ru");
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Параметр = "СНИЛС"; 
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Параметр = "Номер";
	ИначеЕсли Действие = "АннулироватьЭЛН" Тогда
		ЭтаФорма.Заголовок = нСтр("ru='Прекращение действия ЭЛН'", "ru");
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Параметр = "СНИЛС";
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Параметр = "Номер";
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Параметр = "Причина";
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Параметр = "Комментарий";
	
	ИначеЕсли Действие = "СписокЭЛН" Тогда
		ЭтаФорма.Заголовок = нСтр("ru='Получение списка ЭЛН'", "ru");
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Параметр = "СНИЛС";
	КонецЕсли;
	
	Для Каждого ЭлементФормы Из Элементы.ГруппаПолейВвода.ПодчиненныеЭлементы Цикл
		ЭлементФормы.Видимость = ТаблицаПараметров.НайтиСтроки(Новый Структура("Параметр",ЭлементФормы.Имя)).Количество() <> 0;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	ОписаниеРезультата = "";
	
	ТекстОшибки = "";
	
	ПараметрыЗапроса = Новый Структура;
	Для каждого СтрокаПараметра Из ТаблицаПараметров Цикл
		Если СтрокаПараметра.Параметр <> "Комментарий" И Не ЗначениеЗаполнено(ЭтаФорма[СтрокаПараметра.Параметр]) Тогда
			ТекстОшибки = ТекстОшибки + нСтр("ru='Заполните параметр '", "ru") + СтрокаПараметра.Параметр + Символы.ПС;
		Иначе
			ПараметрыЗапроса.Вставить(СтрокаПараметра.Параметр,ЭтаФорма[СтрокаПараметра.Параметр]);	
		КонецЕсли; 	
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОписаниеРезультата = ТекстОшибки;	
	Иначе	
		ПараметрыЗапроса.Вставить("Документ", Документ);
		ОбработчикЗавершения = Новый ОписаниеОповещения("ОтправкаЗавершение", ЭтаФорма, Действие);
		ПараметрыЗапроса.Вставить("ОбработчикРезультата", ОбработчикЗавершения);

		ОписаниеРезультата = нСтр("ru='Выполняется...'", "ru");
		
		Если Действие = "ПолучитьСписокНомеров" Тогда
			ОбменФССКлиент.ПолучитьСписокНомеров(ПараметрыЗапроса);
		ИначеЕсли Действие = "ОтправитьЭЛН" Тогда 
			ОбменФССКлиент.ОтправитьЭЛН(ПараметрыЗапроса);
		ИначеЕсли Действие = "ОбновитьЭЛН" Тогда
			ОбменФССКлиент.ОбновитьЭЛН(ПараметрыЗапроса);
		ИначеЕсли Действие = "АннулироватьЭЛН" Тогда 
			ОбменФССКлиент.АннулироватьЭЛН(ПараметрыЗапроса);	
		ИначеЕсли Действие = "СписокЭЛН" Тогда
			ОбменФССКлиент.СписокЭЛН(ПараметрыЗапроса);	
		КонецЕсли;		
	КонецЕсли;  

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОГРННачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ОГРНОкончаниеВыбора", ЭтотОбъект);
	ОткрытьФорму("Справочник.Организации.ФормаВыбора",,,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОГРНОкончаниеВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОГРН = ОбщегоНазначенияСервер.ПолучитьРеквизитСсылки(Результат, "ОГРН"); 	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОтправкаЗавершение(Результат, Действие) Экспорт
	Если ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
		ОписаниеОшибки = ПолучитьОписаниеОшибки(Результат.ТекстОшибки);
		ОписаниеРезультата = ОписаниеОшибки;
	Иначе
		ОписаниеРезультата = Нстр("ru='Успешно выполнено.'", "ru");
	КонецЕсли;	
	Если Действие = "СписокЭЛН" И Не ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
		Закрыть(Результат.Успех);	
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Функция ПолучитьОписаниеОшибки(ОписаниеОшибки)
	
	Если Найти(ОписаниеОшибки, "Couldn't resolve host name") > 0 Тогда
		Возврат "Нет связи с сервисами ФСС (Couldn't resolve host name).
				|Проверьте настройки локальной сети и подключение к интернету, либо попробуйте позже.
				|Вы можете проверить доступность серверов ФСС по кнопке ""Обмен с ФСС"" -> ""Проверить связь с ФСС"", которая находится в списке документов листков нетрудоспособности.";	
	Иначе
		Возврат ОписаниеОшибки; 
	КонецЕсли;
		
КонецФункции

#КонецОбласти