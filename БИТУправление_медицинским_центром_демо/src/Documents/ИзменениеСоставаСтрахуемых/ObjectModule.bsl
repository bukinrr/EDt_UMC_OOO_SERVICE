#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИзменениеСоставаСтрахуемыхСтрахуемые.Клиент,
	               |	ИзменениеСоставаСтрахуемыхСтрахуемые.СерияПолиса,
	               |	ИзменениеСоставаСтрахуемыхСтрахуемые.НомерПолиса,
	               |	ИзменениеСоставаСтрахуемыхСтрахуемые.НомерСтроки
	               |ПОМЕСТИТЬ ВремТЧ
	               |ИЗ
	               |	Документ.ИзменениеСоставаСтрахуемых.Страхуемые КАК ИзменениеСоставаСтрахуемыхСтрахуемые
	               |ГДЕ
	               |	ИзменениеСоставаСтрахуемыхСтрахуемые.Ссылка = &Ссылка
	               |	И ИзменениеСоставаСтрахуемыхСтрахуемые.Полис = ЗНАЧЕНИЕ(Справочник.СтраховыеПолисы.ПустаяСсылка)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВремТЧ.НомерСтроки,
	               |	СтраховыеПолисы.Ссылка,
	               |	ВремТЧ.Клиент,
	               |	ВремТЧ.СерияПолиса,
	               |	ВремТЧ.НомерПолиса
	               |ИЗ
	               |	ВремТЧ КАК ВремТЧ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраховыеПолисы КАК СтраховыеПолисы
	               |		ПО ВремТЧ.Клиент = СтраховыеПолисы.Владелец
	               |			И (ВремТЧ.СерияПолиса ПОДОБНО СтраховыеПолисы.Серия)
	               |			И (ВремТЧ.НомерПолиса ПОДОБНО СтраховыеПолисы.Номер)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ИскомаяСтроки = Страхуемые[Выборка.НомерСтроки-1];
		
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			ИскомаяСтроки.Полис = Выборка.Ссылка;		
		Иначе
			Если ВидОперации <> Перечисления.ВидыОперацийИзмененияСоставаСтрахуемых.ИсключениеИзСписка Тогда
				
				НовыйПолис = Справочники.СтраховыеПолисы.СоздатьЭлемент();
				НовыйПолис.Владелец = Выборка.Клиент;
				НовыйПолис.ВидПолиса = ВидПолиса;
				НовыйПолис.Контрагент = ВидПолиса.Контрагент;
				НовыйПолис.Номер = Выборка.НомерПолиса;
				НовыйПолис.Серия = Выборка.СерияПолиса;
				НовыйПолис.Записать();
				
				ИскомаяСтроки.Полис = НовыйПолис.Ссылка;
				
				Если НЕ ЗначениеЗаполнено(Выборка.Клиент.умцОсновнойСтраховойПолис) Тогда
					ОбъектКлиент = Выборка.Клиент.ПолучитьОбъект();
					ОбъектКлиент.умцОсновнойСтраховойПолис = НовыйПолис.Ссылка;
					ОбъектКлиент.Записать();
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Записать(РежимЗаписиДокумента.Запись);
	
	// Регистр СоставыСтрахуемых
	Движения.СоставыСтрахуемых.Записывать = Истина;
	Для Каждого ТекСтрокаСтрахуемые Из Страхуемые Цикл
		Если ЗначениеЗаполнено(ТекСтрокаСтрахуемые.Полис) Тогда
			Движение = Движения.СоставыСтрахуемых.Добавить();
			Движение.Период = Дата;
			Движение.ВидПолиса = ТекСтрокаСтрахуемые.Полис.ВидПолиса;
			Движение.Полис = ТекСтрокаСтрахуемые.Полис;
			Движение.Клиент = ТекСтрокаСтрахуемые.Клиент;
			Движение.Исключен = ВидОперации = Перечисления.ВидыОперацийИзмененияСоставаСтрахуемых.ИсключениеИзСписка;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
