#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	
	ПодсветкаРаспределенияИспользование = Истина;
	Элементы.ЗатратыПодсветкаРаспределенияЗатраты.Пометка = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриИзмененииВидОперации();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьДокументаРаспределениеЗатрат");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьЗатраты(Команда)
	
	Если Объект.Затраты.Количество() <> 0 Тогда
		ТекстВопроса = НСтр("ru='Перед заполнением табличная часть будет очищена. Заполнить?'");
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьЗатратыВопросЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Иначе
		ЗаполнитьЗатратыСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьЗатраты(Команда)
	РаспределитьЗатратыСервер();
КонецПроцедуры

&НаКлиенте
Процедура ПодсветкаРаспределенияЗатраты(Команда)
	
	Элементы.ЗатратыПодсветкаРаспределенияЗатраты.Пометка = Не Элементы.ЗатратыПодсветкаРаспределенияЗатраты.Пометка;
	
	ПодсветкаРаспределенияИспользование = Элементы.ЗатратыПодсветкаРаспределенияЗатраты.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОшибкиРаспределения(Команда)
	
	КопияОбъекта = Объект;
	ТабДок = ПолучитьОтчетОшибкиРаспределения(КопияОбъекта);
	
	СтруктураНаПечать = УниверсальныеМеханизмыСервер.НапечататьДокумент(ТабДок, 0, , НСтр("ru='Ошибки распределения затрат'"), Строка(Объект.Ссылка), Объект.Ссылка);
	ПечатьДокументовКлиент.ВывестиНапечататьДокумент(СтруктураНаПечать, Объект.Ссылка);
	
КонецПроцедуры

#Область ОбработчикиДинамическиСоздаваемыхКоманд

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Объект.Затраты.Очистить();
	Объект.Распределение.Очистить();
	
	ПриИзмененииВидОперации();

КонецПроцедуры

&НаКлиенте
Процедура ЗатратыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТекущийЭлемент = Элементы.ЗатратыОшибкаРаспределения Тогда
		СтандартнаяОбработка = Ложь;
		ОшибкаРаспределения = Элементы.Затраты.ТекущиеДанные.ОшибкаРаспределения;
		Если ЗначениеЗаполнено(ОшибкаРаспределения) Тогда
			ОткрытьЗначение(ОшибкаРаспределения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗатратыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Затраты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодсветкаРаспределенияСтатьяЗатрат = ТекущиеДанные.СтатьяЗатрат;
	ПодсветкаРаспределенияФилиал = ТекущиеДанные.Филиал;
	ПодсветкаРаспределенияНаправление = ТекущиеДанные.Специализация;
	
	Если ПодсветкаРаспределенияИспользование Тогда
		
		Отбор = Новый Структура("СтатьяЗатрат, Филиал");
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРаспределениеЗатрат.РаспределениеЗатратСпециализацийНаНоменклатуру") Тогда
			Отбор.Вставить("Специализация");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Отбор, ТекущиеДанные);
		
		НайденныеСтроки = Объект.Распределение.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() <> 0 Тогда
			
			Элементы.Распределение.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриИзмененииВидОперации()
	
	ЭтоРапределениеНаНоменклатуру = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРаспределениеЗатрат.РаспределениеЗатратСпециализацийНаНоменклатуру");
	
	ПодсветкаРаспределенияУчетНаправления					 = ЭтоРапределениеНаНоменклатуру;
	Элементы.ЗатратыСпециализация.Видимость					 = ЭтоРапределениеНаНоменклатуру;
	Элементы.ЗатратыИсточникЗаполненияЗатрат.Видимость		 = ЭтоРапределениеНаНоменклатуру;
	Элементы.РаспределениеНоменклатура.Видимость			 = ЭтоРапределениеНаНоменклатуру;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗатратыВопросЗавершение(Ответ, ДопонительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьЗатратыСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗатратыСервер()
	
	Документы.РаспределениеЗатрат.ЗаполнитьЗатратыДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьЗатратыСервер()
	
	Документы.РаспределениеЗатрат.РаспределитьЗатратыДокумента(Объект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОтчетОшибкиРаспределения(Объект)
	
	Возврат Документы.РаспределениеЗатрат.ПолучитьОтчетОшибкиРаспределения(Объект);
	
КонецФункции

#КонецОбласти