
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма, Ложь);
	Если Объект.Ссылка.Пустая() Тогда
		Если Не ЗначениеЗаполнено(Объект.Сотрудник) Тогда
			Объект.Сотрудник = ПараметрыСеанса.ТекущийПользователь.Сотрудник;
		КонецЕсли;
	КонецЕсли;		
	
	ЗаполнитьСписокДействийМедосмотра(Объект.Ссылка.Пустая());
	
	Если Объект.Ссылка.Пустая()
		И Объект.ПараметрыПриема.Количество() = 0
	Тогда
		МедосмотрыСервер.ОбновитьСписокПараметровДокументаМедосмотраСправки(ОбщегоНазначенияКлиентСервер.ПолучитьМассивОтмеченныхЭлементовСписка(ДействияМедосмотра),
			Объект.ПараметрыПриема, Объект, , Объект.ПрохождениеМедОсмотра);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПечатьДокументовКлиент.УстановитьЗаголовокПечатнойФормы(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Для Каждого СтрокаДействия Из ДействияМедосмотра Цикл
		Если СтрокаДействия.Пометка Тогда
			ТекущийОбъект.ДействияМедосмотра.Добавить().Действие = СтрокаДействия.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(ТекущийОбъект,,ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокПараметров(Команда)
	
	ОбновитьСписокПараметровДокументаМедосмотраСправки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоУмолчаниюЗначенияПараметров(Команда)
	
	ПараметрыДляАвтоЗаполнения = Новый Массив;
	Для Каждого СтрокаПараметр Из Объект.ПараметрыПриема Цикл
		ПараметрыДляАвтоЗаполнения.Добавить(СтрокаПараметр.Параметр);
	КонецЦикла;
	
	ЗначенияПоУмолчанию = РаботаСШаблонамиHTML.ПолучитьЗначенияПоУмолчаниюМассиваПараметров(ПараметрыДляАвтоЗаполнения, Объект);
	Для Каждого ЗначениеПоУмолчанию Из ЗначенияПоУмолчанию Цикл
		НайденныеСтроки = Объект.ПараметрыПриема.НайтиСтроки(Новый Структура("Параметр", ЗначениеПоУмолчанию.Параметр));
		Если НайденныеСтроки.Количество() <> 0 Тогда
			НайденныеСтроки[0].Значение = ЗначениеПоУмолчанию.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпециализацияПриИзменении(Элемент)
	
	ЗаполнитьСписокДействийМедосмотра();
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияМедосмотраПриИзменении(Элемент)
	
	ОбновитьСписокПараметровДокументаМедосмотраСправки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыПриемаЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПараметрыПриема.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.Параметр) Тогда
			ТипЗначения = ДопСерверныеФункции.ПолучитьРеквизит(ТекущиеДанные.Параметр, "ТипЗначения");
			
			Если ТипЗначения.СодержитТип(Тип("Строка")) Тогда
				// Если это строковый параметр, то открываем форму ввода медицинского текстового параметра.
				СтандартнаяОбработка = Ложь;
				ОповещениеОВыборе = Новый ОписаниеОповещения("ВыбораЗначенияПараметраЗавершение", ЭтотОбъект); 
				МедицинскаяДеятельностьКлиент.ПоказатьФормуВводаТекста(Строка(ТекущиеДанные.Значение), ТекущиеДанные.Параметр, ОповещениеОВыборе, ЭтотОбъект);
				
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбораЗначенияПараметраЗавершение(Значение, ДополнительныеПараметры) Экспорт 
	
	Если Значение = Неопределено Тогда // Выбора не было
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ТекущиеДанные = Элементы.ПараметрыПриема.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.Значение = Значение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьСписокПараметровДокументаМедосмотраСправки()
	
	мПараметрыПриема = Объект.ПараметрыПриема;
	
	МедосмотрыСервер.ОбновитьСписокПараметровДокументаМедосмотраСправки(ОбщегоНазначенияКлиентСервер.ПолучитьМассивОтмеченныхЭлементовСписка(ДействияМедосмотра),
		мПараметрыПриема, Объект, , Объект.ПрохождениеМедОсмотра);
	
	Объект.ПараметрыПриема.Очистить();
	Для Каждого ПараметрПриема Из мПараметрыПриема Цикл
		ЗаполнитьЗначенияСвойств(Объект.ПараметрыПриема.Добавить(), ПараметрПриема);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДействийМедосмотра(НовыеОтметитьВыполненными = Ложь);
	
	ДействияМедосмотра.Очистить();
	
	Если ЗначениеЗаполнено(Объект.ПрохождениеМедОсмотра) Тогда
		
		ТаблицаДействий = МедосмотрыСервер.ПолучитьДействияМедосмотраПМОПоСпециализации(Объект.ПрохождениеМедОсмотра, Объект.Специализация);
		
		Для Каждого СтрокаДействия Из ТаблицаДействий Цикл
			
			Если Не СтрокаДействия.Выполнено Или СтрокаДействия.ДокументРегистратор = Объект.Ссылка Тогда
			
				НоваяСтрока = ДействияМедосмотра.Добавить();
				НоваяСтрока.Значение = СтрокаДействия.Действие;
				НоваяСтрока.Пометка	 = СтрокаДействия.Выполнено Или НовыеОтметитьВыполненными;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
