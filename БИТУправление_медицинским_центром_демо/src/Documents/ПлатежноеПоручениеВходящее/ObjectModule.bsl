
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	РегистрыНакопления.ДвиженияДенежныхСредств.СформироватьДвиженияПоШапкеДокумента(Новый Структура("Дата, Касса, СуммаДокумента,СтатьяДвиженияДенежныхСредств",ДатаОплаты,Неопределено,СуммаДокумента, СтатьяДвиженияДенежныхСредств),Движения.ДвиженияДенежныхСредств, Истина, Отказ);
		
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		Движение = Движения.ВзаиморасчетыСКонтрагентами.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Сумма = СуммаДокумента;
		
	ИначеЕсли ТипЗнч(Контрагент) = Тип("СправочникСсылка.Клиенты") Тогда
		
		Движение = Движения.ВзаиморасчетыСКлиентами.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Клиент = Контрагент;
		Движение.Сумма	= СуммаДокумента;
		
		// Комплексные расчеты в взиморасчетах с клиентами.
		Если Метаданные.Документы.Найти("КомплексныйРасчетКлиента") <> Неопределено
			И ЗначениеЗаполнено(ЭтотОбъект.Сделка)
			И ТипЗнч(ЭтотОбъект.Сделка) = Тип("ДокументСсылка.КомплексныйРасчетКлиента")
			И ОбщегоНазначения.ОбщийМодуль("КомплексныеРасчетыКлиентов").РасчетСОбособленнымУчетомВзаиморасчетов(ЭтотОбъект.Сделка)
		Тогда
			Движение.Сделка = ЭтотОбъект.Сделка;
		КонецЕсли;
		
		ДвиженияПоРегиструОплаты();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда

		Контрагент			= Основание.Контрагент;
		ДокументОснование	= Основание.Ссылка;
		СуммаДокумента		= Основание.СуммаДокумента;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КомплексныйРасчетКлиента") Тогда
		
		СуммаДокумента = Макс(0, РаботаСКлиентамиПереопределяемый.ПолучитьВзаиморасчетыСКлиентом(Основание.Клиент, ТекущаяДата(), Основание));
		Контрагент = Основание.Клиент;
		Сделка = Основание;
		
	КонецЕсли;
	НомерЧекаККМ = "";
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	НомерЧекаККМ = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДвиженияПоРегиструОплаты()
	
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Клиенты") Тогда
		Движение = Движения.Оплаты.Добавить();
		Движение.Период		= Дата;
		Движение.ВидОплаты	= Перечисления.ВидыОплаты.Безналичные;
		Движение.Клиент		= Контрагент;
		Движение.Сумма		= СуммаДокумента;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

