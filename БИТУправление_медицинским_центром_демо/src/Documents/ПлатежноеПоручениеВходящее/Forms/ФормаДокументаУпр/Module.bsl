#Область ОписаниеПеременных
&НаКлиенте
Перем мТекущаяДатаДокумента Экспорт; // Хранит текущую дату документа - для проверки перехода документа в другой период
 
#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	
	Если Объект.Ссылка.Пустая()
		И Не ЗначениеЗаполнено(Объект.ДатаОплаты)
	Тогда
		Объект.ДатаОплаты = Объект.Дата;
	КонецЕсли;
	
	Элементы.ГруппаПечать.Видимость = ЗначениеЗаполнено(Объект.НомерЧекаККМ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастроитьЭлементыВыбораСделки();
	мТекущаяДатаДокумента = Объект.Дата;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(Объект.Ссылка, Ложь, ЭтаФорма);	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ВызватьОбщийМодульПроверитьНомерДокумента(мТекущаяДатаДокумента);
	мТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура ВызватьОбщийМодульПроверитьНомерДокумента(ДатаДок)
	
	РаботаСДиалогамиСервер.ПроверитьНомерДокумента(ЭтаФорма, ДатаДок);
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентПриИзменении(Элемент)
	НастроитьЭлементыВыбораСделки();
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	ДокументОснованиеПриИзмененииСервер();
КонецПроцедуры

&НаСервере
Процедура ДокументОснованиеПриИзмененииСервер()
	
	ДокументОбъект = ЭтаФорма.РеквизитФормыВЗначение("Объект");
	РаботаСДокументамиСервер.ПлатежноеПоручениеПриИзмененииДокументОснования(ДокументОбъект);
	ЭтаФорма.ЗначениеВРеквизитФормы(ДокументОбъект,"Объект");
	Объект.ПринятоОт = Объект.Контрагент;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Подключаемый_КнопкаФилиалПриНажатии(Команда)
	РаботаСДиалогамиКлиент.ДиалогКнопкаФилиалПриНажатии(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиДвиженияДокумента(Команда)
	РаботаСДиалогамиКлиент.ВывестиДвиженияДокумента(Объект.Ссылка, Команда);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьЭлементыВыбораСделки()
	
	ЭлементСделка = Элементы.Найти("Сделка");
	
	Если ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		Элементы.ДокументОснование.ОграничениеТипа = Новый ОписаниеТипов;
		
		СделкаВидимость = Ложь;
		
		ПараметрыВыбораЗначения = Новый Массив;
		Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
			Параметр = Новый ПараметрВыбора("Отбор.Контрагент", Объект.Контрагент);
			ПараметрыВыбораЗначения.Добавить(Параметр);
		КонецЕсли;
		Элементы.ДокументОснование.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЗначения);
		
	ИначеЕсли ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.Клиенты") Тогда
		
		Элементы.ДокументОснование.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.ОказаниеУслуг");
		Объект.ДокументОснование = Элементы.ДокументОснование.ОграничениеТипа.ПривестиЗначение(Объект.ДокументОснование);

		СделкаВидимость = Истина;
		
		Параметр = Новый ПараметрВыбора("Отбор.Клиент", Объект.Контрагент);
		ПараметрыВыбораЗначения = Новый Массив;
		ПараметрыВыбораЗначения.Добавить(Параметр);
		
		Если ЭлементСделка <> Неопределено Тогда
			ЭлементСделка.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЗначения);
		КонецЕсли;
		
	Иначе
		СделкаВидимость = Ложь;
	КонецЕсли;
	
	Если ЭлементСделка <>  Неопределено Тогда
		ЭлементСделка.Видимость = СделкаВидимость;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти