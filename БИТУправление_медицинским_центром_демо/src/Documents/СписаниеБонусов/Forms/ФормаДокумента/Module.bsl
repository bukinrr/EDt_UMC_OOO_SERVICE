#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.ФормаДокументаПриОткрытииСервер(ЭтаФорма);
	
	Если Объект.Ссылка.Пустая() Тогда
		// Автозаполнение нового документа.
		Если Не ЗначениеЗаполнено(Объект.ВидСкидки) Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ТипСкидки", Перечисления.ТипыСкидок.Бонусы);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВидыСкидок.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ВидыСкидок КАК ВидыСкидок
			|ГДЕ
			|	ВидыСкидок.ТипСкидки = &ТипСкидки";
			
			РезультатЗапроса = Запрос.Выполнить();
			Если Не РезультатЗапроса.Пустой() Тогда
				Выборка = РезультатЗапроса.Выбрать();
				Если Выборка.Количество() < 2 Тогда
					Выборка.Следующий();
					Объект.ВидСкидки = Выборка.Ссылка;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.ВидОперации) Тогда
			Объект.ВидОперации = Перечисления.ВидыОперацийРаботыСБонусами.Списание;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ДатаСгоранияБонуса.Видимость = ЗначениеЗаполнено(Объект.ДатаСгорания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.СозданоАвтоматически.Видимость = Объект.СозданАвтоматически;
	Если ТипЗнч(Объект.Получатель) = Тип("СправочникСсылка.КартыСкидок") Тогда
		ТипПолучателя = "Карта";
	Иначе
		ТипПолучателя = "Клиент";
	КонецЕсли;
	ИзменениеФорматаВводаПолучателя();
	
	Если Объект.Получатель.Пустая() Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Получатель;
	Иначе
		ЭтаФорма.ТекущийЭлемент = Элементы.СуммаСписания;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	РаботаСФормамиСервер.ВывестиЗаголовокФормыДокумента(ТекущийОбъект,,ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВыборТипаПолучателяПриИзменении(Элемент)
	ИзменениеФорматаВводаПолучателя();
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	ОбновитьПредставлениеКлиента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменениеФорматаВводаПолучателя()
	
	Если ТипПолучателя = "Клиент" Тогда
		ОписаниеТипа = Новый ОписаниеТипов("СправочникСсылка.Клиенты");
	ИначеЕсли ТипПолучателя = "Карта" Тогда
		ОписаниеТипа = Новый ОписаниеТипов("СправочникСсылка.КартыСкидок");
	Иначе
		Возврат;
	КонецЕсли;
	Элементы.Получатель.ОграничениеТипа = ОписаниеТипа;
	Объект.Получатель = ОписаниеТипа.ПривестиЗначение(Объект.Получатель);
	
	ОбновитьПредставлениеКлиента();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеКлиента()
	
	Если ТипЗнч(Объект.Получатель) = Тип("СправочникСсылка.КартыСкидок") Тогда
		Элементы.КлиентПредставление.Видимость = Истина;
		Клиент = ДопСерверныеФункции.ПолучитьРеквизит(Объект.Получатель, "ВладелецКарты");
	Иначе
		Элементы.КлиентПредставление.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти