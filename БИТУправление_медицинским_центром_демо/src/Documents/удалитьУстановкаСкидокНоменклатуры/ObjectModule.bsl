////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА.

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Проверка заполения документа.
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = "Проведение документа """ + СокрЛП(Ссылка) + """: ";
	СтруктураОбязательныхПолей = Новый Структура("ДатаНачалаСкидки");

	// Вызываем общую процедуру проверки.
	ПроведениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	// Движения по регистру "Размеры скидок".
	Если ДляВсейНоменклатуры	Тогда
		ДобавитьСтрокуДвиженияПоСкидкам();	
	Иначе	
		Для Каждого ТекСтрокаНоменклатура Из Фильтр Цикл
			ДобавитьСтрокуДвиженияПоСкидкам(ТекСтрокаНоменклатура.Номенклатура);
		КонецЦикла;
	КонецЕсли;
	Если ПоДнямНедели Тогда
		Для Каждого СтрВремяДействия Из ВремяПоДнямНедели Цикл
			Если СтрВремяДействия.Выбран Тогда
				Движение = Движения.ВремяДействияСкидок.Добавить();
				ЗаполнитьЗначенияСвойств(Движение,СтрВремяДействия);
				Движение.Период = Дата;
				Движение.Регистратор = Ссылка;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		флЗаписать = Ложь;
		Если Накопительная И Скидка <> 0 Тогда
			Скидка = 0;
			флЗаписать = Истина;
		КонецЕсли;
		
		Если ДляВсейНоменклатуры И Фильтр.Количество() <> 0 Тогда
			Фильтр.Очистить();
			флЗаписать = Истина;
		КонецЕсли;
		
		Если флЗаписать Тогда
			Записать();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСтрокуДвиженияПоСкидкам(Номенклатура = Неопределено)
	Движение = Движения.РазмерыСкидок.Добавить();
	Движение.Регистратор     = Ссылка;
	Движение.Период			 = ДатаНачалаСкидки;
	Движение.КонецИнтервала  = ДатаОкончанияСкидки;
	Движение.Фильтр          = ?(ЗначениеЗаполнено(ЗначенияФильтра),ЗначенияФильтра,Неопределено);
	Движение.Номенклатура    = Номенклатура;
	Движение.ПроцентСкидки   = Скидка;	
	Движение.Накопительная   = Накопительная;
	Движение.ПоДнямНедели    = ПоДнямНедели;
КонецПроцедуры

#КонецОбласти