#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодЗаписи.ДатаНачала = НачалоДня(ТекущаяДата() + 86400);
	ПериодЗаписи.ДатаОкончания = ПериодЗаписи.ДатаНачала;
	ПричинаЗаписи = Справочники.ПричиныЗаписиНаПрием.КонтрольныйОсмотр;
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	СписокКлиентов = Параметры.СписокКлиентов;
	СписокКлиентов.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СоздатьЛистыОжидания(Команда)
	
	СозданоОбъектов = СоздатьЛистыОжиданияНаСервере();
	Предупреждение("Создано задач: " + Строка(СозданоОбъектов), 10, "Оповещение");
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция СоздатьЛистыОжиданияНаСервере()
	
	СчСозданных = 0;
	Для Каждого элКлиент Из СписокКлиентов Цикл 
		
		Если Не элКлиент.Пометка Тогда 
			Продолжить;
		КонецЕсли;
		
		ЛООбъект = Документы.ЛистОжидания.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(ЛООбъект, ЭтаФорма);
		ЛООбъект.Дата = ТекущаяДата();
		ЛООбъект.Клиент = элКлиент.Значение;
		ЛООбъект.ДатаНачалаПлан = ПериодЗаписи.ДатаНачала;
		ЛООбъект.ДатаОкончанияПлан = ПериодЗаписи.ДатаОкончания;
		ЛООбъект.Сотрудник = Сотрудник;
		Если ЗначениеЗаполнено(Номенклатура) Тогда 
			НоваяСтрока = ЛООбъект.Работы.Добавить();
			НоваяСтрока.Номенклатура = Номенклатура;
		КонецЕсли;
		ЛООбъект.Записать();
		
		СчСозданных = СчСозданных + 1;
		
	КонецЦикла;
	
	Возврат СчСозданных;
	
КонецФункции

#КонецОбласти

