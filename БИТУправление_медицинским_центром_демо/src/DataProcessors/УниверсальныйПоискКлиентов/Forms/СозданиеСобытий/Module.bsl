#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Филиал = УправлениеНастройками.ПолучитьФилиалПоУмолчаниюПользователя();
	ДатаПлан = ТекущаяДата() + 86400;
	Исполнитель = ПараметрыСеанса.ТекущийПользователь;
	СписокКлиентов = Параметры.СписокКлиентов;
	СписокКлиентов.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапланироватьЗвонки(Команда)
	
	ДанныеФормы = Новый Структура("ДатаПлан, Исполнитель, Филиал, СписокКлиентов, ТипЗвонка, Цель");
	ЗаполнитьЗначенияСвойств(ДанныеФормы, ЭтаФорма);
	СозданоОбъектов = ЗапланироватьЗвонкиНаСервере(ДанныеФормы);
	ОповещениеПродолжения = Новый ОписаниеОповещения("ЗапланироватьЗвонкиЗавершение", ЭтаФорма);
	ТекстПредупреждения = СтрШаблон(НСтр("ru= 'Запланировано звонков: %1'"), Строка(СозданоОбъектов));
	ПоказатьПредупреждение(ОповещениеПродолжения, ТекстПредупреждения, 10, "Оповещение");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапланироватьЗвонкиЗавершение(ДопПараметры) Экспорт
	
	Закрыть();	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапланироватьЗвонкиНаСервере(ДанныеФормы)
	
	СчСозданных = 0;
	Для Каждого элКлиент Из ДанныеФормы.СписокКлиентов Цикл 
		
		Если Не элКлиент.Пометка Тогда 
			Продолжить;
		КонецЕсли;
		
		СобытиеОбъект = Документы.Событие.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(СобытиеОбъект, ДанныеФормы);
		СобытиеОбъект.Дата = ТекущаяДата();
		СобытиеОбъект.ВидСобытия = Перечисления.ВидыСобытий.ИсходящийЗвонок;
		СобытиеОбъект.Клиент = элКлиент.Значение;
		СобытиеОбъект.ПлановыйИсполнитель = ДанныеФормы.Исполнитель;
		СобытиеОбъект[ПроцедурыСпециализацииПоставки.ИмяРеквизитаФилиалДокументов()] = ДанныеФормы.Филиал;
		СобытиеОбъект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		СобытиеОбъект.Записать();
		СчСозданных = СчСозданных + 1;
		
	КонецЦикла;
	
	Возврат СчСозданных;
	
КонецФункции

#КонецОбласти
