#Область ПрограммныйИнтерфейс

// Возвращает статусы запрошенных документов
//
// Параметры:
//  ЗапрошенныеДокументы - Массив - массив ссылок на документы.
//  АдресРезультата		 - Строка - адрес результата.
//
Процедура ОбновитьСтатусыЗапрошенныхДокументов(ЗапрошенныеДокументы, АдресРезультата = Неопределено) Экспорт
	
	МассивИдентификаторовРЭМД = Новый Массив;
	Для Каждого СтрокаДокумента Из ЗапрошенныеДокументы Цикл
		МассивИдентификаторовРЭМД.Добавить(СтрокаДокумента.emdrid);
	КонецЦикла;
	
	ПолученныеФайлы = ПолучитьФайлыДокументовПоУИДРЭМД(МассивИдентификаторовРЭМД);
	
	УИДыПолученныхФайлов = Новый Массив;
	
	Для Каждого ЗапрошенныйДокумент Из ЗапрошенныеДокументы Цикл
		ЗапрошенныйДокумент.ФайлДокумента = ПолученныеФайлы.Получить(ЗапрошенныйДокумент.emdrid);
		Если ЗначениеЗаполнено(ЗапрошенныйДокумент.ФайлДокумента) Тогда
			УИДыПолученныхФайлов.Добавить(ЗапрошенныйДокумент.emdrid);
		Иначе
			ЗапрошенныйДокумент.ОтправленЗапросНаПолучение = Не ЗначениеЗаполнено(ЗапрошенныйДокумент.ФайлДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Если АдресРезультата <> Неопределено Тогда
		ПоместитьВоВременноеХранилище(УИДыПолученныхФайлов, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

// Получить файлы документов по УИДРЭМД.
//
// Параметры:
//  МассивИдентификаторовРЭМД	 - Массив	 - массив идентификаторов
// 
// Возвращаемое значение:
//  Соответствие - файлы документов.
//
Функция ПолучитьФайлыДокументовПоУИДРЭМД(МассивИдентификаторовРЭМД) Экспорт
	
	ФайлыПоИдентификаторуРЭМД = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХранилищеДополнительнойИнформации.Ссылка КАК Ссылка,
	|	ХранилищеДополнительнойИнформации.Идентификатор КАК Идентификатор
	|ИЗ
	|	Справочник.ХранилищеДополнительнойИнформации КАК ХранилищеДополнительнойИнформации
	|ГДЕ
	|	ХранилищеДополнительнойИнформации.Классификация = &Классификация
	|	И ХранилищеДополнительнойИнформации.Идентификатор В(&ИдентификаторыРЭМД)
	|	И ХранилищеДополнительнойИнформации.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ИдентификаторыРЭМД", МассивИдентификаторовРЭМД);
	Запрос.УстановитьПараметр("Классификация", Справочники.КлассификацияПрикрепленныхФайлов.ИзВнешнейМИС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ФайлыПоИдентификаторуРЭМД.Получить(Выборка.Идентификатор) = Неопределено Тогда
			ФайлыПоИдентификаторуРЭМД.Вставить(Выборка.Идентификатор, Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ФайлыПоИдентификаторуРЭМД;
	
КонецФункции

#КонецОбласти