
&НаКлиенте
Процедура Сохранить(Команда)
	
	Результат = Новый Соответствие;
	
	Для Каждого Эл Из ПараметрыВерхнее Цикл
		Результат.Вставить(Эл, Верхнее);
	КонецЦикла; 
	
	Для Каждого Эл Из ПараметрыНижнее Цикл
		Результат.Вставить(Эл, Нижнее);
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьПараметрыСТребуемымТипом();
КонецПроцедуры 

&НаСервере
Процедура УстановитьПараметрыСТребуемымТипом()
	
	ВерхнееПоКлассификатору = ИнтеграцияЕГИСЗОбщегоНазначенияПовтИсп.ЭлементКлассификатораПоУИДЕГИСЗ(3);
	НижнееПоКлассификатору = ИнтеграцияЕГИСЗОбщегоНазначенияПовтИсп.ЭлементКлассификатораПоУИДЕГИСЗ(2);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыHTML.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ПараметрыHTML.Назначение = &Нижнее
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Нижнее
	|ИЗ
	|	ПланВидовХарактеристик.ПараметрыHTML КАК ПараметрыHTML
	|ГДЕ
	|	(ПараметрыHTML.Назначение = &Верхнее
	|			ИЛИ ПараметрыHTML.Назначение = &Нижнее)
	|	И (НЕ &ИспользоватьОтбор
	|			ИЛИ ПараметрыHTML.Ссылка В (&ПараметрыШаблона))";
	
	Запрос.УстановитьПараметр("Верхнее", ВерхнееПоКлассификатору);
	Запрос.УстановитьПараметр("Нижнее", НижнееПоКлассификатору);
	
	ПараметрыШаблона = Новый Массив();
	
	Если Параметры.Свойство("ПараметрыШаблона") Тогда
		Для Каждого Эл Из Параметры.ПараметрыШаблона Цикл
			ПараметрыШаблона.Добавить(Эл.Параметр);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИспользоватьОтбор", ?(ЗначениеЗаполнено(ПараметрыШаблона), Истина, Ложь)); 
	Запрос.УстановитьПараметр("ПараметрыШаблона", ПараметрыШаблона);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	мВерхнее = Новый Массив;
	мНижнее = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Нижнее Тогда
			мНижнее.Добавить(Выборка.Ссылка);
		Иначе
			мВерхнее.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыВерхнее = Новый ФиксированныйМассив(мВерхнее);
	ПараметрыНижнее = Новый ФиксированныйМассив(мНижнее);
	
	Элементы.Верхнее.Видимость = мВерхнее.Количество() <> 0;
	Элементы.Нижнее.Видимость = мНижнее.Количество() <> 0;
	
КонецПроцедуры
