#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Результат = Параметры.НачальноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Для Каждого ЗначениеЗанятости Из Элементы.Занятость.СписокВыбора Цикл
		Если СтрНачинаетсяС(Результат, ЗначениеЗанятости.Значение) Тогда
			
			Занятость = ЗначениеЗанятости.Значение;
			Если Элементы.Занятость.СписокВыбора.Индекс(ЗначениеЗанятости) = ИндексПрочейЗанятости() Тогда
				ЧастиСтроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Результат, РазделительПрочее());
				ЧастиСтроки.Удалить(0);
				Прочее = СтрСоединить(ЧастиСтроки, РазделительПрочее());
			ИначеЕсли Элементы.Занятость.СписокВыбора.Индекс(ЗначениеЗанятости) >= ИндексПервойДетскойЗанятости() Тогда
				ЧастиСтроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Результат, РазделительСоциальноеПоложение());
				ЧастиСтроки.Удалить(0);
				СтрокаСоциальногоПоложения = СтрСоединить(ЧастиСтроки, РазделительСоциальноеПоложение());
				
				Для Каждого ЗначениеСоциальногоПоложения Из Элементы.СоциальноеПоложение.СписокВыбора Цикл
					Если СтрокаСоциальногоПоложения = ЗначениеСоциальногоПоложения.Значение Тогда
						СоциальноеПоложение = СтрокаСоциальногоПоложения;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ВычислитьРезультатПоПолям();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Принять(Команда)
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗанятостьПриИзменении(Элемент)
	
	ВычислитьРезультатПоПолям();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПриИзменении(Элемент)
	
	ВычислитьРезультатПоПолям();
	
КонецПроцедуры

&НаКлиенте
Процедура СоциальноеПоложениеПриИзменении(Элемент)
	
	ВычислитьРезультатПоПолям();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ИндексПрочейЗанятости()
	
	Возврат 5;
	
КонецФункции

&НаКлиенте
Функция ИндексПервойДетскойЗанятости()
	
	Возврат 6;
	
КонецФункции

&НаКлиенте
Функция РазделительПрочее()
	
	Возврат ": ";
	
КонецФункции

&НаКлиенте
Функция РазделительСоциальноеПоложение()
	
	Возврат "; социальное положение: ";
	
КонецФункции

&НаКлиенте
Процедура ВычислитьРезультатПоПолям()
	
	Если Не ЗначениеЗаполнено(Занятость) Тогда
		Результат = "";
		Элементы.Прочее.Видимость = Ложь;
		Элементы.СоциальноеПоложение.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	мЧастиСтроки = Новый Массив;
	мЧастиСтроки.Добавить(Занятость);
	Разделитель = "";
	
	ЗначениеЗанятости = Элементы.Занятость.СписокВыбора.НайтиПоЗначению(Занятость);
	
	Если Элементы.Занятость.СписокВыбора.Индекс(ЗначениеЗанятости) >= ИндексПервойДетскойЗанятости() Тогда
		Элементы.СоциальноеПоложение.Видимость = Истина;
		Элементы.Прочее.Видимость = Ложь;
		Если ЗначениеЗаполнено(СоциальноеПоложение) Тогда
			Разделитель = РазделительСоциальноеПоложение();
			мЧастиСтроки.Добавить(СоциальноеПоложение);
		КонецЕсли;
	Иначе
		Элементы.СоциальноеПоложение.Видимость = Ложь;
		Если Элементы.Занятость.СписокВыбора.Индекс(ЗначениеЗанятости) = ИндексПрочейЗанятости() Тогда
			Элементы.Прочее.Видимость = Истина;
			Разделитель = РазделительПрочее();
			Если ЗначениеЗаполнено(Прочее) Тогда
				мЧастиСтроки.Добавить(Прочее);
			КонецЕсли;
		Иначе
			Элементы.Прочее.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Результат = СтрСоединить(мЧастиСтроки, Разделитель);
	
КонецПроцедуры

#КонецОбласти