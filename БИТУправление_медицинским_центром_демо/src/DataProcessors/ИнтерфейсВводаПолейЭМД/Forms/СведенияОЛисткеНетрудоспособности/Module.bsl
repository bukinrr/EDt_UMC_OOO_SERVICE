#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Результат = Параметры.НачальноеЗначение;
	
	ЗаполнитьПоляПоНачальномуЗначению();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Принять(Команда)
	
	Если Результат <> Параметры.НачальноеЗначение Тогда
		
		Ошибки = ФЛК();
		Если Ошибки.Количество() = 0 Тогда
			Закрыть(Результат);
		Иначе
			// Вывод ошибок
			ТекстПредупреждения = "Найдены ошибки:";
			Для Каждого Ошибка Из Ошибки Цикл
				ТекстПредупреждения = ТекстПредупреждения + Символы.ПС + " - " + Ошибка;
			КонецЦикла;
			ПоказатьПредупреждение(, ТекстПредупреждения, 30);
		КонецЕсли;
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсе(Команда)
	Номер = "";
	ОсвобождениеС	 = Неопределено;
	ОсвобождениеПо	 = Неопределено;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПолеПриИзменении(Элемент)
	ВычислитьРезультатПоПолям();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВычислитьРезультатПоПолям()
	
	Результат = "";
	
	Если ЗначениеЗаполнено(Номер) Тогда
		СтрокаПоля = "№ " + СокрЛП(Номер);
		ОбщегоНазначенияКлиентСервер.КонкатенацияСтрок(Результат, СтрокаПоля, " ");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОсвобождениеС) Тогда
		Если ЗначениеЗаполнено(ОсвобождениеПо) Тогда
			ШаблонПоля = "на период с %1 по %2";
			СтрокаПоля = СтрШаблон(ШаблонПоля, Формат(ОсвобождениеС, "ДФ=dd.MM.yyyy"), Формат(ОсвобождениеПо, "ДФ=dd.MM.yyyy"));
		Иначе
			ШаблонПоля = "период с %1";
			СтрокаПоля = СтрШаблон(ШаблонПоля, Формат(ОсвобождениеС, "ДФ=dd.MM.yyyy"));
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.КонкатенацияСтрок(Результат, СтрокаПоля, " ");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ФЛК()
	
	Ошибки = Новый Массив;
	
	// Длина номера
	Если Не ПустаяСтрока(Номер) И СтрДлина(СокрЛП(Номер)) <> 12 Тогда
		Ошибки.Добавить("Номер должен состоять из 12 цифр");
	КонецЕсли;
	
	// Период освобождения
	Если ЗначениеЗаполнено(ОсвобождениеПо) Тогда
		Если ЗначениеЗаполнено(ОсвобождениеС) Тогда
			Если ОсвобождениеС > ОсвобождениеПо Тогда
				Ошибки.Добавить("Неправильный период освобождения");
				
			ИначеЕсли (ОсвобождениеПо - ОсвобождениеС) > (90 * 86400) Тогда
				// Неадекватно большой период
				Ошибки.Добавить("Период освобождения невозможно большой");
			КонецЕсли;
		Иначе
			Ошибки.Добавить("Не указано начало периода освобождения");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ошибки;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоляПоНачальномуЗначению()
	
	ПозНомер = СтрНайти(Результат, "№");
	Если ПозНомер <> 0 Тогда
		
		ПервЗнач = Неопределено;
		ПослЗнач = Неопределено;
		
		Для Сч = ПозНомер+1 По СтрДлина(Результат) Цикл
			
			Символ = Сред(Результат, Сч, 1);
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Символ)
				И ПервЗнач = Неопределено
			Тогда
				// Нашли первый символ номера.
				ПервЗнач = Сч;

			ИначеЕсли Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Символ) 
				И ПервЗнач <> Неопределено
			Тогда
				// Нашли конец номера
				ПослЗнач = Сч - 1;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ПервЗнач <> Неопределено И ПослЗнач <> Неопределено
			И (ПослЗнач - ПервЗнач + 1) = 12
		Тогда
			Номер = Сред(Результат, ПервЗнач, 12);
		КонецЕсли;
	КонецЕсли;
	
	ЧастиСтроки = СтрРазделить(Результат, ".", Ложь);
	КолЧастей = ЧастиСтроки.Количество();
	Если КолЧастей >= 3 Тогда
		
		Если КолЧастей >= 5 Тогда
			// Есть конец периода
			Попытка
				ОсвобождениеПо = Дата(	Лев(ЧастиСтроки[КолЧастей-1], 4),
										ЧастиСтроки[КолЧастей-2],
										Прав(ЧастиСтроки[КолЧастей-3], 2));
			Исключение КонецПопытки;
			
			// Вырезать дату конца периода
			ЧастиСтроки.Удалить(КолЧастей-1);
			ЧастиСтроки.Удалить(КолЧастей-2);
			ЧастиСтроки[КолЧастей-3] = Лев(ЧастиСтроки[КолЧастей-3], СтрДлина(ЧастиСтроки[КолЧастей-3]) - 2);
		КонецЕсли;
		
		// Начало периода
		КолЧастей = ЧастиСтроки.Количество();
		Попытка
			ОсвобождениеС = Дата(Лев(ЧастиСтроки[КолЧастей-1], 4), ЧастиСтроки[КолЧастей-2], Прав(ЧастиСтроки[КолЧастей-3], 2));
		Исключение КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти