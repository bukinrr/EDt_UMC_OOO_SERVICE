
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Параметр") Тогда
		Параметр = Параметры.Параметр;		
	ИначеЕсли Параметры.Свойство("КодировкаПоля") Тогда 
		Параметр = Параметры.КодировкаПоля;	
	КонецЕсли;     
	
	ТипУчреждения = Элементы.ТипУчреждения.СписокВыбора[0].Значение;
	ВыводитьАдрес = Истина;  
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриИзмененииТипаУчреждения();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	ЭтотОбъект.Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипУчрежденияПриИзменении(Элемент)
	
	ПриИзмененииТипаУчреждения();
	
КонецПроцедуры 

&НаКлиенте
Процедура УчреждениеПриИзменении(Элемент)
	
	Результат = РассчитатьРезультат(Учреждение, ВыводитьАдрес);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьАдресПриИзменении(Элемент)
	
	Результат = РассчитатьРезультат(Учреждение, ВыводитьАдрес);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка) 
	
	СтандартнаяОбработка = Ложь;
	ЭтотОбъект.ТекущийЭлемент = Элементы.ФормаСохранить;
	
	Оповещение = Новый ОписаниеОповещения("РезультатКонецВыбора", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаВводаТекста",Новый Структура("Параметр, Текст", Параметр, Результат),Элементы.Результат, УникальныйИдентификатор,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура ПриИзмененииТипаУчреждения()

	Если ТипУчреждения = 1 Тогда
		ОжидаемыйТип = Тип("СправочникСсылка.Контрагенты");
	ИначеЕсли ТипУчреждения = 2 Тогда
		ОжидаемыйТип = Тип("СправочникСсылка.Филиалы");
	Иначе
		Возврат;
	КонецЕсли;
	
	// Создаем описание типа.
	ТипыДляОписания = Новый Массив;
	ТипыДляОписания.Добавить(ОжидаемыйТип);
	ОписаниеТипа = Новый ОписаниеТипов(ТипыДляОписания);
	
	// Приведение значения
	Учреждение = ОписаниеТипа.ПривестиЗначение(Учреждение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РассчитатьРезультат(Учреждение, ВыводитьАдрес)
	
	Результат = "";
	
	Если ЗначениеЗаполнено(Учреждение)
		И ТипЗнч(Учреждение) <> Тип("Строка")
	Тогда
		Если ТипЗнч(Учреждение) = Тип("СправочникСсылка.Филиалы") Тогда
			НаименованиеУчреждения = Учреждение.Организация.НаименованиеПолное;
			
			Если ВыводитьАдрес Тогда
				Адрес = Справочники.Филиалы.ПолучитьКИ(Учреждение, Перечисления.ТипыКонтактнойИнформации.Адрес, Справочники.ВидыКонтактнойИнформации.АдресФактический);
				Если Не ЗначениеЗаполнено(Адрес) Тогда
					Адрес = Справочники.Филиалы.ПолучитьКИ(Учреждение, Перечисления.ТипыКонтактнойИнформации.Адрес);
				КонецЕсли;
			Иначе
				Адрес = "";
			КонецЕсли;
		Иначе
			НаименованиеУчреждения = ?(ЗначениеЗаполнено(Учреждение.НаименованиеПолное), Учреждение.НаименованиеПолное, Учреждение.Наименование);
			Если ВыводитьАдрес Тогда
				Адрес = КонтактнаяИнформацияСерверПереопределяемый.НайтиКонтактнуюИнформацию(Учреждение, Перечисления.ТипыКонтактнойИнформации.Адрес);
			Иначе
				Адрес = "";
			КонецЕсли;
		КонецЕсли;
		
		Результат = НаименованиеУчреждения;
		ОбщегоНазначенияКлиентСервер.КонкатенацияСтрок(Результат, Адрес,,Истина);
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура РезультатКонецВыбора(Текст, ДополнительныеПараметры) Экспорт
	
	Если Текст <> Неопределено Тогда
		Результат = Текст;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти