#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЗначенияЗаполнения") 
		И ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура")
	Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма.Объект, Параметры.ЗначенияЗаполнения);
		Параметры.ЗначенияЗаполнения.Свойство("Редактирование", ЭтаФорма.Редактирование);
	КонецЕсли;	
	
	Если Редактирование Тогда
		ПоляКлючаЗаписи = Новый Структура("Объект, Тип, Вид", Объект.Объект, Объект.Тип, Объект.Вид);
		КлючЗаписи = РегистрыСведений.КонтактнаяИнформация.СоздатьКлючЗаписи(ПоляКлючаЗаписи);
	КонецЕсли;
	
	Объект.Тип = Объект.Вид.Тип;
	ЭтаФорма.Заголовок = ЭтаФорма.Заголовок + ?(Объект.Объект = Неопределено,""," ["+Объект.Объект.Наименование+"]");

	СписокВидов = КонтактнаяИнформацияСервер.ЗаполнитьСписокВидовКонтактнойИнформации(Объект.Вид);
	Для Каждого Стр Из СписокВидов Цикл
    	Элементы.Вид.СписокВыбора.Добавить(Стр.Значение);
	КонецЦикла;	
	
	Попытка 
		Элементы.НадписьВладелец.Заголовок = Объект.Объект["НаименованиеПолное"];
	Исключение
		Попытка
			Элементы.НадписьВладелец.Заголовок = Объект.Наименование;
		Исключение
		КонецПопытки;
	КонецПопытки;
	
	// Заполнение полей частей адреса из сохраненного представления
	Если Параметры.Свойство("ЗначенияЗаполнения") 
		И Параметры.ЗначенияЗаполнения.Свойство("Представление")
	Тогда
		ПолныйАдрес = Параметры.ЗначенияЗаполнения.Представление;
		
		ЧастиАдреса = СтрЗаменить(ПолныйАдрес, "@", Символы.ПС);
		ТелоПочты = СтрПолучитьСтроку(ЧастиАдреса, 1);
		ЧастиАдреса = СтрЗаменить(СтрПолучитьСтроку(ЧастиАдреса,2),".",Символы.ПС);
		Домен = СтрПолучитьСтроку(ЧастиАдреса, 1);
		ДоменПослеТочки = СтрПолучитьСтроку(ЧастиАдреса, 2);
	КонецЕсли;	
	
	// Стандартные домены
	Элементы.Домен.СписокВыбора.ЗагрузитьЗначения(КонтактнаяИнформацияСерверПереопределяемый.СтандартныеДоменыПочты());
	
	Если ЗначениеЗаполнено(Объект.Вид) Тогда
		ЭтотОбъект.ТекущийЭлемент = Элементы.ТелоПочты;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОсновныеДействияФормыДействиеОК(Команда)
	
	ЗаписатьЭП(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОсновныеДействияФормыДействиеЗаписать(Команда)
	
	ЗаписатьЭП();

КонецПроцедуры

&НаКлиенте
Процедура вEN(Команда)
	
	ТелоПочты		 = КонвертироватьРаскладкуРусАнг(ТелоПочты);
	Домен			 = КонвертироватьРаскладкуРусАнг(Домен);
	ДоменПослеТочки	 = КонвертироватьРаскладкуРусАнг(ДоменПослеТочки);
	
КонецПроцедуры

&НаКлиенте
Процедура вRUS(Команда)
	
	ТелоПочты		 = КонвертироватьРаскладкуРусАнг(ТелоПочты, Ложь);
	Домен			 = КонвертироватьРаскладкуРусАнг(Домен, Ложь);
	ДоменПослеТочки	 = КонвертироватьРаскладкуРусАнг(ДоменПослеТочки, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаписатьЭП(Закрыть = Ложь)
	
	Объект.Представление = ТелоПочты + "@" + Домен + "." + ДоменПослеТочки;
	
	Если Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Объект.Представление) Тогда
		Ответ = Вопрос(НСтр("ru = 'Почтовый адрес заполнен неверно. Продолжить запись?'"), РежимДиалогаВопрос.ОКОтмена, 30, КодВозвратаДиалога.Отмена);
		Если Ответ <> КодВозвратаДиалога.ОК Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаписатьСервер() Тогда
		Модифицированность = Ложь;
		Если Закрыть Тогда
			Закрыть(Истина);
		КонецЕсли;	
		// Оповещение динамических списков контактной информации без основной таблицы об изменении данных.
		Оповестить("КонтактнаяИнформацияИзменение", Объект.Объект);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЗаписатьСервер()
	
	Об = ДанныеФормыВЗначение(Объект, ТИп("ОбработкаОбъект.РедактированиеКонтактнойИнформации"));
	Возврат Об.ЗаписатьСервер(Объект.ЗначениеПоУмолчанию, Редактирование, КлючЗаписи);
	
КонецФункции	

&НаКлиенте
Процедура ДоменОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ЧастиСтроки = СтрРазделить(ВыбранноеЗначение, ".", Ложь);
		
		ПоследнийИндекс = ЧастиСтроки.Количество() - 1;
		ДоменПослеТочки = ЧастиСтроки[ПоследнийИндекс];
		
		ЧастиСтроки.Удалить(ПоследнийИндекс);
		Домен = СтрСоединить(ЧастиСтроки, ".");
		
		ПолныйАдрес = ТелоПочты + "@" + Домен + "." + ДоменПослеТочки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция КонвертироватьРаскладкуРусАнг(Текст, в_EN = Истина)
	
	Рус = "ё йцукенгшщзхъ фывапролджэ ячсмитьбю Ё ЙЦУКЕНГШЩЗХЪ ФЫВАПРОЛДЖЭ ЯЧСМИТЬБЮ";
	Анг = "` qwertyuiop[] asdfghjkl;' zxcvbnm,. ~ QWERTYUIOP{} ASDFGHJKL:"+Символ(34)+" ZXCVBNM<>";
	
	Если в_EN Тогда
		ИсходныйСловарь	= Рус;
		ЦелевойСловарь	= Анг;
	Иначе
		ИсходныйСловарь	= Анг;
		ЦелевойСловарь	= Рус;
	КонецЕсли;
	
	ТекстИтог = "";
	
	Для Сч = 1 По СтрДлина(Текст) Цикл
		
		Символ = Сред(Текст, Сч, 1);
		
		ИндСловаря = СтрНайти(ИсходныйСловарь, Символ);
		Если ИндСловаря = 0 Тогда
			ТекстИтог = ТекстИтог + Символ;
		Иначе
			НовыйСимвол = Сред(ЦелевойСловарь, ИндСловаря, 1);
			ТекстИтог = ТекстИтог + НовыйСимвол;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТекстИтог;
	
КонецФункции

&НаКлиенте
Процедура ТелоПочтыАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание <> 0 Тогда
		Инд = СтрНайти(Текст, "@");
		Если Инд > 0 Тогда
			АдресаПочты = ОбщегоНазначенияКлиентСервер.АдресаЭлектроннойПочтыИзСтроки(Текст);
			АдресПочты = Неопределено;
			Для Каждого СтруктураАдреса Из АдресаПочты Цикл
				Если ПустаяСтрока(СтруктураАдреса.ОписаниеОшибки) Тогда
					АдресПочты = СтруктураАдреса.Адрес;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЗначениеЗаполнено(АдресПочты) Тогда
				Инд = СтрНайти(АдресПочты, "@");
				
				ТелоПочты = Лев(АдресПочты, Инд - 1);
				
				ДоменПолный = Прав(АдресПочты, СтрДлина(АдресПочты) - Инд);
				
				ЧастиДомена = СтрРазделить(ДоменПолный, ".", Истина);
				
				Домен = "";
				ДоменПослеТочки = "";
				Если ЧастиДомена.Количество() = 1 Тогда
					Домен = ДоменПолный;
				ИначеЕсли ЧастиДомена.Количество() > 1 Тогда
					ДоменПослеТочки = ЧастиДомена[ЧастиДомена.Количество() - 1];
					ЧастиДомена.Удалить(ЧастиДомена.Количество() - 1);
					Домен = СтрСоединить(ЧастиДомена, ".");
				КонецЕсли;
				
				ПолныйАдрес = ТелоПочты + "@" + Домен + "." + ДоменПослеТочки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПочтыПриИзменении(Элемент)
	
	ПолныйАдрес = ТелоПочты + "@" + Домен + "." + ДоменПослеТочки;
	
КонецПроцедуры

#КонецОбласти