#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УдалитьВсехПриИзменении(Элемент)
	Элементы.Клиент.Доступность = НЕ УдалитьВсех;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Удалить(Команда)
	Результат = ОткрытьФормуМодально("Обработка.УдалениеПерсональныхДанных.Форма.ФормаВыбора");
	Если Результат = "Удалить" Тогда
		
		Если УдалитьВсех Тогда
			Ответ = Вопрос("Вы уверены, что хотите удалить данные всех пользователей?",РежимДиалогаВопрос.ДаНет,10, КодВозвратаДиалога.Нет, "Внимание", КодВозвратаДиалога.Нет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Ответ = Вопрос("Подтвердите еще раз выполнение опасной операции.",РежимДиалогаВопрос.ОКОтмена,10, КодВозвратаДиалога.Отмена, "Опасная операция!", КодВозвратаДиалога.Отмена);
			КонецЕсли;
		Иначе
			Ответ = КодВозвратаДиалога.ОК;
		КонецЕсли;
		
		Если Ответ = КодВозвратаДиалога.ОК Тогда
			УдалитьСервер();
			ПоказатьПредупреждение(, "Удаление персональных данных выполнено.");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УдалитьСервер();
	
	Если УдалитьВсех
		И Не РольДоступна("ПолныеПрава")
	Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Недостаточно прав для выполнения операции");
		Возврат;
	КонецЕсли;
	
	СписокНаУдаление = Новый Массив;
	Если УдалитьВсех Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Клиенты.Ссылка
		               |ИЗ
		               |	Справочник.Клиенты КАК Клиенты
		               |ГДЕ
		               |	НЕ Клиенты.ЭтоГруппа";
		
		Результат = Запрос.Выполнить();
		СписокНаУдаление = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
			
	Иначе
		СписокНаУдаление.Добавить(Клиент);
	КонецЕсли;
	
	Для каждого КлиентСсылка из СписокНаУдаление Цикл
		КлиентОбъект = КлиентСсылка.ПолучитьОбъект();
		Если ложь тогда КлиентОбъект = Справочники.Клиенты.СоздатьЭлемент() КонецЕсли;
		КлиентОбъект.ДатаРождения = Дата("00010101000000");
		КлиентОбъект.умцМестоРаботы = "";
		КлиентОбъект.СНИЛС = "";
		КлиентОбъект.Фамилия = НСтр("ru='Клиент'") + " №" + КлиентОбъект.Код;
		КлиентОбъект.Наименование = КлиентОбъект.Фамилия;
		КлиентОбъект.Имя = "";
		КлиентОбъект.Отчество = "";
		НаборЗаписейПД = РегистрыСведений.ПаспортныеДанные.СоздатьНаборЗаписей();
		НаборЗаписейПД.Отбор.ФизЛицо.Установить(КлиентСсылка);
		НаборЗаписейПД.Записать();
		
		НаборЗаписейКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
		НаборЗаписейКИ.Отбор.Объект.Установить(КлиентСсылка);
		НаборЗаписейКИ.Записать();
		КлиентОбъект.Записать();
	КонецЦикла;
	
	УдалитьЗначенияПараметровHTML(СписокНаУдаление);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьЗначенияПараметровHTML(СписокНаУдаление)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗначенияПараметровHTML.Параметр КАК Параметр,
	               |	ЗначенияПараметровHTML.Документ КАК Документ
	               |ИЗ
	               |	РегистрСведений.ЗначенияПараметровHTML КАК ЗначенияПараметровHTML
	               |ГДЕ
	               |	ЗначенияПараметровHTML.Параметр.ПерсональныеДанные
	               |	И ЗначенияПараметровHTML.Документ.Клиент В(&СписокНаУдаление)";
	
	Запрос.УстановитьПараметр("СписокНаУдаление", СписокНаУдаление);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ЗначенияПараметровHTML.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Параметр.Установить(Выборка.Параметр);
		НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
