#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	Макет = Обработки.ПоискКлиентовДляРассылки.ПолучитьМакет("Макет");
	
	АдресСхемы = ПоместитьВоВременноеХранилище(Макет, УникальныйИдентификатор);	
	
	Настройки.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	Настройки.ЗагрузитьНастройки(Макет.НастройкиПоУмолчанию);
	
	БезПустыхЗначений = Истина;
	
	СформироватьСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ПолеКомпоновки = Новый ПолеКомпоновкиДанных(?(ЭтоSMS, "Клиент.НеОтправлятьСМС", "Клиент.НеОтправлятьEMAIL"));
	Для Каждого ЭлементОтбора Из Настройки.Настройки.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки Тогда 
			ЭлементОтбора.Использование = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ТаблицаРезультатТелефон.Видимость = ЭтоSMS;
	Элементы.ТаблицаРезультатДомEmail.Видимость = НЕ ЭтоSMS;
	Элементы.ТаблицаРезультатРабEmail.Видимость = НЕ ЭтоSMS;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура НастройкиНастройкиОтборПриИзменении(Элемент)
	СформироватьСервер();	
КонецПроцедуры

&НаКлиенте
Процедура БезПустыхЗначенийПриИзменении(Элемент)
	СформироватьСервер();
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьСервер();
КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
	
	МассивКлиентов = ПеренестиСервер();
	Закрыть(МассивКлиентов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура СформироватьСервер()
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Обработки.ПоискКлиентовДляРассылки.ПолучитьМакет("Макет"), Настройки.ПолучитьНастройки(),,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);	
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТабРезультат = Новый ТаблицаЗначений;
	ТабРезультат = ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	Если БезПустыхЗначений Тогда
		ТабКопия = ТабРезультат.СкопироватьКолонки();
		Для Каждого СтрокаТР Из ТабРезультат Цикл
			Если ЭтоSMS Тогда
				Если ЗначениеЗаполнено(СтрокаТР.Телефон) Тогда
					СтрокаКопии = ТабКопия.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаКопии,СтрокаТР);
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(СтрокаТР.ДомEmail) Или ЗначениеЗаполнено(СтрокаТР.РабEmail) Тогда
					СтрокаКопии = ТабКопия.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаКопии,СтрокаТР);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ТаблицаРезультат.Загрузить(ТабКопия);
	Иначе
		ТаблицаРезультат.Загрузить(ТабРезультат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПеренестиСервер()
	
	ТР = РеквизитФормыВЗначение("ТаблицаРезультат");
	Возврат ТР.ВыгрузитьКолонку("Клиент")
	
КонецФункции

#КонецОбласти