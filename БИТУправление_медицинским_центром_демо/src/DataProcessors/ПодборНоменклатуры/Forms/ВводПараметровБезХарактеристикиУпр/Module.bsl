#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗапрашиватьКоличество = Ложь;
	ЗапрашиватьЦену = Ложь;
	
	Если Параметры.Свойство("Номенклатура") Тогда 
		Номенклатура = Параметры.Номенклатура;	
	КонецЕсли;	
	
	Если Параметры.Свойство("ХарактеристикаНоменклатуры") Тогда 
		ХарактеристикаНоменклатуры = Параметры.ХарактеристикаНоменклатуры;	
	КонецЕсли;	
	
	Если Параметры.Свойство("ЕдиницаИзмерения") Тогда 
		ЕдиницаИзмерения = Параметры.ЕдиницаИзмерения;
	КонецЕсли;
	
	Если Параметры.Свойство("Количество") Тогда 
		Количество = Параметры.Количество;	
	КонецЕсли;
	
	Если Параметры.Свойство("Цена") Тогда 
		Цена = Параметры.Цена;	
	КонецЕсли;
	
	Если Параметры.Свойство("Действие") Тогда 
		Действие = Параметры.Действие;	
	КонецЕсли;
	
	Если Параметры.Свойство("ЗапрашиватьКоличество") Тогда 
		ЗапрашиватьКоличество = Параметры.ЗапрашиватьКоличество;	
	КонецЕсли;
	
	Если Параметры.Свойство("ЗапрашиватьЦену") Тогда 
		ЗапрашиватьЦену = Параметры.ЗапрашиватьЦену	
			И Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Набор;
	КонецЕсли;
	
	Элементы.Цена.Доступность = ЗапрашиватьЦену;
	Элементы.Количество.Доступность = ЗапрашиватьКоличество;
	Элементы.ЕдиницаИзмерения.Доступность = (ЗапрашиватьКоличество Или (ЗапрашиватьЦену)) И РаботаСФормамиСервер.ЭтоУслуга(Номенклатура) = Ложь;
	
	Если Не ЗапрашиватьКоличество И Не ЗапрашиватьЦену Тогда
		Элементы.НадписьФормулаСумма.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗапрашиватьЦену Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Номенклатура", Номенклатура);
		СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
		СтруктураПараметров.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		СтруктураПараметров.Вставить("Количество", Количество);
		СтруктураПараметров.Вставить("Цена", Цена);
		СтруктураПараметров.Вставить("ЗапрашиватьХарактеристику", Ложь);
		РаботаСФормамиКлиент.мПолучитьЦенуВФормуКлиент(ЭтаФорма, СтруктураПараметров);		
	КонецЕсли;
	мОбновитьНадписьФормулаСумма();
	ЭтаФорма.ОбновитьОтображениеДанных();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Номенклатура", Номенклатура);
	СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
	СтруктураПараметров.Вставить("СерияНоменклатуры", СерияНоменклатуры);
	СтруктураПараметров.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	СтруктураПараметров.Вставить("Количество", Количество);
	СтруктураПараметров.Вставить("Цена", ?(ЗапрашиватьЦену, Цена, Неопределено));
	СтруктураПараметров.Вставить("Действие", Действие);
	
	Закрыть(СтруктураПараметров);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	// Сначала пытаемся найти цену для новой единицы, если не найдется, то будет вычислена пропорционально коэффициентам.
	СтараяЦена = Цена;
	Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		СтарыйКоэффициент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницаИзмерения,"Коэффициент");
	Иначе
		СтарыйКоэффициент = 0;
	КонецЕсли;

	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Номенклатура", Номенклатура);
	СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
	СтруктураПараметров.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	СтруктураПараметров.Вставить("Количество", Количество);
	СтруктураПараметров.Вставить("Цена", Цена);
	СтруктураПараметров.Вставить("ЗапрашиватьХарактеристику", Ложь);
	
	РаботаСФормамиКлиент.мПолучитьЦенуВФормуКлиент(ЭтотОбъект, СтруктураПараметров, ВыбранноеЗначение);
	
	Если Цена = 0 Тогда
		Цена = СтараяЦена;
		
		Если СтарыйКоэффициент <> 0 Тогда
			НовыйКоэффициент = ДопСерверныеФункции.ПолучитьРеквизит(ВыбранноеЗначение,"Коэффициент");
			Цена = Цена * НовыйКоэффициент / СтарыйКоэффициент;
		КонецЕсли;
	КонецЕсли;
	Активизировать();
	

КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)

	мОбновитьНадписьФормулаСумма();

КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)

	мОбновитьНадписьФормулаСумма();

КонецПроцедуры // КоличествоПриИзменении()

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)

	мОбновитьНадписьФормулаСумма();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура мОбновитьНадписьФормулаСумма() 

	НадписьФормулаСумма = Формат(Цена * Количество,"ЧДЦ=2; ЧН=");

КонецПроцедуры // МОбновитьНадписьФомулаСумма().

#КонецОбласти
