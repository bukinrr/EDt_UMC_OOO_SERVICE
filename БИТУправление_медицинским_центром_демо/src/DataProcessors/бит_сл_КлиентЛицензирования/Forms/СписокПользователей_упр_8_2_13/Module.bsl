
// ЛОКАЛЬНЫЕ ПЕРЕМЕННЫЕ  ///////////////////////////////////////////////////////////////////////////////////////////

// СЕРВЕРНЫЕ МЕТОДЫ  ///////////////////////////////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЛокализироватьИнтерфейс();
	
КонецПроцедуры


&НаСервере
// Загружает во все элементы формы локализированные данные
//
Процедура ЛокализироватьИнтерфейс();

	ЛокализацияСоответствие = бит_сл_общий.ПолучитьДанныеЛокализации();
	        
	Для Каждого ЭлементЗн Из ЛокализацияСоответствие Цикл
		
		Если Лев(ЭлементЗн.Ключ, 49) = "сл_КлиентЛицензирования_Интерфейс_Пользователи_16" Тогда
			
			Попытка
				
				СтрокаКомандыЛокализации = Прав(ЭлементЗн.Ключ, СтрДлина(ЭлементЗн.Ключ) - 50);
				СтрокаКомандыЛокализации = СтрЗаменить(СтрокаКомандыЛокализации, "_", ".") + " = """ + ЭлементЗн.Значение + """";
				
				Выполнить(СтрокаКомандыЛокализации);
				
			Исключение
				
				ПараметрыСообщения = Новый Соответствие;
				ПараметрыСообщения.Вставить("##1##", ЭлементЗн.Ключ);
				Сообщить( бит_сл_общий.ПолучитьСтрокуСообщения("сл_КлиентЛицензирования_ЛокализацияСодержитОшибки", ПараметрыСообщения) );							
				
			КонецПопытки;
			
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры

// ОБРАБОТЧИКИ ОЖИДАНИЯ ////////////////////////////////////////////////////////////////////////////////////////////

// КЛИЕНТСКИЕ МЕТОДЫ ///////////////////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
// Для всех строк устанавливает флажки в заданное значение
//
Процедура УстановитьФлажки(Значение)

	Для Каждого СтрокаСписка Из Объект.СписокПользователей Цикл
	
		СтрокаСписка.Исключить = Значение;	
	
	КонецЦикла; 	

КонецПроцедуры // УстановитьФлажки() 

&НаКлиенте
// Определяет, находится ли пользователь в списке исключения
//
Функция ПользовательЕстьВСпискеИсключения(ИДПользователя)

	Возврат Найти(ЭтаФорма.ВладелецФормы.Объект.общ_СписокИсключения, ИДПользователя) > 0;	

КонецФункции // ПользовательЕстьВСпискеИсключения() 

// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Объект.СписокПользователей.Очистить();
	
	// Получим список пользователей ИБ
	ТекущиеПользователиИБ = Новый Массив;
	Результат = бит_сл_сервер.ПолучитьСписокПользователей(ТекущиеПользователиИБ);
	
	Если Не Результат Тогда
	
		бит_сл_общий.ВывестиСообщение( бит_сл_общий.ПолучитьСтрокуСообщения("сл_сервер_НедостаточноПравДляСпискаПользователей") );
		Отказ = Истина;
	
	КонецЕсли; 
	
	Если Не Отказ Тогда
		
		// Заполним таблицу списка пользователей
		Для Каждого ПользовательИБ Из ТекущиеПользователиИБ Цикл
			
			Строка = Объект.СписокПользователей.Добавить();
			Строка.ПользовательИБ = ПользовательИБ.ПолноеИмя;
			Строка.Идентификатор = ПользовательИБ.УникальныйИдентификатор;
			Строка.Исключить = ПользовательЕстьВСпискеИсключения(ПользовательИБ.УникальныйИдентификатор);
			
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеСтроки(Команда)

	УстановитьФлажки(Истина);	
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеСтроки(Команда)

	УстановитьФлажки(Ложь);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)

	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)

	// Сформируем список исключения на основе таблицы списка пользователей и запишем его
	СтрокаСРазделителями = "";
	Для Каждого СтрокаСписка Из Объект.СписокПользователей Цикл
	
		Если СтрокаСписка.Исключить Тогда
			
			СтрокаСРазделителями = СтрокаСРазделителями + СтрокаСписка.Идентификатор + Символы.ПС;
			
		КонецЕсли;
	
	КонецЦикла;
	
	// Сохраняем в объект владельца новое значение 
	// (у этой формы объект является новым экземпляром обработки и не содержит связи с данными)
	ЭтаФорма.ВладелецФормы.Объект.общ_СписокИсключения = СтрокаСРазделителями;
	
	ЭтаФорма.Закрыть(Истина);	
	
КонецПроцедуры

// ИНИЦИАЛИЗАЦИЯ ЛОКАЛЬНЫХ ПЕРЕМЕННЫХ //////////////////////////////////////////////////////////////////////////////

