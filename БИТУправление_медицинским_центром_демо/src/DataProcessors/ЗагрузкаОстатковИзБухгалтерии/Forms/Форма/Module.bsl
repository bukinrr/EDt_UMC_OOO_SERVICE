
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКФайлуВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "(*.xml)|*.xml";
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Заголовок = "Выберите файл выгрузки";
	Если Диалог.Выбрать() Тогда
		ПутьКФайлуВыгрузки = Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗагрузить(Команда)
	
	Если ЗначениеЗаполнено(ПутьКФайлуВыгрузки) Тогда
		Файл = Новый Файл(ПутьКФайлуВыгрузки);
		Если Файл.Существует() Тогда
			Состояние("Выполняется загрузка данных");
			ДвоичныеДанныеФайлаВыгрузки = Новый ДвоичныеДанные(ПутьКФайлуВыгрузки);
			ВыполнитьЗагрузку(ДвоичныеДанныеФайлаВыгрузки);
		Иначе
			Предупреждение("Файл не обнаружен '" + ПутьКФайлуВыгрузки + "'");
		КонецЕсли;
	Иначе
		Предупреждение("Не указан файл выгрузки данных из 1С:Бухгалтерии предприятия");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьЗагрузку(ДвоичныеДанныеФайлаВыгрузки)
	
	ПутьКФайлуВыгрузкиСервер = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанныеФайлаВыгрузки.Записать(ПутьКФайлуВыгрузкиСервер);
	
	УниверсальныйОбмен = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	УниверсальныйОбмен.ИмяФайлаОбмена = ПутьКФайлуВыгрузкиСервер;
	УниверсальныйОбмен.РежимОтладкиАлгоритмов = Истина;
	УниверсальныйОбмен.РежимОбмена = "Загрузка";
	
	УниверсальныйОбмен.ВыполнитьЗагрузку();
	
	Если ЗначениеЗаполнено(КатегорияВыработки) Тогда
		Для Каждого КлючЗначение Из УниверсальныйОбмен.ЗагруженныеОбъекты Цикл
			СтруктураЗагрузки = КлючЗначение.Значение;
			Если КлючЗначение.Значение.Свойство("СсылкаНаОбъект")
				И ТипЗнч(КлючЗначение.Значение.СсылкаНаОбъект) = Тип("СправочникСсылка.Номенклатура")
			Тогда
				НоменклатураСсылка = КлючЗначение.Значение.СсылкаНаОбъект;
				Если Не НоменклатураСсылка.ЭтоГруппа
					И Не ЗначениеЗаполнено(НоменклатураСсылка.КатегорияВыработки)
				Тогда
					НоменклатураОбъект = НоменклатураСсылка.ПолучитьОбъект();
					НоменклатураОбъект.КатегорияВыработки = КатегорияВыработки;
					НоменклатураОбъект.ОбменДанными.Загрузка = Истина;
					НоменклатураОбъект.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

