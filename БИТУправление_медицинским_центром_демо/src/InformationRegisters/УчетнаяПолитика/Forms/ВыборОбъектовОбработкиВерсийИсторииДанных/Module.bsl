#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ПолноеИмяМетаданных Из Параметры.ОбъектыМетаданных.Добавленные Цикл
		СозданиеВерсий.Добавить(ПолноеИмяМетаданных);
	КонецЦикла;
	
	Для Каждого ПолноеИмяМетаданных Из Параметры.ОбъектыМетаданных.Удаленные Цикл
		УдалениеВерсий.Добавить(ПолноеИмяМетаданных);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОбработку(Команда)
	
	ЕстьУдалениеВерсий = Ложь;
	Для Каждого ЭлементСписка Из УдалениеВерсий Цикл
		Если ЭлементСписка.Пометка Тогда
			ЕстьУдалениеВерсий = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЕстьДобавлениеВерсий = Ложь;
	Для Каждого ЭлементСписка Из СозданиеВерсий Цикл
		Если ЭлементСписка.Пометка Тогда
			ЕстьДобавлениеВерсий = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьДобавлениеВерсий Или ЕстьУдалениеВерсий Тогда
		
		Если Вопрос(НСтр("ru='Будет выполнена обработка версий выбранных объектов.'") + " " + НСтр("ru = 'Продолжить?'"), РежимДиалогаВопрос.ОКОтмена, 30, КодВозвратаДиалога.Отмена) = КодВозвратаДиалога.ОК Тогда
			
			Если ЕстьУдалениеВерсий Тогда
				
				Если Вопрос(НСтр("ru='Версии объектов, выбранных для удаления, будут безвозвратно утеряны.'") + " " + НСтр("ru = 'Продолжить?'"), РежимДиалогаВопрос.ОКОтмена, 30, КодВозвратаДиалога.Отмена) <> КодВозвратаДиалога.ОК Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			ВыполнитьОбработкуНаСервере();
			
		КонецЕсли;
	КонецЕсли;
	
	ПоказатьПредупреждение(,НСтр("ru='Операция выполнена!'"), 60);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьОбработкуНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого ЭлементСписка Из СозданиеВерсий Цикл
		Если ЭлементСписка.Пометка Тогда
			ИсторияДанныхСервер.СоздатьПервыеВерсииВидаОбъекта(ЭлементСписка.Значение, ПериодДокументтов.ДатаНачала, ПериодДокументтов.ДатаОкончания);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлементСписка Из УдалениеВерсий Цикл
		Если ЭлементСписка.Пометка Тогда
			ИсторияДанныхСервер.УдалитьВерсииДанных(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ИсторияДанныхСервер.ОбновитьИсториюДанных();
	
КонецПроцедуры

#КонецОбласти