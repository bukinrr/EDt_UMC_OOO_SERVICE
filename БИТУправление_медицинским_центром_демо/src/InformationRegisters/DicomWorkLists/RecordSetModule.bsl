#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Интерфейс = DICOMРаботаСДрайверомКлиентСервер.ПолучитьИнтерфейсDICOM();
	Если Интерфейс <> Неопределено Тогда
		Для Каждого Запись Из ЭтотОбъект Цикл
			Запись.RequestedProcedurePriority = "LOW";
			Запись.StudyInstanceUID = Интерфейс.GenerateUniqueIdentifier();
			Запись.УникальныйИдентификаторИсследования = Новый УникальныйИдентификатор;
			#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
			Попытка
				Запись.Ответственный = Пользователи.ТекущийПользователь();
			Исключение
			КонецПопытки;
			#КонецЕсли
			Запись.RequestedProcedureID = Запись.УникальныйИдентификаторИсследования; 
			Запись.ScheduledProcedureStepID = "0";
			Запись.СозданФайл = Истина;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	Для Каждого Запись Из ЭтотОбъект Цикл
		СистемнаяИнформация = Новый СистемнаяИнформация;
		Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64
			Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86
			Тогда
			Запись.ИмяФайла = Запись.ScheduledStationAETitle + "/" + Запись.StudyInstanceUID + ".wl";
		Иначе
			Запись.ИмяФайла = Запись.ScheduledStationAETitle + "\" + Запись.StudyInstanceUID + ".wl";
		КонецЕсли;
		
		Если Запись.Выполнена Или Запись.ДобавленоИзMPPS Тогда
			Запись.СозданФайл = Ложь;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Данные = Новый Структура;
		Для Каждого Ресурс Из Метаданные.РегистрыСведений.DicomWorkLists.Ресурсы Цикл
			Данные.Вставить(Ресурс.Имя, Запись[Ресурс.Имя]);
		КонецЦикла;
		
		Данные.Вставить("ИмяФайла", Запись.ИмяФайла);
		
		Если Запись.СозданФайл Тогда
			DICOMРаботаСДрайвером.ЗаписатьЭлементWorkList(Данные, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
