#Область ОбработчикиСобытий
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	// Очистка даты начала примения для новой записи
	Если Не ЗначениеЗаполнено(Запись.ИсходныйКлючЗаписи.Период) Тогда
	
		// Основной вид документа.
		Если Не ЗначениеЗаполнено(Запись.ДокументВид) Тогда
			Запись.ДокументВид = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ОсновнойВидДУЛ");
		КонецЕсли;
	КонецЕсли;
	
	УстановитьМаскуСерииНомера();
	
	// Установка текущего элемента исходя из заполненности полей.
	Если Запись.ФизЛицо.Пустая() Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.ФизЛицо;
	ИначеЕсли Запись.ДокументВид.Пустая() Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.ДокументВид;
	Иначе
		ЭтаФорма.ТекущийЭлемент = Элементы.ДокументСерия;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДокументКемВыданНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Форма = ПолучитьФорму("ОбщаяФорма.ПодборУчрежденияВыдачиПаспорта", Новый Структура("КемВыдан", Запись.ДокументКемВыдан));
	Форма.КемВыдан = Запись.ДокументКемВыдан;
	КемВыдан = Форма.ОткрытьМодально();
	Если КемВыдан <> Неопределено Тогда
		Запись.ДокументКемВыдан = КемВыдан;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументКодПодразделенияПриИзменении(Элемент)
	
	Наименования = ДокументКодПодразделенияПриИзмененииНаСервере(Запись.ДокументКодПодразделения, Запись.ДокументДатаВыдачи);
	Если ТипЗнч(Наименования) = Тип("Строка") Тогда
		Запись.ДокументКемВыдан = Наименования;
	ИначеЕсли  ТипЗнч(Наименования) = Тип("Массив") Тогда
		спЗначений = Новый СписокЗначений();
		спЗначений.ЗагрузитьЗначения(Наименования);
		ДопПараметры = Новый Структура;
		Оповещение = Новый ОписаниеОповещения("ВыполнитьПослеОкончания", ЭтаФорма, ДопПараметры);
		ЭтаФорма.ПоказатьВыборИзСписка(Оповещение,спЗначений);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДокументВидПриИзменении(Элемент)
	
	УстановитьМаскуСерииНомера();
	Запись.ДокументСерия = "";
	Запись.ДокументНомер = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументДатаВыдачиПриИзменении(Элемент)
	
	Запись.Период = Запись.ДокументДатаВыдачи;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПослеОкончания(ЗначениеВыбора, ДопПараметры) Экспорт
	
	Если НЕ (ЗначениеВыбора = Неопределено) Тогда
		Запись.ДокументКемВыдан = ЗначениеВыбора.Значение;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ДокументКодПодразделенияПриИзмененииНаСервере(КодУФМС, Дата)
	
	Возврат РегистрыСведений.ПаспортныеДанные.НаименованияПоКоду(КодУФМС, Дата);
	
КонецФункции

&НаСервере
Процедура УстановитьМаскуСерииНомера()
	
	Элементы.ДокументСерия.Доступность = Не Запись.ДокументВид.НеИмеетСерию;
	Элементы.ДокументСерия.Маска = Запись.ДокументВид.МаскаСерииДокумента;
	Элементы.ДокументНомер.Маска = Запись.ДокументВид.МаскаНомераДокумента;
	Элементы.ДокументКодПодразделения.Маска = Запись.ДокументВид.МаскаКодаПодразделения;
	
КонецПроцедуры

#КонецОбласти
