
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	МаршрутАнализа = ПолучитьОбщийМакет("МаршрутАнализов");
	Если ЗначениеЗаполнено(Параметры.Филиал) Тогда
		Филиал = Параметры.Филиал;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	МаршрутыАнализов.Шаг,
		               |	МаршрутыАнализов.Активирован,
		               |	МаршрутыАнализов.ИмяЭлемента
		               |ИЗ
		               |	РегистрСведений.МаршрутыАнализов КАК МаршрутыАнализов
		               |ГДЕ
		               |	МаршрутыАнализов.Филиал = &Филиал";
		
		Запрос.УстановитьПараметр("Филиал", Филиал);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
		    Если Не ПустаяСтрока(Выборка.ИмяЭлемента) Тогда
				МаршрутАнализа.ЭлементыГрафическойСхемы[Выборка.ИмяЭлемента].ПрозрачныйФон = НЕ Выборка.Активирован;		
		    КонецЕсли;
		КонецЦикла;
	ИначеЕсли Не ПараметрыСеанса.УчетПоНесколькимФилиалам Тогда
		Филиал = УправлениеНастройками.ПолучитьФилиалПоУмолчаниюПользователя();
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНЗ(Команда)
	Если ЗначениеЗаполнено(Филиал) Тогда	
		ЗаписатьСервер();
		Оповестить("ЗаписьМаршрутаЛабораторныхИсследований");
		Закрыть();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не указан филиал, для которого применяется данная схема'");
		Сообщение.Поле	= "Филиал";
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура МаршрутАнализаВыбор(Элемент)
	Если ТипЗнч(Элемент.ТекущийЭлемент) = Тип("ЭлементГрафическойСхемыДействие") Тогда
		Если Элемент.ТекущийЭлемент.Наименование = "Назначение"
			ИЛИ Элемент.ТекущийЭлемент.Наименование = "Забор"
			ИЛИ Элемент.ТекущийЭлемент.Наименование = "Обработка" Тогда
			ТекстСообщения = НСтр("ru='Блок %1 Отключить нельзя'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Элемент.ТекущийЭлемент.Наименование); 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ИначеЕсли Элемент.ТекущийЭлемент.Наименование = "Передача в свою либо стороннюю лабораторию" Тогда
			НайтиПоНаименованиеЭГС("Передача в свою либо стороннюю лабораторию").ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;	
			НайтиПоНаименованиеЭГС("Получение результатов из лаборатории").ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;
			Модифицированность = Истина;	
		ИначеЕсли Элемент.ТекущийЭлемент.Наименование = "Получение результатов из лаборатории" Тогда
			Если Элемент.ТекущийЭлемент.ПрозрачныйФон = Истина Тогда 
				НайтиПоНаименованиеЭГС("Передача в свою либо стороннюю лабораторию").ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;	
				НайтиПоНаименованиеЭГС("Получение результатов из лаборатории").ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;	
			Иначе
				НайтиПоНаименованиеЭГС("Получение результатов из лаборатории").ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;
			КонецЕсли;
			Модифицированность = Истина;
		ИначеЕсли Элемент.ТекущийЭлемент.Наименование = "Получение из другой  клиники"
			ИЛИ Элемент.ТекущийЭлемент.Наименование = "Передача результатов исследования источнику" Тогда
			НайтиПоНаименованиеЭГС("Получение из другой  клиники").ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;	
			НайтиПоНаименованиеЭГС("Анализ чужих материалов").ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;
			НайтиПоНаименованиеЭГС("Передача результатов исследования источнику").ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;
			Модифицированность = Истина;	
		Иначе
			МаршрутАнализа.ЭлементыГрафическойСхемы[Элемент.ТекущийЭлемент.Имя].ПрозрачныйФон = НЕ Элемент.ТекущийЭлемент.ПрозрачныйФон;
			Модифицированность = Истина;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьСервер()
	НЗ = РегистрыСведений.МаршрутыАнализов.СоздатьНаборЗаписей();
	НЗ.Отбор.Филиал.Установить(Филиал);
	Для каждого графЭлемент Из МаршрутАнализа.ЭлементыГрафическойСхемы Цикл
		Если ТипЗнч(графЭлемент) = Тип("ЭлементГрафическойСхемыДействие") Тогда
			НоваяЗапись = НЗ.Добавить();
			НоваяЗапись.Филиал = Филиал;
			НоваяЗапись.Активирован = НЕ графЭлемент.ПрозрачныйФон;
			НоваяЗапись.Шаг = ОпределитьШагВПеречислении(графЭлемент.Наименование);
			НоваяЗапись.ИмяЭлемента = графЭлемент.Имя;
		КонецЕсли;
	КонецЦикла;
	НЗ.Записать();
КонецПроцедуры

&НаСервере
Функция ОпределитьШагВПеречислении(НаименованиеЭлемента)
	Если НаименованиеЭлемента = "Назначение" Тогда
		Возврат Перечисления.ШагиМаршрутаАнализов.Назначение;
	ИначеЕсли НаименованиеЭлемента = "Забор" Тогда 
		Возврат Перечисления.ШагиМаршрутаАнализов.Забор;
	ИначеЕсли НаименованиеЭлемента = "Получение из другой  клиники" Тогда
		Возврат Перечисления.ШагиМаршрутаАнализов.ПолучениеИзДругойКлиники;
	ИначеЕсли НаименованиеЭлемента = "Анализ чужих материалов" Тогда
		Возврат Перечисления.ШагиМаршрутаАнализов.АнализЧужихМатериалов;
	ИначеЕсли НаименованиеЭлемента = "Передача результатов исследования источнику" Тогда
		Возврат Перечисления.ШагиМаршрутаАнализов.ПередачаРезультатовИсследованияИсточнику;
	ИначеЕсли НаименованиеЭлемента = "Анализ своих материалов" Тогда
		Возврат Перечисления.ШагиМаршрутаАнализов.АнализСвоихМатериалов;
	ИначеЕсли НаименованиеЭлемента = "Обработка" Тогда
		Возврат Перечисления.ШагиМаршрутаАнализов.Обработка;
	ИначеЕсли НаименованиеЭлемента = "Передача в свою либо стороннюю лабораторию" Тогда
		Возврат Перечисления.ШагиМаршрутаАнализов.ПередачаВСвоюЛибоСтороннююЛабораторию;
	ИначеЕсли НаименованиеЭлемента = "Получение результатов из лаборатории" Тогда
		Возврат Перечисления.ШагиМаршрутаАнализов.ПолучениеРезультатовИзЛаборатории;
	КонецЕсли;
КонецФункции

&НаКлиенте
Функция НайтиПоНаименованиеЭГС(НаименованиеЭлемента) 
	Для каждого ЭлементаЭГС Из МаршрутАнализа.ЭлементыГрафическойСхемы Цикл
		Если ЭлементаЭГС.Наименование = НаименованиеЭлемента Тогда
			Возврат ЭлементаЭГС;
		КонецЕсли;
	КонецЦикла;
КонецФункции

#КонецОбласти
