
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("БиоматериалыИКонтейнеры") Тогда
		Для Каждого СтрокаБиоматериалыИКонтейнеры Из Параметры.БиоматериалыИКонтейнеры Цикл
			НоваяСтрока = БиоматериалыИКонтейнеры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБиоматериалыИКонтейнеры);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого СтрТаблица Из БиоматериалыИКонтейнеры Цикл
		Если СтрТаблица.ОбязательностьБиоматериала И СтрТаблица.ОбязательностьКонтейнера Тогда 
			СтрТаблица.Пометка = Истина;	
		КонецЕсли;
		
		Если Биоматериалы.НайтиСтроки(Новый Структура("ИДБиоматериала", СтрТаблица.ИДБиоматериала)).Количество() = 0 Тогда
			НоваяСтрока = Биоматериалы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТаблица);
			НоваяСтрока.Пометка = НоваяСтрока.ОбязательностьБиоматериала;
		КонецЕсли;
	КонецЦикла;
	
	Если Биоматериалы.Количество() <> 0 Тогда 
		Биоматериалы.Сортировать("ОбязательностьБиоматериала Убыв, Биоматериал");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОчиститьСообщения();
	
	ВыбранныеСтроки = БиоматериалыИКонтейнеры.НайтиСтроки(Новый Структура("Пометка", Истина));
	
	Если ВыбранныеСтроки.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Нет выбранных биоматериалов и контейнеров!'"));
		Возврат;
	КонецЕсли;
	
	МассивСтроки = Биоматериалы.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если МассивСтроки.Количество() <> 0 Тогда
		МассивБиоматериалыБезКонтейнеров = Новый Массив;
		Для Каждого ЭлементСтрока Из МассивСтроки Цикл
			Если БиоматериалыИКонтейнеры.НайтиСтроки(
				Новый Структура("ИДБиоматериала, Пометка", ЭлементСтрока.ИДБиоматериала, Истина)).Количество() = 0
			Тогда
				МассивБиоматериалыБезКонтейнеров.Добавить(ЭлементСтрока.Биоматериал);
			КонецЕсли;
		КонецЦикла;
		Если МассивБиоматериалыБезКонтейнеров.Количество() <> 0 Тогда
			СтрокаБиоматериалы = "";
			Для Итератор = 0 По МассивБиоматериалыБезКонтейнеров.Количество() - 1 Цикл
				СтрокаБиоматериалы = СтрокаБиоматериалы + ?(Итератор <> 0, ", ","") + """" 
					+ СокрЛП(МассивБиоматериалыБезКонтейнеров[Итератор]) + """";
			КонецЦикла;
			СообщениеОбОшибке = НСтр("ru = 'Нет выбранных контейнеров для биоматериалов: %1!'");	
			ОбщегоНазначения.СообщитьОбОшибке(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				СообщениеОбОшибке, СтрокаБиоматериалы));
			Возврат;	
		КонецЕсли;	
	КонецЕсли;
	
	Закрыть(ВыбранныеСтроки);

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура БиоматериалыПриАктивизацииСтроки(Элемент)

	Контейнеры.Очистить();
	
	ТекущиеДанные = Элементы.Биоматериалы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Если ТекущиеДанные.Пометка Тогда
		
		МассивСтроки = БиоматериалыИКонтейнеры.НайтиСтроки(Новый Структура("ИДБиоматериала", ТекущиеДанные.ИДБиоматериала));
		
		Для Каждого ЭлементСтрока Из МассивСтроки Цикл
			НоваяСтрока = Контейнеры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементСтрока);
			Если НоваяСтрока.ОбязательностьКонтейнера Или ЭлементСтрока.Пометка Тогда  
				НоваяСтрока.Пометка = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если Контейнеры.Количество() <> 0 Тогда 
			Контейнеры.Сортировать("ОбязательностьКонтейнера Убыв, Контейнер");	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БиоматериалыПометкаПриИзменении(Элемент)
	
	Контейнеры.Очистить();
	
	ТекущиеДанные = Элементы.Биоматериалы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Если ТекущиеДанные.Пометка Тогда
		
		УстановитьИспользованиеБиоматериаловИКонтейнеров(ТекущиеДанные.ИДБиоматериала,, Истина);
			
		Для Каждого СтрокаБиоматериалы Из Биоматериалы Цикл
			Если СтрокаБиоматериалы.ПолучитьИдентификатор() <> Элементы.Биоматериалы.ТекущаяСтрока
				И Не СтрокаБиоматериалы.ОбязательностьБиоматериала
			Тогда
				СтрокаБиоматериалы.Пометка = Ложь;
				УстановитьИспользованиеБиоматериаловИКонтейнеров(СтрокаБиоматериалы.ИДБиоматериала,, Ложь);
			КонецЕсли;
		КонецЦикла;
		
		МассивСтроки = БиоматериалыИКонтейнеры.НайтиСтроки(Новый Структура("ИДБиоматериала", ТекущиеДанные.ИДБиоматериала));
		
		Для Каждого ЭлементСтрока Из МассивСтроки Цикл
			НоваяСтрока = Контейнеры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементСтрока);
			НоваяСтрока.Пометка = НоваяСтрока.ОбязательностьКонтейнера;
		КонецЦикла;
		
		Если Контейнеры.Количество() <> 0 Тогда 
			Контейнеры.Сортировать("ОбязательностьКонтейнера Убыв, Контейнер");
		КонецЕсли;
		
	Иначе
		Если ТекущиеДанные.ОбязательностьБиоматериала Тогда
			ТекущиеДанные.Пометка = Истина;
		Иначе 
			УстановитьИспользованиеБиоматериаловИКонтейнеров(ТекущиеДанные.ИДБиоматериала,, Ложь);
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КонтейнерыПометкаПриИзменении(Элемент)

	ТекущиеДанныеКонтейнеры = Элементы.Контейнеры.ТекущиеДанные;
	ТекущиеДанныеБиоматериалы = Элементы.Биоматериалы.ТекущиеДанные;
	
	Если ТекущиеДанныеКонтейнеры = Неопределено Или ТекущиеДанныеБиоматериалы = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Если ТекущиеДанныеКонтейнеры.Пометка Тогда
		
		УстановитьИспользованиеБиоматериаловИКонтейнеров(ТекущиеДанныеБиоматериалы.ИДБиоматериала, 
			ТекущиеДанныеКонтейнеры.ИДКонтейнера, Истина);
			
		Для Каждого СтрокаКонтейнеры Из Контейнеры Цикл
			Если СтрокаКонтейнеры.ПолучитьИдентификатор() <> Элементы.Контейнеры.ТекущаяСтрока
				И Не СтрокаКонтейнеры.ОбязательностьКонтейнера
			Тогда
				СтрокаКонтейнеры.Пометка = Ложь;
				УстановитьИспользованиеБиоматериаловИКонтейнеров(ТекущиеДанныеБиоматериалы.ИДБиоматериала, 
					СтрокаКонтейнеры.ИДКонтейнера, Ложь);
			КонецЕсли;
		КонецЦикла;
	
	Иначе
		Если ТекущиеДанныеКонтейнеры.ОбязательностьКонтейнера Тогда
			ТекущиеДанныеКонтейнеры.Пометка = Истина;
		Иначе 
			УстановитьИспользованиеБиоматериаловИКонтейнеров(ТекущиеДанныеБиоматериалы.ИДБиоматериала, 
				ТекущиеДанныеКонтейнеры.ИДКонтейнера, Ложь);
		КонецЕсли; 
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьИспользованиеБиоматериаловИКонтейнеров(ИДБиоматериала, ИДКонтейнера = Неопределено, Пометка)

	Если ИДКонтейнера = Неопределено Тогда
		МассивСтроки = БиоматериалыИКонтейнеры.НайтиСтроки(Новый Структура("ИДБиоматериала", ИДБиоматериала));
		Для Каждого ЭлементСтрока Из МассивСтроки Цикл
			Если Пометка И ЭлементСтрока.ОбязательностьКонтейнера Тогда
				ЭлементСтрока.Пометка = Истина;
			Иначе 
				ЭлементСтрока.Пометка = Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе 
		МассивСтроки = БиоматериалыИКонтейнеры.НайтиСтроки(Новый Структура("ИДБиоматериала, ИДКонтейнера", 
			ИДБиоматериала, ИДКонтейнера));
		Для Каждого ЭлементСтрока Из МассивСтроки Цикл
			ЭлементСтрока.Пометка = Пометка;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
