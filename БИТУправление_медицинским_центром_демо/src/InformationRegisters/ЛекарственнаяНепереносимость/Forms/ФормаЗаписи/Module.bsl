#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.Клиент) Тогда 
		ЭтаФорма.ТекущийЭлемент=Элементы.ДействующееВещество;
	КонецЕсли;
	
	ГотовыеФразыРеакций.Параметры.УстановитьЗначениеПараметра("Параметр", УправлениеНастройкамиПереопределяемый.ПолучитьПараметрУчета("ПакетСоставныхФразРеакцииНаАллергены"));
	
	Реакция = Параметры.Реакция;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Запись.Реакция) Тогда
		Элементы.Реакция.УстановитьГраницыВыделения(СтрДлина(Запись.Реакция)+1, СтрДлина(Запись.Реакция)+1, СтрДлина(Запись.Реакция)+1, СтрДлина(Запись.Реакция)+1);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЛекарственнаяНепереносимостьИзменение", Запись.Клиент);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ГотовыеФразыРеакцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачСтр = 0;
	НачКол = 0;
	КонСтр = 0;
	КонКол = 0;
	
	Элементы.Реакция.ПолучитьГраницыВыделения(НачСтр, НачКол, КонСтр, КонКол);

	ТекстФразы = ПолучитьТекстФразы(ВыбраннаяСтрока);
	
	Элементы.Реакция.ВыделенныйТекст = ТекстФразы;
	
	ДлинаКуска = СтрДлина(ТекстФразы);
	
	Элементы.Реакция.ОбновитьТекстРедактирования(); // Требуется начиная с 8.3.8 для работоспособности УстановитьГраницыВыделения().
	Элементы.Реакция.УстановитьГраницыВыделения(НачСтр, НачКол + ДлинаКуска, КонСтр, НачКол + ДлинаКуска);
 	
 	ЭтаФорма.ТекущийЭлемент = Элементы.Реакция;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Функция ПолучитьТекстФразы(ГотоваяФраза)
	
	ТекстФразы = "";
	
	Пока ЗначениеЗаполнено(ГотоваяФраза) Цикл
		
		ТекстФразы = ГотоваяФраза.Текст + " " + ТекстФразы;
		ГотоваяФраза = ГотоваяФраза.Родитель;
			
	КонецЦикла; 
	
	Возврат ТекстФразы;
	
КонецФункции

#КонецОбласти



