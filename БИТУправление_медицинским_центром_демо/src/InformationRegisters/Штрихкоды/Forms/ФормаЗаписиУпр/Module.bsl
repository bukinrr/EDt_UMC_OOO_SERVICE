
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.Владелец) Тогда
		Элементы.Владелец.Доступность = Ложь;
		
		Если Запись.ИсходныйКлючЗаписи.Пустой() 
			И ТипЗнч(Запись.Владелец) = Тип("СправочникСсылка.Номенклатура")
		Тогда
			Запись.ЕдиницаИзмерения = Ценообразование.ПолучитьЕдиницуПродажиНоменклатуры(Запись.Владелец);
		КонецЕсли;
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		Запись.ТипШтрихкода = ПланыВидовХарактеристик.ТипыШтрихкодов.EAN13;
		
	КонецЕсли;
	
	ЭтаФорма.ТипШтрихкодаEAN13 = ПланыВидовХарактеристик.ТипыШтрихкодов.EAN13;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьВторостепенныхПолей();
	УстановитьДоступностьКнопкиГенерацииШтрихкода();
	
	Если Истина // ИспользоватьПодключаемоеОборудование Проверка на включенную ФО "Использовать ВО".
	   И МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО.
		ОписаниеОшибки = "";
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
		
		ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);    
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	// МеханизмВнешнегоОборудования
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(Неопределено, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	// Конец МеханизмВнешнегоОборудования

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ScanData" Тогда
		
		Если ВводДоступен() Тогда
			
			ТипШК = Неопределено;
			РаботаСТорговымОборудованиемКлиент.ОбработатьСобытиеСШКФормы(ЭтаФорма, Параметр, ТипШК);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СгенерироватьШтрихкод(Команда)
	Запись.Штрихкод = РаботаСТорговымОборудованием.СформироватьШтрихКод();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	УстановитьДоступностьВторостепенныхПолей();
КонецПроцедуры

&НаКлиенте
Процедура ТипШтрихкодаПриИзменении(Элемент)
	
	УстановитьДоступностьКнопкиГенерацииШтрихкода();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьВторостепенныхПолей()
	
	Если Не ТипЗнч(Запись.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		Элементы.ЕдиницаИзмерения.Доступность = Ложь;
		Элементы.ХарактеристикаНоменклатуры.Доступность = Ложь;
		Элементы.СерияНоменклатуры.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопкиГенерацииШтрихкода()
	ЭтаФорма.КоманднаяПанель.ПодчиненныеЭлементы.ФормаСгенерироватьШтрихкод.Доступность = Запись.ТипШтрихкода = ЭтаФорма.ТипШтрихкодаEAN13;
КонецПроцедуры

&НаКлиенте
// Функция СШКНоменклатура.
//
// Параметры:
//  ДанныеНоменклатуры - Неопределено
//  СШК - Неопределено
//
// Возвращаемое значение:
//  Неопределено.
//
Функция СШКНоменклатура(ДанныеНоменклатуры, СШК) Экспорт
	Номенклатура				= ДанныеНоменклатуры.Номенклатура;
	Количество					= ДанныеНоменклатуры.Количество;
	ХарактеристикаНоменклатуры	= ДанныеНоменклатуры.ХарактеристикаНоменклатуры;
	Единица						= ДанныеНоменклатуры.ЕдиницаИзмерения;
	Цена						= ДанныеНоменклатуры.Цена;
	
	ОткрытьЗначение(Номенклатура);
	Возврат Истина;
	
КонецФункции // СШКНоменклатура()

&НаКлиенте
// Функция СШКНеизвестныйКод.
//
// Параметры:
//  Штрихкод - Неопределено
//  ТипКода - Неопределено
//  СШК - Неопределено
//
// Возвращаемое значение:
//  Неопределено.
//
Функция СШКНеизвестныйКод(Штрихкод, ТипКода, СШК) Экспорт
	
	Запись.ТипШтрихкода = ТипКода;
	Запись.Штрихкод		= Штрихкод;

	Возврат Истина;
	
КонецФункции // СШКНеизвестныйКод()

&НаКлиенте
// Процедура ПодключитьОборудованиеЗавершение.
//
// Параметры:
//  РезультатВыполнения - Неопределено
//  Параметры - Неопределено
//
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:
				|""%ОписаниеОшибки%"".'" );
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
