#Область ПрограммныйИнтерфейс

// Функция возвращает соответствие с данными штрихкодов.
//
// Параметры:
//  Штрихкоды - Массив из Строка - Массив штрихкодов
//
// Возвращаемое значение:
//  Неопределено - Соответствие с данными штрихкодов
//
Функция ПолучитьДанныеПоШтрихкодам(Штрихкоды) Экспорт
	
	ДанныеПоШтрихкодам = Новый Соответствие;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод						КАК Штрихкод,
	|	ШтрихкодыНоменклатуры.Владелец						КАК Владелец,
	|	ШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры	КАК ХарактеристикаНоменклатуры,
	|	ШтрихкодыНоменклатуры.СерияНоменклатуры				КАК СерияНоменклатуры,
	|	ШтрихкодыНоменклатуры.ЕдиницаИзмерения				КАК ЕдиницаИзмерения
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Штрихкод В (&МассивШтрихкодов)
	|");
	
	МассивШтрихкодов = Новый Массив;
	
	Для Каждого ТекущийШтрихкод Из Штрихкоды Цикл
		МассивШтрихкодов.Добавить(ТекущийШтрихкод);
		ДанныеПоШтрихкодам.Вставить(ТекущийШтрихкод, ОбработкаТабличнойЧастиКлиентСервер.СтруктураКэшируемойИнформацииПоШтрихкоду(ТекущийШтрихкод));
	КонецЦикла;
	
	Запрос.УстановитьПараметр("МассивШтрихкодов", МассивШтрихкодов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИнформацияПоШтрихкоду = ДанныеПоШтрихкодам[Выборка.Штрихкод];
		ИнформацияПоШтрихкоду.Владелец						= Выборка.Владелец;
		ИнформацияПоШтрихкоду.ХарактеристикаНоменклатуры	= Выборка.ХарактеристикаНоменклатуры;
		ИнформацияПоШтрихкоду.СерияНоменклатуры				= Выборка.СерияНоменклатуры;
		ИнформацияПоШтрихкоду.ЕдиницаИзмерения				= Выборка.ЕдиницаИзмерения;
		
		ИнформацияПоШтрихкоду.НеизвестныйШтрихкод = Ложь;
	КонецЦикла;
	
	Возврат ДанныеПоШтрихкодам;
	
КонецФункции
#КонецОбласти
