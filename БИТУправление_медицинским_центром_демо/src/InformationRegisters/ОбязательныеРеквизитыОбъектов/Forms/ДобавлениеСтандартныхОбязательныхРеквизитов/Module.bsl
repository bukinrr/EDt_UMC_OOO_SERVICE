#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбязательныеРеквизиты = РегистрыСведений.ОбязательныеРеквизитыОбъектов;
	
	МассивСтандартныхРеквизитов = ОбязательныеРеквизиты.ПолучитьСтандартныеРеквизиты();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбязательныеРеквизитыОбъектов.ИмяСправочника КАК ИмяСправочника,
	               |	ОбязательныеРеквизитыОбъектов.ИмяРеквизита КАК ИмяРеквизита,
	               |	ОбязательныеРеквизитыОбъектов.ТолькоПриЗаписиНового КАК ТолькоПриЗаписиНового
	               |ИЗ
	               |	РегистрСведений.ОбязательныеРеквизитыОбъектов КАК ОбязательныеРеквизитыОбъектов";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Реквизит Из МассивСтандартныхРеквизитов Цикл
		
		Если РезультатЗапроса.НайтиСтроки(Новый Структура("ИмяСправочника, ИмяРеквизита", Реквизит.ИмяСправочника, Реквизит.ИмяРеквизита)).Количество() = 0 Тогда
			
			НоваяСтрока = СтандартныеРеквизиты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Реквизит);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СтандартныеРеквизиты.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Все стандартные записи регистра уже созданы!");
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	БылаЗапись = Ложь;
	ДобавитьНаСервере(БылаЗапись);		
	Если БылаЗапись Тогда
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ОбязательныеРеквизитыОбъектов"));	
	КонецЕсли;
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	ЭтаФорма.Закрыть();	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	ПроставитьФлажки();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	ПроставитьФлажки(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаСервере
Процедура ДобавитьНаСервере(БылаЗапись)
		
	Для Каждого Реквизит Из СтандартныеРеквизиты Цикл
		
		Если Реквизит.Добавить Тогда
			Запись = РегистрыСведений.ОбязательныеРеквизитыОбъектов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, Реквизит);
			Запись.Записать();
			БылаЗапись = Истина;
			
			ВидКИ = ПолучитьВидКИ(Реквизит);
			
			Если ВидКИ <> Неопределено Тогда
				Попытка
					ВидКИОбъект = ВидКИ.ПолучитьОбъект();
					Если Не ВидКИОбъект.РедактируетсяВФормеОбъекта Тогда
						ВидКИОбъект.РедактируетсяВФормеОбъекта = Истина;
						ВидКИОбъект.Записать();
					КонецЕсли;
				Исключение КонецПопытки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВидКИ(Реквизит)

	ПрефиксКИ = РегистрыСведений.ОбязательныеРеквизитыОбъектов.ПрефиксРеквизитовВидовКИ();
	Если СтрНайти(Реквизит.ИмяРеквизита, ПрефиксКИ) <> 0 Тогда
		Имя = Прав(Реквизит.ИмяРеквизита, СтрДлина(Реквизит.ИмяРеквизита) - СтрДлина(ПрефиксКИ));
		Попытка
			Возврат Справочники.ВидыКонтактнойИнформации[Имя];	
		Исключение
			Попытка
				УИД = СтрЗаменить(Имя, "_", "-");
			  	Ссылка = Справочники.ВидыКонтактнойИнформации.ПолучитьСсылку(Новый УникальныйИдентификатор(УИД));
				Возврат Ссылка; 
			Исключение
				Возврат Неопределено;	
			КонецПопытки;
		КонецПопытки;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПроставитьФлажки(Значение = Истина)
	
	Для Каждого Реквизит Из СтандартныеРеквизиты Цикл
		Реквизит.Добавить = Значение;	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
