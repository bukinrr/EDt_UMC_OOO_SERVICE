#Область РазделОписанияПеременных
&НаКлиенте
Перем мТекущийрежимВыбораРедактирование;
#КонецОбласти

#Область ОбработчикиСобытий
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(Элемент.ТекущиеДанные.Противопоказание);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	мТекущийрежимВыбораРедактирование = Ложь;
	ОткрытьФорму("Справочник.МедицинскиеПротивопоказания.ФормаВыбора",, Элемент); 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	мТекущийрежимВыбораРедактирование = Истина;
	ПараметрыФормы = Новый Структура("ТекущаяСтрока", Элемент.ТекущиеДанные.Противопоказание);
	ОткрытьФорму("Справочник.МедицинскиеПротивопоказания.ФормаВыбора", ПараметрыФормы, Элемент); 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда 
		Если мТекущийрежимВыбораРедактирование Тогда
			ИзменитьЗаписьРегистра(ВыбранноеЗначение, Элемент.ТекущиеДанные.Противопоказание);
		Иначе
			ИзменитьЗаписьРегистра(ВыбранноеЗначение);
		КонецЕсли;		
	КонецЕсли;
	Элемент.Обновить();
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Процедура ИзменитьЗаписьРегистра(НовоеПротивопоказание, СтароеПротивопоказание = Неопределено)
	
	Запись = РегистрыСведений.ОбщиеПротивопоказанияМедосмотров.СоздатьМенеджерЗаписи();
	Запись.Противопоказание = СтароеПротивопоказание;
	Запись.Прочитать();
	Запись.Противопоказание = НовоеПротивопоказание;
	Запись.Записать();
	
КонецПроцедуры


&НаКлиенте
Процедура ИзменитьГруппуОпасныхВеществ(Команда)
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		ПараметрыОткрытия = Новый Структура("Ключ", ПолучитьКлючТекущейЗаписи(Элементы.Список.ТекущиеДанные.Противопоказание, Элементы.Список.ТекущиеДанные.ГруппаОпасныхВеществ));
		ОткрытьФорму("РегистрСведений.ОбщиеПротивопоказанияМедосмотров.ФормаЗаписи", ПараметрыОткрытия);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючТекущейЗаписи(Противопоказание, ГруппаОпасныхВеществ)
	
	Ключ = Новый Структура;
    Ключ.Вставить("Противопоказание", Противопоказание);
    Ключ.Вставить("ГруппаОпасныхВеществ", ГруппаОпасныхВеществ);
    Возврат РегистрыСведений.ОбщиеПротивопоказанияМедосмотров.СоздатьКлючЗаписи(Ключ);
	
КонецФункции

#КонецОбласти