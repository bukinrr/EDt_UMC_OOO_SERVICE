#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДопустимоРедактирование = ИнтеграцияЕГИСЗОбщегоНазначенияПовтИсп.ДопустимоРедактированиеЭлементовЕГИСЗ();
	
	Элементы.ДанныеСообщения.ТолькоПросмотр	 = Не ДопустимоРедактирование;
	Элементы.ФормаЗаписатьИЗакрыть.Видимость = ДопустимоРедактирование;
	Элементы.ФормаЗаписать.Видимость		 = ДопустимоРедактирование;
	Элементы.ФормаЗакрыть.Видимость			 = Не ДопустимоРедактирование;
	Элементы.ФормаЗакрыть.КнопкаПоУмолчанию	 = Не ДопустимоРедактирование;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПутьКФайлуОткрытие(Элемент, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.ПутьКФайлу) Тогда
		СтандартнаяОбработка = Ложь;
		
		ДанныеФайла = ПолучитьДанныеФайла(Запись.ПутьКФайлу);
		Если ДанныеФайла <> Неопределено Тогда
			ИВФ	= ПолучитьИмяВременногоФайла(ДанныеФайла.РасширениеФайла);
			ДанныеФайла.ДД.Записать(ИВФ);
			ЗапуститьПриложение(ИВФ);
		Иначе
			Сообщить(НСтр("ru='Не удалось получить файл.'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеФайла(ПутьКФайлу)
	
	Попытка
		Возврат Новый Структура("РасширениеФайла, ДД", ПутьКФайлу.Расширение, ПутьКФайлу.Хранилище.Получить());
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

#КонецОбласти