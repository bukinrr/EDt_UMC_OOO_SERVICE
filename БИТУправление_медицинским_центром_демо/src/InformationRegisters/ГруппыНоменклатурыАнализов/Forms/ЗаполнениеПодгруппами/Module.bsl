#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ВыполнитьЗаполнение(Команда)
	
	Если ЭтаФорма.ПроверитьЗаполнение() Тогда
		КлючЗаписи = ВыполнитьЗаполнениеНаСервере();
		Если КлючЗаписи <> Неопределено Тогда
			ОповеститьОбИзменении(КлючЗаписи);
			Закрыть();
		Иначе
			Предупреждение(НСтр("ru='В выбранной группе нет подгрупп для заполнения'"), 30);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыполнитьЗаполнениеНаСервере()
	
	Перем КлючЗаписи;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Родитель", ГруппаНоменклатуры);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ЭтоГруппа
	|	И Номенклатура.Родитель = &Родитель"
	;
	Выб = Запрос.Выполнить().Выбрать();
	Пока Выб.Следующий() Цикл
		
		Запись = РегистрыСведений.ГруппыНоменклатурыАнализов.СоздатьМенеджерЗаписи();
		Запись.Лаборатория = Лаборатория;
		Запись.Номенклатура = Выб.Ссылка;
		Запись.Пользователь = Пользователь;
		
		Запись.Записать();
		
		Если КлючЗаписи = Неопределено Тогда
			КлючЗаписи = РегистрыСведений.ГруппыНоменклатурыАнализов.ПустойКлюч();
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат КлючЗаписи;
	
КонецФункции

#КонецОбласти