#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ГИП.Видимость = Ложь; // Интеграция с подсистемой ГИП не реализуется.
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьСостояниеРегламентногоЗадания();
	ОбновитьКэшОтветов();
	ОбновитьПовторноИспользуемыеЗначения();
	ИнтеграцияЕГИСЗ_СЭМД.ПроверитьДоступностьКаталоговИзНастроекИнтеграцииСЕГИСЗНаСервере(, ТекущийОбъект.МедицинскаяОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьИнтерфейс();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолноеРезервноеКопирование(Команда)
	
	Оповещение = Новый ОписаниеОповещения("РезервноеКопированиеЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, "Начать полное резервное копирование?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьКэшОтветов()
	
	ТекстУспешноПодписан = Истина;
	ИнтеграцияЕГИСЗСервер.ОбновитьКэшОтветов(Запись.МедицинскаяОрганизация, ТекстУспешноПодписан);
	
	Если Не ТекстУспешноПодписан Тогда
		МЗ = РегистрыСведений.ЕГИСЗНастройкиИнтеграции.СоздатьМенеджерЗаписи();
		МЗ.МедицинскаяОрганизация = Запись.МедицинскаяОрганизация;
		МЗ.Прочитать();
		Если МЗ.Выбран() Тогда
			МЗ.СертификатЭЦППодписиSOAPСообщений = Неопределено;
			МЗ.Записать();
		КонецЕсли;
		
		Запись.СертификатЭЦППодписиSOAPСообщений = Неопределено;
		Модифицированность = Ложь;
		ОбновитьПовторноИспользуемыеЗначения();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не удалось установить подпись. Проверьте настройки ЭЦП.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезервноеКопированиеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗапускРезервногоКопирования(Запись.МедицинскаяОрганизация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗапускРезервногоКопирования(МедицинскаяОрганизация)
	
	Попытка
		ИнтеграцияЕГИСЗСлужебныйСервер.РезервноеКопирование(МедицинскаяОрганизация, Истина);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Полное резервное копирование завершено!'"));
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru='В ходе копирования возникла ошибка: %1'"), ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьСостояниеРегламентногоЗадания()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕГИСЗНастройкиИнтеграции.МедицинскаяОрганизация КАК МедицинскаяОрганизация
		|ИЗ
		|	РегистрСведений.ЕГИСЗНастройкиИнтеграции КАК ЕГИСЗНастройкиИнтеграции
		|ГДЕ
		|	ЕГИСЗНастройкиИнтеграции.РежимОтправкиСообщения = ЗНАЧЕНИЕ(Перечисление.РежимыОтправкиСообщенийРЭМД.РегламентнымЗаданием)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	РегламентноеЗаданиеОтправкиСообщенийРЭМД = РегламентныеЗадания.НайтиПредопределенное("ИнтеграцияЕГИСЗОтправитьСформированныеСообщенияРЭМД");
	РегламентноеЗаданиеОтправкиСообщенийРЭМД.Использование = Выборка.Следующий();
	РегламентноеЗаданиеОтправкиСообщенийРЭМД.Записать();
	
КонецПроцедуры

#КонецОбласти