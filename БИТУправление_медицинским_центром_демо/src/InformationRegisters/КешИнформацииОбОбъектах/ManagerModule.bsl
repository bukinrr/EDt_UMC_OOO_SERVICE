#Область ПрограммныйИнтерфейс

// Устанавливает значение указанного ресурса регистра для переданного объекта.
//
// Параметры:
//  Объект	 - ОпределяемыйТип.ПодписанныйОбъект - подписанный документ.
//  Признак	 - Строка							 - имя признака
//  Значение - Произвольный						 - значение признака, любое.
//
Процедура УстановитьПризнак(Объект, Признак, Значение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.КешИнформацииОбОбъектах.СоздатьМенеджерЗаписи();
	Запись.Объект = Объект;
	Если Запись.Объект = Неопределено Тогда // Данный тип объекта не обрабатывается.
		Возврат;
	КонецЕсли;
	
	Запись.Прочитать();
	
	Если Запись[Признак] <> Значение Тогда
		Запись.Объект = Объект;
		Запись[Признак] = Значение;
		Запись.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти