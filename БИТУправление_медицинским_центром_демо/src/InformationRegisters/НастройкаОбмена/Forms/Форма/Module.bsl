
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьОбъектамиИИхЗначениями();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьНаСервере();
	
	Если Команда.Имя = "СохранитьЗакрыть" Тогда
		
		ЭтаФорма.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаНастроекСопоставлениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		
		НазваниеОбъекта = Элементы.ТаблицаНастроек.ТекущиеДанные.НазваниеОбъекта;	
		СписокВыбора = СформироватьСписокЗначений(НазваниеОбъекта);
		Элемент.СписокВыбора.Очистить();
		Для Каждого ЭлементСписка Из СписокВыбора Цикл
			Элемент.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);	
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОбъектамиИИхЗначениями()
	
	Макет = РегистрыСведений.НастройкаОбмена.ПолучитьМакет("ПредопределенныеЭлементы");	
	Область = Макет.Области["Объекты"];
	
	ОтборРегистра = Новый Структура("НазваниеОбъекта");
	
	Для счетчик = Область.Верх По Область.Низ Цикл
		
		НазваниеОбъекта = СокрЛП(Макет.Область("R"+счетчик+"C1").Текст);	
		Синоним = СокрЛП(Макет.Область("R"+счетчик+"C2").Текст);
		Строка = ТаблицаНастроек.Добавить();
		Строка.НазваниеОбъекта = НазваниеОбъекта;
		Строка.СинонимОбъекта = Синоним;
		ОтборРегистра.НазваниеОбъекта = НазваниеОбъекта;
		Строка.ПолеСопоставления = РегистрыСведений.НастройкаОбмена.Получить(ОтборРегистра).ПолеСопоставления;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьСписокЗначений(НазваниеОбъекта)
	СписокЗначений = Новый СписокЗначений;
	Макет = РегистрыСведений.НастройкаОбмена.ПолучитьМакет("ПредопределенныеЭлементы");	
	Область = Макет.Области["Реквизиты"];
	ТекущийВидРеквизита = "*";	
	Для счетчик = Область.Верх По Область.Низ Цикл
		
		ВидРеквизита = СокрЛП(Макет.Область("R"+счетчик+"C1").Текст);
		Если ВидРеквизита <> "" Тогда
			ТекущийВидРеквизита = ВидРеквизита;
		КонецЕсли;

		Если ТекущийВидРеквизита = "*" ИЛИ ТекущийВидРеквизита = НазваниеОбъекта Тогда
			НазваниеРеквизита = СокрЛП(Макет.Область("R"+счетчик+"C2").Текст);	
			Представление = СокрЛП(Макет.Область("R"+счетчик+"C3").Текст);
			СписокЗначений.Добавить(НазваниеРеквизита,Представление);  
		КонецЕсли;

	КонецЦикла;
	Возврат СписокЗначений;
КонецФункции

&НаСервере
Процедура СохранитьНаСервере()
		
	НаборЗаписей = РегистрыСведений.НастройкаОбмена.СоздатьНаборЗаписей();
	
	НаборЗаписей.Загрузить(ТаблицаНастроек.Выгрузить());
	
	НаборЗаписей.Записать(Истина);  	
	
КонецПроцедуры
#КонецОбласти
