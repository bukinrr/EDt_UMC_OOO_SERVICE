#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Филиал = Параметры.Филиал;
	
	СоответствиеФормРцептов = ПолучитьСерииРецептов(Филиал);
	
	Для Каждого Эл Из СоответствиеФормРцептов Цикл 
		Если Эл.Ключ = Перечисления.ФормыРецептов.Форма107_у_НП Тогда
			СерияФорма107_у_НП = Эл.Значение;
		ИначеЕсли Эл.Ключ = Перечисления.ФормыРецептов.Форма148_1_у_04_л Тогда
			СерияФорма148_1_у_04_л = Эл.Значение;
		ИначеЕсли Эл.Ключ = Перечисления.ФормыРецептов.Форма148_1_у_88 Тогда
			СерияФорма148_1_у_88 = Эл.Значение;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ФилиалПриИзменении(Элемент)
	
	СоответствиеФормРцептов = ПолучитьСерииРецептов(Филиал);
	
	Для Каждого Эл Из СоответствиеФормРцептов Цикл 
		Если Эл.Ключ = ПредопределенноеЗначение("Перечисление.ФормыРецептов.Форма107_у_НП") Тогда
			СерияФорма107_у_НП = Эл.Значение;
		ИначеЕсли Эл.Ключ = ПредопределенноеЗначение("Перечисление.ФормыРецептов.Форма148_1_у_04_л") Тогда
			СерияФорма148_1_у_04_л = Эл.Значение;
		ИначеЕсли Эл.Ключ = ПредопределенноеЗначение("Перечисление.ФормыРецептов.Форма148_1_у_88") Тогда
			СерияФорма148_1_у_88 = Эл.Значение;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Записать(Неопределено);
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.СерииРецептов"));
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСерииРецептов(Филиал)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СерииРецептов.Серия КАК Серия,
		|	СерииРецептов.ФормаРецепта КАК ФормаРецепта
		|ИЗ
		|	РегистрСведений.СерииРецептов КАК СерииРецептов
		|ГДЕ
		|	СерииРецептов.Филиал = &Филиал";
	
	Запрос.УстановитьПараметр("Филиал", Филиал);
		
	СоответствиеФормаРецепт = Новый Соответствие;
	СоответствиеФормаРецепт.Вставить(Перечисления.ФормыРецептов.Форма107_у_НП, "");
	СоответствиеФормаРецепт.Вставить(Перечисления.ФормыРецептов.Форма148_1_у_04_л, "");
	СоответствиеФормаРецепт.Вставить(Перечисления.ФормыРецептов.Форма148_1_у_06_л, "");
	СоответствиеФормаРецепт.Вставить(Перечисления.ФормыРецептов.Форма148_1_у_88, "");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеФормаРецепт.Вставить(Выборка.ФормаРецепта, Выборка.Серия);		
	КонецЦикла;
	
	Возврат СоответствиеФормаРецепт;
	
КонецФункции

&НаСервере
Процедура ЗаписатьНаСервере()
	
	НЗ = РегистрыСведений.СерииРецептов.СоздатьНаборЗаписей();
	НЗ.Отбор.Филиал.Установить(Филиал);

	Если ЗначениеЗаполнено(СерияФорма107_у_НП) Тогда
		ДобавитьЗаписьВНЗ(НЗ, Филиал, Перечисления.ФормыРецептов.Форма107_у_НП, СерияФорма107_у_НП)
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СерияФорма148_1_у_04_л) Тогда
		ДобавитьЗаписьВНЗ(НЗ, Филиал, Перечисления.ФормыРецептов.Форма148_1_у_04_л, СерияФорма148_1_у_04_л); 
	КонецЕсли;

	Если ЗначениеЗаполнено(СерияФорма148_1_у_88) Тогда
		ДобавитьЗаписьВНЗ(НЗ, Филиал, Перечисления.ФормыРецептов.Форма148_1_у_88, СерияФорма148_1_у_88);
	КонецЕсли;
	
	НЗ.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗаписьВНЗ(НЗ, Филиал, ФормаРецепта, Серия)
	Запись = НЗ.Добавить();
	Запись.Филиал = Филиал;
	Запись.ФормаРецепта = ФормаРецепта;
	Запись.Серия = Серия;
КонецПроцедуры;

#КонецОбласти
