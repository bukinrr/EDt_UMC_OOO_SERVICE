#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьКакОдобренную(Команда)
	
	ТекСтрока = Элементы.Заявки.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		ВебИнтеграция.ПометитьКакПрочитанныйДокумент(ТекСтрока.Заявка);
		Элементы.Заявки.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗаявку(Команда)
	
	ТекДанные = Элементы.Заявки.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Попытка
			ПометитьНаУдалениеНаСервере(ТекДанные.Заявка);
			ВебИнтеграция.ПометитьКакПрочитанныйДокумент(ТекДанные.Заявка);
			ПоКлиентуНетДокументов = ПроверитьКлиентаПоНаличиюПокупокИЗаписей(ТекДанные.Заявка);
		Исключение
			Предупреждение("Не удалось пометить на удаление заявку! По причине: "+ ОписаниеОшибки());
		КонецПопытки;
		Элементы.Заявки.Обновить();
		Если ПоКлиентуНетДокументов Тогда
			Ответ = Вопрос("Клиент не был ранее записан, и ему не оказывались услуги. Пометить его на удаление?",РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				ПометитьНаУдалениеНаСервере(ТекДанные.Заявка, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗаявкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьЗначение(Элементы.Заявки.ТекущиеДанные.Заявка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПометитьНаУдалениеНаСервере(Знач Ссылка, ЭтоУдалениеКлиента = Ложь)
	
	Если ЭтоУдалениеКлиента Тогда
		Ссылка = Ссылка.Клиент;
	КонецЕсли;

	Если ЗначениеЗаполнено(Ссылка) Тогда
		Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьКлиентаПоНаличиюПокупокИЗаписей(Заявка)
	
	Клиент = Заявка.Клиент;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Продажи.Клиент
		|ИЗ
		|	РегистрНакопления.Продажи КАК Продажи
		|ГДЕ
		|	Продажи.Клиент = &Клиент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Заявка.Клиент
		|ИЗ
		|	Документ.Заявка КАК Заявка
		|ГДЕ
		|	Заявка.Клиент = &Клиент
		|	И НЕ Заявка.Состояние = &Состояние";
	
	Запрос.УстановитьПараметр("Клиент", Клиент);
	Запрос.УстановитьПараметр("Состояние", Справочники.ВидыСостоянийЗаявок.СозданаНаСайте);
	
	Возврат Запрос.Выполнить().Пустой();
		
КонецФункции

#КонецОбласти