#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивЛаборатории = Новый Массив;
	
	Если Параметры.Свойство("МассивЛаборатории") Тогда 
		МассивЛаборатории = Параметры.МассивЛаборатории;
	Иначе 
		МассивЛаборатории = ЛабораторияСервер.ПроверитьАктуальностьСправочниковЛабораторий();
	КонецЕсли;
	
	Если МассивЛаборатории.Количество() > 0 Тогда
		Для Каждого Лаборатория Из МассивЛаборатории Цикл
			СтрокаЛаборатория = Лаборатории.Добавить();
			СтрокаЛаборатория.Лаборатория = Лаборатория;
		КонецЦикла;
	Иначе
		Элементы.ГруппаКомандыФормы.ПодчиненныеЭлементы.ФормаОткрытьЗагрузкуСправочников.Видимость = Ложь;
		Элементы.Лаборатории.Видимость = Ложь;
		Элементы.ДекорацияСообщение.Заголовок = НСтр("ru='Справочники используемых лабораторий актуальны.'") + Символы.ПС 
			+ НСтр("ru='Выполнение загрузки не требуется.'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		
		ПриЗакрытииНаСервере();
		
		СтрокиФормаЗагрузкиОткрыта = Лаборатории.НайтиСтроки(Новый Структура("ФормаЗагрузкиОткрыта", Истина));
		
		Если СтрокиФормаЗагрузкиОткрыта.Количество() > 0 Тогда 
			Состояние("Инициализируется кеш лабораторных справочников");
			ЛабораторияКлиент.ПолучитьКешНСИЛабораторийДляСтороныКлиента(Истина);
			Оповестить("ОбновленКешНСИЛаборатории");
		КонецЕсли;
		
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьЗагрузкуСправочников(Команда)
	
	МассивЛаборатории = Новый Массив;
	
	ВыделенныеСтроки = Элементы.Лаборатории.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() > 0 Тогда 
		Для Каждого ЭлементВыделенныеСтроки Из ВыделенныеСтроки Цикл
			СтрокаЛаборатория = Лаборатории.НайтиПоИдентификатору(ЭлементВыделенныеСтроки);
			Если СтрокаЛаборатория <> Неопределено Тогда 
				МассивЛаборатории.Добавить(СтрокаЛаборатория);	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОткрытьФормуЗагрузкиСправочниковЛабораторий(МассивЛаборатории);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЛабораторииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивЛаборатории = Новый Массив;
	
	СтрокаЛаборатория = Лаборатории.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если СтрокаЛаборатория <> Неопределено Тогда 
		МассивЛаборатории.Добавить(СтрокаЛаборатория);
	КонецЕсли;
	
	ОткрытьФормуЗагрузкиСправочниковЛабораторий(МассивЛаборатории);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПриЗакрытииНаСервере()

	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыСеанса.АктуальностьСправочниковЛабораторийПроверена = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗагрузкиСправочниковЛабораторий(МассивЛаборатории)
	
	Для Каждого ЭлементМассивЛаборатории Из МассивЛаборатории Цикл
		ЭлементМассивЛаборатории.ФормаЗагрузкиОткрыта = Истина;
		ОткрытьФорму("Обработка.ЗагрузкаСправочниковВнешнихЛабораторий.Форма",
			Новый Структура("Лаборатория", ЭлементМассивЛаборатории.Лаборатория), ЭтаФорма, ЭлементМассивЛаборатории.Лаборатория);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
