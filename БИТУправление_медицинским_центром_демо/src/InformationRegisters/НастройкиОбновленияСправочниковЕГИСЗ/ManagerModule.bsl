#Область ПрограммныйИнтерфейс

// Первоначальное заполнение настроек по схеме.
//
// Параметры:
//  Схема	 - Число - схема.
//
Процедура ПервоначальноеЗаполнение(Схема = 0) Экспорт
	
	СписокСправочников = ИнтеграцияЕГИСЗСерверПовтИсп.ПолучитьТаблицуСправочниковЕГИСЗ();
	Для Каждого ПараметрыСправочника Из СписокСправочников Цикл
		
		РежимАвтообновления = РежимАвтообновленияПоУмолчанию(ПараметрыСправочника.Имя, ПараметрыСправочника.ВидКлассификатора, Схема);
		
		МенеджерЗаписи = РегистрыСведений.НастройкиОбновленияСправочниковЕГИСЗ.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Классификатор = ПараметрыСправочника.OID;
		
		МенеджерЗаписи.Прочитать();
		
		Если РежимАвтообновления = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.Выключено Тогда
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Удалить();
			КонецЕсли;
		Иначе	
			Если Не МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Классификатор = ПараметрыСправочника.OID;	
			КонецЕсли; 
			
			МенеджерЗаписи.РежимОбновления = РежимАвтообновления;
			МенеджерЗаписи.Записать();
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

// Режимы загрузки для указанного классификатора.
//
// Параметры:
//  OID	 - Строка	 - OID классификатора.
// 
// Возвращаемое значение:
//  Массив - из ПеречислениеСсылка.РежимыАвтоОбновленияСправочниковЕГИСЗ - все режимы или ограничения.
//
Функция РежимыЗагрузкиКлассификатора(OID = Неопределено) Экспорт
	
	Полное = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.Полное;
	ТолькоЗагруженных = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.ТолькоЗагруженных;
	Выключено = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.Выключено;
	
	Ограничения = Новый Соответствие();
	
	НовыйOID = "1.2.643.5.1.13.2.1.1.635"; // РеестрСтраховыхМедОрганизаций
	Ограничения.Вставить(НовыйOID, Новый Массив());
	Ограничения[НовыйOID].Добавить(ТолькоЗагруженных);
	Ограничения[НовыйOID].Добавить(Выключено);
	
	НовыйOID = "1.2.643.5.1.13.13.11.1461"; // РеестрМедицинскихОрганизаций
	Ограничения.Вставить(НовыйOID, Новый Массив());
	Ограничения[НовыйOID].Добавить(Выключено);
	
	НовыйOID = "1.2.643.5.1.13.13.99.2.114"; // СтруктураПодразделений
	Ограничения.Вставить(НовыйOID, Новый Массив());
	Ограничения[НовыйOID].Добавить(Выключено);
	
	НовыйOID = "1.2.643.5.1.13.2.1.1.734"; // РолиПриПодписиМедДокументов
	Ограничения.Вставить(НовыйOID, Новый Массив());
	Ограничения[НовыйOID].Добавить(Полное);
	
	НовыйOID = "1.2.643.5.1.13.13.99.2.42"; // РЭМДПравилаПодписи
	Ограничения.Вставить(НовыйOID, Новый Массив());
	Ограничения[НовыйOID].Добавить(Полное);
	
	НовыйOID = "1.2.643.5.1.13.13.99.2.181"; // ФРМРДолжностиМедицинскогоПерсонала
	Ограничения.Вставить(НовыйOID, Новый Массив());
	Ограничения[НовыйOID].Добавить(Полное);
	
	Если OID = Неопределено Тогда
		Возврат Ограничения;
	ИначеЕсли Ограничения.Получить(OID) <> Неопределено Тогда
		Возврат Ограничения.Получить(OID);
	Иначе
		ВсеРежимы = Новый Массив();
		ВсеРежимы.Добавить(Полное);
		ВсеРежимы.Добавить(ТолькоЗагруженных);
		ВсеРежимы.Добавить(Выключено);
		Возврат ВсеРежимы;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РежимАвтообновленияПоУмолчанию(ИмяКлассификатора, КлассификаторМинздрава = Неопределено, Схема = 0)
	
	Если Схема = 1 Тогда
		// Схема "Используется РЭМД".
		РежимПоУмолчанию = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.Полное;
	Иначе
		РежимПоУмолчанию = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.ТолькоЗагруженных;
	КонецЕсли;
	
	// Возможно, этот классификатор во всех схемах всегда имеет одинаковый режим. Тогда его берем, а не дефолт схемы.
	Если ЭтоКлассификаторВСхемахВсегда_Полный(ИмяКлассификатора, КлассификаторМинздрава) Тогда
		РежимПоУмолчанию = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.Полное;
		
	ИначеЕсли ЭтоКлассификаторВСхемахВсегда_Выключено(ИмяКлассификатора, КлассификаторМинздрава) Тогда
		РежимПоУмолчанию = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.Выключено;
		
	ИначеЕсли ЭтоКлассификаторВСхемахВсегда_ТолькоЗагруженных(ИмяКлассификатора, КлассификаторМинздрава) Тогда
		РежимПоУмолчанию = Перечисления.РежимыАвтоОбновленияСправочниковЕГИСЗ.ТолькоЗагруженных;
	КонецЕсли;
	
	Возврат РежимПоУмолчанию;
			
КонецФункции

Функция ЭтоКлассификаторВСхемахВсегда_Полный(ИмяКлассификатора, КлассификаторМинздрава)
	
	ИменаСправочников = Новый Массив;
	
	Виды = Новый Массив;
	Если ЗначениеЗаполнено(КлассификаторМинздрава) Тогда
		пчМенеджер = Перечисления.ВидыКлассификаторовМинЗдрава;
		Виды.Добавить(пчМенеджер.КлассификаторДокументовУдостоверяющихЛичность);
		Виды.Добавить(пчМенеджер.КлассификаторКатегорийЛьгот);
		Виды.Добавить(пчМенеджер.КлассификаторСоциальныхСтатусов);
		Виды.Добавить(пчМенеджер.ЛьготныеКатегорииНаселения);
		Виды.Добавить(пчМенеджер.ПризнакЖителяГородаСела);
		Виды.Добавить(пчМенеджер.ПризнакиНаличияИнвалидности);
		Виды.Добавить(пчМенеджер.ПричиныНетрудоспособности);
		Виды.Добавить(пчМенеджер.СоциальныеГруппыНаселения);
		Виды.Добавить(пчМенеджер.КлассификаторПоловойПринадлежности);
		Виды.Добавить(пчМенеджер.ВидыПолисаОМС);
		Виды.Добавить(пчМенеджер.ТипАдресаПациента);
		Виды.Добавить(пчМенеджер.ДокументыУдостоверяющиеЛичность);
		Виды.Добавить(пчМенеджер.ТипыДокументовОснований);
		Виды.Добавить(пчМенеджер.ТипыДокументированныхСобытий);
		Виды.Добавить(пчМенеджер.ФормыОказанияМедицинскойПомощи);
		Виды.Добавить(пчМенеджер.ВидыМедПомощи);
		Виды.Добавить(пчМенеджер.КодируемыеПоляCDAДокументов);
		Виды.Добавить(пчМенеджер.СправочникСекцийДокументов);
		Виды.Добавить(пчМенеджер.СправочникМедицинскихПоказанийКУправлениюТС);
		Виды.Добавить(пчМенеджер.СправочникМедицинскихОграниченийКУправлениюТС);
		Виды.Добавить(пчМенеджер.СправочникНаличияМедицинскихПоказанийОграниченийИПротивопоказанийКУправлениюТС);
		Виды.Добавить(пчМенеджер.КатегорииИПодкатегорииТранспортныхСредств);
		Виды.Добавить(пчМенеджер.ВидыНозологическихЕдиницДиагноза);
		Виды.Добавить(пчМенеджер.ПрофессииРабочихИДолжностейСлужащих);
		Виды.Добавить(пчМенеджер.ГруппыЗдоровья);
		Виды.Добавить(пчМенеджер.УровеньКонфиденциальностиМедицинскогоДокумента);
		Виды.Добавить(пчМенеджер.ФРМРДолжностиМедицинскогоПерсонала);
	КонецЕсли;
	
	Возврат ЭтоКлассификаторВСхемахВсегда_ОдинРежим(ИмяКлассификатора, КлассификаторМинздрава, ИменаСправочников, Виды);
	
КонецФункции

Функция ЭтоКлассификаторВСхемахВсегда_Выключено(ИмяКлассификатора, КлассификаторМинздрава)
	
	ИменаСправочников = Новый Массив;
	ИменаСправочников.Добавить("РеестрМедицинскихОрганизаций");
	ИменаСправочников.Добавить("РеестрСтраховыхМедОрганизаций");
	ИменаСправочников.Добавить("ДействующиеВеществаЛекарственныхПрепаратов");
	ИменаСправочников.Добавить("СтруктураПодразделений");
	
	Виды = Новый Массив;
	
	Возврат ЭтоКлассификаторВСхемахВсегда_ОдинРежим(ИмяКлассификатора, КлассификаторМинздрава, ИменаСправочников, Виды);
	
КонецФункции

Функция ЭтоКлассификаторВСхемахВсегда_ТолькоЗагруженных(ИмяКлассификатора, КлассификаторМинздрава)
	
	ИменаСправочников = Новый Массив;
	ИменаСправочников.Добавить("Диагнозы");
	
	Виды = Новый Массив;
	
	Возврат ЭтоКлассификаторВСхемахВсегда_ОдинРежим(ИмяКлассификатора, КлассификаторМинздрава, ИменаСправочников, Виды);
	
КонецФункции

Функция ЭтоКлассификаторВСхемахВсегда_ОдинРежим(ИмяКлассификатора, КлассификаторМинздрава, ИменаСправочников, Виды)
	
	Возврат ИменаСправочников.Найти(ИмяКлассификатора) <> Неопределено
			Или (ЗначениеЗаполнено(КлассификаторМинздрава) И Виды.Найти(КлассификаторМинздрава) <> Неопределено);
		  
КонецФункции

#КонецОбласти