#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ПрохождениеМедосмотра)
		Или ТипЗнч(Параметры.Действия) <> Тип("Массив")
		Или Параметры.Действия.Количество() = 0
	Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПрохождениеМедОсмотра = Параметры.ПрохождениеМедосмотра;
	ЗаполнитьТаблицуДействий(Параметры.Действия);
	
	Если ПрохождениеМедОсмотра.ПодписанЭП Тогда
		Элементы.ДействияМедОсмотра.ТолькоПросмотр = Истина;
		Элементы.ФормаЗаписатьИЗакрыть.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПередЗакрытиемМодифицированнойФормы", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДействияМедОсмотра

&НаКлиенте
Процедура ДействияМедОсмотраЗаключениеПриИзменении(Элемент)
	
	ТекДанные = Элементы.ДействияМедОсмотра.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекДанные.Заключение) Тогда
		Элементы.ДействияМедОсмотра.ТекущиеДанные.Заключение = ТекДанные.ЗаключениеПоУмолчанию;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИЗакрытьНаСервере(ПрохождениеМедОсмотра, ДействияМедОсмотра);
	Закрыть(Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьИЗакрытьНаСервере(Знач ПрохождениеМедОсмотра, Знач ДействияМедОсмотра)
	
	НачатьТранзакцию();
	
	МенеджерЗаписи = РегистрыСведений.ЗаключенияПоДействиямМедосмотра.СоздатьМенеджерЗаписи();
	Для Каждого СтрокаДействия Из ДействияМедОсмотра Цикл
		Действие = СтрокаДействия.Действие;
		
		МенеджерЗаписи.Действие = СтрокаДействия.Действие;
		МенеджерЗаписи.ПрохождениеМедОсмотра = ПрохождениеМедОсмотра;
		МенеджерЗаписи.Прочитать();
		
		ВручнуюВведеннаяДатаВыполнения = Дата(1,1,1);
		Если Не ЗначениеЗаполнено(СтрокаДействия.Прием) Тогда
			ВручнуюВведеннаяДатаВыполнения = СтрокаДействия.ДатаВыполнения;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВручнуюВведеннаяДатаВыполнения)
			И (СтрокаДействия.Заключение = СтрокаДействия.ЗаключениеПоУмолчанию
				Или ПустаяСтрока(СтрокаДействия.Заключение))
		Тогда
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Удалить();
			КонецЕсли;
			
		ИначеЕсли МенеджерЗаписи.Заключение <> СтрокаДействия.Заключение
			Или МенеджерЗаписи.ДатаВыполнения <> ВручнуюВведеннаяДатаВыполнения
		Тогда
			МенеджерЗаписи.Заключение = СтрокаДействия.Заключение;
			МенеджерЗаписи.Действие = СтрокаДействия.Действие;
			МенеджерЗаписи.ПрохождениеМедОсмотра = ПрохождениеМедОсмотра;
			МенеджерЗаписи.ДатаВыполнения = ВручнуюВведеннаяДатаВыполнения;
			МенеджерЗаписи.Записать();
		КонецЕсли;
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуДействий(СписокДействий)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДействий",СписокДействий);
	Запрос.УстановитьПараметр("ПрохождениеМедОсмотра",ПрохождениеМедОсмотра);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДействияМедосмотра.Ссылка КАК Действие
		|ПОМЕСТИТЬ СписокДействий
		|ИЗ
		|	Справочник.ДействияМедосмотра КАК ДействияМедосмотра
		|ГДЕ
		|	ДействияМедосмотра.Ссылка В(&СписокДействий)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаключенияПоДействиямМедосмотраСрезПоследних.Заключение КАК Заключение,
		|	ЗаключенияПоДействиямМедосмотраСрезПоследних.Действие КАК Действие,
		|	ЗаключенияПоДействиямМедосмотраСрезПоследних.ДатаВыполнения КАК ДатаВыполнения
		|ПОМЕСТИТЬ ЗаключенияПоРегистру
		|ИЗ
		|	РегистрСведений.ЗаключенияПоДействиямМедосмотра КАК ЗаключенияПоДействиямМедосмотраСрезПоследних
		|ГДЕ
		|	ЗаключенияПоДействиямМедосмотраСрезПоследних.ПрохождениеМедОсмотра = &ПрохождениеМедОсмотра
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокДействий.Действие КАК Действие,
		|	ЗаключенияПоРегистру.Заключение КАК Заключение,
		|	ЗаключенияПоРегистру.ДатаВыполнения КАК ДатаВыполнения,
		|	СписокДействий.Действие.ТекстЗаключенияПоУмолчанию КАК ТекстЗаключенияПоУмолчанию
		|ИЗ
		|	СписокДействий КАК СписокДействий
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаключенияПоРегистру КАК ЗаключенияПоРегистру
		|		ПО СписокДействий.Действие = ЗаключенияПоРегистру.Действие
		|
		|УПОРЯДОЧИТЬ ПО
		|	СписокДействий.Действие.Вид УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СписокПротивопоказаний = Документы.ПрохождениеМедосмотра.ПротивопоказанияМедОсмотра(ПрохождениеМедОсмотра);
	
	ДействияМедОсмотра.Очистить();
	ТекстЗаключенияПоУмолчанию	 = МедосмотрыКлиентСервер.ТекстЗаключенияПоУмолчанию();
	ТаблицаПараметровИЗначений	 = МедосмотрыСервер.ПолучитьТаблицуДействийМедосмотраИЗначений(ПрохождениеМедОсмотра, СписокДействий);
	ПриемыПоДействиямМедосмотров = МедосмотрыСервер.ПриемыПоДействиямМедосмотров(ПрохождениеМедОсмотра, СписокДействий);
	
	ОтборДействиеМедосмотра = Новый Структура("ДействиеМедосмотра");
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ДействияМедОсмотра.Добавить();
		НоваяСтрока.Действие = Выборка.Действие;
		НоваяСтрока.ЗаключениеПоУмолчанию = ?(ЗначениеЗаполнено(Выборка.ТекстЗаключенияПоУмолчанию),Выборка.ТекстЗаключенияПоУмолчанию,ТекстЗаключенияПоУмолчанию);
		
		ОтборДействиеМедосмотра.ДействиеМедосмотра = Выборка.Действие;
		ТаблицаПараметровДействия = ТаблицаПараметровИЗначений.Скопировать(ТаблицаПараметровИЗначений.НайтиСтроки(ОтборДействиеМедосмотра));
		
		СтрокиВредныхФакторов = СписокПротивопоказаний.НайтиСтроки(Новый Структура("Действие",Выборка.Действие));
		СписокВредныхФакторов = СписокПротивопоказаний.Скопировать(СтрокиВредныхФакторов, "ВредныйФактор").ВыгрузитьКолонку("ВредныйФактор");
		НоваяСтрока.ЗаключениеПоУмолчанию = МедосмотрыСервер.ЗаключениеДляДействияМедОсмотра(СписокВредныхФакторов, НоваяСтрока.ЗаключениеПоУмолчанию, ТаблицаПараметровДействия, Выборка.Действие.Вид);
		
		Если ЗначениеЗаполнено(Выборка.Заключение) Тогда
			НоваяСтрока.Заключение = Выборка.Заключение;
		Иначе
			НоваяСтрока.Заключение = НоваяСтрока.ЗаключениеПоУмолчанию;
		КонецЕсли;
		
		НоваяСтрока.Прием = ПриемыПоДействиямМедосмотров.Получить(НоваяСтрока.Действие);
		Если ЗначениеЗаполнено(НоваяСтрока.Прием) Тогда
			НоваяСтрока.ДатаВыполнения = НоваяСтрока.Прием.Дата;
		Иначе
			НоваяСтрока.ДатаВыполнения = Выборка.ДатаВыполнения;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемМодифицированнойФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		ЗаписатьИЗакрытьНаСервере(ПрохождениеМедОсмотра, ДействияМедОсмотра);
		Закрыть(Истина);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



