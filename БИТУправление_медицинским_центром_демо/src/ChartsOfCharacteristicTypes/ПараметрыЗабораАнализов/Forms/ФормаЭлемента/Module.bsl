
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИспользуетсяВычисляемоеЗначениеПоУмолчанию = ТипЗнч(Объект.ЗначениеПоУмолчанию) = Тип("СправочникСсылка.ВычисляемыеЗначенияПараметров");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПривестиТипЗначенияПоУмолчанию();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипЗначенияПриИзменении(Элемент)
	ПривестиТипЗначенияПоУмолчанию();
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеПоУмолчаниюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = РаботаСДиалогамиКлиент.ЗаписатьНовыйОбъектВФорме(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяВычисляемоеЗначениеПоУмолчаниюПриИзменении(Элемент)
	
	Если Не ИспользуетсяВычисляемоеЗначениеПоУмолчанию Тогда
		Объект.ЗначениеПоУмолчанию = Неопределено;
	КонецЕсли;
	ПривестиТипЗначенияПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПривестиТипЗначенияПоУмолчанию()
	
	Если ИспользуетсяВычисляемоеЗначениеПоУмолчанию Тогда
		ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ВычисляемыеЗначенияПараметров");
	Иначе
		ОграничениеТипа = Объект.ТипЗначения;
	КонецЕсли;
	
	Объект.ЗначениеПоУмолчанию = ОграничениеТипа.ПривестиЗначение(Объект.ЗначениеПоУмолчанию);
	
КонецПроцедуры

#КонецОбласти
