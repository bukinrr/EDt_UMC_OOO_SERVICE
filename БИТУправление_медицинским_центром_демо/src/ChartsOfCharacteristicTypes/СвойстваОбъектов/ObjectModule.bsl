
#Область ОбработчикиСобытий

// Обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ)
	Если НЕ ЭтоНовый()
		И НЕ ОбменДанными.Загрузка
		И НазначениеСвойства <> Ссылка.НазначениеСвойства 
		И ЭтотОбъект.СуществуютСсылки() 
	Тогда
		ТекстСообщения = НСтр("ru='Существуют объекты, которым назначено свойство ""%1"".
		         |Назначение свойства не может быть изменено, элемент не записан.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,
			Наименование); 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

// Обработчик события ПередУдалением объекта.
//
Процедура ПередУдалением(Отказ)
	Если Предопределенный Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не допускается удаление предопределенных элементов!'"));
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#Область ЭкспортируемыеПроцедурыИФункции

// Функция проверяет, используется ли свойство для задания значений. 
// Или назначения каким-либо объектам.
//
// Возвращаемое значение:
//  Истина, если используется, Ложь, если нет.
//
Функция СуществуютСсылки() Экспорт
	Запрос = Новый Запрос();

	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегистрСведений.ЗначенияСвойствОбъектов.Свойство КАК Свойство
	|ИЗ
	|	РегистрСведений.ЗначенияСвойствОбъектов
	|
	|ГДЕ
	|	РегистрСведений.ЗначенияСвойствОбъектов.Свойство = &Свойство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегистрСведений.НазначенияСвойствОбъектов.Свойство КАК Свойство
	|ИЗ
	|	РегистрСведений.НазначенияСвойствОбъектов
	|
	|ГДЕ
	|	РегистрСведений.НазначенияСвойствОбъектов.Свойство = &Свойство
	|";

	Запрос.УстановитьПараметр("Свойство", Ссылка);
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

#КонецОбласти

#КонецОбласти
