#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполнение дерева выбора по фразам параметра-донора.
	Объект = Параметры.Объект;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СоставныеФразы.Ссылка КАК Фраза,
	               |	ИСТИНА КАК Выбрано
	               |ИЗ
	               |	Справочник.СоставныеФразы КАК СоставныеФразы
	               |ГДЕ
	               |	СоставныеФразы.Параметр = &Параметр
	               |ИТОГИ
	               |	МАКСИМУМ(Выбрано)
	               |ПО
	               |	Фраза ТОЛЬКО ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("Параметр", Параметры.Донор);
	ФразыОбъекта = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	СкорректироватьВыборкуВложенныеРодители(ФразыОбъекта);
	
	СкорректироватьВыборкуПустыеВетви(ФразыОбъекта);
	
	ЗначениеВРеквизитФормы(ФразыОбъекта, "Фразы");
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Скопировать(Команда)
	
	СкопироватьНаСервере();
	ЭтаФорма.Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Дерево = Фразы.ПолучитьЭлементы();
	ИзменитьОтметкиВДереве(Дерево, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Дерево = Фразы.ПолучитьЭлементы();
	ИзменитьОтметкиВДереве(Дерево, Ложь);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийДереваФормы

&НаКлиенте
Процедура ФразыВыбраноПриИзменении(Элемент)
	
	ИзменитьОтметкиВДереве(Элементы.Фразы.ТекущиеДанные.ПолучитьЭлементы(), Элементы.Фразы.ТекущиеДанные.Выбрано);
	Если Элементы.Фразы.ТекущиеДанные.Выбрано = Истина Тогда 
		Родитель = Элементы.Фразы.ТекущиеДанные.ПолучитьРодителя();
		Пока Не Родитель = Неопределено Цикл
			Родитель.Выбрано = Истина;
			Родитель = Родитель.ПолучитьРодителя();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДействияПриСозданииНаСервере

&НаСервере
Процедура СкорректироватьВыборкуВложенныеРодители(Дерево)
	
	Для Каждого Строка Из Дерево.Строки Цикл
		Если ЗначениеЗаполнено(Строка.Родитель) Тогда
			Если Строка.Родитель.Фраза = Строка.Фраза Тогда
				Дерево.Строки.Удалить(Строка);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если Строка.Строки.Количество() > 0 Тогда
			СкорректироватьВыборкуВложенныеРодители(Строка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СкорректироватьВыборкуПустыеВетви(Дерево)
	
	Для Каждого Строка Из Дерево.Строки Цикл
		Если Не ЗначениеЗаполнено(Строка.Фраза) Тогда
			Для Каждого СтрокаИзПустойСтроки Из Строка.Строки Цикл
				НоваяСтрока = Дерево.Строки.Добавить();
				НоваяСтрока.Фраза = СтрокаИзПустойСтроки.Фраза;
				НоваяСтрока.Выбрано = СтрокаИзПустойСтроки.Выбрано;
			КонецЦикла;
			Дерево.Строки.Удалить(Строка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ИзменитьОтметкиВДереве(УровеньДерева, Выбрано)
	
	Для Каждого Строка Из УровеньДерева Цикл
		Строка.Выбрано = Выбрано;
		Если Строка.ПолучитьЭлементы().Количество() > 0 Тогда
			ИзменитьОтметкиВДереве(Строка.ПолучитьЭлементы(), Выбрано);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Область КопированиеФраз

&НаСервере
Процедура СкопироватьНаСервере()
	
	Дерево = Фразы.ПолучитьЭлементы();
	СкопироватьУровеньДерева(Дерево);
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьУровеньДерева(Дерево, Родитель = Неопределено)
	
	Для Каждого Строка Из Дерево Цикл
		Если Строка.Выбрано = Истина Тогда
			НоваяФраза = СоздатьФразу(Строка.Фраза, Родитель);
			Ветка = Строка.ПолучитьЭлементы();
			Если Ветка.Количество() > 0 Тогда
				СкопироватьУровеньДерева(Ветка, НоваяФраза);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
			
КонецПроцедуры

&НаСервере
Функция СоздатьФразу(Фраза, Родитель)
	
	СпрСоставныеФразы = Справочники.СоставныеФразы;
	НоваяФраза = СпрСоставныеФразы.СоздатьЭлемент();
	НоваяФраза.Текст = Фраза.Текст;
	НоваяФраза.Наименование = Фраза.Наименование;
	НоваяФраза.Параметр = Объект;
	Если Не Родитель = Неопределено Тогда
		НоваяФраза.Родитель = Родитель.Ссылка;	
	КонецЕсли;
	НоваяФраза.Записать();
	Возврат НоваяФраза;
	
КонецФункции

#КонецОбласти

#КонецОбласти




