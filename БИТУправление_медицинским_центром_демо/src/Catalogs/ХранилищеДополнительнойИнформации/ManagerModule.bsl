#Область ПрограммныйИнтерфейс

Процедура АктуализироватьКонечногоВладельцаФайловОбъекта(ОбъектСсылка, Объект = Неопределено) Экспорт
	
	Если ОбъектСсылка.Метаданные().Реквизиты.Найти("Клиент") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Объект <> Неопределено Тогда
		КонечныйВладелец = Объект.Клиент;
	Иначе
		КонечныйВладелец = ОбъектСсылка.Клиент;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", ОбъектСсылка);
	Запрос.УстановитьПараметр("КонечныйВладелец", КонечныйВладелец);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХранилищеДополнительнойИнформации.Ссылка КАК Ссылка,
	|	ХранилищеДополнительнойИнформации.КонечныйВладелец КАК КонечныйВладелец
	|ИЗ
	|	Справочник.ХранилищеДополнительнойИнформации КАК ХранилищеДополнительнойИнформации
	|ГДЕ
	|	ХранилищеДополнительнойИнформации.Объект = &Объект
	|	И ХранилищеДополнительнойИнформации.КонечныйВладелец <> &КонечныйВладелец"
	;
	Выб = Запрос.Выполнить().Выбрать();
	
	Пока Выб.Следующий() Цикл
		ФайлОбъект = Выб.Ссылка.ПолучитьОбъект();
		ФайлОбъект.КонечныйВладелец = КонечныйВладелец;
		ФайлОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти