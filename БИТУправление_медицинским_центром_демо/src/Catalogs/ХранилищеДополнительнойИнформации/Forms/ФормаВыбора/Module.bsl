#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ФормаВключитьПредпросмотр.Видимость = Параметры.Свойство("Предпросмотр") И Параметры.Предпросмотр = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьПредпросмотр(Команда)
	
	ВключатьПредпросмотр = Не ВключатьПредпросмотр;
	Элементы.ФормаВключитьПредпросмотр.Пометка = ВключатьПредпросмотр;
	ОбновитьПредпросмотр(Элементы.Список.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ОбновитьПредпросмотр(Элемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьПредпросмотр(Строка = Неопределено)
	
	Если Строка = Неопределено
		Или Не ВключатьПредпросмотр
	Тогда
		Если Элементы.ПредпросмотрКартинка.Видимость Тогда
			Элементы.ПредпросмотрКартинка.Видимость = Ложь;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если Не ВключатьПредпросмотр Тогда
		Элементы.ПредпросмотрКартинка.Видимость = Ложь;
	Иначе
		Элементы.ПредпросмотрКартинка.Видимость = Истина;
		
		// Получаем файл
		СтрокаКеш = ПолучитьКешированныйПутьКФайлу(Строка.Ссылка);
		Если СтрокаКеш = Неопределено Тогда
			ПутьКФайлу = РаботаСФайламиКлиент.ОткрытьФайл(Строка.Ссылка, , , Истина);
			Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
				СтрокаКеш = ДобавитьКешированныйПутьКФайлу(Строка.Ссылка, ПутьКФайлу);
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ПредпросмотрКартинка.Видимость = Истина;
		ПредпросмотрКартинка = ПоместитьВоВременноеХранилище(Новый Картинка(СтрокаКеш.ПутьКФайлу), УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКешированныйПутьКФайлу(СсылкаНаФайл)
	
	НайдСтроки = КешПутейКФайлам.НайтиСтроки(Новый Структура("Файл", СсылкаНаФайл));
	Если НайдСтроки.Количество() > 0 Тогда
		Возврат НайдСтроки[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ДобавитьКешированныйПутьКФайлу(СсылкаНаФайл, ПутьКФайлу)
	
	СтрокаКеш = ПолучитьКешированныйПутьКФайлу(СсылкаНаФайл);
	Если СтрокаКеш = Неопределено Тогда
		НоваяСтрока = КешПутейКФайлам.Добавить();
		НоваяСтрока.Файл = СсылкаНаФайл;
		НоваяСтрока.ПутьКФайлу = ПутьКФайлу;
		СтрокаКеш = НоваяСтрока;
	Иначе
		СтрокаКеш.ПутьКФайлу = ПутьКФайлу;
	КонецЕсли;
	
	Возврат СтрокаКеш;
	
КонецФункции

#КонецОбласти