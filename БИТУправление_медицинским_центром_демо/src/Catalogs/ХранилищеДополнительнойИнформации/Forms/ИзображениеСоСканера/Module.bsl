
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОтображениеИзображения();
	МассивИмен = ПолучитьИменаФайловДляСканирования();
	Для Каждого Имя Из МассивИмен Цикл
		Элементы.Наименование.СписокВыбора.Добавить(Имя);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекОснИзображение = Справочники.ХранилищеДополнительнойИнформации.ПустаяСсылка();
	
	Элементы.ГруппаСтандартныеИмена.Доступность = ЕстьПравоМенятьСтандартныеИменаСканов();
	
	Модифицированность = Ложь;
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если  НЕ ЗначениеЗаполнено(ПолеИзображения) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите изображение.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Объект.Объект = Неопределено ИЛИ Объект.Объект.Пустая() Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Укажите владельца изображения.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Модифицированность И СохранитьИмя Тогда
		
		МассивИмен = Новый Массив;
		Для Каждого Эл Из Элементы.Наименование.СписокВыбора Цикл
			МассивИмен.Добавить(Эл.Значение);
		КонецЦикла;
		
		Если МассивИмен.Найти(ТекущийОбъект.Наименование) = Неопределено Тогда
			МассивИмен.Добавить(ТекущийОбъект.Наименование);
			Константы.Сканирование_СтандартныеИменаСканов.ЗаписатьИмена(МассивИмен);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьИмена(Команда)
	Оповещение = Новый ОписаниеОповещения("ОбновитьСписокИмен", ЭтаФорма);
	ОткрытьФорму("Справочник.ХранилищеДополнительнойИнформации.Форма.РедактированияСтандартныхИменСканов",,,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтображениеИзображения()
	
	Если НРег(Объект.Расширение) = ".pdf" Тогда
		ИмяКартинкиPDF = Метаданные.ОбщиеКартинки.pdfФайл.Имя; 
		ДанныеКартинки = БиблиотекаКартинок[ИмяКартинкиPDF];
	Иначе
		ДанныеКартинки = объект.Ссылка.Хранилище.Получить(); // Получаем данные картинки
	КонецЕсли;
	// Помещаем в реквизит формы для отображения.
	ПолеИзображения = ПоместитьВоВременноеХранилище(ДанныеКартинки, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокИмен(Результат, ДопПараметры) Экспорт
	
	мИмена = ПолучитьИменаФайловДляСканирования();
	Элементы.Наименование.СписокВыбора.Очистить();
	Для Каждого Имя Из мИмена Цикл
		Элементы.Наименование.СписокВыбора.Добавить(Имя);		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИменаФайловДляСканирования()
	
	Возврат Константы.Сканирование_СтандартныеИменаСканов.ПолучитьСтандартныеИменаСканов();
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьПравоМенятьСтандартныеИменаСканов()
	
	Возврат ПравоДоступа("Изменение", Метаданные.Константы.Сканирование_СтандартныеИменаСканов);
	
КонецФункции

#КонецОбласти
