#Область РазделОписанияПеременных

&НаКлиенте
Перем соотвКешИзображенийШаблонов;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПравоДоступа("Редактирование", Метаданные.Справочники.ВерсииШаблоновHTML) Тогда
		Элементы.Дата.ТолькоПросмотр = Ложь;
		Элементы.НеИспользоватьШапкуШаблона.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Для Каждого СтрокаРоли Из Объект.РолиПодписей Цикл
		Если СтрокаРоли.РежимНастройкиДоступности = Перечисления.РежимыНастройкиДоступностиРоли.ЗначениеПараметра Тогда
			СтрокаРоли.ПредставлениеНастройкаДоступности = СтрШаблон("%1: %2", СтрокаРоли.РежимНастройкиДоступности, СтрокаРоли.ПризнакДоступностиРоли);
		Иначе
			СтрокаРоли.ПредставлениеНастройкаДоступности = СтрокаРоли.РежимНастройкиДоступности;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СписокШаблонов = Новый Массив;
	СписокШаблонов.Добавить(Объект.Ссылка);
	РаботаСШаблонамиHTMLКлиентСервер.ИнициализироватьНастройкиИзображенийПриема(соотвКешИзображенийШаблонов, СписокШаблонов, ЭтаФорма.УникальныйИдентификатор);
	
	КодHTML.УстановитьТекст(Объект.ТекстHTML);
	
	ТекстHTMLПоУмолчанию = РаботаСШаблонамиHTMLКлиентСервер.ПолучитьТекстШаблонаHTMLПоУмолчанию();
	ДокументHTML = РаботаСDOMКлиентСервер.СоздатьДокументDOM(?(ПустаяСтрока(Объект.ТекстHTML), ТекстHTMLПоУмолчанию, Объект.ТекстHTML));
	ЗаполнитьСтраницыШаблонаПоДокументуHTML(ДокументHTML);
	УстановитьВидимостьКолонокСЭМД();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПросмотрHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура СоответствияПараметрамПараметрНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьСтраницыШаблонаПоДокументуHTML(ДокументHTML, ОбновлятьИзображения = Истина)
	
	НеАктивизироватьСтрокуПараметра = Истина;
	
	Если ОбновлятьИзображения Тогда 
		РаботаСШаблонамиHTMLКлиентСервер.ЗагрузитьИзображенияПриема(ДокументHTML, соотвКешИзображенийШаблонов, ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
	
	ПросмотрHTML = РаботаСDOMКлиентСервер.ПолучитьТекстHTMLДокументаDOM(ДокументHTML);
	КодHTML.УстановитьТекст(РаботаСDOMКлиентСервер.ПолучитьКодТелаHTMLИзДокументаDOM(ДокументHTML));	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьКолонокСЭМД()
	
	ВидимостьЭлементовСЭМД = ЗначениеЗаполнено(Объект.ВидДокумента) И ФорматДокументаCDA(Объект.ВидДокумента);
	Элементы.СоответствияПараметрамlocalid.Видимость			= ВидимостьЭлементовСЭМД;
	Элементы.СоответствияПараметрамКлассификацияПоля.Видимость	= ВидимостьЭлементовСЭМД;
	Элементы.СоответствияПараметрамГруппирующееПоле.Видимость	= ВидимостьЭлементовСЭМД;
	Элементы.СоответствияПараметрамОбъединяющееПоле.Видимость	= ВидимостьЭлементовСЭМД;
	Элементы.ГруппаТаблицыСЭМД.Видимость						= ВидимостьЭлементовСЭМД;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФорматДокументаCDA(ВидДокумента)
	Возврат ИнтеграцияЕГИСЗСервер.ПолучитьЗначениеАтрибутаЭлементаКлассификатора(ВидДокумента, "FORMAT") = "2";
КонецФункции

#КонецОбласти