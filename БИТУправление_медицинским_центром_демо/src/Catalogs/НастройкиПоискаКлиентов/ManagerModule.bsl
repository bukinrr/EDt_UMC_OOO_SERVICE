#Область ПрограммныйИнтерфейс

// Записывает настройки поиска по параметрам из формы.
//
// Параметры:
//  ИмяНастройки			 - Строка	- настройка.
//  СхемыКомпоновкиДанных	 - Массив	- настройки компоновки с отборами.
//  НастройкиОсновнойСхемы	 - КомпоновщикНастроекКомпоновкиДанных	- настройки компоновки с отборами из основной схемы.
//  НастройкаРасположения	 - ПеречислениеСсылка.ВидыНастроекРасположенияОтборовФормыПодбораКлиентов	 - расположение.
// 
// Возвращаемое значение:
//  СправочникСсылка.НастройкиПоискаКлиентов - .
//
Функция ЗаписатьНастройкиПоиска(ИмяНастройки, СхемыКомпоновкиДанных, НастройкиОсновнойСхемы, НастройкаРасположения) Экспорт
	
	Если ТипЗнч(ИмяНастройки) = Тип("СправочникСсылка.НастройкиПоискаКлиентов") Тогда
		НастройкаОбъект = ИмяНастройки.ПолучитьОбъект();
	Иначе
		НастройкаОбъект = Справочники.НастройкиПоискаКлиентов.СоздатьЭлемент();
		НастройкаОбъект.Наименование = ИмяНастройки; 
	КонецЕсли;
	
	НастройкаОбъект.НастройкаРасположенияОтборов = НастройкаРасположения;
	НастройкаОбъект.КомпоновщикНастроекОсновнойСхемы = Новый ХранилищеЗначения(НастройкиОсновнойСхемы);
	
	ТаблицаНастроек = НастройкаОбъект.НастройкиОтдельныхСхемКомпоновки;
	ТаблицаНастроек.Очистить();
	
	Для Каждого СтрокаСхемы Из СхемыКомпоновкиДанных Цикл
		НоваяСтрока = ТаблицаНастроек.Добавить();
		НоваяСтрока.ИмяСхемы = СтрокаСхемы.ИмяСхемы;
		НоваяСтрока.НастройкиКомпоновки = 
			Новый ХранилищеЗначения(СтрокаСхемы.НастройкиКомпоновки);
	КонецЦикла;
		
	НастройкаОбъект.Записать();
	
	Возврат НастройкаОбъект.Ссылка;
	
КонецФункции
#КонецОбласти