#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Клиент = ?(Параметры.Свойство("Клиент"), Параметры.Клиент, Справочники.Клиенты.ПустаяСсылка());
	Дата = ?(Параметры.Свойство("Дата"), Параметры.Дата, ТекущаяДата());
	
	МассивПредставителей = Справочники.Клиенты.ПолучитьПредставителейКлиента(Клиент, Дата);
	Для Каждого Представитель Из МассивПредставителей Цикл
		ЗаполнитьЗначенияСвойств(СписокПредставителей.Добавить(), Представитель);	
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПредставителейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьИЗакрыть();	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	
	ТекущиеДанные = Элементы.СписокПредставителей.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЭтотОбъект.Закрыть(Новый Структура("Объект,СтепеньРодства",ТекущиеДанные.Объект,ТекущиеДанные.СтепеньРодства));
	Иначе
		ЭтотОбъект.Закрыть();
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти