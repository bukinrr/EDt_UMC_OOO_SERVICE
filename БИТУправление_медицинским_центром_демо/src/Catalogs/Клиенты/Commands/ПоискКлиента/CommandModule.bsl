#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Источник = ПараметрыВыполненияКоманды.Источник;
	
	АктивноеОкно = АктивноеОкно();
	
	Если АктивноеОкно <> Неопределено
		И АктивноеОкно.Содержимое.Количество() <> 0
	Тогда
		ПолеКлиент = АктивноеОкно.Содержимое[0].Элементы.Найти("Клиент");
		Если ПолеКлиент <> Неопределено
			И ПолеКлиент.Вид = ВидПоляФормы.ПолеВвода
			И ПолеФормыДоступно(ПолеКлиент) 
		Тогда
				Источник = ПолеКлиент;
		КонецЕсли;
	КонецЕсли;
		
	ОткрытьФорму("ОбщаяФорма.ФормаПоискаКлиентаУпр",,Источник);
	
КонецПроцедуры

&НаКлиенте
Функция ПолеФормыДоступно(Знач ТекущееПоле)
	
	Сч = 0;
	Пока ТипЗнч(ТекущееПоле) <> Тип("ФормаКлиентскогоПриложения") И Сч < 100 Цикл
		
		Если Не ТекущееПоле.Доступность
			Или ТекущееПоле.ТолькоПросмотр
			Или ТипЗнч(ТекущееПоле) = Тип("ТаблицаФормы") // Колонка таблицы.
		Тогда
			Возврат Ложь;
		КонецЕсли;
		ТекущееПоле = ТекущееПоле.Родитель;
		Сч = Сч + 1;
		
	КонецЦикла;
		
	Если Сч < 100 И ТекущееПоле.Доступность И Не ТекущееПоле.ТолькоПросмотр Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти