#Область ОбработчикиСобытий
// Стандартный обработчик ПередЗаписью элемента справочника.
Процедура ПередЗаписью(Отказ)
	
	// Техническая запись объекта без проверок и прикладных операций записи..
	Если ДополнительныеСвойства.Свойство("НеВыполнятьСтандартныеДейтсвияПередЗаписью") Тогда
		ДополнительныеСвойства.Удалить("НеВыполнятьСтандартныеДейтсвияПередЗаписью");
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		ФИО = ОтраслевыеДополнения.КонтрольКорректностиФИО(Наименование,Фамилия, Имя, Отчество);
		Фамилия		= ФИО["Фамилия"];
		Имя			= ФИО["Имя"];
		Отчество	= ФИО["Отчество"];
		Наименование= ФИО["Наименование"];
	КонецЕсли;
	
	Если Не Отказ И Не ЭтоГруппа И
		((НапоминатьОДР и (ЭтоНовый() Или Не Ссылка.НапоминатьОДР)) Или
		 ( Не ЭтоНовый() и ДатаРождения<>Ссылка.ДатаРождения)) Тогда
	    УправлениеКонтактами.ЗаполнитьДР();
	КонецЕсли;
	
	Если ЭтоНовый() И Не ЭтоГруппа Тогда
		
		Если Не ЗначениеЗаполнено(Ответственный) Тогда
			Ответственный = ПараметрыСеанса.ТекущийПользователь;
		КонецЕсли;
		
		Автор = ПараметрыСеанса.ТекущийПользователь;
		ДатаСоздания = ТекущаяДатаСеанса();
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	Если Не ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("ИзменениеПометкиУдаления", ЭтотОбъект.ПометкаУдаления <> ЭтотОбъект.Ссылка.ПометкаУдаления);
	КонецЕсли;
	
КонецПроцедуры

// Стандартный обработчик ПриЗаписи элемента справочника.
Процедура ПриЗаписи(Отказ)
	
	СоздаватьМедкарту = Не ДополнительныеСвойства.Свойство("НеСоздаватьМедкарту") И 
		(ДополнительныеСвойства.Свойство("СоздаватьМедкарту") Или УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("АвтоСозданиеМедкарт"));
	
	Если ДополнительныеСвойства.Свойство("ЭтоНовый")
		И ДополнительныеСвойства.ЭтоНовый 
		И Не ЭтоГруппа
		И СоздаватьМедкарту
		И Не ЗначениеЗаполнено(ОсновнаяМедицинскаяКарта)
	Тогда
	
		ПараметрыМедкарты = Новый Структура;
		ВидКарты = Неопределено;
		Если ДополнительныеСвойства.Свойство("ПараметрыМедкарты") Тогда 
			ПараметрыМедкарты = ДополнительныеСвойства.ПараметрыМедкарты;
			ПараметрыМедкарты.Свойство("ВидКарты", ВидКарты);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВидКарты) Тогда 
			ВидКарты = УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("ОсновнойВидМедКарт");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВидКарты) Тогда
			Медкарта = Справочники.МедицинскиеКарты.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(Медкарта, ПараметрыМедкарты);
			Медкарта.Клиент = Ссылка;
			Медкарта.ВидКарты = ВидКарты;
			Медкарта.ДатаРегистрации = ТекущаяДатаСеанса();
			Медкарта.Филиал = УправлениеНастройками.ПолучитьФилиалПоУмолчаниюПользователя();
			ОтраслевыеДополнения.ФормированиеНомера(Медкарта, Не ЗначениеЗаполнено(Медкарта.НомерКарты));
			Медкарта.Записать();
			
			ОсновнаяМедицинскаяКарта = Медкарта.Ссылка;
			ДополнительныеСвойства.Вставить("НеВыполнятьСтандартныеДейтсвияПередЗаписью");
			ДополнительныеСвойства.ЭтоНовый = Ложь;
			Записать();
		КонецЕсли;
	КонецЕсли;
	
	ИзменениеПометкиУдаления = Ложь;
	Если ДополнительныеСвойства.Свойство("ИзменениеПометкиУдаления", ИзменениеПометкиУдаления) И ИзменениеПометкиУдаления Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МедицинскиеКарты.Ссылка
		|ИЗ
		|	Справочник.МедицинскиеКарты КАК МедицинскиеКарты
		|ГДЕ
		|	МедицинскиеКарты.Клиент = &Клиент";
		Запрос.УстановитьПараметр("Клиент", ЭтотОбъект.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(ЭтотОбъект.ПометкаУдаления);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ОсновнаяМедицинскаяКарта = Неопределено;
	умцОсновнойСтраховойПолис = Неопределено;
КонецПроцедуры
#КонецОбласти
