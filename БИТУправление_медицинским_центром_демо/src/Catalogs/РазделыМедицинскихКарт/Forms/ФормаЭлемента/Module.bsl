#Область РазделОписанияПеременных

&НаКлиенте
Перем ТекущийТипМедкарты;

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая()
		И ЗначениеЗаполнено(Объект.ТипМедкарты)
		И Не ЗначениеЗаполнено(Объект.Порядок)
	Тогда
		Объект.Порядок = ПолучитьПорядковыйНомер(Объект.ТипМедкарты, Объект.Ссылка);
	КонецЕсли;
	
	Элементы.ВыводитьРезультатыАнализовБезШаблона.Видимость = Объект.ВыводитьРезультатыАнализовБезШаблона
		Или (Метаданные.Подсистемы.Найти("ЛабораторныеИсследования") <> Неопределено
			И УправлениеНастройкамиПереопределяемый.ПолучитьПараметрУчета("ИспользоватьЛабораторныеИсследования"));

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийТипМедкарты = Объект.ТипМедкарты;
	
	НастроитьТабличныеЧасти();
	
	ДоступностьЭлементовПоВидуРаздела();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.ТипМедкарты)
		И (Объект.ВыводитьШаблоныБезРазделов
			Или Объект.ВыводитьРезультатыАнализовБезШаблона)
	Тогда
		РезультатПроверки = НаличиеРазделовОбобщенногоСпискаВывода(Объект.ТипМедкарты, Объект.Ссылка);
		ПереченьРеквизитов = "";
		Для Каждого КлючЗначение Из РезультатПроверки.НаличиеРазделов Цикл
			Если КлючЗначение.Значение
				И Объект[КлючЗначение.Ключ]
			Тогда
				ОбщегоНазначенияКлиентСервер.КонкатенацияСтрок(ПереченьРеквизитов, Символы.Таб + "-" + РезультатПроверки.СинонимыРеквизитов[КлючЗначение.Ключ], Символы.ПС);
			КонецЕсли;
		КонецЦикла;
		
		Если Не ПустаяСтрока(ПереченьРеквизитов) Тогда
			
			ТекстВопроса = НСтр("ru='Для этого типа медкарт уже существуют разделы с отметками:'")
						 + Символы.ПС + ПереченьРеквизитов
						 + Символы.ПС + НСтр("ru='Продолжить запись?'");
						 
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, Строка(Объект.ТипМедкарты));
						 
	    	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, 60, КодВозвратаДиалога.Отмена);
					
			Если Ответ <> КодВозвратаДиалога.ОК Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	НастроитьТабличныеЧасти();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	
	ОткрытьФорму("Справочник.ШаблоныHTML.ФормаВыбора", ПараметрыФормы, Элементы.ШаблоныРаздела);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипМедкартыПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.Порядок) Или ТекущийТипМедкарты <> Объект.ТипМедкарты Тогда
		Объект.Порядок = ПолучитьПорядковыйНомер(Объект.ТипМедкарты, Объект.Ссылка);
	КонецЕсли;
	
	ТекущийТипМедкарты = Объект.ТипМедкарты;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипРазделаПриИзменении(Элемент)
	ДоступностьЭлементовПоВидуРаздела();
КонецПроцедуры

&НаКлиенте
Процедура ШаблоныРазделаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ДобавитьШаблоныРазделу(ВыбранноеЗначение, Объект.Ссылка);
	Элементы.ШаблоныРаздела.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДоступностьЭлементовПоВидуРаздела()
	
	ЭтоВыводШаблонов =	Объект.ТипРаздела = ПредопределенноеЗначение("Перечисление.ТипыРазделовМедицинскихКарт.ВыводШаблонов")
						Или Не ЗначениеЗаполнено(Объект.ТипРаздела);
	
	Элементы.ВыводитьШаблоныБезРазделов.Доступность = ЭтоВыводШаблонов;
	Элементы.ВыводитьРезультатыАнализовБезШаблона.Доступность = ЭтоВыводШаблонов;
	Элементы.ШаблоныРаздела.Доступность = ЭтоВыводШаблонов;
	
	Объект.ВыводитьШаблоныБезРазделов			= ЭтоВыводШаблонов И Объект.ВыводитьШаблоныБезРазделов;
	Объект.ВыводитьРезультатыАнализовБезШаблона	= ЭтоВыводШаблонов И Объект.ВыводитьРезультатыАнализовБезШаблона;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьТабличныеЧасти()
	
	РаботаСФормамиКлиент.УстановитьОтборСписка("Раздел", Объект.Ссылка, ШаблоныРаздела);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НаличиеРазделовОбобщенногоСпискаВывода(ТипМедкарты, РазделСсылка)
	
	НаличиеРазделов = Новый Структура("ВыводитьШаблоныБезРазделов, ВыводитьРезультатыАнализовБезШаблона", Ложь, Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(МАКСИМУМ(РазделыМедицинскихКарт.ВыводитьШаблоныБезРазделов), ЛОЖЬ) КАК ВыводитьШаблоныБезРазделов,
	               |	ЕСТЬNULL(МАКСИМУМ(РазделыМедицинскихКарт.ВыводитьРезультатыАнализовБезШаблона), ЛОЖЬ) КАК ВыводитьРезультатыАнализовБезШаблона
	               |ИЗ
	               |	Справочник.РазделыМедицинскихКарт КАК РазделыМедицинскихКарт
	               |ГДЕ
	               |	РазделыМедицинскихКарт.ТипМедкарты = &ТипМедкарты
	               |	И РазделыМедицинскихКарт.Ссылка <> &Ссылка
	               |	И НЕ РазделыМедицинскихКарт.ПометкаУдаления
	               |	И (РазделыМедицинскихКарт.ВыводитьШаблоныБезРазделов
	               |			ИЛИ РазделыМедицинскихКарт.ВыводитьРезультатыАнализовБезШаблона)";
	
	Запрос.УстановитьПараметр("ТипМедкарты", ТипМедкарты);
	Запрос.УстановитьПараметр("Ссылка", РазделСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(НаличиеРазделов, Выборка);
	КонецЕсли;
	
	СинонимыРеквизитов = Новый Структура;
	МетаданныеРеквизитов = РазделСсылка.Метаданные().Реквизиты;
	Для Каждого КлючЗначение Из НаличиеРазделов Цикл
		СинонимыРеквизитов.Вставить(КлючЗначение.Ключ, МетаданныеРеквизитов.Найти(КлючЗначение.Ключ).Синоним);
	КонецЦикла;
	
	Возврат Новый Структура("НаличиеРазделов, СинонимыРеквизитов", НаличиеРазделов, СинонимыРеквизитов);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПорядковыйНомер(ТипМедкарты, РазделСсылка)
	
	Возврат Справочники.РазделыМедицинскихКарт.ПолучитьПорядковыйНомер(ТипМедкарты, РазделСсылка);		
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьШаблонПриемаРазделу(ШаблонПриема, Раздел)
	
	Запись = РегистрыСведений.ШаблоныРазделовМедицинскихКарт.СоздатьМенеджерЗаписи();
	Запись.Раздел = Раздел;
	Запись.ШаблонПриема = ШаблонПриема;
	Запись.Записать();	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьШаблоныРазделу(ШаблонПриема, Раздел)
	
	Если ШаблонПриема.ЭтоГруппа Тогда
		Выборка = Справочники.ШаблоныHTML.Выбрать(ШаблонПриема);
		Пока Выборка.Следующий() Цикл
			ДобавитьШаблоныРазделу(Выборка.Ссылка, Раздел);
		КонецЦикла;
	Иначе
		ДобавитьШаблонПриемаРазделу(ШаблонПриема, Раздел);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
