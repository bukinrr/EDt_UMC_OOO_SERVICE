#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ФормаНастройки.Доступность = ЗначениеЗаполнено(Объект.ВнешняяЛаборатория);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ТекущийОбъект.Ссылка.Пустая()
		И ЛабораторияКлиентСерверПовтИсп.ИспользуютсяПравилаЗабора(ТекущийОбъект.ВнешняяЛаборатория)
			<> ЛабораторияКлиентСерверПовтИсп.ИспользуютсяПравилаЗабора(ТекущийОбъект.Ссылка.ВнешняяЛаборатория)
	Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Другим пользователям для ввода забора анализа по данной лаборатории потребуется завершить сеанс и зайти заново'"));
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВнешняяЛабораторияПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.Наименование)
		И ЗначениеЗаполнено(Объект.ВнешняяЛаборатория)
	Тогда
		Объект.Наименование = Строка(Объект.ВнешняяЛаборатория);
	КонецЕсли;
	
	Элементы.ФормаНастройки.Доступность = ЗначениеЗаполнено(Объект.ВнешняяЛаборатория);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Настройки(Команда)
	
	Если ЗначениеЗаполнено(Объект.ВнешняяЛаборатория) Тогда
		
		Если РаботаСДиалогамиКлиент.ПроверитьМодифицированностьВФорме(ЭтотОбъект) Тогда
		
			ОписаниеФормы = ЛабораторияСервер.ФормаНастройкиИнтеграцииСЛабораторией(Объект.Ссылка);
			Если ТипЗнч(ОписаниеФормы) = Тип("Структура") Тогда
				
				Если ОписаниеФормы.ИнтеграцияИспользуется Тогда
					// Ключ или значения заполнения формы
					ПараметрыФормы = Новый Структура;
					Если ОписаниеФормы.Свойство("Ключ") Тогда
						ПараметрыФормы.Вставить("Ключ", ОписаниеФормы.Ключ); // Для открытия существующей записи регистра.
						
					ИначеЕсли ОписаниеФормы.Свойство("ЗначенияЗаполнения") Тогда
						ПараметрыФормы.Вставить("ЗначенияЗаполнения", ОписаниеФормы.ЗначенияЗаполнения); // Для создания новой записи регистра.
					КонецЕсли;
					
					ОткрытьФорму(ОписаниеФормы.ИмяФормы, ПараметрыФормы, ЭтотОбъект, Объект.Ссылка);
				Иначе
					ПоказатьПредупреждение(,СтрШаблон(НСтр("ru='Интеграция с лабораториями %1 не включена'"), Строка(Объект.ВнешняяЛаборатория)));
				КонецЕсли;
			Иначе
				ПоказатьПредупреждение(,НСтр("ru='Вид лаборатории не поддерживает открытие настроек из этой формы'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры                            

#КонецОбласти