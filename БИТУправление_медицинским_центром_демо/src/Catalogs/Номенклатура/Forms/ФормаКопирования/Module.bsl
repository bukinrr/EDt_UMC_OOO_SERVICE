#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Реквизиты = Параметры.Реквизиты;
	Номенклатура = Параметры.Номенклатура;
	КопируемаяНоменклатура = Параметры.КопируемаяНоменклатура;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Скопировать(Команда)
	
	СкопироватьНаСервере(Реквизиты, Номенклатура, КопируемаяНоменклатура);
	Оповестить("ОбновлениеФормыНоменклатуры", Номенклатура);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Процедура СкопироватьРегистр(ИмяРегистра, Номенклатура, КопируемаяНоменклатура)

	Если ИмяРегистра = "ЗначенияСвойствОбъектов" Тогда
		ИмяИзмеренияНоменклатуры = "Объект";
	Иначе
		ИмяИзмеренияНоменклатуры = "Номенклатура";
	КонецЕсли;

	НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор[ИмяИзмеренияНоменклатуры].Установить(КопируемаяНоменклатура);
	НаборЗаписей.Прочитать();
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	ТаблицаЗаписей.ЗаполнитьЗначения(Номенклатура, ИмяИзмеренияНоменклатуры);
	
	НаборЗаписей.Отбор[ИмяИзмеренияНоменклатуры].Установить(Номенклатура);
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СкопироватьНаСервере(Реквизиты, Номенклатура, КопируемаяНоменклатура)
	
	Для Каждого Реквизит Из Реквизиты Цикл
		Если Реквизит.Пометка Тогда
			 СкопироватьРегистр(Реквизит.Значение, Номенклатура, КопируемаяНоменклатура);
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти