#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Состояние") Тогда
		
		НачалоДня = Дата(1,1,1);
		ВыбранныйФилиал = Неопределено;
		
		Если Параметры.Свойство("ЗаСегодня") И Параметры.ЗаСегодня Тогда
			НачалоДня = НачалоДня(ТекущаяДатаСеанса());
		КонецЕсли;
		
		Если Параметры.Свойство("Филиал") Тогда
			ВыбранныйФилиал = Параметры.Филиал;
		КонецЕсли;
		
		ИзменитьТекстЗапроса(Параметры.Состояние, НачалоДня, ВыбранныйФилиал);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИзменитьТекстЗапроса(Состояние, НачалоДня, ВыбранныйФилиал)
	
	НовыйТекстЗапроса =
		"ВЫБРАТЬ
		|	СообщенияРЭМД.Ссылка КАК Ссылка,
		|	СообщенияРЭМД.ПометкаУдаления КАК ПометкаУдаления,
		|	СообщенияРЭМД.Код КАК Код,
		|	СообщенияРЭМД.Наименование КАК Наименование,
		|	СообщенияРЭМД.Документ КАК Документ,
		|	СообщенияРЭМД.Клиент КАК Клиент,
		|	СообщенияРЭМД.ДатаСоздания КАК ДатаСоздания,
		|	СообщенияРЭМД.ПутьКФайлуЭМД КАК ПутьКФайлуЭМД,
		|	СообщенияРЭМД.Отправить КАК Отправить,
		|	СообщенияРЭМД.Обновить КАК Обновить,
		|	СообщенияРЭМД.НомерДокумента КАК НомерДокумента,
		|	СообщенияРЭМД.ИдентификаторДокумента КАК ИдентификаторДокумента,
		|	СообщенияРЭМД.ТекстПоследнейОшибки КАК ТекстПоследнейОшибки,
		|	СообщенияРЭМД.ВидДокумента КАК ВидДокумента,
		|	СообщенияРЭМД.НомерВерсии КАК НомерВерсии,
		|	СообщенияРЭМД.ИдентификаторРЭМД КАК ИдентификаторРЭМД,
		|	СообщенияРЭМД.ХранитьДо КАК ХранитьДо,
		|	СообщенияРЭМД.ИдентификаторШаблонаЭМД КАК ИдентификаторШаблонаЭМД,
		|	СообщенияРЭМД.Предопределенный КАК Предопределенный,
		|	СообщенияРЭМД.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
		|	СообщенияРЭМД.Документ.Филиал КАК Филиал
		|ИЗ
		|	Справочник.СообщенияРЭМД КАК СообщенияРЭМД
		|ГДЕ
		|	СообщенияРЭМД.Ссылка В
		|			(ВЫБРАТЬ
		|				СообщенияРЭМД.Ссылка
		|			ИЗ
		|				СообщенияПоСостоянию КАК СообщенияРЭМД)";
	
	ТекстыЗапросов = ПолучитьТекстыЗапросовПоСостояниям();
	
	ЧастиТекстаЗапроса = Новый Массив;
	ЧастиТекстаЗапроса.Добавить(ТекстыЗапросов.Получить(Состояние));
	ЧастиТекстаЗапроса.Добавить(НовыйТекстЗапроса);
	
	Список.ТекстЗапроса = СтрСоединить(ЧастиТекстаЗапроса, ";" + Символы.ПС);
	Список.Параметры.УстановитьЗначениеПараметра("НачалоДня", НачалоДня);
	Список.Параметры.УстановитьЗначениеПараметра("ПоВыбранномуФилиалу", ВыбранныйФилиал <> Неопределено);
	Список.Параметры.УстановитьЗначениеПараметра("ВыбранныйФилиал", ВыбранныйФилиал);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекстыЗапросовПоСостояниям()
	
	ИмяВТ = "СообщенияПоСостоянию";
	
	Тексты = Новый Соответствие;
	Тексты.Вставить(Перечисления.СостоянияСообщенийРЭМД.Зарегистрированные,	ИнтеграцияЕГИСЗ_РЭМД.ТекстЗапросаВТСообщенияЗарегистрированные(ИмяВТ));
	Тексты.Вставить(Перечисления.СостоянияСообщенийРЭМД.КОтправке,			ИнтеграцияЕГИСЗ_РЭМД.ТекстЗапросаВТСообщенияКОтправке(ИмяВТ));
	Тексты.Вставить(Перечисления.СостоянияСообщенийРЭМД.СОшибками,			ИнтеграцияЕГИСЗ_РЭМД.ТекстЗапросаВТСообщенияСОшибками(ИмяВТ));
	Тексты.Вставить(Перечисления.СостоянияСообщенийРЭМД.НеПодписаныМО,		ИнтеграцияЕГИСЗ_РЭМД.ТекстЗапросаВТСообщенияНеПодписаныМО(ИмяВТ));
	Тексты.Вставить(Перечисления.СостоянияСообщенийРЭМД.НеПолученОтвет,		ИнтеграцияЕГИСЗ_РЭМД.ТекстЗапросаВТСообщенияНеПолученОтвет(ИмяВТ));
	
	Возврат Тексты;
	
КонецФункции

#КонецОбласти