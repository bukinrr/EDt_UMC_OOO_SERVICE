#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("УжеВыбранные", УжеВыбранные);
	ЗаполнитьСписокОбъектов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОсновныеДействияФормыКнопкаОК(Кнопка)
	
	ОповеститьОВыборе(ПолучитьСписокВыбранныхОбъектов());
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если РежимПодбора Тогда
		ТекущиеДанные.Пометка = Не ТекущиеДанные.Пометка;
	Иначе
		СписокОбъектов = Новый СписокЗначений;
		СписокОбъектов.Добавить(Новый Структура("Имя, Синоним", ТекущиеДанные.Имя, ТекущиеДанные.Синоним));
		ОповеститьОВыборе(СписокОбъектов);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьСписокВыбранныхОбъектов()
	
	СписокОбъектов = Новый СписокЗначений;
	
	Для Каждого Документ Из СписокДокументов Цикл
		
		Если Документ.Пометка Тогда
			СписокОбъектов.Добавить(Новый Структура("Имя, Синоним", Документ.Имя, Документ.Синоним));
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Справочник Из СписокСправочников Цикл
		
		Если Справочник.Пометка Тогда
			СписокОбъектов.Добавить(Новый Структура("Имя, Синоним", Справочник.Имя, Справочник.Синоним));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокОбъектов;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокОбъектов()
	
	ПропуститьОтборУжеВыбранные = УжеВыбранные = Неопределено Или УжеВыбранные.Количество() = 0;
	
	// Документы
	ДобавитьОбъектыВСписок(Метаданные.Документы, СписокДокументов, УжеВыбранные, ПропуститьОтборУжеВыбранные);
	
	// Справочники
	ДоступныеСправочники = Новый Массив;
	ДоступныеСправочники.Добавить(Метаданные.Справочники.Клиенты);
	
	ДобавитьОбъектыВСписок(ДоступныеСправочники, СписокСправочников, УжеВыбранные, ПропуститьОтборУжеВыбранные);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОбъектыВСписок(ОбъектыМетаданных, Список, УжеВыбранные, ПропуститьОтборУжеВыбранные)
	
	Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
		
		Если Не СтрНачинаетсяС(НРег(ОбъектМетаданных.Имя), "удалить")
			И Не СтрНачинаетсяС(НРег(ОбъектМетаданных.Синоним), НСтр("ru='(не используется)'"))
			И (ПропуститьОтборУжеВыбранные Или УжеВыбранные.НайтиПоЗначению(ОбъектМетаданных) = Неопределено)
		Тогда
			НовСтр = Список.Добавить();
			НовСтр.Имя = ОбъектМетаданных.Имя;
			НовСтр.Синоним = ОбъектМетаданных.Синоним;
			
			НовСтр.Пометка = ТипЗнч(УжеВыбранные) = Тип("СписокЗначений") И УжеВыбранные.НайтиПоЗначению(ОбъектМетаданных.Имя) <> Неопределено;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

