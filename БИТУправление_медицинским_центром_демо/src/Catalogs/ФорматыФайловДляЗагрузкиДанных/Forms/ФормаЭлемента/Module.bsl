
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФорматыФайловДляЗагрузкиДанныхСписокКолонок.Колонка,
	|	ФорматыФайловДляЗагрузкиДанныхСписокКолонок.НомерСтроки,
	|	ФорматыФайловДляЗагрузкиДанныхСписокКолонок.ИмяКолонкиВФайле
	|ПОМЕСТИТЬ ВыбранныеКолонки
	|ИЗ
	|	Справочник.ФорматыФайловДляЗагрузкиДанных.СписокКолонок КАК ФорматыФайловДляЗагрузкиДанныхСписокКолонок
	|ГДЕ
	|	ФорматыФайловДляЗагрузкиДанныхСписокКолонок.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КолонкиФайлаДляЗагрузкиДанных.Ссылка КАК Колонка,
	|	ВЫБОР
	|		КОГДА КолонкиФайлаДляЗагрузкиДанных.Ссылка = ЗНАЧЕНИЕ(Перечисление.КолонкиФайлаДляЗагрузкиДанных.ФИО)
	|				ИЛИ КолонкиФайлаДляЗагрузкиДанных.Ссылка = ЗНАЧЕНИЕ(Перечисление.КолонкиФайлаДляЗагрузкиДанных.ДатаРождения)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВыбранныеКолонки.Колонка ЕСТЬ NULL 
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	КОНЕЦ КАК Установлена,
	|	ВЫБОР
	|		КОГДА ВыбранныеКолонки.Колонка ЕСТЬ NULL 
	|			ТОГДА КолонкиФайлаДляЗагрузкиДанных.Порядок
	|		ИНАЧЕ ВыбранныеКолонки.НомерСтроки
	|	КОНЕЦ КАК Порядок,
	|	ВЫБОР
	|		КОГДА ВыбранныеКолонки.ИмяКолонкиВФайле ЕСТЬ NULL 
	|				ИЛИ ВыбранныеКолонки.ИмяКолонкиВФайле = """"
	|			ТОГДА КолонкиФайлаДляЗагрузкиДанных.Ссылка
	|		ИНАЧЕ ВыбранныеКолонки.ИмяКолонкиВФайле
	|	КОНЕЦ КАК ИмяКолонкиВФайле
	|ИЗ
	|	Перечисление.КолонкиФайлаДляЗагрузкиДанных КАК КолонкиФайлаДляЗагрузкиДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыбранныеКолонки КАК ВыбранныеКолонки
	|		ПО (ВыбранныеКолонки.Колонка = КолонкиФайлаДляЗагрузкиДанных.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Установлена УБЫВ,
	|	Порядок";
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	СписокКолонок.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если Объект.Ссылка.Пустая()
		И Объект.ОбластьПрименения = Перечисления.ОбластиПримененияФорматовФайловДляЗагрузкиДанных.Медосмотры
	Тогда
		КолонкаВредныеФакторы = СписокКолонок.НайтиСтроки(Новый Структура("Колонка", Перечисления.КолонкиФайлаДляЗагрузкиДанных.ВредныеФакторыИУсловия));
		Если КолонкаВредныеФакторы.Количество() <> 0 Тогда
			КолонкаВредныеФакторы[0].Установлена = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.СписокКолонок.Очистить();
	Для Каждого СтрокаКолонка Из СписокКолонок Цикл
		Если СтрокаКолонка.Установлена Тогда
			ЗаполнитьЗначенияСвойств(ТекущийОбъект.СписокКолонок.Добавить(), СтрокаКолонка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокКолонокУстановленаПриИзменении(Элемент)
	
	ТекДанные = Элементы.СписокКолонок.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекДанные.Колонка = ПредопределенноеЗначение("Перечисление.КолонкиФайлаДляЗагрузкиДанных.ФИО") Или
		ТекДанные.Колонка = ПредопределенноеЗначение("Перечисление.КолонкиФайлаДляЗагрузкиДанных.ДатаРождения") Тогда
		ТекДанные.Установлена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти