#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Сотрудник = ПараметрыСеанса.ТекущийПользователь.Сотрудник;
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		
		Если ЗначениеЗаполнено(Сотрудник.Специализация) Тогда
			
			Специализация = Сотрудник.Специализация;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВидГруппировки = Перечисления.ВидыГруппировкиДиагнозов.ЧастоИспользуемый;
	
	Элементы.КнопкаОткрытьНастройкуГрупп.Пометка	= Ложь;
	Элементы.ГруппаГруппировкаДиагнозов.Видимость	= Ложь;
	
	УстановитьОтборТаблицыГруппировки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьДоступностьЭлементов();
	РаботаСДиалогамиКлиент.СкрытьПоказатьАрхивные(Элементы.СписокСкрытьПоказатьАрхивные, Список, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
 
&НаКлиенте
Процедура КомандаОткрытьНастройкуГрупп(Команда)
	
	Если Элементы.КнопкаОткрытьНастройкуГрупп.Пометка = Истина Тогда
		
		Элементы.КнопкаОткрытьНастройкуГрупп.Пометка = Ложь;
		
		Элементы.ГруппаГруппировкаДиагнозов.Видимость = Ложь;
		
	Иначе
		
		Элементы.КнопкаОткрытьНастройкуГрупп.Пометка = Истина;
		
		Элементы.ГруппаГруппировкаДиагнозов.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСправочникИзЕГИСЗ(Команда)
	
	СтруктураПараметры = Новый Структура("НаименованиеСправочника", "Диагнозы");
	Оповещение = Новый ОписаниеОповещения("ЗагрузкаКлассификатораЗавершена", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ЗагрузкаНСИЕГИСЗ", СтруктураПараметры, Элементы.Список, ЭтаФорма,,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСправочникИзМКБС(Команда)
	
	СтруктураПараметры = Новый Структура("НаименованиеСправочника, OID",
											"Диагнозы",
											ОбщегоНазначенияСервер.ФункцияМенеджера("Справочники.Диагнозы", "OIDСправочникаМКБС"));
											
	Оповещение = Новый ОписаниеОповещения("ЗагрузкаКлассификатораЗавершена", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ЗагрузкаНСИЕГИСЗ", СтруктураПараметры, Элементы.Список, ЭтаФорма,,,Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ПоместитьВАрхив(Команда)

	РаботаСДиалогамиКлиент.ПоместитьВАрхив(Элементы.Список.ВыделенныеСтроки);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПоказатьАрхивные(Команда)

	РаботаСДиалогамиКлиент.СкрытьПоказатьАрхивные(Элементы.СписокСкрытьПоказатьАрхивные, Список);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпециализацияПриИзменении(Элемент)
	
	НастроитьДоступностьЭлементов();
	
	УстановитьОтборТаблицыГруппировки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидГруппировкиПриИзменении(Элемент)
	
	УстановитьОтборТаблицыГруппировки();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбраннаяСтрока)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбраннаяСтрока, "ЭтоГруппа")
	Тогда
		СтандартнаяОбработка = Ложь;
		Если Элемент.Развернут(ВыбраннаяСтрока) Тогда
			Элемент.Свернуть(ВыбраннаяСтрока);
		Иначе
			Элемент.Развернуть(ВыбраннаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Копирование;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаГруппировкиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ДобавитьДиагнозы(ПараметрыПеретаскивания.Значение, ВидГруппировки, Специализация);
	
	Элементы.ТаблицаГруппировки.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаГруппировкиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) <> Тип("СправочникСсылка.Диагнозы") Тогда 
  		
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		
	Иначе
		
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Копирование;	                 
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьДоступностьЭлементов()
	
	Если ЗначениеЗаполнено(Специализация) Тогда
		
		Элементы.ВидГруппировки.Доступность = Истина;
		
		Элементы.ТаблицаГруппировки.Доступность = Истина;
		
	Иначе
		
		Элементы.ВидГруппировки.Доступность = Ложь;
		
		Элементы.ТаблицаГруппировки.Доступность = Ложь;
		                                                           
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборТаблицыГруппировки()
	
	Если ЗначениеЗаполнено(Специализация) Тогда
		
		ТаблицаГруппировки.ТекстЗапроса =
		"ВЫБРАТЬ
		|РегистрСведенийГруппыДиагнозов.Специализация,
		|РегистрСведенийГруппыДиагнозов.ВидГруппировкиДиагнозов КАК ВидГруппировки,
		|РегистрСведенийГруппыДиагнозов.Диагноз
		|ИЗ
		|РегистрСведений.ГруппыДиагнозов КАК РегистрСведенийГруппыДиагнозов
		|ГДЕ
		|РегистрСведенийГруппыДиагнозов.Специализация = &Специализация
		|И РегистрСведенийГруппыДиагнозов.ВидГруппировкиДиагнозов = &ВидГруппировки";
		
		ТаблицаГруппировки.Параметры.УстановитьЗначениеПараметра("Специализация", Специализация);
		
		ТаблицаГруппировки.Параметры.УстановитьЗначениеПараметра("ВидГруппировки", ВидГруппировки);
		
	Иначе
				
		ТаблицаГруппировки.ТекстЗапроса =
		"ВЫБРАТЬ
		|	ГруппыДиагнозов.Специализация,
		|	ГруппыДиагнозов.ВидГруппировкиДиагнозов,
		|	ГруппыДиагнозов.Диагноз
		|ИЗ
		|	РегистрСведений.ГруппыДиагнозов КАК ГруппыДиагнозов
		|ГДЕ
		|	ГруппыДиагнозов.Специализация = ЗНАЧЕНИЕ(Справочник.Диагнозы.ПустаяСсылка)";
		
	КонецЕсли;  	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьДиагнозы(ВыбранныеДиагнозы, ВидГруппировки, Специализация)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВыбранныеДиагнозы", ВыбранныеДиагнозы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Диагнозы.Ссылка
	|ИЗ
	|	Справочник.Диагнозы КАК Диагнозы
	|ГДЕ
	|	(Диагнозы.Ссылка В (&ВыбранныеДиагнозы)
	|			ИЛИ Диагнозы.Родитель В (&ВыбранныеДиагнозы))
	|	И НЕ Диагнозы.ЭтоГруппа"
	;
	ДобавляемыеДиагнозы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");	
	
	Для Каждого Диагноз Из ДобавляемыеДиагнозы Цикл
		
		МенеджерЗаписи = РегистрыСведений.ГруппыДиагнозов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Специализация = Специализация;
		МенеджерЗаписи.ВидГруппировкиДиагнозов = ВидГруппировки;
		МенеджерЗаписи.Диагноз = Диагноз;
		МенеджерЗаписи.Записать(Истина);
			
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура ЗагрузкаКлассификатораЗавершена(Результат, ДополнительныеПараметры) Экспорт
	Элементы.Список.Обновить();
	РаботаСДиалогамиКлиент.СкрытьПоказатьАрхивные(Элементы.СписокСкрытьПоказатьАрхивные, Список, Элементы.СписокСкрытьПоказатьАрхивные.Пометка);
КонецПроцедуры

#КонецОбласти

