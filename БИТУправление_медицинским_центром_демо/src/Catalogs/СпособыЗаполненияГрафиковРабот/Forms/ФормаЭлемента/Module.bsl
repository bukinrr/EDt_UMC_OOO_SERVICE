#Область ОписаниеПеременных
&НаКлиенте
Перем ПредыдущееЗначение;

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если объект.Ссылка.Пустая() Тогда
		Объект.ТипГрафика = Перечисления.ТипыГрафика.Недельный;
		ПерезаполнитьТабличнуюЧастьПоТипуПеречисления();
	КонецЕсли;
	Элементы.ТипГрафика.Доступность = НЕ Объект.Предопределенный;
	Элементы.Смены.Доступность = НЕ Объект.Предопределенный;
	Элементы.ЧетностьНедели.Видимость = Не Объект.Предопределенный;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОпределитьВидимостьЭлементовИсходяИзТипаГрафика();
	ПредыдущееЗначение = Объект.ТипГрафика;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ТипГрафикаПриИзменении(Элемент)
	
	Если Объект.Смены.Количество() <> 0 Тогда
		Если Вопрос(НСтр("ru = 'Табличная часть будет очищена и перезаполнена, продолжить?'"), РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ПерезаполнитьТабличнуюЧастьПоТипуПеречисления();
	ОпределитьВидимостьЭлементовИсходяИзТипаГрафика();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипГрафикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПредыдущееЗначение = Объект.ТипГрафика;
КонецПроцедуры

&НаКлиенте
Процедура СменыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Объект.ТипГрафика <> ПредопределенноеЗначение("Перечисление.ТипыГрафика.Сменный") Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Добавление строк невозможно при данном типе графика'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СменыПередУдалением(Элемент, Отказ)
	Если Объект.ТипГрафика <> ПредопределенноеЗначение("Перечисление.ТипыГрафика.Сменный") Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Удаление строк невозможно при данном типе графика'"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ОпределитьВидимостьЭлементовИсходяИзТипаГрафика()
	Если Объект.ТипГрафика = ПредопределенноеЗначение("Перечисление.ТипыГрафика.Недельный") Тогда
		Элементы.СменыДеньНедели.Видимость = Истина;
		Элементы.СменыНомерДня.Видимость = Ложь;
		Элементы.ЧетностьНедели.Видимость = Ложь;
	ИначеЕсли Объект.ТипГрафика = ПредопределенноеЗначение("Перечисление.ТипыГрафика.Двухнедельный") Тогда
		Элементы.СменыДеньНедели.Видимость = Истина;
		Элементы.СменыНомерДня.Видимость = Истина;
		Элементы.СменыНомерДня.Заголовок = "Номер недели";
		Элементы.СменыНомерДня.Доступность = Ложь;
		Элементы.ЧетностьНедели.Видимость = Истина;		
	ИначеЕсли Объект.ТипГрафика = ПредопределенноеЗначение("Перечисление.ТипыГрафика.Сменный") Тогда
		Элементы.СменыДеньНедели.Видимость = Ложь;
		Элементы.СменыНомерДня.Видимость = Ложь;
		Элементы.ЧетностьНедели.Видимость = Ложь;
		Элементы.СменыНомерДня.Заголовок = "Номер дня";
		Элементы.СменыНомерДня.Доступность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьТабличнуюЧастьПоТипуПеречисления()
	Объект.Смены.Очистить();
	Если Объект.ТипГрафика = Перечисления.ТипыГрафика.Недельный Тогда
		Для каждого ДеньНедели Из Перечисления.ДниНедели Цикл
			НоваяСтрока = Объект.Смены.Добавить();
			НоваяСтрока.ДеньНедели = ДеньНедели;
		КонецЦикла;
	ИначеЕсли Объект.ТипГрафика = Перечисления.ТипыГрафика.Двухнедельный Тогда
		Для счетчик = 1 По 2 Цикл
			Для каждого ДеньНедели Из Перечисления.ДниНедели Цикл
				НоваяСтрока = Объект.Смены.Добавить();
				НоваяСтрока.ДеньНедели = ДеньНедели;
				НоваяСтрока.НомерДня = счетчик;
			КонецЦикла;	
		КонецЦикла;
	ИначеЕсли Объект.ТипГрафика = Перечисления.ТипыГрафика.Сменный Тогда
		
	КонецЕсли;					
КонецПроцедуры

#КонецОбласти