#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьОтборШаблоновОсмотра();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Запись набора регистра шаблонов осмотра специализации
	НаборЗаписей = РеквизитФормыВЗначение("ШаблоныОсмотра");
	Если ЗначениеЗаполнено(НаборЗаписей.Отбор.Специализация.Значение) Тогда
		ТабНЗ = НаборЗаписей.Выгрузить();
		Для Каждого СтрТаб Из ТабНЗ Цикл
			Если СтрТаб.Основной Тогда
				ОсновнойШаблон = СтрТаб.ШаблонОсмотра;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ТабНЗ.Свернуть("Специализация, ШаблонОсмотра");
		ТабНЗ.Колонки.Добавить("Основной", Новый ОписаниеТипов("Булево"));
		Если ОсновнойШаблон <> Неопределено Тогда
			СтрНЗ = ТабНЗ.Найти(ОсновнойШаблон,"ШаблонОсмотра");
			СтрНЗ.Основной = Истина;
		КонецЕсли;
		ТабНЗ.ЗаполнитьЗначения(Объект.Ссылка, "Специализация");
		НаборЗаписей.Загрузить(ТабНЗ);
		НаборЗаписей.Записать();
		ЗначениеВРеквизитФормы(НаборЗаписей,"ШаблоныОсмотра");
	Иначе
		НаборЗаписей.Отбор.Специализация.Установить(Объект.Ссылка);
		ЗначениеВРеквизитФормы(НаборЗаписей,"ШаблоныОсмотра");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборШаблонов(Команда)
	
	Если РаботаСДиалогамиКлиент.ЗаписатьНовыйОбъектВФорме(ЭтаФорма) Тогда
		ПараметрыФормы = Новый Структура("МножественныйВыбор, ЗакрыватьПриВыборе, ВыборГруппИЭлементов", Истина, Ложь, ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
		ОткрытьФорму("Справочник.ШаблоныHTML.ФормаВыбора", ПараметрыФормы, Элементы.ШаблоныОсмотра);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойШаблон(Команда)

	ТекущиеДанные = Элементы.ШаблоныОсмотра.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если Не ТекущиеДанные.Основной Тогда
			Для Каждого ЗаписьШаблон Из ШаблоныОсмотра Цикл
				ЗаписьШаблон.Основной = Ложь;
			КонецЦикла;
		КонецЕсли;
		ТекущиеДанные.Основной = Не ТекущиеДанные.Основной;
		Элементы.ШаблоныОсмотра.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ШаблоныОсмотраПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	РаботаСДиалогамиКлиент.ПередНачаломДобавленияВПодчиненныйСписокНаФормеОбъекта(ЭтаФорма, Элемент, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ШаблоныОсмотраОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыбранныеЭлементыСправочника = ПолучитьЭлементыПоВыбраннымШаблонам(ВыбранноеЗначение);
	
	Для Каждого Шаблон Из ВыбранныеЭлементыСправочника Цикл
		
		Если ШаблоныОсмотра.НайтиСтроки(Новый Структура("ШаблонОсмотра", Шаблон)).Количество() = 0 Тогда
			Запись = ШаблоныОсмотра.Добавить();
			Запись.Специализация = Объект.Ссылка;
			Запись.ШаблонОсмотра = Шаблон;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЭлементыПоВыбраннымШаблонам(МассивГруппИЭлементов, Результат = Неопределено)
	
	Если Результат = Неопределено Тогда
		Результат = Новый Массив;
	КонецЕсли;
	
	Для Каждого ШаблонСсылка Из МассивГруппИЭлементов Цикл
		Если ШаблонСсылка.ЭтоГруппа Тогда
			
			Выборка = Справочники.ШаблоныHTML.Выбрать(ШаблонСсылка);
			МассивПараметр = Новый Массив;
			Пока Выборка.Следующий() Цикл
				Если Выборка.ЭтоГруппа Тогда
					МассивПараметр.Добавить(Выборка.Ссылка);
				ИначеЕсли Результат.Найти(Выборка.Ссылка) = Неопределено Тогда
					Результат.Добавить(Выборка.Ссылка);
				КонецЕсли;
			КонецЦикла;
			
			Если МассивПараметр.Количество() <> 0 Тогда
				ПолучитьЭлементыПоВыбраннымШаблонам(МассивПараметр, Результат);
			КонецЕсли;
		ИначеЕсли Результат.Найти(ШаблонСсылка) = Неопределено Тогда
			Результат.Добавить(ШаблонСсылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборШаблоновОсмотра()
	
	НаборЗаписей = РеквизитФормыВЗначение("ШаблоныОсмотра");
	НаборЗаписей.Отбор.Специализация.Установить(Объект.Ссылка);
	НаборЗаписей.Прочитать();
	ЗначениеВРеквизитФормы(НаборЗаписей,"ШаблоныОсмотра");
	
КонецПроцедуры

#КонецОбласти


