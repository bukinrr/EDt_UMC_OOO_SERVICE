#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЭтоГруппа Тогда
	
		Если СоставПолисаОпределяетсяПрейскурантом
			Или ПроверятьНаличиеЦеныВПрейскуранте
		Тогда
			ПроверяемыеРеквизиты.Добавить("Прейскурант");
		КонецЕсли;
		
		// Проверка наложения дат договоров по одной организации (неоднозначный договор по организации на дату)
		КонфликтСтрока2 = 0;
		Для Каждого СтрокаДоговор Из Договоры Цикл
			Для Сч = СтрокаДоговор.НомерСтроки По Договоры.Количество() Цикл
				СтрокаДоговорПроверяемый = Договоры[Сч - 1];
				
				Если СтрокаДоговор.Договор <> СтрокаДоговорПроверяемый.Договор
					И СтрокаДоговор.Договор.Организация = СтрокаДоговорПроверяемый.Договор.Организация
					// Условия дат
					И СтрокаДоговор.Договор.ДатаНачала	  <= СтрокаДоговорПроверяемый.Договор.ДатаОкончания
					И СтрокаДоговор.Договор.ДатаОкончания >= СтрокаДоговорПроверяемый.Договор.ДатаНачала
				Тогда
					КонфликтСтрока2 = СтрокаДоговорПроверяемый.НомерСтроки;
				КонецЕсли;
			КонецЦикла;
			
			Если ЗначениеЗаполнено(КонфликтСтрока2) Тогда
				
				Отказ = Истина;
				
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "Строки списка договоров №%1 и №%2 конфликтуют периодами: для организации нельзя однозначно определить актуальный договор на даты пересечения периодов";
				Сообщение.Текст = СтрШаблон(Сообщение.Текст, СтрокаДоговор.НомерСтроки, КонфликтСтрока2);
				Сообщение.Поле = "Договоры";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если Не ЭтоГруппа Тогда
		
		// Необязательное действие по пересохранению первого актуального договора в устаревший реквизит шапки. (07.2023).
		мСоглашениеСтрахования = Неопределено;
		ТекДата = ТекущаяДатаСеанса();
		Для Каждого СтрокаДоговор Из Договоры Цикл
			
			Если ЗначениеЗаполнено(СтрокаДоговор.Договор.Организация)
				И (Не ЗначениеЗаполнено(СтрокаДоговор.Договор.ДатаНачала)	 Или СтрокаДоговор.Договор.ДатаНачала	 <= ТекДата) // Дата начала.
				И (Не ЗначениеЗаполнено(СтрокаДоговор.Договор.ДатаОкончания) Или СтрокаДоговор.Договор.ДатаОкончания >= ТекДата) // Дата окончания.
			Тогда
				мСоглашениеСтрахования = СтрокаДоговор.Договор;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(мСоглашениеСтрахования) Тогда
			СоглашениеСтрахования = мСоглашениеСтрахования;
			
		ИначеЕсли Не ЗначениеЗаполнено(СоглашениеСтрахования)
			И Договоры.Количество() <> 0
		Тогда
			СоглашениеСтрахования = Договоры[Договоры.Количество() - 1].Договор;
			
		ИначеЕсли Договоры.Количество() = 0 Тогда
			СоглашениеСтрахования = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
