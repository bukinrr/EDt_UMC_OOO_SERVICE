&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Значение = ДанныеФормыВЗначение(Объект, Тип("СправочникОбъект.ОбработкиSMSрассылки"));
	
	ХранилищеВнешнейОбработки = Неопределено;
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ХранилищеВнешнейОбработки = Параметры.ЗначениеКопирования.ХранилищеВнешнейОбработки;
		ВнешняяОбработка = ХранилищеВнешнейОбработки.Получить();
		ЭтаФорма.ПараметрыОбработки = Параметры.ЗначениеКопирования.ДополнительныеПараметры.Получить();
	Иначе
		ВнешняяОбработка = Значение.ХранилищеВнешнейОбработки.Получить();
		ЭлементОбъект = РеквизитФормыВЗначение("Объект");
		ЭтаФорма.ПараметрыОбработки = ЭлементОбъект.ДополнительныеПараметры.Получить();
	КонецЕсли; 

	ХранилищеСодержитДанные = ЗначениеЗаполнено(ВнешняяОбработка);
	
	Если НЕ ХранилищеСодержитДанные Тогда
		Объект.КомментарийКФайлуИсточнику = "Файл обработки не выбран";
	Иначе
		ВывестиСведенияОбОбработке();
	КонецЕсли;
	
КонецПроцедуры                                            

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ХранилищеСодержитДанные Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не выбран файл обработки рассылки!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ДанныеХранилища = ТекущийОбъект.ХранилищеВнешнейОбработки.Получить();
	Если Не ЗначениеЗаполнено(ДанныеХранилища) И ЗначениеЗаполнено(ХранилищеВнешнейОбработки) Тогда
		ТекущийОбъект.ХранилищеВнешнейОбработки = ХранилищеВнешнейОбработки;	
	КонецЕсли; 
	ТекущийОбъект.ДополнительныеПараметры = Новый ХранилищеЗначения(ЭтаФорма.ПараметрыОбработки);
КонецПроцедуры

&НаСервере
Процедура ВывестиСведенияОбОбработке(ПриЗаменеОбработки = Ложь)
	
	// Получение информации об обработке		
	Попытка
		
		СведенияОбОбработке = ПолучитьСведенияОВнешнейОбработкеСерверФорма();
		
		Версия = СведенияОбОбработке.Версия;
		СсылкаНаСайт = СведенияОбОбработке.СсылкаНаСайт;
		СсылкаНаСайтПредставление = СведенияОбОбработке.СсылкаНаСайтПредставление;
		Информация = СведенияОбОбработке.Информация;
		
		Если ПриЗаменеОбработки Тогда
			ПараметрыОбработки = СведенияОбОбработке.ДополнительныеПараметры;
			ДополнительныеПараметры = Новый ХранилищеЗначения(ПараметрыОбработки);
		КонецЕсли;
		
	Исключение
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке(СведенияОбОбработке);
		
	КонецПопытки;
		
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПараметрыОбработки(Команда)
	
	Если ЭтаФорма.ПараметрыОбработки = Неопределено Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'Обработка не предусматривает ввод дополнительных параметров.'"));
		
	Иначе
		мПараметрыОбработки = ПолучитьПараметрыОбработки();	
		
		ФормаПараметров = ПолучитьФорму("Справочник.ОбработкиSMSрассылки.Форма.ФормаПараметровОбработкиУпр", мПараметрыОбработки, ЭтаФорма);
		Результат = ФормаПараметров.ОткрытьМодально();
		
		Если Результат <> Неопределено Тогда
		
			ЭтаФорма.ПараметрыОбработки = Результат;
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Функция ПолучитьПараметрыОбработки()
	
	Возврат Новый Структура("Хранилище", Новый ХранилищеЗначения(ЭтаФорма.ПараметрыОбработки));
		
КонецФункции

&НаКлиенте
Функция ВыбратьФайл()
	
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогФайла.Фильтр = "Внешняя обработка(*.epf)|*.epf";
	
	ВнОбработка = Неопределено;
	
	Если ДиалогФайла.Выбрать() Тогда
		
		//ДДФайла = ДвоичныеДанныеФайла(ДиалогФайла.ПолноеИмяФайла);
		ДвоичныеДанные = Новый ДвоичныеДанные(ДиалогФайла.ПолноеИмяФайла);
		СообщениеОшибки = ВыбратьФайлСервер(ДвоичныеДанные);
		Если ЗначениеЗаполнено(СообщениеОшибки) Тогда
			ПолноеИмяФайлаОбработки = "";
			Возврат Неопределено;
		Иначе
			ПолноеИмяФайлаОбработки = ДиалогФайла.ПолноеИмяФайла;
		КонецЕсли; 	
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Файл = Новый Файл(ДиалогФайла.ПолноеИмяФайла);
	
	КомментарийКФайлу = Файл.Имя + Символы.ПС + "размер:" + Файл.Размер()+" байт; изменен:" + Файл.ПолучитьВремяИзменения() + "; сохранен в ИБ:" + ТекущаяДата();
	
	СтруктураВозврата = Новый Структура("КомментарийКФайлу,ПолноеИмяФайлаОбработки,ИмяФайлаОбработки,ВнешняяОбработка", КомментарийКФайлу, ПолноеИмяФайлаОбработки, Файл.Имя, ВнОбработка);
	
	Возврат СтруктураВозврата;
	
КонецФункции // ВыбратьФайл()

&НаСервере
Функция ВыбратьФайлСервер(ДвоичныеДанныеФайла)
	Попытка
		
		СообщениеОшибки = НСтр("ru ='Выбранный файл не является внешней обработкой.
		|Либо, данная обработка не предназначена для
		|запуска в этой конфигурации.'");
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("epf");	
		ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);
				
		ВнОбработка = ВнешниеОбработки.Создать(ИмяВременногоФайла, Ложь, ОбщегоНазначения.ОписаниеЗащитыБезПредупреждений());
		
		Попытка
			УдалитьФайлы(ИмяВременногоФайла);
		Исключение
		КонецПопытки;
		
		СообщениеОшибки = НСтр("ru ='Произошла ошибка при попытке получения сведений от обработки.
		|Возможно, данная обработка не предназначена для
		|запуска в этой конфигурации. 
		|С информацией о формате обработок SMS-рассылки можно ознакомиться в справке по данному объекту конфигуарции.'");
		
		СведенияОбОбработке = ВнОбработка.СведенияОВнешнейОбработке();
		
		Версия = СведенияОбОбработке.Версия;
		СсылкаНаСайт = СведенияОбОбработке.СсылкаНаСайт;
		СсылкаНаСайтПредставление = СведенияОбОбработке.СсылкаНаСайтПредставление;
		Информация = СведенияОбОбработке.Информация;
		
		Если СведенияОбОбработке.Свойство("Наименование")
			И ЗначениеЗаполнено(СведенияОбОбработке.Наименование)
			И Не ЗначениеЗаполнено(Объект.Наименование)
		Тогда
			Объект.Наименование = СведенияОбОбработке.Наименование;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
			Объект.Наименование = ВнОбработка.Метаданные().Синоним;
		КонецЕсли;
		
		ЭтаФорма.ПараметрыОбработки = СведенияОбОбработке.ДополнительныеПараметры;
		ДополнительныеПараметры = Новый ХранилищеЗначения(ПараметрыОбработки);
		Возврат "";				   
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат СообщениеОшибки;
	КонецПопытки;
	
КонецФункции	

&НаКлиенте
Процедура ЗаменитьФайл(Команда)
	
	ЗначениеВозврата = ВыбратьФайл();
	Если ЗначениеВозврата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаВнешнейОбработки = ЗначениеВозврата.ПолноеИмяФайлаОбработки;
	
	Попытка
		ЗаменитьФайлСервер(ИмяФайлаВнешнейОбработки);
		
		Объект.КомментарийКФайлуИсточнику = "Исходный файл: " + ЗначениеВозврата.КомментарийКФайлу;
		Элементы.КомментарийКФайлуИсточнику.ЦветТекста = ЦветТекстаФормы;
	Исключение
	КонецПопытки; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьФайлСервер(ФайлВнешнейОбработки)
	
	// Проверка содержимого хранилища
	об = ДанныеФормыВЗначение(Объект, Тип("СправочникОбъект.ОбработкиSMSрассылки"));
	
	Если ТипЗнч(ФайлВнешнейОбработки) = Тип("ХранилищеЗначения") Тогда
		об.ХранилищеВнешнейОбработки = ФайлВнешнейОбработки;
	Иначе
		об.ХранилищеВнешнейОбработки = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ФайлВнешнейОбработки));	
	КонецЕсли;
	
	ХранилищеСодержитДанные = Истина;
	
	об.Записать();
	
	ЗначениеВДанныеФормы(об,Объект);
	
КонецПроцедуры	

&НаКлиенте
Процедура СохранитьФайлНаДиск(Команда)
	
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогФайла.Фильтр = "Внешняя обработка(*.epf)|*.epf";
	
	Если НЕ ХранилищеСодержитДанные Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Внешний файл отсутствует в хранилище'")); 
		Возврат;
	КонецЕсли; 
	
	ДиалогФайла.ПолноеИмяФайла = ОбщегоНазначенияСервер.УдалитьЗапрещенныеСимволыИмени(Объект.Наименование);
	Если ДиалогФайла.Выбрать() Тогда
		ИмяФайла = ДиалогФайла.ПолноеИмяФайла;
		Попытка
			ПолучитьДанныеИзХранилищаВДДСервер(Объект).Записать(ИмяФайла);
		Исключение
			ПоказатьПредупреждение(,НСтр("ru = 'Внешний файл не сохранен
							|'") + ОписаниеОшибки()); 
		КонецПопытки; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеИзХранилищаВДДСервер(Знач Объект)
	Значение = ДанныеФормыВЗначение(Объект, Тип("СправочникОбъект.ОбработкиSMSрассылки"));
	Возврат Значение.ХранилищеВнешнейОбработки.Получить();
КонецФункции	

&НаКлиенте
Процедура УдалитьФайл(Команда)
	
	УдалитьФайлСервер();
	Объект.КомментарийКФайлуИсточнику = "Файл обработки не выбран";
	
КонецПроцедуры

&НаСервере
Процедура УдалитьФайлСервер()
	ХранилищеВнешнейОбработки = Новый ХранилищеЗначения(Неопределено);
	ХранилищеСодержитДанные = Ложь;
КонецПроцедуры	

&НаСервере
Функция ПолучитьСведенияОВнешнейОбработкеСерверФорма()
	Значение = ДанныеФормыВЗначение(Объект, Тип("СправочникОбъект.ОбработкиSMSрассылки"));
		
	Возврат Значение.ПолучитьСведенияОВнешнейОбработкеСервер();
КонецФункции	

&НаКлиенте
Процедура СсылкаНаСайтНажатие(Элемент, СтандартнаяОбработка)
	ЗапуститьПриложение(СсылкаНаСайт);
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьФайлТиповойStream(Команда)
	ЗаменитьФайлТиповойСервер("StreamSMS");
КонецПроцедуры

&НаСервере
Процедура ЗаменитьФайлТиповойСервер(ИмяВстроеннойОбработки)
	
	ДанныеОбработки = Справочники.ОбработкиSMSрассылки.ПолучитьДанныеВстроеннойОбработкиРассылки(ИмяВстроеннойОбработки);	
	Объект.КомментарийКФайлуИсточнику = ДанныеОбработки.КомментарийКФайлуИсточнику;
	ЗаменитьФайлСервер(ДанныеОбработки.ХранилищеВнешнейОбработки);
	
	ВывестиСведенияОбОбработке(Истина);

КонецПроцедуры

&НаКлиенте
Процедура СсылкаПереходВЛичныйКабинетНажатие(Элемент)
	
	ИсполняемыйТекст = ПолучитьИсполняемыйТекст();
	Если ЗначениеЗаполнено(ИсполняемыйТекст) Тогда
		Попытка
			Выполнить(ИсполняемыйТекст);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru ='При выполнении обработкой рассылки операции перехода в личный кабинет произошла ошибка: 
						|'") + ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьИсполняемыйТекст()
	
	ИсполняемыйТекст = "";
	
	Значение = ДанныеФормыВЗначение(Объект, Тип("СправочникОбъект.ОбработкиSMSрассылки"));
	
	ДанныеХранилища = Значение.ХранилищеВнешнейОбработки.Получить();
	Если ДанныеХранилища = Неопределено И ЗначениеЗаполнено(ХранилищеВнешнейОбработки) Тогда
		ДанныеХранилища = ХранилищеВнешнейОбработки.Получить();
	КонецЕсли; 
	
	Если ДанныеХранилища <> Неопределено Тогда
		ИмяФайла = ПолучитьИмяВременногоФайла("epf");
		ДанныеХранилища.Записать(ИмяФайла);
		ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяФайла, Ложь, ОбщегоНазначения.ОписаниеЗащитыБезПредупреждений());
			
		Попытка
			ИсполняемыйТекст = ВнешняяОбработка.ПолучитьТекстПереходВЛичныйКабинет(Значение.Пользователь, Значение.Пароль);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru ='Сохраненная обработка рассылки не поддерживает возможность перехода в личный кабинет.'"));
		КонецПопытки;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru ='Не загружена обработка рассылки. Переход в личный кабинет невозможен.'"));
	КонецЕсли;
		
	Возврат ИсполняемыйТекст;
	
КонецФункции
