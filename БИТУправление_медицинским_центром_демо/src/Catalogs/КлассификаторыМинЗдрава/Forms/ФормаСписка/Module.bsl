#Область ОбработчикиСобытий

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Перем ИдентификаторПользовательскойНастройки;
	
	Если ЗначениеЗаполнено(Параметры.ВидКлассификатора) Тогда
		Попытка
			ЭлементОтбора = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[0];
			ЭлементОтбора.ПравоеЗначение = Параметры.ВидКлассификатора;
			ЭлементОтбора.Использование = Истина;
		Исключение
			РаботаСФормамиКлиент.УстановитьОтборСписка("ВидКлассификатора", Параметры.ВидКлассификатора, Список);
		КонецПопытки;
	КонецЕсли;
	
	РаботаСДиалогамиКлиент.СкрытьПоказатьАрхивные(Элементы.СписокСкрытьПоказатьАрхивные, Список, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗагрузкаНСИМинздрава" Тогда
		Элементы.Список.Обновить();
		Сообщить("Загрузка/обновление данных классификатора прошло успешно!");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСправочник(Команда)
	
	ТекущийВидКлассификатора = ТекущийОтборВидКлассификатора(Список.КомпоновщикНастроек);
	Если ЗначениеЗаполнено(ТекущийВидКлассификатора) Тогда
		
		СтруктураПараметры = Новый Структура("НаименованиеСправочника,	 ВидКлассификатора",
											 "КлассификаторыМинЗдрава",	 ТекущийВидКлассификатора);
		
		Оповещение = Новый ОписаниеОповещения("ЗагрузкаКлассификатораЗавершена",ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.ЗагрузкаНСИЕГИСЗ", СтруктураПараметры, ЭтотОбъект, ТекущийВидКлассификатора,,,Оповещение);
		
	Иначе
		ОткрытьФорму("Справочник.КлассификаторыМинЗдрава.Форма.ЗагрузкаСправочника");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьВАрхив(Команда)

	РаботаСДиалогамиКлиент.ПоместитьВАрхив(Элементы.Список.ВыделенныеСтроки);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПоказатьАрхивные(Команда)

	РаботаСДиалогамиКлиент.СкрытьПоказатьАрхивные(Элементы.СписокСкрытьПоказатьАрхивные, Список);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ТекущийВидКлассификатора = ТекущийОтборВидКлассификатора(Настройки.Отбор);
	
	Если ЗначениеЗаполнено(ТекущийВидКлассификатора) Тогда
		Для Каждого СтрокаСписка Из Строки Цикл
			Если СтрокаСписка.Значение.Данные.ВидКлассификатора <> ТекущийВидКлассификатора Тогда
				Для Каждого ОформлениеЯчейки Из СтрокаСписка.Значение.Оформление Цикл
					ОформлениеЯчейки.Значение.УстановитьЗначениеПараметра("Видимость", Ложь);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ТекущийОтборВидКлассификатора(Знач КомпоновщикНастроек)
	
	Если ТипЗнч(КомпоновщикНастроек) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ОтборКомпоновкиСписка  = КомпоновщикНастроек.ПолучитьНастройки().Отбор.Элементы;
	Иначе
		ОтборКомпоновкиСписка = КомпоновщикНастроек.Элементы;	
	КонецЕсли; 
	
	Для Каждого ЭлементОтбора Из ОтборКомпоновкиСписка  Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
			И ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидКлассификатора")
			И ЭлементОтбора.Использование
			И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
		Тогда
			Возврат ЭлементОтбора.ПравоеЗначение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузкаКлассификатораЗавершена(Результат, ДополнительныеПараметры) Экспорт
	Элементы.Список.Обновить();
	РаботаСДиалогамиКлиент.СкрытьПоказатьАрхивные(Элементы.СписокСкрытьПоказатьАрхивные, Список, Элементы.СписокСкрытьПоказатьАрхивные.Пометка);
КонецПроцедуры

#КонецОбласти
