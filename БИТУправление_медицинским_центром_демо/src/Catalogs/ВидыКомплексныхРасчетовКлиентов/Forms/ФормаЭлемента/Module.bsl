#Область ОписаниеПеременных
&НаКлиенте 
Перем мКэшВидовНоменклатуры;

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчетнаяПолитика = УправлениеНастройками.ПолучитьУчетнуюПолитику();
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Если Объект.Прейскурант.Пустая() Тогда
			Объект.Прейскурант = УчетнаяПолитика.ОсновнойПрейскурант;
		КонецЕсли;
		
	КонецЕсли;
	
	ЕстьХарактеристикиВСоставе = Ложь;
	Для Каждого СтрокаСостава Из Объект.Состав Цикл
		Если ЗначениеЗаполнено(СтрокаСостава.ХарактеристикаНоменклатуры) Тогда
			ЕстьХарактеристикиВСоставе = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Элементы.СоставХарактеристикаНоменклатуры.Видимость = УчетнаяПолитика.ВестиУчетПоХарактеристикам Или ЕстьХарактеристикиВСоставе;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Состав.Количество() > 0 Тогда
		НоменклатураСостава = Новый Массив;
		Для Каждого СтрокаСостава Из Объект.Состав Цикл
			НоменклатураСостава.Добавить(СтрокаСостава.Номенклатура);
		КонецЦикла;
		мКэшВидовНоменклатуры = ОбщегоНазначения.ПолучитьСоответствиеВидовНоменклатуры(НоменклатураСостава);
	Иначе
		мКэшВидовНоменклатуры = Новый Соответствие; 
	КонецЕсли;
	
	НастроитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СоставПриАктивизацииЯчейки(Элемент)
	
	Если Элементы.Состав.ТекущийЭлемент = Элементы.СоставЕдиницаИзмерения Тогда
		ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Элементы.СоставЕдиницаИзмерения.ТолькоПросмотр = 
				мКэшВидовНоменклатуры.Получить(ТекущиеДанные.Номенклатура) <> ПредопределенноеЗначение("Перечисление.ВидыНоменклатуры.Материал");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		Элемент.ТекущиеДанные.Количество = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		ТекущиеДанные.ЕдиницаИзмерения = Ценообразование.ПолучитьЕдиницуПродажиНоменклатуры(ТекущиеДанные.Номенклатура);
	КонецЕсли;
	
	ВидНоменклатуры = мКэшВидовНоменклатуры.Получить(ТекущиеДанные.Номенклатура);
	Если ВидНоменклатуры = Неопределено Тогда
		ВидНоменклатуры = ?(ЗначениеЗаполнено(ТекущиеДанные.ЕдиницаИзмерения), 
							ПредопределенноеЗначение("Перечисление.ВидыНоменклатуры.Материал"), 
							ПредопределенноеЗначение("Перечисление.ВидыНоменклатуры.Услуга"));
							
		мКэшВидовНоменклатуры.Вставить(ТекущиеДанные.Номенклатура, ВидНоменклатуры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	НастроитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВидимостьДоступность()
	
	ЭтоПредварительныйРасчет = Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыКомплексныхРасчетовКлиентов.ПредварительныйРасчет");
	ЭтоПроизводственныйРасчет = Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыКомплексныхРасчетовКлиентов.ПроизводственныйРасчет");
	
	Элементы.ОбособленныйУчетВзаиморасчетов.Видимость = Не ЭтоПредварительныйРасчет;
	Элементы.ГруппаНастройкиОграниченийРасчета.Видимость = Не ЭтоПредварительныйРасчет;
	Элементы.СоставИгнорироватьДолг.Видимость = Не ЭтоПредварительныйРасчет;
	Элементы.ПроцентАванса.Видимость = (Объект.ОбособленныйУчетВзаиморасчетов И ЭтоПроизводственныйРасчет);
	Элементы.ДопустимыйДолг.Видимость = Не Объект.ИгнорироватьДолг;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбособленныйУчетВзаиморасчетовПриИзменении(Элемент)
	
	НастроитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИгнорироватьДолгПриИзменении(Элемент)
	
	НастроитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти