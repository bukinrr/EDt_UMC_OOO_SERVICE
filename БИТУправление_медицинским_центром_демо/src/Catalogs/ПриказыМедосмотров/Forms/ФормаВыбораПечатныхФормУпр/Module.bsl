#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// УСЛОВНОЕ ОФОРМЛЕНИЕ ДЕРЕВА ДЛЯ ВЫДЕЛЕНИЯ ЖИРНЫМ
	ЭлементОУ = УсловноеОформление.Элементы.Добавить();
	ЭлементОУ.Использование    = Истина;
	ЭлементОУ.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,Истина));
	
	ЭлементУсловия                = ЭлементОУ.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементУсловия.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДеревоМакетовПечати.Пометка");
	ЭлементУсловия.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементУсловия.ПравоеЗначение = Истина;
	ЭлементУсловия.Использование  = Истина;
	
	ОформляемоеПоле      = ЭлементОУ.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ДеревоМакетовПечатиТекст");
	Если Параметры.Свойство("ДокументСсылка") Тогда
		ДокументСсылка = Параметры.ДокументСсылка;
	КонецЕсли;
	Если Параметры.Свойство("ТолькоПечать") Тогда
		ТолькоПечать = Параметры.ТолькоПечать;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Получить объект метаданных (переданный форме)
	Объект = КлючУникальности;
	
	// Установить заголовок
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		ЭтаФорма.Заголовок = "Печать: " + ДокументСсылка;
	КонецЕсли;
	
	Если Не ТолькоПечать Тогда 
		НаПринтер = Ложь;
		НаПринтер = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(Пользователи.ТекущийПользователь(),
		"ПечатьДокументовБезПредварительногоПросмотра") = Истина;
	Иначе 
		НаПринтер = Истина;
		Элементы.НаПринтер.ТолькоПросмотр = Истина;
	КонецЕсли;
		
	РедактированиеПакета = Ложь;
	
	// Получить внутренние печатные формы (имена макетов объектов).
	НастроитьДеревоМакетовПечатиСервер(ДокументСсылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Процедура - обработчик события "ПечатиПриАктивизацииСтроки" элемента формы ДеревоМакетовПечати.
// Устанавливает текст подсказки.
//
&НаКлиенте
Процедура ДеревоМакетовПечатиПриАктивизацииСтроки(Элемент)
	
	Подсказка = ?(Элемент.ТекущиеДанные = Неопределено, "", Элемент.ТекущиеДанные.Подсказка);
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		НаПринтер = Элемент.ТекущиеДанные.НаПринтер;
		Элементы.НаПринтер.ТолькоПросмотр = ТипЗнч(Элемент.ТекущиеДанные.СсылкаНаВнешнююОбработку) = Тип("СправочникСсылка.ПакетыПечати");
	КонецЕсли; 
	
КонецПроцедуры // ДеревоМакетовПечатиПриАктивизацииСтроки()

// Процедура - обработчик события "Выбор" элемента формы ДеревоМакетовПечати.
// Закрывает форму. Возвращает значение Истина. Признак выбора макета для печати.
//
&НаКлиенте
Процедура ДеревоМакетовПечатиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Результат = Новый Структура;

	Результат.Вставить("РедактированиеПакета", РедактированиеПакета);
	Результат.Вставить("ДеревоМакетовПечати", Элементы.ДеревоМакетовПечати);
	Результат.Вставить("НаПринтер", НаПринтер);
	
	Закрыть(Результат);

	
КонецПроцедуры // ДеревоМакетовПечатиВыбор()

// Процедура - обработчик события "Нажатие" элемента формы Печать.
// Закрывает форму. Возвращает значение Истина. Признак выбора макета для печати.
//
&НаКлиенте
Процедура ПечатьНажатие(Элемент)
	
	Результат = Новый Структура;

	Результат.Вставить("РедактированиеПакета", РедактированиеПакета);
	Результат.Вставить("ДеревоМакетовПечати", Элементы.ДеревоМакетовПечати);
	Результат.Вставить("НаПринтер", НаПринтер);
	
	Закрыть(Результат);
	
КонецПроцедуры // ПечатьНажатие()

// Процедура - обработчик события "Нажатие" элемента формы ПоУмолчанию.
// Устанавливает макет печати по умолчанию.
//
&НаКлиенте
Процедура ПоУмолчаниюНажатие(Элемент)
	
	// Устанавливает пометку текущей строке дерева макетов печати.
	// Снимает пометку у остальных строк.
	ТекущиеДанные = Элементы.ДеревоМакетовПечати.ТекущиеДанные;
	ТекущиеДанныеСтр= новый Структура;
	ТекущиеДанныеСтр.Вставить("Действие", ТекущиеДанные.Действие);
	ТекущиеДанныеСтр.Вставить("ИзменяетДанные", ТекущиеДанные.ИзменяетДанные);
	ТекущиеДанныеСтр.Вставить("Имя", ТекущиеДанные.Имя);
	ТекущиеДанныеСтр.Вставить("Подсказка", ТекущиеДанные.Подсказка);
	ТекущиеДанныеСтр.Вставить("Пометка", ТекущиеДанные.Пометка);
	ТекущиеДанныеСтр.Вставить("Расшифровка", ТекущиеДанные.Расшифровка);
	ТекущиеДанныеСтр.Вставить("Текст", ТекущиеДанные.Текст);
	
	ПоУмолчаниюНажатиеСервер(ТекущиеДанныеСтр, ДокументСсылка);
	
	Если ТекущиеДанные <> Неопределено
		И ТипЗнч(ВладелецФормы) = Тип("ФормаКлиентскогоПриложения")
	Тогда
		КнопкаПечатьПоУмолчанию = ВладелецФормы.Элементы.Найти("ФормаОбщаяКомандаПечатьПоУмолчанию");
		Если ТипЗнч(КнопкаПечатьПоУмолчанию) = Тип("КнопкаФормы") Тогда
			ПечатьДокументовКлиент.ПрисвоитьЗаголовокПечатнойФормыКнопкеФормы(КнопкаПечатьПоУмолчанию, ТекущиеДанные.Текст)
		КонецЕсли;
	КонецЕсли;
	
	глКэшПечатныхФорм = ПечатьДокументовСервер.ЗаполнитьКэшПечатныхФорм();

КонецПроцедуры // ПоУмолчаниюНажатие()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоУмолчаниюНажатиеСервер(ТекущиеДанныеСтр, СсылкаДок)
	
	Дерево = ДанныеФормыВЗначение(ДеревоМакетовПечати, Тип("ДеревоЗначений"));
	
	Для каждого Строка Из Дерево.Строки Цикл
		Если СтрокиРавны(Строка, ТекущиеДанныеСтр) Тогда
			Строка.Пометка = Истина;
		ИначеЕсли Строка.Пометка Тогда
			Строка.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	// Запоминает макет печать по умолчанию для текущего объекта метаданных.
 	ИмяМетаданных = СсылкаДок.Метаданные().Имя;
	Если ТекущиеДанныеСтр = Неопределено Тогда
		ХранилищеПользовательскихНастроекОтчетов.Сохранить(ИмяМетаданных,"ПечатнаяФорма", Ложь, , Пользователи.ТекущийПользователь());
	Иначе
		ХранилищеПользовательскихНастроекОтчетов.Сохранить(ИмяМетаданных,"ПечатнаяФорма",ТекущиеДанныеСтр.Текст);
	КонецЕсли;
	
	ЗначениеВДанныеФормы(Дерево, ДеревоМакетовПечати);
	
КонецПроцедуры	

&НаСервере
Функция СтрокиРавны(Строка, Структура)
	Возврат Строка.Действие = Структура.Действие
	И Строка.ИзменяетДанные = Структура.ИзменяетДанные
	И Строка.Имя = Структура.Имя
	И Строка.Подсказка = Структура.Подсказка
	И Строка.Пометка = Структура.Пометка
	И Строка.Расшифровка = Структура.Расшифровка
	И Строка.Текст = Структура.Текст;
КонецФункции	

&НаСервере
Процедура НастроитьДеревоМакетовПечатиСервер(Ссылка)
	Попытка
		ТипОбъекта = "Документ";
		Если Метаданные.Справочники.Содержит(Ссылка.Метаданные()) Тогда
			ТипОбъекта = "Справочник";
		КонецЕсли;
		СтруктураДинамическиВыбранныхФорм = ПолучитьСтруктуруПечатныхФормСервер(Ссылка.Метаданные().Имя, ТипОбъекта, Ссылка);
	Исключение
		СтруктураДинамическиВыбранныхФорм = Новый Структура;
	КонецПопытки;
	
	Дерево = ДанныеФормыВЗначение(ДеревоМакетовПечати, Тип("ДеревоЗначений"));	
	
	РазделенныеДанные = РазделитьВнешниеИВнутренниеПечатныеФормы(СтруктураДинамическиВыбранныхФорм);
	СтруктураВнутреннихПечатныхФорм = РазделенныеДанные.СтруктураВнутреннихПечатныхФорм;
	
	Дерево1 = УниверсальныеМеханизмыСервер.ПолучитьДеревоМакетовПечати(Ссылка, СтруктураВнутреннихПечатныхФорм,,,Ложь);
	
	ДополнитьДеревоВнешнимиПечатнымиФормамиПоУсловию(Дерево1, РазделенныеДанные.МассивВнешнихПечатныхФорм);
	
	// Установить макеты печати по умолчанию
	ВыделенныеСтроки = Элементы.ДеревоМакетовПечати.ВыделенныеСтроки;
	ВыделенныеСтроки.Очистить();
	
	Подсказка = "";
	
	Для каждого ТекущаяСтрока Из Дерево1.Строки Цикл
		
		НовСтр = Дерево.Строки.Добавить();
		НовСтр.Текст = ТекущаяСтрока.Текст;
		НовСтр.Действие = ТекущаяСтрока.Действие;
		НовСтр.Имя = ТекущаяСтрока.Имя;
		НовСтр.ИзменяетДанные = ТекущаяСтрока.ИзменяетДанные;
		НовСтр.Пометка = Ложь;
		НовСтр.Расшифровка = ТекущаяСтрока.Расшифровка;
		НовСтр.Подсказка = ТекущаяСтрока.Подсказка;
		НовСтр.СсылкаНаВнешнююОбработку = ТекущаяСтрока.СсылкаНаВнешнююОбработку;
		НовСтр.НомерСтроки = ТекущаяСтрока.НомерСтроки;
		НовСтр.ВидПечатнойФормы = ТекущаяСтрока.ВидПечатнойФормы;
		НовСтр.ЗаменяемаяПечатнаяФорма = ТекущаяСтрока.ЗаменяемаяПечатнаяФорма;
		НовСтр.НаПринтер = ТипЗнч(ТекущаяСтрока.СсылкаНаВнешнююОбработку) = Тип("СправочникСсылка.ПакетыПечати")
							И ТекущаяСтрока.СсылкаНаВнешнююОбработку.НаПринтер;		
	КонецЦикла;
	
	// -- определенение формы по умолчанию, выбранной ранее для документа.
	ИмяФормыПоУмолчанию = ХранилищеПользовательскихНастроекОтчетов.Загрузить(ДокументСсылка.Метаданные().Имя, "ПечатнаяФорма");
	СтрокаПоУмолчанию = Дерево.Строки.Найти(ИмяФормыПоУмолчанию, "Текст");
	
	Если СтрокаПоУмолчанию <> Неопределено Тогда
		СтрокаПоУмолчанию.Пометка = Истина;
	КонецЕсли;	
	// ----------------------------------------------------------------------.
	
	
	ЗначениеВДанныеФормы(Дерево, ДеревоМакетовПечати);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ПолучитьСтруктуруПечатныхФормСервер(ИмяОбъекта, ТипОбъекта = "Документ", Ссылка) Экспорт
	
	Попытка
		Если ТипОбъекта = "Документ" Тогда
			Если ТипЗнч(ИмяОбъекта) = Тип("Строка") Тогда
				СтруктураПечатныхФорм = Документы[ИмяОбъекта].ПолучитьСтруктуруДинамическихПечатныхФорм(Ссылка);
			Иначе
				СтруктураПечатныхФорм = Документы[ИмяОбъекта.Метаданные().Имя].ПолучитьСтруктуруДинамическихПечатныхФорм(Ссылка);
			КонецЕсли;
		ИначеЕсли ТипОбъекта = "Справочник" Тогда
			СтруктураПечатныхФорм = Справочники[ИмяОбъекта].ПолучитьСтруктуруДинамическихПечатныхФорм(Ссылка);
		КонецЕсли;
	Исключение
		СтруктураПечатныхФорм = Новый Структура;
	КонецПопытки;
	
	Возврат СтруктураПечатныхФорм;
	
КонецФункции 

&НаСервере
Процедура ДобавитьВДеревоМакетовДополнительныеФормы(ДеревоМакетов, Ссылка, Действие, ДобавлятьВДеревоРазделительИПечатьПоУмолчанию = Истина, ПредставлениеПечатнойФормы = Неопределено)
	
	СтрокиДерева = ДеревоМакетов.Строки;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВнешниеОбработки.Ссылка КАК Ссылка,
	|	ВнешниеОбработки.Наименование КАК Наименование,
	|	ВнешниеОбработки.ВидОбработки КАК ВидОбработки,
	|	ВнешниеОбработки.Наименование КАК ПредставлениеКнопки
	|ИЗ
	|	Справочник.ВнешниеОбработки КАК ВнешниеОбработки
	|ГДЕ
	|	ВнешниеОбработки.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ПредставлениеКнопки) Тогда
			НаименованиеПечатнойФормы = Выборка.ПредставлениеКнопки;
		КонецЕсли;
		
		Подсказка = "Подключаемая печатная форма";
		
		ПолноеНаименованиеПечатнойФормы = НаименованиеПечатнойФормы;
		СуществующаяСтрока = СтрокиДерева.Найти(ПолноеНаименованиеПечатнойФормы, "Текст");
		Счетчик = 1;
		Пока Не СуществующаяСтрока = Неопределено Цикл
			Счетчик = Счетчик + 1;
			ПолноеНаименованиеПечатнойФормы = НаименованиеПечатнойФормы + "(" + Счетчик + ")";
			ИндексСтрокиДереваЗаменяемойПечатнойФормы = Неопределено;
			СуществующаяСтрока = СтрокиДерева.Найти(ПолноеНаименованиеПечатнойФормы, "Текст");
		КонецЦикла; 
		
		СтруктураРасшифровки = Новый Структура("ВидПечатнойФормы, СсылкаНаВнешнююОбработку, ЗаменяемаяПечатнаяФорма, НомерСтроки", Выборка.ВидОбработки,Выборка.Ссылка, Неопределено, Неопределено);
		
		ДобавитьСтрокуВДеревоКнопок(ДеревоМакетов, Строка(Новый УникальныйИдентификатор), Тип("КнопкаФормы"), ПолноеНаименованиеПечатнойФормы, Действие, СтруктураРасшифровки, , Подсказка, , );
		
		Если Не ПредставлениеПечатнойФормы = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ДобавитьВДеревоМакетовДополнительныеФормы()

&НаСервере
Функция ДобавитьСтрокуВДеревоКнопок(СтрокаПодменю, Имя, Знач ТипКнопки = Неопределено ,Текст = "", Действие = Неопределено, Расшифровка = Неопределено, Пометка = Ложь, Подсказка = "", Картинка = Неопределено, Позиция = Неопределено, ИзменяетДанные = Ложь)
	
	Если ТипКнопки = Неопределено Тогда
		
		ТипКнопки = Тип("КнопкаФормы");
		
	КонецЕсли;
	Если Позиция = Неопределено Тогда
		СтрокаКнопки = СтрокаПодменю.Строки.Добавить();
	Иначе
		СтрокаКнопки = СтрокаПодменю.Строки.Вставить(Позиция);
	КонецЕсли;
	
	СтрокаКнопки.Имя                = Имя;
	СтрокаКнопки.ТипКнопки          = ТипКнопки;
	СтрокаКнопки.Текст              = Текст;
	СтрокаКнопки.Действие           = Действие;
	СтрокаКнопки.Картинка           = Картинка;
	СтрокаКнопки.Подсказка          = Подсказка;
	СтрокаКнопки.Пометка            = Пометка;
	СтрокаКнопки.ИзменяетДанные     = ИзменяетДанные;
	
	СтрокаКнопки.Расшифровка = Расшифровка;
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		СтрокаКнопки.Расшифровка = "Структура";
		СтрокаКнопки.СсылкаНаВнешнююОбработку = Расшифровка.СсылкаНаВнешнююОбработку;
		СтрокаКнопки.НомерСтроки = Расшифровка.НомерСтроки;
		СтрокаКнопки.ВидПечатнойФормы = Расшифровка.ВидПечатнойФормы;
		СтрокаКнопки.ЗаменяемаяПечатнаяФорма = Расшифровка.ЗаменяемаяПечатнаяФорма;
	КонецЕсли;
	
	Возврат СтрокаКнопки;
	
КонецФункции

&НаСервере
// Структура: Имя:Имя СсылкаНаВнешнюю:Ссылка
Функция РазделитьВнешниеИВнутренниеПечатныеФормы(СтруктураДанных)
	 СтруктураВнутреннихПечатныхФорм = Новый Структура;
	 МассивВнешнихПечатныхФорм = Новый Массив;
	 Для Каждого Эл Из СтруктураДанных Цикл
		Если ЗначениеЗаполнено(Эл.Значение.СсылкаНаВнешнюю) Тогда
			МассивВнешнихПечатныхФорм.Добавить(Эл.Значение.СсылкаНаВнешнюю);
		Иначе
			СтруктураВнутреннихПечатныхФорм.Вставить(Эл.Ключ, Эл.Значение.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("СтруктураВнутреннихПечатныхФорм, МассивВнешнихПечатныхФорм", СтруктураВнутреннихПечатныхФорм, МассивВнешнихПечатныхФорм);
	
КонецФункции

&НаСервере
Процедура ДополнитьДеревоВнешнимиПечатнымиФормамиПоУсловию(Дерево, МассивВнешнихПечатныхФорм)
		
	Для Каждого Эл Из МассивВнешнихПечатныхФорм Цикл
		НайденныеСтроки = Дерево.Строки.НайтиСтроки(Новый Структура("СсылкаНаВнешнююОбработку", Эл),Истина);
		Если НайденныеСтроки.Количество() = 0 Тогда
			ДобавитьВДеревоМакетовДополнительныеФормы(Дерево, Эл, Неопределено);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

