#Область ПрограммныйИнтерфейс

// Функция устанавливает новое наименование характеристики по значениям свойств.
//
// Параметры:
//  КоллекцияЗначенийСвойств - СписокЗначений, Структура - коллекция значений, имеющая свойство Значение.
//
// Возвращаемое значение:
//  Строка - сформированное наименование.
//
Функция СформироватьНаименование(КоллекцияЗначенийСвойств) Экспорт
	Строка = "";
	Для каждого ЭлементКоллекции Из КоллекцияЗначенийСвойств Цикл
		Если ЗначениеЗаполнено(ЭлементКоллекции.Значение) Тогда
			Строка = Строка + ЭлементКоллекции.Значение + ", ";
		КонецЕсли;
	КонецЦикла;
	Строка = Лев(Строка, СтрДлина(Строка) - 2);
	Если ПустаяСтрока(Строка) Тогда
		Строка = "<Свойства не назначены>";
	КонецЕсли;
	Возврат Строка;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ЭтоНовый() 
		И НЕ ОбменДанными.Загрузка
	   	И НЕ Владелец.ВестиУчетПоХарактеристикам 
	Тогда
		ШаблонТекста = "Для номенклатуры %1 не ведется учет по характеристикам.
		|Характеристика %2 не может быть записана.";
		
		Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Строка(Владелец), Наименование);

		ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке(Сообщение, Отказ);
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
