#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Номенклатура") Тогда 
		Номенклатура = Параметры.Номенклатура;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда 
		Если Параметры.Свойство("Отбор") Тогда 
			Если ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда 
				Номенклатура = Параметры.Отбор.Владелец;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если Не ЗначениеЗаполнено(Номенклатура) Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Не указана номенклатура. Выбор характеристики не возможен!'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСФормамиКлиент.УстановитьОтборСписка("Владелец", Номенклатура, Список); 
	РаботаСФормамиКлиент.УстановитьОтборСписка("Архив", Ложь, Список);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	СтруктураПараметров = Новый Структура("Структура");
	СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", Элементы.Список.ТекущиеДанные.Ссылка);
	СтруктураПараметров.Вставить("ЗапрашиватьСериюПослеВыбораХарактеристики", Истина);
	
	Закрыть(СтруктураПараметров);	

КонецПроцедуры

&НаКлиенте
Процедура СкрытьПоказатьАрхивные(Команда)
	
	ЭлементФормы = Элементы.СписокКоманднаяПанель.ПодчиненныеЭлементы.СписокСкрытьПоказатьАрхивные;
	ЭлементФормы.Пометка = Не ЭлементФормы.Пометка;
	
	Если ЭлементФормы.Пометка Тогда
		РаботаСФормамиКлиент.СнятьОтборСписка("Архив", Список);
	Иначе 
		РаботаСФормамиКлиент.УстановитьОтборСписка("Архив", Ложь, Список);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Выбрать(Неопределено);
	
КонецПроцедуры

#КонецОбласти

