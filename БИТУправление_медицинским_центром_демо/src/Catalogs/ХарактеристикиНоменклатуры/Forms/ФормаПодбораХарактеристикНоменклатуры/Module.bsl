
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗапрашиватьКоличество = Ложь;
	ЗапрашиватьЦену = Ложь;
	
	Если Параметры.Свойство("Номенклатура") Тогда 
		Номенклатура = Параметры.Номенклатура;	
	КонецЕсли;	
	
	Если Параметры.Свойство("Склад") Тогда 
		Склад = Параметры.Склад;
	Иначе
		ТребуетсяАнализВладельцаФормыПриОткрытии = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("ЕдиницаИзмерения") Тогда 
		ЕдиницаИзмерения = ?(ЗначениеЗаполнено(Параметры.ЕдиницаИзмерения), Параметры.ЕдиницаИзмерения, Номенклатура.ЕдиницаХраненияОстатков);
	КонецЕсли;
	
	Если Параметры.Свойство("Количество") Тогда 
		Количество = Параметры.Количество;	
	КонецЕсли;
	
	Если Параметры.Свойство("ЭтоПодбор") Тогда 
		ЭтоПодбор = Параметры.ЭтоПодбор;	
	КонецЕсли;
	
	Если Параметры.Свойство("Действие") Тогда 
		Действие = Параметры.Действие;	
	КонецЕсли;
	
	Если Параметры.Свойство("ЗапрашиватьКоличество") Тогда 
		ЗапрашиватьКоличество = Параметры.ЗапрашиватьКоличество;	
	КонецЕсли;
	
	Если Параметры.Свойство("ЗапрашиватьЦену") Тогда 
		ЗапрашиватьЦену = Параметры.ЗапрашиватьЦену;	
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоВНаличии") Тогда 
		ТолькоВНаличии = Параметры.ТолькоВНаличии;
	Иначе
		ТолькоВНаличии = Истина;
		ТребуетсяАнализВладельцаФормыПриОткрытии = Истина;
	КонецЕсли;
		
	Элементы.Количество.Доступность = ЗапрашиватьКоличество;
	Элементы.ГруппаЦена.Видимость = ЗапрашиватьЦену;
	Элементы.ГруппаПодвал.Видимость = ЗапрашиватьКоличество Или ЗапрашиватьЦену;	
		
	Если ЗначениеЗаполнено(Номенклатура) Тогда 
		Если Не ТребуетсяАнализВладельцаФормыПриОткрытии Тогда 
			ЗаполнитьХарактеристикиНоменклатурыСервер(Номенклатура, Склад);
		Иначе 
			ЗаполнятьНаКлиенте = Истина;
		КонецЕсли;
	Иначе
		Если Параметры.Свойство("Отбор") Тогда 
			Если ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда 
				Номенклатура = Параметры.Отбор.Владелец;
			КонецЕсли;
		КонецЕсли;
		ЗаполнятьНаКлиенте = Истина;
	КонецЕсли;
	
	Параметры.Свойство("ТекущаяСтрока", ХарактеристикаТекущая);
	
	Если Не ЗначениеЗаполнено(Номенклатура)
		Или Не Номенклатура.ВестиУчетПоХарактеристикам
	Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Не указана номенклатура. Выбор характеристики не возможен!'"));
		Отказ = Истина;
	ИначеЕсли Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда 
		Попытка
			ЕдиницаИзмерения = ЭтаФорма.ВладелецФормы.Родитель.ТекущиеДанные.ЕдиницаИзмерения;
		Исключение
			ПоказатьПредупреждение(, НСтр("ru = 'Не указана единица измерения. Выбор характеристики не возможен!'"));
			Отказ = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// Определение склада и необходимости отбора по складу.
	Если ТребуетсяАнализВладельцаФормыПриОткрытии Тогда
		ОпределитьПараметрыОстатковПоВладельцуФормы();
	КонецЕсли;
	
	Элементы.ГруппаТолькоВНаличии.Видимость = ЗначениеЗаполнено(Склад);
	
	Если ТолькоВНаличии И Не ЗначениеЗаполнено(Склад) Тогда
		ТолькоВНаличии = Ложь;
		ЗаполнятьНаКлиенте = Истина;
	КонецЕсли;
	
	Если ЗаполнятьНаКлиенте Тогда 
		ЗаполнитьХарактеристикиНоменклатурыСервер(Номенклатура, Склад);
	КонецЕсли;
		
	Если ЗапрашиватьЦену Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ЕдиницаИзмерения",ЕдиницаИзмерения);
		СтруктураПараметров.Вставить("Количество"	   ,Количество);
		СтруктураПараметров.Вставить("Цена"			   ,Цена);
		СтруктураПараметров.Вставить("Номенклатура"	   ,Номенклатура);
		СтруктураПараметров.Вставить("ЗапрашиватьХарактеристику", Истина);
		СтруктураПараметров.Вставить("Действие", Действие);
		Если Характеристики.Количество() <> 0 Тогда 
			СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", Характеристики[0].ХарактеристикаСсылка);
		КонецЕсли;
		РаботаСФормамиКлиент.мПолучитьЦенуВФормуКлиент(ЭтаФорма, СтруктураПараметров);		
	КонецЕсли;
	
	ОбновитьЗаголовокОстаток();
	
	Если ЗначениеЗаполнено(ХарактеристикаТекущая) Тогда
		УстановитьТекущуюСтроку(ХарактеристикаТекущая);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОпределитьПараметрыОстатковПоВладельцуФормы()
	
	ТекущийВладелец = ЭтаФорма.ВладелецФормы;
	
	Попытка
		Пока ТипЗнч(ТекущийВладелец) <> Тип("ФормаКлиентскогоПриложения") Цикл
			ТекущийВладелец = ТекущийВладелец.Родитель;
		КонецЦикла;
		
		ОбъектВладельца = ТекущийВладелец.Объект;
		Если ОбъектВладельца.Свойство("Ссылка") Тогда

			Если ТипЗнч(ОбъектВладельца.Ссылка) = Тип("ДокументСсылка.ПеремещениеМатериалов") Тогда
				Склад = ОбъектВладельца.СкладОтправитель;
			ИначеЕсли ТипЗнч(ОбъектВладельца.Ссылка) = Тип("ДокументСсылка.ОказаниеУслуг")
					И ЗначениеЗаполнено(Действие)
			Тогда
				Если Действие = "ПодборМатериала" Тогда
					Склад = ОбъектВладельца.СкладМатериалов;
				Иначе
					Склад = ОбъектВладельца.Склад;
				КонецЕсли;
			ИначеЕсли Не ЗначениеЗаполнено(Склад) Тогда
				Если ОбъектВладельца.Свойство("Склад") Тогда
					Склад = ОбъектВладельца.Склад;
				ИначеЕсли ОбъектВладельца.Свойство("СкладМатериалов") Тогда
					Склад = ОбъектВладельца.СкладМатериалов;
				КонецЕсли;
			КонецЕсли;
			
			Если ТипЗнч(ОбъектВладельца.Ссылка) = Тип("ДокументСсылка.ОприходованиеТоваров")
				Или ТипЗнч(ОбъектВладельца.Ссылка) = Тип("ДокументСсылка.ИнвентаризацияТоваров")
				Или ТипЗнч(ОбъектВладельца.Ссылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
			Тогда
				ТолькоВНаличии = Ложь;
			КонецЕсли;
			
		ИначеЕсли ОбъектВладельца.Свойство("Склад") Тогда
			Склад = ОбъектВладельца.Склад;
		Иначе
			Склад = ТекущийВладелец.Склад;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

// Экспортная процедура, используемая для обработки оповещения от формы объекта характеритик и
//  когда требуется обновить список на форме.
//
// Параметры:
//  ХарактеристикаНоменклатуры	 - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика.
//  ДополнитлеьныеПараметры		 - Произвольный - не используется.
//
&НаКлиенте
Процедура ОбновитьХарактеристикиНоменклатуры(ХарактеристикаНоменклатуры, ДополнитлеьныеПараметры) Экспорт
	
	Характеристики.Очистить();
	ЗаполнитьХарактеристикиНоменклатурыСервер(Номенклатура, Склад);
	УстановитьТекущуюСтроку(ХарактеристикаНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтроку(ХарактеристикаНоменклатуры)
	
	СтрокиТаблицы = Характеристики.НайтиСтроки(Новый Структура("ХарактеристикаСсылка", ХарактеристикаНоменклатуры));
	Если СтрокиТаблицы.Количество() <> 0 Тогда
		
		Элементы.Характеристики.ТекущаяСтрока = СтрокиТаблицы[0].ПолучитьИдентификатор();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьХарактеристикиНоменклатурыСервер(Номенклатура, Склад)
	
	Перем ВыборкаОстатки;
	
	Характеристики.Очистить();
	
	Выборка = Справочники.ХарактеристикиНоменклатуры.Выбрать(, Номенклатура);
	ХарактеристикиНоменклатуры = Новый Массив;
	ХарактеристикиНоменклатурыМассивСсылок = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Если Не Выборка.ПометкаУдаления
			И (ПоказыватьАрхивные Или Не Выборка.Архив)
		Тогда
			ДанныеХарактеристики = Новый Структура("Ссылка, Наименование, Архив", Выборка.Ссылка, Выборка.Наименование, Выборка.Архив);
			ХарактеристикиНоменклатуры.Добавить(ДанныеХарактеристики);
			ХарактеристикиНоменклатурыМассивСсылок.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ХарактеристикиНоменклатуры", ХарактеристикиНоменклатурыМассивСсылок);
		Запрос.УстановитьПараметр("СерияНоменклатуры", ?(ЭтоПодбор, Справочники.СерииНоменклатуры.ПустаяСсылка(), Null));
		Запрос.УстановитьПараметр("Склад", Склад);
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПартииТоваровНаСкладахОстатки.Склад КАК Склад,
		               |	ПартииТоваровНаСкладахОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ПартииТоваровНаСкладахОстатки.КоличествоОстаток КАК Остаток
		               |ИЗ
		               |	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
		               |			,
		               |			ХарактеристикаНоменклатуры В (&ХарактеристикиНоменклатуры)
					   |			И Склад = &Склад) КАК ПартииТоваровНаСкладахОстатки";
		ВыборкаОстатки = Запрос.Выполнить().Выбрать();
	КонецЕсли;
	
	Отбор = Новый Структура("ХарактеристикаНоменклатуры");
	Для Каждого ДанныеХарактеристики Из ХарактеристикиНоменклатуры Цикл
		
		ХарактеристикаНоменклатуры = ДанныеХарактеристики.Ссылка;
		Отбор.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;
		
		БуферОстаток = 0;
		
		Если ВыборкаОстатки <> Неопределено Тогда
			
			ВыборкаОстатки.Сбросить();
			Если ВыборкаОстатки.НайтиСледующий(Отбор) Тогда
				
				Единица = ?(ЗначениеЗаполнено(ЕдиницаИзмерения), ЕдиницаИзмерения, Номенклатура.ЕдиницаХраненияОстатков);
				Коэффициент = ?(Единица.Коэффициент = 0, 1, Единица.Коэффициент); 
				БуферОстаток = Окр(ВыборкаОстатки.Остаток * Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / Коэффициент, 3);
				
			КонецЕсли;
		КонецЕсли;
		
		Если Не ТолькоВНаличии Или БуферОстаток > 0 Тогда
			НовыйНабор = Характеристики.Добавить();
			НовыйНабор.ХарактеристикаПредставление = ДанныеХарактеристики.Наименование; 
			НовыйНабор.Остаток = БуферОстаток;
			НовыйНабор.ХарактеристикаСсылка = ХарактеристикаНоменклатуры;
			НовыйНабор.Архив = ДанныеХарактеристики.Архив;
		КонецЕсли;
	КонецЦикла;
	
	Характеристики.Сортировать("ХарактеристикаПредставление");
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоВНаличииПриИзменении(Элемент)
	
	ЗаполнитьХарактеристикиНоменклатурыСервер(Номенклатура, Склад);
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Не ЭтоПодбор Тогда 	
		ОповеститьОВыборе(Элемент.ТекущиеДанные.ХарактеристикаСсылка);
	Иначе
		СтруктураПараметров = Новый Структура("Структура");
		СтруктураПараметров.Вставить("Номенклатура", Номенклатура);
		СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", Элемент.ТекущиеДанные.ХарактеристикаСсылка);
		СтруктураПараметров.Вставить("СерияНоменклатуры", Неопределено);
		СтруктураПараметров.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		СтруктураПараметров.Вставить("Количество", Количество);
		//СтруктураПараметров.Вставить("Цена", Цена);
		СтруктураПараметров.Вставить("Цена", ?(ЗапрашиватьЦену, Цена, Неопределено));
		СтруктураПараметров.Вставить("Действие", Действие);
		Закрыть(СтруктураПараметров);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Не ЭтоПодбор Тогда 	
		ОповеститьОВыборе(Элементы.Характеристики.ТекущиеДанные.ХарактеристикаСсылка);
	Иначе
		СтруктураПараметров = Новый Структура("Структура");
		СтруктураПараметров.Вставить("Номенклатура", Номенклатура);
		СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", Элементы.Характеристики.ТекущиеДанные.ХарактеристикаСсылка);
		СтруктураПараметров.Вставить("СерияНоменклатуры", Неопределено);
		СтруктураПараметров.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		СтруктураПараметров.Вставить("Количество", Количество);
		//СтруктураПараметров.Вставить("Цена", Цена);
		СтруктураПараметров.Вставить("Цена", ?(ЗапрашиватьЦену, Цена, Неопределено));
		СтруктураПараметров.Вставить("Действие", Действие);
		Закрыть(СтруктураПараметров);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("Владелец", Номенклатура));
	ОткрытьФорму("Справочник.ХарактеристикиНоменклатуры.ФормаОбъекта", ПараметрыФормы, ЭтаФорма,,,,
				Новый ОписаниеОповещения("ОбновитьХарактеристикиНоменклатуры", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	
	ТекущиеДанные = Элементы.Характеристики.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.ХарактеристикаСсылка);
	ОткрытьФорму("Справочник.ХарактеристикиНоменклатуры.ФормаОбъекта", ПараметрыФормы, ЭтаФорма,,,,
				Новый ОписаниеОповещения("ОбновитьХарактеристикиНоменклатуры", ЭтотОбъект));
	
КонецПроцедуры
			
&НаКлиенте
Процедура ОбновитьНадписьФормулаСумма() 
	
	НадписьФормулаСумма = Формат(Цена * Количество,"ЧДЦ=2; ЧН=");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокОстаток() 
	
	Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда 
		Элементы.ХарактеристикиОстаток.Заголовок = "Остаток, " + Строка(ЕдиницаИзмерения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	ОбновитьНадписьФормулаСумма();

КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда 
		Характеристики.Очистить();
		ЗаполнитьХарактеристикиНоменклатурыСервер(Номенклатура, Склад);
	КонецЕсли;
	ОбновитьНадписьФормулаСумма();
	ОбновитьЗаголовокОстаток();

КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	ОбновитьНадписьФормулаСумма();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	// Сначала пытаемся найти цену для новой единицы, если не найдется, то будет вычислена пропорционально коэффициентам.
	СтараяЦена = Цена;
	Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		СтарыйКоэффициент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницаИзмерения,"Коэффициент");
	Иначе
		СтарыйКоэффициент = 0;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЕдиницаИзмерения",ЕдиницаИзмерения);
	СтруктураПараметров.Вставить("Количество"	   ,Количество);
	СтруктураПараметров.Вставить("Цена"			   ,Цена);
	СтруктураПараметров.Вставить("Номенклатура"	   ,Номенклатура);
	СтруктураПараметров.Вставить("ЗапрашиватьХарактеристику", Истина);
	СтруктураПараметров.Вставить("ХарактеристикаНоменклатуры", Элементы.Характеристики.ТекущиеДанные.ХарактеристикаСсылка);
	
	РаботаСФормамиКлиент.мПолучитьЦенуВФормуКлиент(ЭтаФорма, СтруктураПараметров, ВыбранноеЗначение);
	Если Цена = 0 Тогда
		Цена = СтараяЦена;
		
		Если СтарыйКоэффициент <> 0 Тогда
			НовыйКоэффициент = ДопСерверныеФункции.ПолучитьРеквизит(ВыбранноеЗначение,"Коэффициент");
			Цена = Цена * НовыйКоэффициент / СтарыйКоэффициент;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоместитьВАрхив(Команда)
	
	ВыбранныеСтроки = Новый Массив;
	
	Для Каждого Строка Из Элементы.Характеристики.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Характеристики.ДанныеСтроки(Строка);
		ВыбранныеСтроки.Добавить(ДанныеСтроки.ХарактеристикаСсылка);
	КонецЦикла;

	РаботаСДиалогамиКлиент.ПоместитьВАрхив(ВыбранныеСтроки);
	
	Характеристики.Очистить();
	ЗаполнитьХарактеристикиНоменклатурыСервер(Номенклатура, Склад);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПоказатьАрхивные(Команда)

	ЭлементФормы = Элементы.СкрытьПоказатьАрхивные;
	
	ПоказыватьАрхивные = Не ЭлементФормы.Пометка;
	
	ЭлементФормы.Пометка = ПоказыватьАрхивные;
		
	Характеристики.Очистить();
	ЗаполнитьХарактеристикиНоменклатурыСервер(Номенклатура, Склад);
	
КонецПроцедуры
