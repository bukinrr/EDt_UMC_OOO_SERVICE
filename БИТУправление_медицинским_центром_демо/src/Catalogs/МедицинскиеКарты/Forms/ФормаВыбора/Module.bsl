#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Клиент <> Справочники.Клиенты.ПустаяСсылка() Тогда
		РаботаСФормамиСервер.УстановитьОтборСписка("Клиент", Параметры.Клиент, Список);
	КонецЕсли;
	
	Если Параметры.ТолькоАктуальныеКарты Тогда
		УстановитьОтборТолькоАктуальныеКарты();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// При создании формы на сервере
&НаСервере
Процедура УстановитьОтборТолькоАктуальныеКарты()
	
	РаботаСФормамиСервер.УстановитьОтборСписка("ПометкаУдаления", Ложь, Список);	
	
	ДатаАктуальностиКарт = ?(ЗначениеЗаполнено(Параметры.ДатаАктуальностиКарт), Параметры.ДатаАктуальностиКарт, НачалоДня(ТекущаяДата()));
	РаботаСФормамиСервер.УстановитьОтборСписка("ДатаРегистрации", ДатаАктуальностиКарт, Список, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
	
	// Группа Или
	ГруппаОтбора = Список.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.Использование = Истина;
	ГруппаОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	// Дата закрытия пустая (в группе Или)
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаЗакрытия");
	
	// Дата закрытия позже даты проверки актуальности (в группе Или)
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаЗакрытия");
	ЭлементОтбора.ПравоеЗначение = ДатаАктуальностиКарт;
	
КонецПроцедуры

#КонецОбласти
