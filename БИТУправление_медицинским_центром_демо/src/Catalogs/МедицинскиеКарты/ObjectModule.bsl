#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.Свойство("ЗаписьНового") И
		ДополнительныеСвойства.ЗаписьНового И
		УправлениеНастройками.ПолучитьПараметрУчетнойПолитики("АвтоматическиШтрихкодироватьМедицинскиеКарты")
	Тогда
		РаботаСТорговымОборудованиемСервер.ЗаписатьШтрихКодВРегистр(Ссылка,Неопределено);
	КонецЕсли;
	
	// Отработка случая, когда пользователь изменил владельца медкарты, которая была основной (продолжение).
	Если ДополнительныеСвойства.Свойство("НайтиНовуюОсновнуюМедкартуСтаромуВладельцу") Тогда
		ДругаМедкартаСтарогоВладельца = ОтраслевыеДополнения.ПолучитьОсновнуюМедКартуКлиента(ДополнительныеСвойства.НайтиНовуюОсновнуюМедкартуСтаромуВладельцу);
		Если ЗначениеЗаполнено(ДругаМедкартаСтарогоВладельца) Тогда
			КлиентОбъект = ДополнительныеСвойства.НайтиНовуюОсновнуюМедкартуСтаромуВладельцу.ПолучитьОбъект();
			КлиентОбъект.ОсновнаяМедицинскаяКарта = ДругаМедкартаСтарогоВладельца;
			КлиентОбъект.ДополнительныеСвойства.Вставить("НеВыполнятьСтандартныеДейтсвияПередЗаписью", Истина);
			КлиентОбъект.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("ЗаписьНового", Истина);
	Иначе
		Если Не ОбменДанными.Загрузка Тогда
			// Отработка случая, когда пользователь изменил владельца медкарты, которая была основной.
			Если Клиент <> Ссылка.Клиент
				И Ссылка.Клиент.ОсновнаяМедицинскаяКарта = Ссылка
			Тогда
				КлиентОбъект = Ссылка.Клиент.ПолучитьОбъект();
				КлиентОбъект.ОсновнаяМедицинскаяКарта = Справочники.МедицинскиеКарты.ПустаяСсылка();
				КлиентОбъект.ДополнительныеСвойства.Вставить("НеВыполнятьСтандартныеДейтсвияПередЗаписью", Истина);
				КлиентОбъект.Записать();
				
				ДругаМедкартаСтарогоВладельца = ОтраслевыеДополнения.ПолучитьОсновнуюМедКартуКлиента(КлиентОбъект.Ссылка, Ссылка);
				Если ЗначениеЗаполнено(ДругаМедкартаСтарогоВладельца) Тогда
					КлиентОбъект.ОсновнаяМедицинскаяКарта = ДругаМедкартаСтарогоВладельца;
					КлиентОбъект.ДополнительныеСвойства.Вставить("НеВыполнятьСтандартныеДейтсвияПередЗаписью", Истина);
					КлиентОбъект.Записать();
				КонецЕсли;
				ДополнительныеСвойства.Вставить("КлиентИзменен", КлиентОбъект.Ссылка);
				ДополнительныеСвойства.Вставить("НайтиНовуюОсновнуюМедкартуСтаромуВладельцу", КлиентОбъект.Ссылка)
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЭтотОбъект.Филиал = УправлениеНастройками.ПолучитьФилиалПоУмолчаниюПользователя();
		
КонецПроцедуры
#КонецОбласти
