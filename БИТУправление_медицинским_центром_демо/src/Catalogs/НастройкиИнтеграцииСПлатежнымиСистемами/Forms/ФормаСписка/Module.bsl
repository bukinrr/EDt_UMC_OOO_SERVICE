///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Результат = ОбновлениеИнформационнойБазы.ОбъектОбработан(
		"Справочник.НастройкиИнтеграцииСПлатежнымиСистемами");
	
	Если Не Результат.Обработан Тогда
		ЭтотОбъект.ТолькоПросмотр = Истина;
		Элементы.НоваяИнтеграция.Доступность = Ложь;
		Элементы.Список.Доступность = Ложь;
		Элементы.ФормаДобавитьИнтеграциюСБП.Доступность = Ложь;
		ОбщегоНазначения.СообщитьПользователю(
			Результат.ТекстИсключения);
	КонецЕсли;
	
	ЭтотОбъект.КоманднаяПанель.Видимость = ИнтеграцияСПлатежнымиСистемами.НастройкаИнтеграцияДоступна();
	Элементы.СписокКонтекстноеМенюСкопировать.Видимость = ЭтотОбъект.КоманднаяПанель.Видимость;
	
	//+бит
	бит_ПриСозданииНаСервере();
	//-бит
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Элемент.ТекущиеДанные <> Неопределено
		И ЗначениеЗаполнено(Элемент.ТекущиеДанные.Родитель) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ВыбраннаяСтрока);
		Если Не Элемент.ТекущиеДанные.ЭтоГруппа Тогда
			ОткрытьФорму(
				"Справочник.НастройкиИнтеграцииСПлатежнымиСистемами.ФормаОбъекта",
				ПараметрыФормы);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьДанныеФормы",
		ЭтотОбъект);
	
	ПараметрыПодключения = Новый Структура;
	ПараметрыПодключения.Вставить("БИК", Неопределено);
	ПараметрыПодключения.Вставить("ТорговаяТочка", Неопределено);
	ПараметрыПодключения.Вставить("ДополнительныеПараметры", Неопределено);
	
	ИнтеграцияСПлатежнымиСистемамиКлиент.СлужебнаяПодключитьИнтеграциюССБП(
		ПараметрыПодключения,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	
	ТекстПредупреждения = НСтр("ru = 'Копирование доступно только для настроек подключения.'");
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьДанныеФормы",
		ЭтотОбъект);
	
	ПараметрыПодключения = Новый Структура;
	ПараметрыПодключения.Вставить("БИК", Неопределено);
	ПараметрыПодключения.Вставить("ТорговаяТочка", Элементы.Список.ТекущиеДанные.Ссылка);
	ПараметрыПодключения.Вставить("ДополнительныеПараметры", Неопределено);
	
	ИнтеграцияСПлатежнымиСистемамиКлиент.СлужебнаяПодключитьИнтеграциюССБП(
		ПараметрыПодключения,
		ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОформлениеОтбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОформлениеОтбор = ОформлениеОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОформлениеОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОформлениеОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОформлениеОтбор.ПравоеЗначение = Истина;
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста",
		Метаданные.ЭлементыСтиля.ЦветНеАктивнойСтроки.Значение);
	
	ОформлениеОтбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОформлениеОтбор = ОформлениеОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОформлениеОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Используется");
	ОформлениеОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОформлениеОтбор.ПравоеЗначение = Ложь;
	
КонецПроцедуры

//+бит
#Область БИТ_Дополнения

&НаСервере
Процедура бит_ПриСозданииНаСервере()
	СоздатьКомандыРаботыСАккаунтомИТС();
	ВыполнитьПроверкуИТСАккаунта();
КонецПроцедуры

&НаСервере
Процедура СоздатьКомандыРаботыСАккаунтомИТС()
	
	НоваяКоманда = Команды.Добавить("бит_АвторизацияНаСайтеПоддержкиПользователей");
	НоваяКоманда.Действие = "бит_АвторизацияНаСайтеПоддержкиПользователей";
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить("бит_АвторизацияНаСайтеПоддержкиПользователей", Тип("КнопкаФормы"), Элементы.ФормаКоманднаяПанель);
	НовыйЭлемент.Заголовок = "Подключить интернет-поддержку";
	НовыйЭлемент.ИмяКоманды = "бит_АвторизацияНаСайтеПоддержкиПользователей";
	НовыйЭлемент.Вид = ВидКнопкиФормы.ГиперссылкаКоманднойПанели;
	
	НоваяКоманда = Команды.Добавить("бит_ОтключитьИнтернетПоддержку");
	НоваяКоманда.Действие = "бит_ОтключитьИнтернетПоддержку";
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить("бит_ОтключитьИнтернетПоддержку", Тип("КнопкаФормы"), Элементы.ФормаКоманднаяПанель);
	НовыйЭлемент.Заголовок = "Отключить интернет поддержку";
	НовыйЭлемент.ИмяКоманды = "бит_ОтключитьИнтернетПоддержку";
	НовыйЭлемент.Вид = ВидКнопкиФормы.ГиперссылкаКоманднойПанели;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПроверкуИТСАккаунта()
	
	Если ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда 
		
		Элементы["бит_АвторизацияНаСайтеПоддержкиПользователей"].Видимость = Ложь;
		ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		Если ТипЗнч(ДанныеАутентификации) = Тип("Структура") Тогда
			РезультатПроверки = ИнтернетПоддержкаПользователей.ПроверитьЛогинИПароль(ДанныеАутентификации.Логин, ДанныеАутентификации.Пароль);
			
			Если Не РезультатПроверки.Результат Тогда
				ТекстСообщения = "Для работы с СБП, необходим действующий аккаунт ИТС. Текст ошибки: " + РезультатПроверки.СообщениеОбОшибке; 
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Элементы["бит_ОтключитьИнтернетПоддержку"].Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура бит_АвторизацияНаСайтеПоддержкиПользователей(Команда)
	
	Оповещение = Новый ОписаниеОповещения("бит_ПослеВводаЛогинИПароля", ЭтотОбъект);
	АдресныйКлассификаторКлиент.АвторизоватьНаСайтеПоддержкиПользователей(Оповещение, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура бит_ПослеВводаЛогинИПароля(Результат, Параметр) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Элементы["бит_АвторизацияНаСайтеПоддержкиПользователей"].Видимость = Ложь;
		Элементы["бит_ОтключитьИнтернетПоддержку"].Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура бит_ОтключитьИнтернетПоддержку(Команда)
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("бит_ПриОтветеНаВопросОВыходеИзИнтернетПоддержки", ЭтотОбъект),
		НСтр("ru = 'Логин и пароль для подключения к сервисам Интернет-поддержки пользователей будут удалены из программы.
		|Отключить Интернет-поддержку?'"),
		РежимДиалогаВопрос.ДаНет,
		,
		КодВозвратаДиалога.Нет,
		НСтр("ru = 'Выход из Интернет-поддержки пользователей'"));
		
КонецПроцедуры

&НаКлиенте
Процедура бит_ПриОтветеНаВопросОВыходеИзИнтернетПоддержки(КодВозврата, Параметры) Экспорт
	
	Если КодВозврата = КодВозвратаДиалога.Да Тогда
		ИнтернетПоддержкаПользователейВызовСервера.ВыйтиИзИПП();
		Оповестить("ИнтернетПоддержкаОтключена");
		Элементы["бит_АвторизацияНаСайтеПоддержкиПользователей"].Видимость = Истина;
		Элементы["бит_ОтключитьИнтернетПоддержку"].Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
//-бит


#КонецОбласти
