#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Макет = Справочники.КлассификаторЕдиницИзмерения.ПолучитьМакет("КлассификаторЕдиницИзмерения");
	Макет.Параметры.Расшифровка = Истина;
	ПолеТабличногоДокумента.Вывести(Макет);
	ПолеТабличногоДокумента.ФиксацияСверху = ПолеТабличногоДокумента.Области.ОбластьРасшифровки.Верх - 1;
	ПолеТабличногоДокумента.ОтображатьЗаголовки = Ложь;
	ПолеТабличногоДокумента.ОтображатьСетку = Ложь;
	ПолеТабличногоДокумента.ТолькоПросмотр = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Получение значений полей выбранной строки.
	
	ТекущаяОбласть    = ПолеТабличногоДокумента.ТекущаяОбласть;

	ОбластьКодЧисловой         = ПолеТабличногоДокумента.Области.КодЧисловой;
	ОбластьНаименованиеКраткое = ПолеТабличногоДокумента.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное  = ПолеТабличногоДокумента.Области.НаименованиеПолное;

	КодЧисловой         = ПолеТабличногоДокумента.Область(ТекущаяОбласть.Верх, ОбластьКодЧисловой.        Лево, ТекущаяОбласть.Низ, ОбластьКодЧисловой.        Право).Текст;
	НаименованиеКраткое = ПолеТабличногоДокумента.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст;
	НаименованиеПолное  = ПолеТабличногоДокумента.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное. Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеПолное. Право).Текст;

	// Проверка наличия выбранной единицы измерения.

	Ссылка = НайтиЭлементПоКоду(КодЧисловой);
    ПараметрыФормы = Новый Структура;
	Если НЕ Ссылка.Пустая() Тогда
		Вопрос = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В справочнике ""Классификатор единиц измерения"" уже существует элемент с кодом ""%1""! Открыть существующий?'"),
			КодЧисловой);
		
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ПараметрыФормы.Вставить("Ключ", Ссылка);
			ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.ФормаОбъекта", ПараметрыФормы);
			Возврат;
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Создание новой единицы измерения.
	ЗначенияЗаполнения = Новый Структура("Код, Наименование, НаименованиеПолное",
										КодЧисловой,
										СтрПолучитьСтроку(НаименованиеКраткое, 1),
										СтрПолучитьСтроку(НаименованиеПолное, 1));
										
	МераКоличества = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.МераКоличестваПредметаРасчетаПоКодуЕдиницыИзмерения(КодЧисловой);
	Если Не ЗначениеЗаполнено(МераКоличества) Тогда
		МераКоличества = РаботаСТорговымОборудованием.МераКоличестваПоНаименованиюЕдиницы(ЗначенияЗаполнения.НаименованиеПолное, Ложь);
	КонецЕсли;
	ЗначенияЗаполнения.Вставить("МераКоличества", МераКоличества);
	
	ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));
	
КонецПроцедуры

&НаСервере
Функция НайтиЭлементПоКоду(КодЧисловой)
	Ссылка = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(КодЧисловой);
	Возврат Ссылка;
КонецФункции

#КонецОбласти