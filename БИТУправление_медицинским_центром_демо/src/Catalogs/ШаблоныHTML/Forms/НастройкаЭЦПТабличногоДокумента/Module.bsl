#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТабличныйДокумент = Параметры.ТабличныйДокумент;
	РасположениеПодписи = Параметры.РасположениеПодписи;
	ПараметрыЭЦПТабличногоШаблона = Параметры.ПараметрыЭЦПТабличногоШаблона;
	
	ИмяРисункаЭЦП = ЭЦП_УМЦ_Сервер.ИмяРисункаЭЦПВТабличномДокументе();
	ВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	
	Если РасположениеПодписи = Перечисления.РасположениеПодписи.ПослеДокумента Тогда
		НомерПервойСтрокиПослеДокумента = ТабличныйДокумент.ВысотаТаблицы + 1;
		СтрокаПодДокументом = ТабличныйДокумент.Область(СтрШаблон("R%1", НомерПервойСтрокиПослеДокумента));
		СтрокаПодДокументом.ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.БольшойПунктир);
	КонецЕсли;
	
	ЭЦП_УМЦ_Сервер.ДобавитьРисунокЭЦПВТабличныйДокумент(ТабличныйДокумент, ПараметрыЭЦПТабличногоШаблона, ПолучитьДанныеПодписи(), РасположениеПодписи, ВысотаТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Закрыть(ПараметрыЭЦПТабличногоШаблона);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоУмолчанию(Команда)
	
	ПараметрыЭЦПТабличногоШаблона = "";
	ОбщегоНазначенияБИТКлиентСервер.УдалитьРисунокИзТабличногоДокумента(ТабличныйДокумент, ИмяРисункаЭЦП);
	ЭЦП_УМЦ_Сервер.ДобавитьРисунокЭЦПВТабличныйДокумент(ТабличныйДокумент, ПараметрыЭЦПТабличногоШаблона, ПолучитьДанныеПодписи(), РасположениеПодписи, ВысотаТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТабличныйДокументПриИзменении(Элемент)
	
	Если ТипЗнч(Элемент.ТекущаяОбласть) = Тип("РисунокТабличногоДокумента")
		И Элемент.ТекущаяОбласть.Имя = ИмяРисункаЭЦП
	Тогда
		ПараметрыЭЦПТабличногоШаблона = СохранитьИОбновитьРисунокЭЦП(ТабличныйДокумент, ИмяРисункаЭЦП, ПолучитьДанныеПодписи(), РасположениеПодписи, ПараметрыЭЦПТабличногоШаблона, НомерПервойСтрокиПослеДокумента, ВысотаТаблицы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеПодписи()
	
	ДанныеПодписи = Новый Структура;
	ДанныеПодписи.Вставить("Сертификат",							"Серийный номер");
	ДанныеПодписи.Вставить("ВладелецСертификата",					"Данные владельца");
	ДанныеПодписи.Вставить("НаименованиеОрганизации",				"Наименование организации");
	ДанныеПодписи.Вставить("ДействительностьСертификатаНачало",		Формат(ТекущаяДата()-48*3600, "ДФ=dd.MM.yyyy"));
	ДанныеПодписи.Вставить("ДействительностьСертификатаОкончание",	Формат(ТекущаяДата()+48*3600, "ДФ=dd.MM.yyyy"));
	ДанныеПодписи.Вставить("ДатаПодписи",							Формат(ТекущаяДата(), "ДФ=dd.MM.yyyy"));
	
	Возврат ДанныеПодписи;
	
КонецФункции

&НаСервереБезКонтекста
Функция СохранитьИОбновитьРисунокЭЦП(ТабличныйДокумент, ИмяРисункаЭЦП, ДанныеПодписи, РасположениеПодписи, ПараметрыЭЦПТабличногоШаблона, НомерПервойСтрокиПослеДокумента, ВысотаТаблицы)
	
	НовыеПараметрыЭЦПТабличногоШаблона = СохранитьПараметрыРисункаЭЦП(ТабличныйДокумент, ИмяРисункаЭЦП, РасположениеПодписи, ПараметрыЭЦПТабличногоШаблона, НомерПервойСтрокиПослеДокумента);
	ОбщегоНазначенияБИТКлиентСервер.УдалитьРисунокИзТабличногоДокумента(ТабличныйДокумент, ИмяРисункаЭЦП);
	ЭЦП_УМЦ_Сервер.ДобавитьРисунокЭЦПВТабличныйДокумент(ТабличныйДокумент, НовыеПараметрыЭЦПТабличногоШаблона, ДанныеПодписи, РасположениеПодписи, ВысотаТаблицы);
	
	Возврат НовыеПараметрыЭЦПТабличногоШаблона;
	
КонецФункции

&НаСервереБезКонтекста
Функция СохранитьПараметрыРисункаЭЦП(ТабличныйДокумент, ИмяРисункаЭЦП, РасположениеПодписи, ПараметрыЭЦП, НомерПервойСтрокиПослеДокумента)
	
	Параметры = Новый Соответствие;
	Параметры.Вставить("Ширина",			НСтр(ПараметрыЭЦП, "Ширина"));
	// По координатам
	Параметры.Вставить("КоордВерх",			НСтр(ПараметрыЭЦП, "КоордВерх"));
	Параметры.Вставить("КоордЛево",			НСтр(ПараметрыЭЦП, "КоордЛево"));
	Параметры.Вставить("КоордРасположение",	НСтр(ПараметрыЭЦП, "КоордРасположение"));
	// После документа
	Параметры.Вставить("ПослеВерх",			НСтр(ПараметрыЭЦП, "ПослеВерх"));
	Параметры.Вставить("ПослеЛево",			НСтр(ПараметрыЭЦП, "ПослеЛево"));
	
	Если РасположениеПодписи = Перечисления.РасположениеПодписи.ПоЗаданнымКоординатам Тогда
		
		НомерПервойСтроки	= ПолучитьКрайОбласти(ТабличныйДокумент, ИмяРисункаЭЦП, СтрШаблон("R%1:R%2", "%1", ТабличныйДокумент.ВысотаТаблицы), -ТабличныйДокумент.ВысотаТаблицы);
		НомерПервойКолонки	= ПолучитьКрайОбласти(ТабличныйДокумент, ИмяРисункаЭЦП, СтрШаблон("R1C%1:R%2C%3", "%1", ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы), -ТабличныйДокумент.ШиринаТаблицы);
		
		НомерПервойСтроки = ?(НомерПервойСтроки = Неопределено, 1, НомерПервойСтроки);
		
		ОбластьСРисункомЭЦП = СтрШаблон("R%1C%2:R%3C%4", НомерПервойСтроки, НомерПервойКолонки, ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы);
		Для Каждого Рисунок Из ТабличныйДокумент.ПолучитьОбласть(ОбластьСРисункомЭЦП).Рисунки Цикл
			Если Рисунок.Имя = ИмяРисункаЭЦП Тогда
				ЯчейкаРисунка = СтрШаблон("R%1C%2", НомерПервойСтроки, НомерПервойКолонки);
				
				Параметры["КоордВерх"]			= ?(Рисунок.Верх < 0, 0, Рисунок.Верх);
				Параметры["КоордЛево"]			= Рисунок.Лево;
				Параметры["КоордРасположение"]	= ЯчейкаРисунка;
				Параметры["Ширина"]				= Рисунок.Ширина;
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		
		РисунокПодДокументом = Ложь;
		
		ОбластьСРисункомЭЦП = СтрШаблон("R%1C1:R%2C%3", НомерПервойСтрокиПослеДокумента, ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы);
		Для Каждого Рисунок Из ТабличныйДокумент.ПолучитьОбласть(ОбластьСРисункомЭЦП).Рисунки Цикл
			Если Рисунок.Имя = ИмяРисункаЭЦП Тогда
				РисунокПодДокументом = Истина;
				
				Параметры["ПослеВерх"]			= ?(Рисунок.Верх < 0, 0, Рисунок.Верх);
				Параметры["ПослеЛево"]			= Рисунок.Лево;
				Параметры["Ширина"]				= Рисунок.Ширина;
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не РисунокПодДокументом Тогда
			ОпуститьРисунокПодДокумент(ТабличныйДокумент, ИмяРисункаЭЦП, Параметры);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СформироватьСтрокуПараметров(Параметры);
	
КонецФункции

&НаСервереБезКонтекста
Функция СформироватьСтрокуПараметров(Параметры)
	
	мПараметры = Новый Массив;
	Для Каждого КлючЗначение Из Параметры Цикл
		мПараметры.Добавить(СтрШаблон("%1='%2'", КлючЗначение.Ключ, КлючЗначение.Значение));
	КонецЦикла;
	
	Возврат СтрСоединить(мПараметры, ";");
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОпуститьРисунокПодДокумент(ТабличныйДокумент, ИмяРисункаЭЦП, Параметры)
	
	НомерПервойКолонки = ПолучитьКрайОбласти(ТабличныйДокумент, ИмяРисункаЭЦП, СтрШаблон("R1C%1:R%2C%3", "%1", ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы), -ТабличныйДокумент.ШиринаТаблицы);
	
	ОбластьСРисункомЭЦП = СтрШаблон("R1:R%1", ТабличныйДокумент.ВысотаТаблицы);
	Для Каждого Рисунок Из ТабличныйДокумент.ПолучитьОбласть(ОбластьСРисункомЭЦП).Рисунки Цикл
		Если Рисунок.Имя = ИмяРисункаЭЦП Тогда
			Параметры["ПослеВерх"]			= 0;
			Параметры["ПослеЛево"]			= Рисунок.Лево;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКрайОбласти(ТабличныйДокумент, ИмяРисункаЭЦП, ШаблонОбласти, НомерПервогоЭлемента)
	
	Для Номер = НомерПервогоЭлемента По -1 Цикл
		
		Область = ТабличныйДокумент.ПолучитьОбласть(СтрШаблон(ШаблонОбласти, -Номер));
		Для Каждого Рисунок Из Область.Рисунки Цикл
			Если Рисунок.Имя = ИмяРисункаЭЦП Тогда
				Возврат -Номер;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти