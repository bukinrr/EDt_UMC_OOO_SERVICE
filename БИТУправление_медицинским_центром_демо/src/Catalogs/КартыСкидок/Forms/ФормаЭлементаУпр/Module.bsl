#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Подстановка вида карты, если он единственный.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	ВидыКартСкидок.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыКартСкидок КАК ВидыКартСкидок
	|ГДЕ
	|	НЕ ВидыКартСкидок.ПометкаУдаления"
	;
	Выб = Запрос.Выполнить().Выбрать();
	Если Выб.Количество() = 1 Тогда
		Выб.Следующий();
		Объект.ВидКарты = Выб.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьТабличныеЧасти();
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		ОписаниеОшибки = "";
		
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
		ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
		
		ОповещенияПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);    
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(ОповещенияПриПодключении, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	// МеханизмВнешнегоОборудования
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	ПоддерживаемыеТипыВО.Добавить("СчитывательМагнитныхКарт");
	
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(Неопределено, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	// Конец МеханизмВнешнегоОборудования
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	РаботаСФормамиКлиент.УстановитьОтборСписка("Владелец", Объект.Ссылка, Штрихкоды);
	РаботаСФормамиКлиент.УстановитьОтборСписка("Владелец", Объект.Ссылка, МагнитныеКарты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ScanData" Тогда
		Если ВводДоступен() Тогда
			ТипШК = Неопределено;
			РаботаСТорговымОборудованиемКлиент.ОбработатьСобытиеСШКФормы(ЭтаФорма, Параметр, ТипШК);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "TracksData" Тогда
		Если ВводДоступен() Тогда
			КодКарты = Неопределено;
			СМК = Неопределено;
			РаботаСТорговымОборудованиемКлиент.ОбработатьСобытиеСМК(ЭтаФорма,  Параметр[0], СМК, Неопределено, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура НастроитьТабличныеЧасти()
	
	РаботаСФормамиКлиент.УстановитьОтборСписка("Владелец", Объект.Ссылка, Штрихкоды);
	РаботаСФормамиКлиент.УстановитьОтборСписка("Владелец", Объект.Ссылка, МагнитныеКарты);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйШтрихКод(Команда)
	Если Не РаботаСДиалогамиКлиент.ЗаписатьНовыйОбъектВФорме(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Штрихкоды.ТекущиеДанные;
	
	РаботаСТорговымОборудованиемКлиент.КнопкаНовыйШрихкодНажатие(ТекущаяСтрока, Объект.Ссылка);
    Элементы.Штрихкоды.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура Штрихкоды1ПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	РаботаСДиалогамиКлиент.ПередНачаломДобавленияВПодчиненныйСписокНаФормеОбъекта(ЭтаФорма, Элемент, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура МагнитныеКарты1ПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не РаботаСДиалогамиКлиент.ЗаписатьНовыйОбъектВФорме(ЭтаФорма) Тогда
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

#Область ШтрихкодированиеМагнитныеКоды

&НаКлиенте
Функция СШККарта(Карта, Клиент, СШК) Экспорт
	
	Если Карта <> Объект.Ссылка Тогда
		ОткрытьЗначение(Карта);
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция СШКНеизвестныйКод(Штрихкод, ТипКода, СШК) Экспорт
	
	Если РаботаСДиалогамиКлиент.ЗаписатьНовыйОбъектВФорме(ЭтаФорма) Тогда
		РаботаСТорговымОборудованиемКлиент.ДобавитьШрихкодВФормеОбъекта(ЭтаФорма, Неопределено, Объект, Штрихкод, ТипКода);
		НастроитьТабличныеЧасти();
		Элементы.Штрихкоды.Обновить();
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции // СШКНеизвестныйКод()

&НаКлиенте
Функция СМКНеизвестныйКод(Код, СМК) Экспорт

	Если РаботаСДиалогамиКлиент.ЗаписатьНовыйОбъектВФорме(ЭтаФорма) Тогда
		РаботаСТорговымОборудованиемСервер.ДобавитМагнитныйКодСервер(Объект.Ссылка, Код);
		НастроитьТабличныеЧасти();
		Элементы.МагнитныеКарты.Обновить();
	КонецЕсли;
	
	Возврат Истина;

КонецФункции // СМКНеизвестныйКод()

&НаКлиенте
Функция СМКМагнитнаяКарта(ВладелецКарты, СМК) Экспорт

	ОткрытьЗначение(ВладелецКарты);
	
	Возврат Истина;

КонецФункции // СМКИнформационнаяКарта()

#КонецОбласти

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:
				|""%ОписаниеОшибки%"".'" );
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры