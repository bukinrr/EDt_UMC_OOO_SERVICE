#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Тип.Пустая() Тогда
		Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыСпособовОплат.Эквайринг");
	КонецЕсли;
	
	НастроитьФормуПоТипу();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	
	Если Объект.Тип <> ПредопределенноеЗначение("Перечисление.ТипыСпособовОплат.СБП") Тогда
		Объект.ТорговаяТочкаСБП = Неопределено;
	КонецЕсли;
	
	НастроитьФормуПоТипу();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЭТАвтономноПриИзменении(Элемент)
	НастроитьФормуПоТипу();
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииИспользоватьЭТАвтономно()
	Элементы.Оборудование.ТолькоПросмотр = Объект.ИспользоватьЭТАвтономно;
	Элементы.НеОповещатьОбОтсутствииНомераСлипЧека.ТолькоПросмотр = Объект.ИспользоватьЭТАвтономно;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЭквайрингНСПКНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ВыборЭТЗавершение", ЭтаФорма);
	ОткрытьФорму("Справочник.ЭквайринговыеТерминалы.Форма.ФормаВыбораЭквайрингаНСПК",, ЭтаФорма,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЭТЗавершение(Результат, ДополнительныеПарамерты) Экспорт
	
	Объект.ЭквайрингНСПК = Результат;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьФормуПоТипу()
	
	ЭтоЭТ	 = Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыСпособовОплат.Эквайринг");
	ЭтоСБП	 = Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыСпособовОплат.СБП");
	ЭтоНСПК	 = Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыСпособовОплат.СертификатНСПК");
	ЭтоПрочее = Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыСпособовОплат.Прочее");
	ЭтоПрочее = ЭтоПрочее Или Не ЗначениеЗаполнено(Объект.Тип);
	
	Если ЭтоЭТ Тогда
		Элементы.СтраницыПоТипу.ТекущаяСтраница = Элементы.СтраницаНастройкиЭТ;
		
		Элементы.ГруппаПодключаемоеОбоудование.Доступность = Не Объект.ИспользоватьЭТАвтономно;
		Элементы.НеОповещатьОбОтсутствииНомераСлипЧека.Доступность = Не Объект.ИспользоватьЭТАвтономно;
		Элементы.ЗапрашиватьПодтверждениеОплатыАвтономногоЭТ.Доступность = Объект.ИспользоватьЭТАвтономно;
		
	ИначеЕсли ЭтоСБП Тогда
		Элементы.СтраницыПоТипу.ТекущаяСтраница = Элементы.СтраницаНастройкиСБП;
	ИначеЕсли ЭтоНСПК Тогда
		Элементы.СтраницыПоТипу.ТекущаяСтраница = Элементы.СтраницаНастройкиНСПК;
	Иначе
		Элементы.СтраницыПоТипу.ТекущаяСтраница = Элементы.СтраницаНастройкиПрочее;
	КонецЕсли;
	
	Элементы.Касса.Доступность = Не ЭтоНСПК;
	
КонецПроцедуры

#КонецОбласти

