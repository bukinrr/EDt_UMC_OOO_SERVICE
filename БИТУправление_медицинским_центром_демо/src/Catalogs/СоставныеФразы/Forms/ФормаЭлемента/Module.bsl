#Область РазделОписанияПеременных

&НаКлиенте
Перем мТекущийПользователь;

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Родитель) Тогда
		
		Элементы.Параметр.Видимость = Ложь;
		
		Элементы.ДляВсехПользователей.Видимость = Ложь;
		
		Если Объект.Ссылка.Пустая() Тогда
			
			Объект.Параметр = Объект.Родитель.Параметр;
			
			Если Объект.Родитель.ДляВсехПользователей Тогда
				
				Объект.ДляВсехПользователей = Истина;
				
			Иначе
				
				Объект.Пользователь = Объект.Родитель.Пользователь;	
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Элементы.Параметр.Видимость = Истина;
		Элементы.ДляВсехПользователей.Видимость = Истина;
		
		Если Объект.Ссылка.Пустая() 
			И Не Объект.ДляВсехПользователей
		Тогда
			
			Объект.Пользователь = ПараметрыСеанса.ТекущийПользователь;	
			
		КонецЕсли;
		
	КонецЕсли;
	
	//Если Объект.Ссылка.Пустая() 
	//	И Не ЗначениеЗаполнено(Объект.Параметр)
	//Тогда
	//	Объект.Параметр = Параметры.Параметр;
	//КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
 	Элементы.Текст.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.Авто;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.Текст.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.Авто;
	
	мТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьРеквизитыПодчиненных();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПараметрОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Тип")
		И НЕ ВыбранСтроковыйПараметр(ВыбранноеЗначение)
	Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Необходимо выбирать параметр с типом Строка'"),,
			"Параметр", "Объект.Параметр");
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДляВсехПользователейПриИзменении(Элемент)
	
	УстановитьПользователя();	
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	Элементы.Текст.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.НеИспользовать;
	Фраза = ?(ЗначениеЗаполнено(Текст), Текст, Объект.Текст); 
	ЗаполнитьНаименование(Фраза);
	Объект.Текст = Фраза;
КонецПроцедуры

&НаКлиенте
Процедура ТекстОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	Элементы.Текст.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.НеИспользовать;
	ЗаполнитьНаименование(Текст);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ВыбранСтроковыйПараметр(Ссылка)
	
	Возврат ТипЗнч(Ссылка) = Тип("СправочникСсылка.ПакетыСоставныхФраз") Или Ссылка.ТипЗначения.СодержитТип(Тип("Строка"));	
	
КонецФункции

&НаКлиенте
Процедура УстановитьПользователя()

	Объект.Пользователь = ?(Объект.ДляВсехПользователей, ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка"), мТекущийПользователь);
	Элементы.Пользователь.Доступность = Не Объект.ДляВсехПользователей;

КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыПодчиненных()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТекущийЭлемент", Объект.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоставныеФразы.Ссылка
	|ИЗ
	|	Справочник.СоставныеФразы КАК СоставныеФразы
	|ГДЕ
	|	СоставныеФразы.Ссылка В ИЕРАРХИИ(&ТекущийЭлемент)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		Если Выборка.Ссылка <> Объект.Ссылка Тогда
			
			СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			СправочникОбъект.Параметр = Объект.Параметр;
			
			СправочникОбъект.Пользователь = Объект.Пользователь;
			
			СправочникОбъект.ДляВсехПользователей = Объект.ДляВсехПользователей;
			
			СправочникОбъект.Записать();
			
		КонецЕсли;
	
	КонецЦикла; 	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаименование(Знач Текст)

	АвтоНаименованиеСтарое = СформироватьНаименование(Объект.Текст);
	Если АвтоНаименованиеСтарое = Объект.Наименование 
		Или ПустаяСтрока(Объект.Наименование)
	Тогда	
		Объект.Наименование = СформироватьНаименование(Текст);
	КонецЕсли;

КонецПроцедуры // ЗаполнитьНаименование()

&НаКлиенте
Функция СформироватьНаименование(Текст)
	
	Наименование = ?(СтрДлина(Текст) > 50, Лев(Текст, 47) + "...", Текст);
	Наименование = СтрЗаменить(Наименование, Символы.ПС, " ");
	
	Возврат Наименование;
	
КонецФункции

#КонецОбласти
