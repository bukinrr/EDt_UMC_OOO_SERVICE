#Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПрочитатьПараметрыПрописи();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ПРОПИСИ

// Обработчик события ПриИзменении элементов ПолеПрописи1наРусском, ..., ПолеПрописи8наРусском.
&НаКлиенте
Процедура ПолеПрописиНаРусскомПриИзменении(Элемент)

	СформироватьПараметрыПрописиНаРусском();
	Если Элемент.Имя = "ПолеПрописи4наРусском" ИЛИ Элемент.Имя = "ПолеПрописи8наРусском" Тогда 
		СформироватьНадписиПолейПрописиНаРусском();
	Иначе
		ПрочитатьПараметрыПрописи();
	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриИзменении элемента ПолеПрописиДлинаДробнойЧасти.
//
&НаКлиенте
Процедура ПолеПрописиДлинаДробнойЧастиПриИзменении(Элемент)

	СформироватьПараметрыПрописиНаРусском();

КонецПроцедуры

// Обработчик события ПриИзменении элемента НаименованиеПолное.
&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)     
	Если ПустаяСтрока(ПолеПрописи1наРусском) Тогда
		ПолеПрописи1наРусском = НРег(Объект.НаименованиеПолное);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Процедура формирует параметры прописи для русского языка из соответствующих полей диалога.
&НаКлиенте
Процедура СформироватьПараметрыПрописиНаРусском()

	Объект.ПараметрыПрописиНаРусском = ПолеПрописи1наРусском + ", "
								     + ПолеПрописи2наРусском + ", "
							  + ПолеПрописи3наРусском + ", "
							  + ПолеПрописи4наРусском + ", "
							  + ПолеПрописи5наРусском + ", "
							  + ПолеПрописи6наРусском + ", "
							  + ПолеПрописи7наРусском + ", "
							  + ПолеПрописи8наРусском + ", "
							  + ПолеПрописиДлинаДробнойЧасти;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ, ВЫЗЫВАЕМЫЕ ИЗ ОБРАБОТЧИКОВ

// Процедура считывает параметры прописи и заполняет соответствующие поля диалога.
&НаКлиенте
Процедура ПрочитатьПараметрыПрописи()

	СтрокаПараметров = СтрЗаменить(Объект.ПараметрыПрописиНаРусском, ",", Символы.ПС);

	ПолеПрописи1наРусском        = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 1));
	ПолеПрописи2наРусском        = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 2));
	ПолеПрописи3наРусском        = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 3));
	ПолеПрописи4наРусском        = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 4));
	ПолеПрописи5наРусском        = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 5));
	ПолеПрописи6наРусском        = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 6));
	ПолеПрописи7наРусском        = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 7));
	ПолеПрописи8наРусском        = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 8));
	ПолеПрописиДлинаДробнойЧасти = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 9));

	Если ПолеПрописи4наРусском = Неопределено Тогда
		ПолеПрописи4наРусском = Элементы.ПолеПрописи4наРусском.СписокВыбора.СписокВыбора[0].Значение;
	КонецЕсли;

	Если ПолеПрописи8наРусском = Неопределено Тогда
		ПолеПрописи8наРусском = Элементы.ПолеПрописи8наРусском.СписокВыбора[0].Значение;
	КонецЕсли;

	Если ПолеПрописиДлинаДробнойЧасти = Неопределено Тогда
		ПолеПрописиДлинаДробнойЧасти = Элементы.ПолеПрописиДлинаДробнойЧасти.СписокВыбора[0].Значение;
	КонецЕсли;

КонецПроцедуры

// Процедура формирует надписи к полям прописи для русского языка.
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура СформироватьНадписиПолейПрописиНаРусском()

	Надписи = Новый Структура("м1, м2, м3, ж1, ж2, ж3, с1, с2, с3");

	Надписи.м1 = "один"; Надписи.м2 = "два"; Надписи.м3 = "пять";
	Надписи.ж1 = "одна"; Надписи.ж2 = "две"; Надписи.ж3 = "пять";
	Надписи.с1 = "одно"; Надписи.с2 = "два"; Надписи.с3 = "пять";

	Род = ПолеПрописи4наРусском;
    Если Род = "" Тогда
		Род = "м";
	КонецЕсли;
	Элементы.ПолеПрописи1наРусском.Заголовок = Надписи[Род + "1"];
	Элементы.ПолеПрописи2наРусском.Заголовок = Надписи[Род + "2"];
	Элементы.ПолеПрописи3наРусском.Заголовок = Надписи[Род + "3"];

	Род = ПолеПрописи8наРусском;
	Если Род = "" Тогда
		Род = "м";
	КонецЕсли;
	Элементы.ПолеПрописи5наРусском.Заголовок = Надписи[Род + "1"];
	Элементы.ПолеПрописи6наРусском.Заголовок = Надписи[Род + "2"];
	Элементы.ПолеПрописи7наРусском.Заголовок = Надписи[Род + "3"];

КонецПроцедуры

#КонецОбласти