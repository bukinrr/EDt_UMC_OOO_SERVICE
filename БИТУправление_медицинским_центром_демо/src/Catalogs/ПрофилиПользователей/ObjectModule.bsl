#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.Ссылка,
		|	Пользователи.Код,
		|	Пользователи.Наименование
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Профиль = &Профиль";	
	Запрос.УстановитьПараметр("Профиль", ЭтотОбъект.Ссылка);	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		// Обработаем Роли ПользователяИБ
		КлиентИБ = ПользователиИнформационнойБазы.НайтиПоИмени(СокрЛП(ВыборкаДетальныеЗаписи.Код));
		Если КлиентИБ = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		КлиентИБ.Роли.Очистить();
		Для каждого Строка из ЭтотОбъект.Роли Цикл			
			РольПоСтроке = Метаданные.Роли.Найти(Строка.Роль);
			Если РольПоСтроке <> Неопределено Тогда
				КлиентИБ.Роли.Добавить(РольПоСтроке);
			КонецЕсли;
		КонецЦикла;
		КлиентИБ.Записать();
		// Обработаем настройки пользователя.
		Набор = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей();
		Набор.Отбор.Пользователь.Установить(ВыборкаДетальныеЗаписи.Ссылка);
		Набор.Прочитать();
		ТабНабор = Набор.Выгрузить();
		Для Каждого СтрокаНастройки Из ЭтотОбъект.Настройки.НайтиСтроки(Новый Структура("Используется", Истина)) Цикл
			
			Запись = ТабНабор.Найти(СтрокаНастройки.Настройка, "Настройка");
			Если Запись = Неопределено Тогда
				Запись				= ТабНабор.Добавить();
				Запись.Пользователь	= ВыборкаДетальныеЗаписи.Ссылка;
				Запись.Настройка	= СтрокаНастройки.Настройка;
			КонецЕсли;
			Запись.Значение = СтрокаНастройки.Настройка.ТипЗначения.ПривестиЗначение(СтрокаНастройки.Значение);
			
		КонецЦикла;
		Набор.Загрузить(ТабНабор);
		Набор.Записать(Истина);
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти
