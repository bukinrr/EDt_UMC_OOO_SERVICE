
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если Объект.ПолучателиЗатрат.Количество() <> 0 Тогда  	
		РедактироватьВСписке = Истина;
	КонецЕсли;	
	
	УстановитьВидимостьДоступностьПоСпособуРаспределенияЗатрат();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.СпособРаспределения = ПредопределенноеЗначение("Перечисление.СпособРаспределенияЗатрат.ПоСуммеПродаж")
		Или Объект.СпособРаспределения = ПредопределенноеЗначение("Перечисление.СпособРаспределенияЗатрат.ПоКоличествуУслуг") 	
	Тогда 
		Если Объект.ПолучателиЗатрат.Количество() <> 0 Тогда
			Если Не РедактироватьВСписке Тогда
				Объект.ПолучателиЗатрат.Очистить();
			Иначе 
				Для Каждого стрПолучателиЗатрат Из Объект.ПолучателиЗатрат Цикл
					стрПолучателиЗатрат.Коэффициент = 0;	
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	
	ОткрытьФорму("Справочник.КатегорииВыработки.ФормаВыбора", ПараметрыФормы, Элементы.ПолучателиЗатрат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпособРаспределенияПриИзменении(Элемент)

    РедактироватьВСписке = Объект.ПолучателиЗатрат.Количество() <> 0;
	УстановитьВидимостьДоступностьПоСпособуРаспределенияЗатрат();
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьВСпискеПриИзменении(Элемент)

	УстановитьВидимостьДоступностьПоСпособуРаспределенияЗатрат();
		
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиЗатратОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	стрПолучатель = Объект.ПолучателиЗатрат.Добавить();
	стрПолучатель.Получатель = ВыбранноеЗначение;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступностьПоСпособуРаспределенияЗатрат()
	
	Если Объект.СпособРаспределения = ПредопределенноеЗначение("Перечисление.СпособРаспределенияЗатрат.ПоСуммеПродаж")
		Или Объект.СпособРаспределения = ПредопределенноеЗначение("Перечисление.СпособРаспределенияЗатрат.ПоКоличествуУслуг") 	
	Тогда 
		Элементы.ПолучателиЗатрат.Доступность = РедактироватьВСписке;
		Элементы.ГруппаНастройкаРедактирования.Видимость = Истина;
		Элементы.ПолучателиЗатратКоэффициент.Видимость = Ложь;
	Иначе 
		Элементы.ПолучателиЗатрат.Доступность = Истина;
		Элементы.ГруппаНастройкаРедактирования.Видимость = Ложь;
		Элементы.ПолучателиЗатратКоэффициент.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти