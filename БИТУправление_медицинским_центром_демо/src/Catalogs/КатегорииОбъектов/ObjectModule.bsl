#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция проверяет, назначена ли категория каким-либо объектам
// 	Если назначена - менять назначение категории нельзя
//
// Возвращаемое значение:
//  Булево - Истина - если назначена, Ложь - если нет
//
Функция СуществуютСсылки() Экспорт
	Запрос = Новый Запрос();

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	РегистрСведений.КатегорииОбъектов.Категория КАК Категория
	|ИЗ
	|	РегистрСведений.КатегорииОбъектов
	|
	|ГДЕ
	|	РегистрСведений.КатегорииОбъектов.Категория = &Категория
	|";
	
	Запрос.УстановитьПараметр("Категория", Ссылка);
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

// Обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ)
	Если НЕ ОбменДанными.Загрузка
	   И НЕ ЭтоНовый() 
	   И НазначениеКатегории <> Ссылка.НазначениеКатегории 
	   И ЭтотОбъект.СуществуютСсылки() Тогда
	    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			    НСтр("ru ='Существуют объекты, которым назначена категория ""%1"".
						|Назначение категории не может быть изменено.'"),
				Наименование));
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
