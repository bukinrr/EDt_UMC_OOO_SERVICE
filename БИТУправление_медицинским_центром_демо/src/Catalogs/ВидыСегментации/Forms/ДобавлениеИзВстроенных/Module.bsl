#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьСписокВстроенныхВидовСегментации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьВидыСегментации(Команда)
	
	ЗагрузитьВидыСегментацииСервер();
	ПоказатьПредупреждение(,НСтр("ru = 'Выбранные позиции успешно загружены!'"));
	Оповестить("ОбновленСписокВидовСегментаций");
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьСписокВстроенныхВидовСегментации()
	
	Если Метаданные.РегистрыСведений.Найти("СегментыКлиентов") <> Неопределено Тогда
		Для Каждого МетаданныеМакета Из Метаданные.РегистрыСведений.СегментыКлиентов.Макеты Цикл
			ВидыСегментации.Добавить(МетаданныеМакета.Имя, МетаданныеМакета.Синоним);
		КонецЦикла;
	КонецЕсли;
	ВидыСегментации.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВидыСегментацииСервер()
	
	Для Каждого СтрокаВидыСегментации Из ВидыСегментации Цикл
		Если СтрокаВидыСегментации.Пометка Тогда
			ДанныеМакета = РегистрыСведений.СегментыКлиентов.ПолучитьМакет(СтрокаВидыСегментации.Значение);
			Справочники.ВидыСегментации.Импортировать(ДанныеМакета);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти
