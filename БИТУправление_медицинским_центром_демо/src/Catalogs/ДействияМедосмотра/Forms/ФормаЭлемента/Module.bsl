#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ГруппаПечать.Доступность = Объект.Вид = Перечисления.ВидыДействийМедицинскогоОсмотра.Исследование;
	Элементы.НомерКабинетаДляПечати.Доступность = ВычислитьДоступностьНомераКабинета(Объект);
	
	Элементы.Врач.Доступность = Объект.ПечататьВПрограммеПрохождения И Не Объект.ТолькоДляОтметкиИсследования;
	Элементы.ТолькоДляОтметкиИсследования.Доступность = Объект.ПечататьВПрограммеПрохождения;
	
	Элементы.ТекстЗаключенияПоУмолчанию.ПодсказкаВвода = МедосмотрыКлиентСервер.ТекстЗаключенияПоУмолчанию();
	Элементы.НаименованиеДляПечати.ПодсказкаВвода = Объект.Наименование;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АвтоформированиеЗаключения(Команда)
	
	Оповещение = Новый ОписаниеОповещения("АвтоформированиеЗаключенияПродолжение", ЭтотОбъект);
	РаботаСДиалогамиКлиент.ПроверитьМодифицированностьВФорме(ЭтотОбъект,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоформированиеЗаключенияПродолжение(Результат, Параметры) Экспорт
	
	Отбор = Новый Структура("ДействиеМедосмотра", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	
	ОткрытьФорму("РегистрСведений.ДействияМедосмотровПараметрыАвтозаключений.ФормаСписка", ПараметрыФормы, ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Элементы.НаименованиеДляПечати.ПодсказкаВвода = Объект.Наименование;
КонецПроцедуры

&НаКлиенте
Процедура ВидПриИзменении(Элемент)
	
	ЭтоИсследование = Объект.Вид = ПредопределенноеЗначение("Перечисление.ВидыДействийМедицинскогоОсмотра.Исследование");
	
	Элементы.ГруппаПечать.Доступность = ЭтоИсследование;
	Элементы.НомерКабинетаДляПечати.Доступность = ВычислитьДоступностьНомераКабинета(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечататьВПрограммеПрохожденияПриИзменении(Элемент)
	Элементы.Врач.Доступность = Объект.ПечататьВПрограммеПрохождения;
	Элементы.ТолькоДляОтметкиИсследования.Доступность = Объект.ПечататьВПрограммеПрохождения;
	Элементы.НомерКабинетаДляПечати.Доступность = ВычислитьДоступностьНомераКабинета(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ТолькоДляОтметкиИсследованияПриИзменении(Элемент)
	Объект.Врач = Неопределено;
	Элементы.Врач.Доступность = Объект.ПечататьВПрограммеПрохождения И Не Объект.ТолькоДляОтметкиИсследования;
	Элементы.НомерКабинетаДляПечати.Доступность = ВычислитьДоступностьНомераКабинета(Объект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВычислитьДоступностьНомераКабинета(Знач Объект)
	
	Если Объект.Вид <> ПредопределенноеЗначение("Перечисление.ВидыДействийМедицинскогоОсмотра.Исследование")
		Или (Объект.ПечататьВПрограммеПрохождения И Не Объект.ТолькоДляОтметкиИсследования)
	Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
