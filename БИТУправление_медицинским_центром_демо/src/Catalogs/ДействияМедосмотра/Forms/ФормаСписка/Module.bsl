#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСФормамиСервер.НастройкаПодбораПриСоздании(ЭтаФорма, Ложь);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	РаботаСФормамиКлиент.КнопкаПодборПриНажатии(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНоменклатуру(Команда)
	Если Вопрос(НСтр("ru ='Будет загружена номенклатура для действий медосмотров.'") + " " + НСтр("ru = 'Продолжить?'"), РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		ЗагрузитьНоменклатуруСервер();
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДействияМОБезНоменклатуры_29н(Команда)
	ОповещениеПродолжения = Новый ОписаниеОповещения("ДействияМОБезНоменклатуры_29нПродолжение", ЭтотОбъект);
	ОткрытьФорму("Справочник.ПриказыМедосмотров.ФормаВыбора",,,,,,ОповещениеПродолжения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ДействияМОБезНоменклатуры_29нПродолжение(Результат, ДопПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		СтруктураНаПечать = ПолучитьСтруктуруПечатиТабДокДействияМОБезНоменклатуры_29н(Результат);
		ОткрытьФорму("ОбщаяФорма.ПечатьДокументов",СтруктураНаПечать,,ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Подключаемый_ВыборПодбор(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока) Тогда
		ПрисвоитьНоменклатуруЭлементуСправочника(Элементы.Список.ТекущаяСтрока, ВыбранноеЗначение);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПрисвоитьНоменклатуруЭлементуСправочника(ДействиеМедосмотраСсылка, Номенклатура)
	
	СправочникОбъект = ДействиеМедосмотраСсылка.ПолучитьОбъект();
	СправочникОбъект.Номенклатура = Номенклатура;
	СправочникОбъект.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузитьНоменклатуруСервер()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДвоичныеДанныеВыгрузки = Справочники.ДействияМедосмотра.ПолучитьМакет("Номенклатура");
	ОбщегоНазначенияСервер.ВыполнитьЗагрузкуУниверсальнымОбменомXML(ДвоичныеДанныеВыгрузки, Истина);
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru ='Выполнено заполенние и создание номенклатуры'"));
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруПечатиТабДокДействияМОБезНоменклатуры_29н(Приказ)
	
	Возврат Справочники.ДействияМедосмотра.СтруктураПечатиДействийМОБезНоменклатуры_29н(Приказ);
	
КонецФункции

#КонецОбласти