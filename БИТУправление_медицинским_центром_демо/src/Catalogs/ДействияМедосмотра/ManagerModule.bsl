#Область ПрограммныйИнтерфейс

// Табличный документ списка действий медосмотра, которым не прописана Номенклатура клиники.
// 
// Возвращаемое значение:
//  Структура - печатная форма.
//
Функция СтруктураПечатиДействийМОБезНоменклатуры_29н(Приказ) Экспорт
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	ОбластьШапкаНомер	 = ТабДокумент.Область(1,1);
	ОбластьШапкаДействие = ТабДокумент.Область(1,2);
	
	ОбластьШапкаНомер.Текст		= "№";
	ОбластьШапкаДействие.Текст	= "Список действий медосмотра без номенклатуры:";
	
	ОбластьШапкаНомер.ЦветФона		 = ЦветаСтиля.ЦветФонаШапкиОтчета;
	ОбластьШапкаДействие.ЦветФона	 = ЦветаСтиля.ЦветФонаШапкиОтчета;
	
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	ОбластьШапкаНомер.Обвести(Линия,Линия,Линия,Линия);
	ОбластьШапкаДействие.Обвести(Линия,Линия,Линия,Линия);
	
	ТабДокумент.Область(1,1,1,2).Шрифт = Новый Шрифт(ТабДокумент.Область(1,1,1,2).Шрифт,,10,Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДействияМедосмотра.Наименование КАК НаименованиеПолное,
		|	ДействияМедосмотра.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПереченьВредныхФакторовИРабот.ЛабораторныеИФункциональныеИсследования КАК ПереченьВредныхФакторовИРаботЛабораторныеИФункциональныеИсследования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДействияМедосмотра КАК ДействияМедосмотра
		|		ПО ПереченьВредныхФакторовИРаботЛабораторныеИФункциональныеИсследования.Исследование = ДействияМедосмотра.Ссылка
		|ГДЕ
		|	ДействияМедосмотра.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ПереченьВредныхФакторовИРаботЛабораторныеИФункциональныеИсследования.Ссылка.НомерПП <> """"
		|	И ПереченьВредныхФакторовИРаботЛабораторныеИФункциональныеИсследования.Ссылка.Приказ = &Приказ";
	
	Запрос.УстановитьПараметр("Приказ", Приказ); 
	
	ДействияМО = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ДействиеМО Из ДействияМО Цикл
		
		НомерСтроки = ДействияМО.Индекс(ДействиеМО)+2;
		
		ОбластьСтрокиДействия = ТабДокумент.ПолучитьОбласть(НомерСтроки,1,НомерСтроки,2);
		
		ОбластьНомер	= ОбластьСтрокиДействия.Область(1,1);
		ОбластьДействие = ОбластьСтрокиДействия.Область(1,2);
		
		ОбластьНомер.Текст		= НомерСтроки - 1;
		ОбластьДействие.Текст	= ДействиеМО.НаименованиеПолное;
		ОбластьДействие.Расшифровка = ДействиеМО.Ссылка;
		
		ОбластьНомер.Обвести(Линия,Линия,Линия,Линия);
		ОбластьДействие.Обвести(Линия,Линия,Линия,Линия);
		
		ТабДокумент.Вывести(ОбластьСтрокиДействия);
	КонецЦикла;
	
	ТабДокумент.Область("C1").ШиринаКолонки = 6;
	ТабДокумент.Область("C2").ШиринаКолонки = 110;
	ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.Защита = Истина;
	
	СтруктураНаПечать = Новый Структура;
	
	ОписаниеПечатнойФормы = Новый Структура;
	ОписаниеПечатнойФормы.Вставить("СинонимМакета", "Список действий медосмотров в 29н без номенклатуры");
	ОписаниеПечатнойФормы.Вставить("Экземпляров",	1);
	ОписаниеПечатнойФормы.Вставить("ИмяМакета", "СписокДействияМОБезНоменклатуры_29н");
	ОписаниеПечатнойФормы.Вставить("ПолныйПутьКМакету", "");
	ОписаниеПечатнойФормы.Вставить("ИмяФайлаПечатнойФормы", "");
	ОписаниеПечатнойФормы.Вставить("ТабличныйДокумент", ТабДокумент);
	
	КоллекцияПечатныхФорм = Новый Массив; 
	КоллекцияПечатныхФорм.Добавить(ОписаниеПечатнойФормы);
	СтруктураНаПечать.Вставить("КоллекцияПечатныхФорм",КоллекцияПечатныхФорм);
	
	Возврат СтруктураНаПечать;
	
КонецФункции

#КонецОбласти