#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ТипСертификата = ПредопределенноеЗначение("Перечисление.ТипыСертификатов.НаОплату") Тогда
		
		ИндексНоменклатуры = ПроверяемыеРеквизиты.Найти("Номенклатура");
		Если ИндексНоменклатуры <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексНоменклатуры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ТипСертификата = Перечисления.ТипыСертификатов.НаОплату Тогда
		Если Услуги.Количество()<>0 Тогда
			Услуги.Очистить();
		КонецЕсли;
		
	ИначеЕсли Услуги.Количество() = 0
		И Не (КомплексныйСертификат И ПополняемыйСертификат) 
		И Не ПометкаУдаления
	Тогда
		ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Не указаны услуги, выполняемые по данному виду сертификата'"), Отказ);
		
	// Проверка, что в комплексном сертификате нет групп.
	ИначеЕсли КомплексныйСертификат
			И Услуги.Количество() <> 0
	Тогда
		Для Каждого СтрокаУслуги Из Услуги Цикл
			Если СтрокаУслуги.Фильтр.ЭтоГруппа Тогда
				
				ОбщегоНазначения.СообщитьОбОшибке(НСтр("ru='Нельзя использовать группы услуг в комплексных сертификатах!'"), Отказ);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти