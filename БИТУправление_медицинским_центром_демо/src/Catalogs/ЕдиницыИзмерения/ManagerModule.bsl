#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	Если ВидФормы = "ФормаОбъекта" Тогда
		ТипЕдиницы = Неопределено;
		Если Параметры.Свойство("Ключ") И Не Параметры.Ключ.Пустая() Тогда
			ТипЕдиницы = Параметры.Ключ.ТипЕдиницы;
		ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") Тогда
			Если Параметры.ЗначенияЗаполнения.Свойство("Номенклатура") Тогда
				ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Упаковка;
				Параметры.ЗначенияЗаполнения.Вставить("ТипЕдиницы", ТипЕдиницы);
			Иначе
				Параметры.ЗначенияЗаполнения.Свойство("ТипЕдиницы", ТипЕдиницы);
			КонецЕсли;
		ИначеЕсли Параметры.Свойство("ЗначениеКопирования") Тогда
			ТипЕдиницы = Параметры.ЗначениеКопирования.ТипЕдиницы;
		КонецЕсли;
		
		Если ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Упаковка Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаУпаковкиНоменклатуры";
		КонецЕсли;
	ИначеЕсли ВидФормы = "ФормаВыбора" Тогда
		Если Параметры.Свойство("ВыборУпаковкиНоменклатуры") Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаВыбораУпаковокНоменклатуры";
		ИначеЕсли Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Номенклатура") Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаВыбораЕдиницИзмеренияНоменклатуры";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти  

#Область ПрограммныйИнтерфейс

// Возвращает количество базовых единиц номенклатуры в упаковке
//
// Параметры:
//  Объект - СправочникСсылка.ЕдиницыИзмерения,СправочникСсылка.Номенклатура  - упаковка/номенклатура
// 
// Возвращаемое значение:
//  Число - количество базовых единиц номенклатуры в упаковке
//
Функция ПолучитьКоличествоБазовыхЕдиницВУпаковке(Объект) Экспорт
	
	Упаковка = ?(ТипЗнч(Объект) = Тип("СправочникСсылка.Номенклатура"), Объект.Упаковка, Объект);	
	
	Если Упаковка <> Неопределено Тогда
		Если Упаковка.ТипЕдиницы = Перечисления.ТипыЕдиницИзмерения.Упаковка Тогда
			Если Упаковка.СостоитИзДругихУпаковок Тогда
				Возврат Упаковка.КоличествоВУпаковке * ПолучитьКоличествоБазовыхЕдиницВУпаковке(Упаковка.ВложеннаяУпаковка);		
			Иначе
				Возврат Упаковка.Коэффициент * ПолучитьКоличествоБазовыхЕдиницВУпаковке(Упаковка.БазоваяЕдиницаИзмерения);	
			КонецЕсли;
		Иначе
			Возврат Упаковка.Коэффициент;	
		КонецЕсли;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
