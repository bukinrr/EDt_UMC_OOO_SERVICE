#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьСписокНоменклатуры();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриИзмененииСрокДействия()
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьСписокНоменклатуры();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ШаблонПечатиПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.Наименование)
		И ЗначениеЗаполнено(Объект.ШаблонПечати)
	Тогда
		Объект.Наименование = Строка(Объект.ШаблонПечати);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СрокДействияПриИзменении(Элемент)
	ПриИзмененииСрокДействия();
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСрокДействия()
	
	Элементы.НадписьСрокНеограничен.Видимость = Объект.СрокДействия = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураВидовДоговоровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	РаботаСДиалогамиКлиент.ПередНачаломДобавленияВПодчиненныйСписокНаФормеОбъекта(ЭтаФорма, Элемент, Отказ); 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ПрочитатьСписокНоменклатуры()
	
	Если Объект.Ссылка <> Справочники.ВидыДоговоров.ПустаяСсылка() Тогда
		
		Отбор = Новый Структура("ВидДоговора");
		
		Отбор.ВидДоговора = Объект.Ссылка;
		
		Выборка = РегистрыСведений.НоменклатураВидовДоговоров.Выбрать(Отбор);
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = НоменклатураВидовДоговоров.Добавить();
			
			НоваяСтрока.Номенклатура = Выборка.Номенклатура;
			
		КонецЦикла;
		
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ЗаписатьСписокНоменклатуры() 
	
    // Удаляем из регистра старые записи с этим видом договора.
	НаборЗаписей = РегистрыСведений.НоменклатураВидовДоговоров.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.ВидДоговора.Установить(Объект.Ссылка);
	
	НаборЗаписей.Записать();
	
	// Записываем новые
	НаборЗаписей = РегистрыСведений.НоменклатураВидовДоговоров.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.ВидДоговора.Установить(Объект.Ссылка);
	
	Выгрузка = НоменклатураВидовДоговоров.Выгрузить();
	
	Для Каждого Строка Из Выгрузка Цикл
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		НоваяЗапись.Номенклатура = Строка.Номенклатура;
		
		НоваяЗапись.ВидДоговора  = Объект.Ссылка;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти 