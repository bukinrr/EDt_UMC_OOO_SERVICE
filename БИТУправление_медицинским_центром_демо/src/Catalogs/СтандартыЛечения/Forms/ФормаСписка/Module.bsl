#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСтандартыПервичнойПомощи(Команда)
	
	ПоказатьПредупреждениеПередЗагрузкойСправочника(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСтандартыСпециализированнойПомощи(Команда)
	
	ПоказатьПредупреждениеПередЗагрузкойСправочника(2);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура ЗагрузитьСправочникПослеПредупреждения(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		Состояние(НСтр("ru='Идёт загрузка...'"));
		ОбщегоНазначенияСервер.ПроцедураМенеджера("Справочники.СтандартыЛечения", "ЗагрузкаРегламентированныхСтандартов", ДополнительныеПараметры.ТипыРегламентированныхСтандартов);
		Элементы.Список.Обновить();
		ПоказатьПредупреждение(,НСтр("ru='Загрузка завершена'"), 60);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьПредупреждениеПередЗагрузкойСправочника(ТипРегламентированногоСтандарта)
	
	ТипыРегламентированныхСтандартов = Новый Массив;
	ТипыРегламентированныхСтандартов.Добавить(999);
	ТипыРегламентированныхСтандартов.Добавить(ТипРегламентированногоСтандарта);
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьСправочникПослеПредупреждения", ЭтотОбъект, Новый Структура("ТипыРегламентированныхСтандартов", ТипыРегламентированныхСтандартов));
	ТекстВопроса = НСтр("ru='Регламентированные стандарты будут загружены, кроме стандартов с включенным ручным редактированием.'") + " " + НСтр("ru='Продолжить?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, 60, КодВозвратаДиалога.ОК);
	
КонецПроцедуры

#КонецОбласти