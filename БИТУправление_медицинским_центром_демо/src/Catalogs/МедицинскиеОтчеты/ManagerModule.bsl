#Область ПрограммныйИнтерфейс	

// Вызывается при первом заполнении либо в случае необходимости обновления справочника медицинских отчетов.
//
Процедура ЗаполнитьСписокМедицинскихОтчетов() Экспорт
    	    			
	ОтчетыБазы = ПереченьОтчетов();

	Если ОтчетыБазы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьСправочникОтчетов(ОтчетыБазы);
	
КонецПроцедуры

// Перечень встроенных видов отчетности. Заполняют справочник автоматически. Не подлежат редактированию.
// 
// Возвращаемое значение:
//  Массив - содержит структуры с ключами Наименование, Описание, ИмяМетаданных, Идентификатор.
//
Функция ПереченьОтчетов() Экспорт
	
	Перечень = Новый Массив;
	
	МакетОтчеты = ПолучитьМакет("ПереченьОтчетов");
	
	Для Сч = 2 По МакетОтчеты.ВысотаТаблицы Цикл
		
		Наименование = МакетОтчеты.Область(Сч, 1).Текст;
		
		Если ЗначениеЗаполнено(Наименование) Тогда
			
			Описание		 = МакетОтчеты.Область(Сч, 2).Текст;
			ИмяМетаданных	 = МакетОтчеты.Область(Сч, 3).Текст;
			Идентификатор	 = МакетОтчеты.Область(Сч, 4).Текст;
			
			ОписаниеОтчета = Новый Структура("Наименование, Описание, ИмяМетаданных, Идентификатор", Наименование, Описание, ИмяМетаданных, Идентификатор);
			
			Перечень.Добавить(ОписаниеОтчета);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Перечень;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьСправочникОтчетов(ОтчетыБазы)
	
	СпрОтчеты = Справочники.МедицинскиеОтчеты;

	Для Каждого ОписаниеОтчета Из ОтчетыБазы Цикл
		
		НайденныйЭлемент = НайтиОтчетПоРеквизитам(ОписаниеОтчета.Наименование, ОписаниеОтчета.ИмяМетаданных);
		Если Не ЗначениеЗаполнено(НайденныйЭлемент) Тогда
			
			// Создаем новый элемент справочника.
			НовыйЭлемент                = СпрОтчеты.СоздатьЭлемент();
			НовыйЭлемент.Наименование   = ОписаниеОтчета.Наименование;
			НовыйЭлемент.Описание       = ОписаниеОтчета.Описание;
			НовыйЭлемент.ИсточникОтчета = ОписаниеОтчета.ИмяМетаданных;
			//НовыйЭлемент.Периоды		= Новый ХранилищеЗначения(РазложитьСтрокуПериодов(Периоды));
			НовыйЭлемент.УИДОтчета		= ОписаниеОтчета.Идентификатор;
			НовыйЭлемент.Записать();
		Иначе
			// Обновляем реквизиты найденного элемента.
			ТекЭлемент = НайденныйЭлемент.ПолучитьОбъект();
			
			Если Не ТекЭлемент.ВнешнийОтчетИспользовать Тогда
				ТекЭлемент.Наименование = ОписаниеОтчета.Наименование;
				ТекЭлемент.Описание     = ОписаниеОтчета.Описание;
			КонецЕсли;
			//ТекЭлемент.Периоды		= Новый ХранилищеЗначения(РазложитьСтрокуПериодов(Периоды));
			ТекЭлемент.УИДОтчета	= ОписаниеОтчета.Идентификатор;
			ТекЭлемент.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
	
Функция НайтиОтчетПоРеквизитам(Наименование, ИмяМетаданных = "", ЭтоГруппа = Ложь)
	
	СпрОтчеты = Справочники.МедицинскиеОтчеты;
	
	Если ЭтоГруппа Тогда
		
		НайденнаяГруппа = СпрОтчеты.НайтиПоНаименованию(Наименование, Истина);
		Если ЗначениеЗаполнено(НайденнаяГруппа) Тогда
			Возврат НайденнаяГруппа;
		КонецЕсли;
	Иначе
		НайденныйЭлемент = СпрОтчеты.НайтиПоРеквизиту("ИсточникОтчета", СокрЛП(ИмяМетаданных));

		Если ЗначениеЗаполнено(НайденныйЭлемент) Тогда
			Возврат НайденныйЭлемент;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти