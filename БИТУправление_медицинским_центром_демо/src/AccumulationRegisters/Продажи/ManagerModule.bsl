#Область ПрограммныйИнтерфейс

// Продажи за короткий период. Не желательна для получения оборотов более двух дней.
//
// Параметры:
//  ПараметрыОтбора	 - Структура - с ключем Дата и возможными ключами Номенклатура - Массив из СправочникСсылка.Номенклатура и Клиент - СправочникСсылка.Клиент.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с колонками Номенклатура, Клиент, Сотрудник, Количество.
//
Функция ПолучитьПродажиКороткогоПериода(ПараметрыОтбора) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ПараметрыОтбора.Дата));
	Запрос.УстановитьПараметр("ДатаКонца",  КонецДня(ПараметрыОтбора.Дата));
	
	Запрос.УстановитьПараметр("ЕстьОтборНоменклатура", ПараметрыОтбора.Свойство("Номенклатура"));
	Запрос.УстановитьПараметр("Номенклатура", ?(Запрос.Параметры.ЕстьОтборНоменклатура, ПараметрыОтбора.Номенклатура, Неопределено));
	
	Если ПараметрыОтбора.Свойство("Клиент")
		И ЗначениеЗаполнено(ПараметрыОтбора.Клиент)
	Тогда
		Запрос.УстановитьПараметр("Клиент", ПараметрыОтбора.Клиент);
	Иначе
		Запрос.УстановитьПараметр("Клиент", Неопределено);
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Продажи.Номенклатура КАК Номенклатура,
	|	Продажи.Клиент КАК Клиент,
	|	Продажи.Сотрудник КАК Сотрудник,
	|	Продажи.Количество КАК Количество
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Период МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И Продажи.Активность
	|	И (&Клиент = НЕОПРЕДЕЛЕНО
	|			ИЛИ Продажи.Клиент В (&Клиент))
	|	И (&ЕстьОтборНоменклатура = ЛОЖЬ
	|			ИЛИ Продажи.Номенклатура В (&Номенклатура))"
	;
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти