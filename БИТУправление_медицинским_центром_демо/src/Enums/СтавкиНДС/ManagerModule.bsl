#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет конкретную ставку НДС, действующую в указанный день, по виду ставки.
//
// Параметры:
//  ВидСтавки	 - ПеречислениеСсылка.ВидыСтавокНДС	 - вид ставки товарной позиции.
//  Период		 - Дата	 - дата документа.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтавкиНДС.
//
Функция СтавкаНДС(ВидСтавки, Период) Экспорт
	
	Если ВидСтавки = Перечисления.ВидыСтавокНДС.Общая Тогда
		Возврат УчетНДСКлиентСервер.ОбщаяСтавкаНДС(Период);
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.ОбщаяРасчетная Тогда
		Возврат УчетНДСКлиентСервер.ОбщаяРасчетнаяСтавкаНДС(Период);
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.Пониженная Тогда
		Возврат Перечисления.СтавкиНДС.НДС10;
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.ПониженнаяРасчетная Тогда
		Возврат Перечисления.СтавкиНДС.НДС10_110;
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.Нулевая Тогда
		Возврат Перечисления.СтавкиНДС.НДС0;
	ИначеЕсли ВидСтавки = Перечисления.ВидыСтавокНДС.БезНДС Тогда
		Возврат Перечисления.СтавкиНДС.БезНДС;
	Иначе
		Возврат Перечисления.СтавкиНДС.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	До2019Года = ОбщегоНазначения.ТекущаяДатаПользователя() < '20190101';
	
	Если Параметры.Свойство("ОграниченныйСписок") Тогда
		
		// Ограниченный список для счета-фактуры на аванс.
		
		Если До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
		Иначе
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		КонецЕсли;
	ИначеЕсли Параметры.Свойство("ОсновныеСтавкиНДС") Тогда
		
		// Ограниченный список для безвозмездной передачи
		
		Если До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
		Иначе
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
		КонецЕсли;
	
	Иначе
		
		Если До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		Иначе
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС0);
		ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.БезНДС);
		
		Если До2019Года Тогда
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
		Иначе
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
			ДанныеВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли