#Область ПрограммныйИнтерфейс

// Имена реквизитов display name и code у классификатора.
//
// Параметры:
//  ВидКлассификатора	 - Перечисления.ВидыКлассификаторовМинЗдрава - вид классификатора.
// 
// Возвращаемое значение:
//  Структура - с ключами DisplayName, Code - строки.
//
Функция ПолучитьИмяРеквизитаDisplayNameИCode(ВидКлассификатора) Экспорт
	
	СтруктураВозвращаемогоЗначения = Новый Структура;
	
	Если ВидКлассификатора = Перечисления.ВидыКлассификаторовМинЗдрава.КлассификаторКатегорийЛьгот Тогда
		СтруктураВозвращаемогоЗначения.Вставить("DisplayName", "ПолноеНаименование");
	ИначеЕсли ВидКлассификатора = Перечисления.ВидыКлассификаторовМинЗдрава.УровеньКонфиденциальностиМедицинскогоДокумента Тогда
		СтруктураВозвращаемогоЗначения.Вставить("ЭтоКлючАтрибута_Code", Истина);
		СтруктураВозвращаемогоЗначения.Вставить("Code", "CODE");
	ИначеЕсли ВидКлассификатора = Перечисления.ВидыКлассификаторовМинЗдрава.ЛьготныеКатегорииГраждан Тогда
		СтруктураВозвращаемогоЗначения.Вставить("ЭтоКлючАтрибута_Code", Истина);
		СтруктураВозвращаемогоЗначения.Вставить("Code", "ID");
	ИначеЕсли ВидКлассификатора = Перечисления.ВидыКлассификаторовМинЗдрава.СправочникКодовИнтерпретацииРезультатов Тогда
		СтруктураВозвращаемогоЗначения.Вставить("ЭтоКлючАтрибута_Code", Истина);
		СтруктураВозвращаемогоЗначения.Вставить("Code", "CODE");
	ИначеЕсли ВидКлассификатора = Перечисления.ВидыКлассификаторовМинЗдрава.СубъектыРФ Тогда
		СтруктураВозвращаемогоЗначения.Вставить("ЭтоКлючАтрибута_Code", Истина);
		СтруктураВозвращаемогоЗначения.Вставить("Code", "CODE_FNS");
	ИначеЕсли ВидКлассификатора = Перечисления.ВидыКлассификаторовМинЗдрава.ЛьготныеКатегорииНаселения Тогда
		СтруктураВозвращаемогоЗначения.Вставить("ЭтоКлючАтрибута_Name", Истина);
		СтруктураВозвращаемогоЗначения.Вставить("Name", "NAME_SHORT");
	//ИначеЕсли ВидКлассификатора = Перечисления.ВидыКлассификаторовМинЗдрава.РолиСотрудниковПриПодписанииМедицинскихДокументовВТомЧислеВЭлектронномВиде Тогда
	//	СтруктураВозвращаемогоЗначения.Вставить("ЭтоКлючАтрибута_Code", Истина);
	//	СтруктураВозвращаемогоЗначения.Вставить("Code", "CODE_ROLE");
	КонецЕсли;
	
	Возврат СтруктураВозвращаемогоЗначения;
	
КонецФункции

// Виды классификаторов для тех параметров приема, что имеют тип: Классификаторы минздрава.
//
// Параметры:
//  ПустойРезультатПриОтсутствииПравДоступа	 - Булево - если у пользователя нет прав, предполагающих ввода медицинских документов, то дать пустой результат.
// 
// Возвращаемое значение:
//  Соответствие - где Ключ - ПланВидовХарактеристикСсылка.ПараметрыHTML, Значение - ПеречислениеСсылка.ВидыКлассификаторовМинЗдрава.
//
Функция ВидыКлассификаторовПараметровПриемаФРНСИ(ПустойРезультатПриОтсутствииПравДоступа = Ложь) Экспорт
	
	Результат = Новый Соответствие;
	
	// Заполнение, если выполнение без учета прав ввода или требуется полный результат в любом случае.
	Если Не ПустойРезультатПриОтсутствииПравДоступа
		Или ПравоДоступа("Редактирование", Метаданные.Документы.Прием)
	Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Ссылка,
		|	ВидКлассификатора
		|ИЗ
		|	ПланВидовХарактеристик.ПараметрыHTML КАК ПараметрыHTML
		|ГДЕ
		|	ПараметрыHTML.ВидКлассификатора <> ЗНАЧЕНИЕ(Перечисление.ВидыКлассификаторовМинЗдрава.ПустаяСсылка)"
		;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Вставить(Выборка.Ссылка, Выборка.ВидКлассификатора); 
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти