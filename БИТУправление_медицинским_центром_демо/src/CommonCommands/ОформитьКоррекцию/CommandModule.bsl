#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ЗначениеЗаполнено(ПараметрКоманды) Тогда
		// Запрет коррекций для некоторых видов операций
		Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") 
			Или ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
		Тогда
			ВидОперации = ДопСерверныеФункции.ПолучитьРеквизит(ПараметрКоманды, "ВидОперации");
			Если ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.ВзносНаличнымиВБанк")
				Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПКО.ПолучениеНаличныхВБанке")
			Тогда
				ШаблонСообщения = НСтр("ru='Для операции ""%1"" не допускается оформление коррекции.'");
				ПоказатьПредупреждение(, СтрШаблон(ШаблонСообщения, ВидОперации));
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Запрет коррекций для ЭС НСПК
		Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ОказаниеУслуг") 
			И ЗначениеЗаполнено(ОбщегоНазначенияСервер.ПолучитьРеквизитСсылки(ПараметрКоманды, "ИдентификаторКорзиныНСПК"))
		Тогда
			ПоказатьПредупреждение(, "Для чеков с ЭС НСПК не допускается оформление коррекции.");
			Возврат;
		КонецЕсли;
		
		// Открытие формы
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("ДокументОснование, Коррекция",
		ПараметрКоманды,
		Истина));
		
		ОткрытьФорму(РаботаСФормамиСервер.ПолучитьПолноеИмяОсновнойФормыПоСсылке(ПараметрКоманды), ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
