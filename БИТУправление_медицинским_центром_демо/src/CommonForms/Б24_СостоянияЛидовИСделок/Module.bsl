#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСпискиВыбора();
	ЗаполнитьСтатусы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	Сохранить();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСпискиВыбора()
	
	ТаблицаСтатусов = ПолучитьТаблицуСтатусов();
	
	// Лиды
	Для Каждого Статус Из ТаблицаСтатусов.НайтиСтроки(Новый Структура("Сущность", "lead")) Цикл
		Если Статус.ГруппаСтатусов = "process" Или Статус.ГруппаСтатусов = "success" Тогда
			ДобавитьВСписокВыбора("ЛидКЗагрузке", Статус);
			ДобавитьВСписокВыбора("ЛидПроработка", Статус);
			ДобавитьВСписокВыбора("ЛидЗаписанНаПрием", Статус);
			ДобавитьВСписокВыбора("ЛидУспешный", Статус);
			ДобавитьВСписокВыбора("ЛидПриВыгрузке", Статус);
		Иначе
			ДобавитьВСписокВыбора("ЛидОтказ", Статус);
			ДобавитьВСписокВыбора("ЛидПросрочка", Статус);
		КонецЕсли;
	КонецЦикла;
	
	// Сделки
	Для Каждого Статус Из ТаблицаСтатусов.НайтиСтроки(Новый Структура("Сущность", "deal")) Цикл
		Если Статус.ГруппаСтатусов = "process" Или Статус.ГруппаСтатусов = "success" Тогда
			ДобавитьВСписокВыбора("СделкаЗавершена", Статус);
			ДобавитьВСписокВыбора("СделкаНовая", Статус);
		Иначе
			ДобавитьВСписокВыбора("СделкаОтказ", Статус);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТаблицуСтатусов()
	
	ТаблицаСтатусов = Новый ТаблицаЗначений;
	ТаблицаСтатусов.Колонки.Добавить("ИдентификаторСтатуса");
	ТаблицаСтатусов.Колонки.Добавить("НаименованиеСтатуса");
	ТаблицаСтатусов.Колонки.Добавить("Сущность");
	ТаблицаСтатусов.Колонки.Добавить("ГруппаСтатусов");
	
	ДополнитьТаблицуСтатусов(ТаблицаСтатусов, "STATUS",		"lead");
	ДополнитьТаблицуСтатусов(ТаблицаСтатусов, "DEAL_STAGE",	"deal");
	
	Возврат ТаблицаСтатусов;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДополнитьТаблицуСтатусов(ТаблицаСтатусов, ИдентификаторСправочника, Сущность)
	
	Статусы = Битрикс.ВыполнитьМетод("crm.status", "list", , , Новый Структура("ENTITY_ID", ИдентификаторСправочника));
	Если Статусы <> Неопределено Тогда
		Для Каждого Статус Из Статусы Цикл
			НовыйСтатус = ТаблицаСтатусов.Добавить();
			НовыйСтатус.ИдентификаторСтатуса = Статус["STATUS_ID"];
			НовыйСтатус.НаименованиеСтатуса = Статус["NAME"];
			НовыйСтатус.Сущность = Сущность;
			НовыйСтатус.ГруппаСтатусов = Статус["EXTRA"]["SEMANTICS"];
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВСписокВыбора(ИмяЭлемента, Статус)
	
	Попытка
		Элементы[ИмяЭлемента].СписокВыбора.Добавить(Статус.ИдентификаторСтатуса, Статус.НаименованиеСтатуса);
	Исключение КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусы()
	
	Статусы = БитриксПовтИсп.СписокСтатусов();
	
	ЛидЗаписанНаПрием	= Статусы.Получить(Перечисления.Б24_Статусы.ЛидЗаписанНаПрием);
	ЛидКЗагрузке		= Статусы.Получить(Перечисления.Б24_Статусы.ЛидКЗагрузке);
	ЛидОтказ			= Статусы.Получить(Перечисления.Б24_Статусы.ЛидОтказ);
	ЛидПросрочен		= Статусы.Получить(Перечисления.Б24_Статусы.ЛидПросрочен);
	ЛидУспешный			= Статусы.Получить(Перечисления.Б24_Статусы.ЛидУспешный);
	ЛидПриВыгрузке		= Статусы.Получить(Перечисления.Б24_Статусы.ЛидПриВыгрузке);
	СделкаЗавершена		= Статусы.Получить(Перечисления.Б24_Статусы.СделкаЗавершена);
	СделкаНовая			= Статусы.Получить(Перечисления.Б24_Статусы.СделкаНовая);
	СделкаОтказ			= Статусы.Получить(Перечисления.Б24_Статусы.СделкаОтказ);
	
	ЗаполнитьПустыеСтатусыПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПустыеСтатусыПоУмолчанию()
	
	Если Не ЗначениеЗаполнено(ЛидОтказ) Тогда
		ЛидОтказ = "JUNK";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЛидПросрочен) Тогда
		ЛидПросрочен = "JUNK";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЛидУспешный) Тогда
		ЛидУспешный = "CONVERTED";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЛидПриВыгрузке) Тогда
		ЛидПриВыгрузке = "NEW";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СделкаНовая) Тогда
		СделкаНовая = "NEW";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СделкаЗавершена) Тогда
		СделкаЗавершена = "WON";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СделкаОтказ) Тогда
		СделкаОтказ = "LOSE";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура Сохранить()
	
	НЗ = РегистрыСведений.Б24_Статусы.СоздатьНаборЗаписей();
	
	НЗ.Загрузить(ПолучитьТЗСтатусов());
	НЗ.Записать();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТЗСтатусов()
	
	тзСтатусов = Новый ТаблицаЗначений;
	тзСтатусов.Колонки.Добавить("Статус");
	тзСтатусов.Колонки.Добавить("ИдентификаторСтатуса");
	
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.ЛидЗаписанНаПрием,	ЛидЗаписанНаПрием);
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.ЛидКЗагрузке,		ЛидКЗагрузке);
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.ЛидОтказ,			ЛидОтказ);
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.ЛидПросрочен,		ЛидПросрочен);
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.ЛидУспешный,		ЛидУспешный);
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.ЛидПриВыгрузке,		ЛидПриВыгрузке);
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.СделкаЗавершена,	СделкаЗавершена);
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.СделкаНовая,		СделкаНовая);
	ДобавитьСтрокуВТаблицуСтатусов(тзСтатусов, Перечисления.Б24_Статусы.СделкаОтказ,		СделкаОтказ);
	
	Возврат тзСтатусов;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьСтрокуВТаблицуСтатусов(Статусы, Статус, ИдентификаторСтатуса)
	
	НоваяНастройка = Статусы.Добавить();
	НоваяНастройка.Статус = Статус;
	НоваяНастройка.ИдентификаторСтатуса = ИдентификаторСтатуса;
	
КонецПроцедуры

#КонецОбласти