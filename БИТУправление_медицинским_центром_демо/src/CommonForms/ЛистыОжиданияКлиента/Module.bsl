#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Заявка") И ЗначениеЗаполнено(Параметры.Заявка) Тогда
		Заявка = Параметры.Заявка;
		ОбновитьЛистыОжидания();
	КонецЕсли;
	
	Если ЛистыОжидания.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.ТолькоПросмотр = Не ПравоДоступа("Изменение", Метаданные.Документы.ЛистОжидания);
	
	Для Каждого СтрокаРаботы Из Заявка.Работы Цикл
		ОбщегоНазначенияКлиентСервер.КонкатенацияСтрок(Работы, Строка(СтрокаРаботы.Номенклатура));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ЛистыОжиданияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекСтрока = ЛистыОжидания.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ТекСтрока <> Неопределено Тогда
		ОткрытьФорму("Документ.ЛистОжидания.ФормаОбъекта", Новый Структура("Ключ", ТекСтрока.Ссылка), ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОбновитьЛистыОжидания();
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВыполнениеЛистаОжидания(Команда)
	ОтметитьВыполнениеЛистаОжиданияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	УстановитьОтметкиЛистов(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	УстановитьОтметкиЛистов(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьЛистыОжидания()
	
	Сотрудник = Заявка.Сотрудник;
	РаботыЗаявки = Заявка.Работы.ВыгрузитьКолонку("Номенклатура");
	СпециализацииСотрудника = Сотрудник.Специализации.ВыгрузитьКолонку("Специализация");
	
	ВыборкаДетальныеЗаписи = CRMСервер.НайтиВероятныеЛистыОжиданияПоЗаявке(Заявка, Ложь);
	
	ЛистыОжидания.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЛО = ЛистыОжидания.Добавить();
		ЗаполнитьЗначенияСвойств(ЛО, ВыборкаДетальныеЗаписи);
		// Собираем информацию об услугах листа ожидания.
		СтрокаУслуги = "";
		ВыборкаРаботы = ВыборкаДетальныеЗаписи.Работы.Выбрать();
		Пока ВыборкаРаботы.Следующий() Цикл
			Если ЗначениеЗаполнено(ВыборкаРаботы.Номенклатура) Тогда
				СтрокаУслуги = СтрокаУслуги + ВыборкаРаботы.Номенклатура.Наименование + ", ";	
			КонецЕсли;
		КонецЦикла;
		ЛО.Услуги = СтрокаУслуги;
		// Проверяем наличие совпадений в листе ожидания с заявкой по номенклатуре.
		ЕстьОбщиеУслуги = Ложь;
		ВыборкаРаботы = ВыборкаДетальныеЗаписи.Работы.Выбрать();
		Пока ВыборкаРаботы.Следующий() Цикл
			Для Каждого РаботаЗаявки Из РаботыЗаявки Цикл
				Если РаботаЗаявки = ВыборкаРаботы.Номенклатура Тогда
					ЕстьОбщиеУслуги = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		// Проверяем наличие совпадений в листе ожидания с заявкой по специализации Сотрудника.
		ЕстьОбщаяСпециализация = Ложь;
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Специализация) Тогда
			Для Каждого СпециализацияСотрудника Из СпециализацииСотрудника Цикл
				Если СпециализацияСотрудника = ВыборкаДетальныеЗаписи.Специализация Тогда
					ЕстьОбщаяСпециализация = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	    // Автоматически помечаем листы ожидания, которые предположительно относятся к текущей заявке. Анализ сотрудника.
		Если ЗначениеЗаполнено(ЛО.Сотрудник) И ЛО.Сотрудник = Сотрудник Тогда
			ЛО.Выбран = ЕстьОбщиеУслуги ИЛИ ВыборкаРаботы.Количество() = 0;	
		ИначеЕсли НЕ ЗначениеЗаполнено(ЛО.Сотрудник) Тогда
			ЛО.Выбран = ЕстьОбщиеУслуги ИЛИ (ВыборкаРаботы.Количество() = 0 И ЕстьОбщаяСпециализация);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьВыполнениеЛистаОжиданияНаСервере()
	
	Для Каждого ЛистОжидания Из ЛистыОжидания Цикл
		Если ЛистОжидания.Выбран Тогда
			ЛООбъект = ЛистОжидания.Ссылка.ПолучитьОбъект();
			ЛООбъект.Выполнен = Истина;
			ЛООбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	// Обновляем информацию на форме.
	ОбновитьЛистыОжидания();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметкиЛистов(Пометка)
	
	Для Каждого ЛистОжидания Из ЛистыОжидания Цикл
		ЛистОжидания.Выбран = Пометка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
