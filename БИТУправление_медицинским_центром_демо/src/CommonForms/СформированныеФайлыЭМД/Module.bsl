#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МедицинскийДокумент = Параметры.МедицинскийДокумент;
	
	ИмяТипаДокумента = ДопСерверныеФункции.ИмяСсылочногоТипа(МедицинскийДокумент);
	
	Если ИмяТипаДокумента = "Документ.ПрохождениеМедосмотра" Тогда
		УстановитьТекстЗапросаПМО(МедицинскийДокумент);
	ИначеЕсли ИмяТипаДокумента = "Документ.ВрачебнаяКомиссия" Тогда // Версия КОРП
		УстановитьТекстЗапросаВК(МедицинскийДокумент);
	КонецЕсли;
	
	ЭЦП_РЭМД.Параметры.УстановитьЗначениеПараметра("Ссылка", МедицинскийДокумент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЭЦП_РЭМДВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.СообщениеРЭМД);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТекстЗапросаПМО(МедицинскийДокумент)
	
	ЭЦП_РЭМД.ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПрохождениеМедосмотраЭМД.ВидДокументаЭМД КАК ВидДокументаЭМД,
		|	ПрохождениеМедосмотраЭМД.ПутьКФайлуВАрхиве КАК ПутьКФайлуВАрхиве,
		|	ПрохождениеМедосмотраЭМД.СообщениеРЭМД КАК СообщениеРЭМД
		|ИЗ
		|	Документ.ПрохождениеМедосмотра.ЭМД КАК ПрохождениеМедосмотраЭМД
		|ГДЕ
		|	ПрохождениеМедосмотраЭМД.Ссылка = &Ссылка";
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстЗапросаВК(МедицинскийДокумент)
	
	ЭЦП_РЭМД.ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&ВидДокументаЭМД КАК ВидДокументаЭМД,
		|	ВК.СообщениеРЭМД КАК СообщениеРЭМД,
		|	ВК.ПутьКФайлуВАрхиве КАК ПутьКФайлуВАрхиве
		|ИЗ
		|	Документ.ВрачебнаяКомиссия КАК ВК
		|ГДЕ
		|	ВК.Ссылка = &Ссылка";
	
	ВидДокумента = ИнтеграцияЕГИСЗОбщегоНазначенияПовтИсп.ВидРЭМДВрачебнойКомиссии();
	ЭЦП_РЭМД.Параметры.УстановитьЗначениеПараметра("ВидДокументаЭМД", ВидДокумента);
	
КонецПроцедуры

#КонецОбласти