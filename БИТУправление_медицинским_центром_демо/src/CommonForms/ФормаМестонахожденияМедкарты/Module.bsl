#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Заявка) Тогда
		Если Не Параметры.ВозвратКарты Тогда
			ВрачЗаявки = Параметры.Заявка.Сотрудник;
			Врач = Параметры.Заявка.Сотрудник;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Клиника) Тогда
		Клиника = Параметры.Клиника;
	Иначе
		Клиника = УправлениеНастройками.ПолучитьФилиалПоУмолчаниюПользователя();
	КонецЕсли;
	МедицинскаяКарта = Параметры.Медкарта;
	Клиент = МедицинскаяКарта.Клиент;
	
	Если Параметры.ВозвратКарты Тогда
		Элементы.Врач.Доступность = Ложь;
		Элементы.ВыдатьНаРуки.Доступность = Ложь;
		
		Элементы.ФормаОК.Заголовок = "Вернуть карту";
	Иначе
		Элементы.ФормаОК.Заголовок = "Выдать/Переместить карту";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьМестонахождениеМедКарты();
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Медкарта", МедицинскаяКарта);
	ПараметрыОповещения.Вставить("Клиент", Клиент);
	ПараметрыОповещения.Вставить("Выдана", Не Параметры.ВозвратКарты);
	Оповестить("ИзменениеМестонахожденияМедкарты", ПараметрыОповещения);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьМестонахождениеМедКарты()
	
	Запись = РегистрыСведений.МестонахожденияМедкарт.СоздатьМенеджерЗаписи();
	Запись.Период = ТекущаяДата();
	Запись.МедицинскаяКарта = МедицинскаяКарта;
	Запись.Клиника = Клиника;
	Запись.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	Если Не Параметры.ВозвратКарты Тогда
		Запись.Сотрудник = ?(ВыдатьНаРуки,Неопределено,Врач);
		Запись.ВыданаНаРуки = ВыдатьНаРуки;
	КонецЕсли;
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВыдатьНаРукиПриИзменении(Элемент)
	
	Если ВыдатьНаРуки Тогда
		Врач = Неопределено;
	Иначе
		Врач = ВрачЗаявки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВрачПриИзменении(Элемент)
	
	ВыдатьНаРуки = Не ЗначениеЗаполнено(Врач);
	
КонецПроцедуры

#КонецОбласти

