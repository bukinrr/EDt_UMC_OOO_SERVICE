#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Состояние") Тогда
		Если Параметры.Состояние = Перечисления.СостоянияНеотправленныхДокументов.ДокументыБезСообщений Тогда
			ТекстЗапросаДокументов = ИнтеграцияЕГИСЗ_РЭМД.ТекстЗапросаДокументыБезСообщений();
			ЭтотОбъект.Заголовок = НСтр("ru='Документы без сообщений'");
		ИначеЕсли Параметры.Состояние = Перечисления.СостоянияНеотправленныхДокументов.ДокументыБезВсехПодписей Тогда
			ТекстЗапросаДокументов = ИнтеграцияЕГИСЗ_РЭМД.ТекстЗапросаДокументыБезВсехПодписей();
			ЭтотОбъект.Заголовок = НСтр("ru='Не все подписи поставлены'");
		ИначеЕсли Параметры.Состояние = Перечисления.СостоянияНеотправленныхДокументов.ДокументыСНесформированнымиФайламиРЭМД Тогда
			ТекстЗапросаДокументов = ИнтеграцияЕГИСЗ_РЭМД.ТекстЗапросаДокументыСНесформированнымиФайламиРЭМД();
			ЭтотОбъект.Заголовок = НСтр("ru='Не сформированы файлы'");
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(ТекстЗапросаДокументов);
	ТекстЗапроса.Добавить(ТекстЗапросаДанныхСоСтатусомЭП());
	
	Список.ТекстЗапроса = СтрСоединить(ТекстЗапроса, ";");
	
	Если Параметры.Свойство("ЗаСегодня") И Параметры.ЗаСегодня Тогда
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "&УсловиеЗаСегодня", "ДокументыРЭМД.ЗаСегодня");
	Иначе
		УстановитьПараметрСписка(Список, "УсловиеЗаСегодня", Истина);
	КонецЕсли;
	
	Если Параметры.Свойство("Филиал") И ЗначениеЗаполнено(Параметры.Филиал) Тогда
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "&УсловиеФилиал", "ДокументыРЭМД.Филиал = &Филиал");
		УстановитьПараметрСписка(Список, "Филиал", Параметры.Филиал);
	Иначе
		УстановитьПараметрСписка(Список, "УсловиеФилиал", Истина);
	КонецЕсли;
	
	УстановитьПараметрСписка(Список, "НачалоСегодняшнегоДня",				НачалоДня(ТекущаяДатаСеанса()));
	УстановитьПараметрСписка(Список, "ДатаНачалаОбменаСРЭМД",				ИнтеграцияЕГИСЗСерверПовтИсп.ПолучитьДатуНачалаОбменаИнформациейСРЭМД());
	УстановитьПараметрСписка(Список, "ВидДокументаВК",						ИнтеграцияЕГИСЗОбщегоНазначенияПовтИсп.ВидРЭМДВрачебнойКомиссии());
	
	Если Метаданные.Документы.Найти("ПрохождениеМедосмотра") <> Неопределено Тогда
		МодульМедосмотрыСервер = ДопСерверныеФункции.ОбщийМодуль("МедосмотрыСервер");
		УстановитьПараметрСписка(Список, "ПунктыВредныхФакторовДляМедкнижки",	МодульМедосмотрыСервер.ПунктыВредныхФакторовДляМедкнижки());
		УстановитьПараметрСписка(Список, "ВидЗаключенияДляМедкнижки",			ИнтеграцияЕГИСЗОбщегоНазначенияПовтИсп.РегистрируемыеЭлектронныеМедДокументы(194));
		УстановитьПараметрСписка(Список, "ВидЗаключенияНеДляМедкнижки",			ИнтеграцияЕГИСЗОбщегоНазначенияПовтИсп.РегистрируемыеЭлектронныеМедДокументы(103));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(,Элемент.ТекущиеДанные.Документ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьПараметрСписка(Список, ИмяПараметра, ЗначениеПараметра)
	
	Если Список.Параметры.ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных(ИмяПараметра)) <> Неопределено Тогда
		Список.Параметры.УстановитьЗначениеПараметра(ИмяПараметра, ЗначениеПараметра);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапросаДанныхСоСтатусомЭП()
	
	Возврат 
		"ВЫБРАТЬ
		|	ДокументыРЭМД.Ссылка КАК Документ,
		|	ДокументыРЭМД.Дата КАК Дата,
		|	ДокументыРЭМД.Клиент КАК Клиент,
		|	ДокументыРЭМД.Врач КАК Врач,
		|	ДокументыРЭМД.Филиал КАК Филиал,
		|	ДокументыРЭМД.ЗаСегодня КАК ЗаСегодня,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписиНет)
		|			ТОГДА 0
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНеПроверена)
		|			ТОГДА 1
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьДействительна)
		|			ТОГДА 2
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНедействительна)
		|			ТОГДА 3
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ДокументыРЭМД.ПодписанЭП
		|					ТОГДА 1
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ КАК СтатусПроверкиЭП
		|ИЗ
		|	ДокументыРЭМД КАК ДокументыРЭМД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО (КешИнформацииОбОбъектах.Объект = ДокументыРЭМД.Ссылка)
		|ГДЕ
		|	&УсловиеЗаСегодня
		|	И &УсловиеФилиал";
	
КонецФункции

#КонецОбласти