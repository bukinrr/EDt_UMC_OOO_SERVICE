
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ГруппаВремяОкончания.Видимость = Параметры.ВыбиратьПериод;
	
	Если ЗначениеЗаполнено(Параметры.ВремяНачала) Тогда
		ВремяНачала = Параметры.ВремяНачала;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ВремяОкончания) Тогда
		ВремяОкончания = Параметры.ВремяОкончания;
	КонецЕсли;
	
	ИнициализироватьПолеВыбораВремени();
	
	ПервыйТакт = Истина;
	
	ОбновитьЦветаФона();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ВыбратьЗначение();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПолеВыбораВремениПриАктивизацииОбласти(Элемент)
	
	ТекВремя = ВремяТекущейЯчейки();
	Если ТекВремя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.ВыбиратьПериод Тогда
		
		Если ПервыйТакт Тогда
			ВремяНачала = ТекВремя;
			ВремяОкончания = ВремяНачала;
		Иначе
			Если ТекВремя >= ВремяНачала Тогда
				ВремяОкончания = ТекВремя;
			Иначе
				ВремяОкончания = ВремяНачала;
				ВремяНачала = ТекВремя;
			КонецЕсли;
		КонецЕсли;
		ОбновитьЦветаФона();
		
		ПервыйТакт = Не ПервыйТакт;
	Иначе
		ВремяНачала = ТекВремя;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВыбораВремениВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если Не Параметры.ВыбиратьПериод Тогда
		ВыбратьЗначение();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьПолеВыбораВремени()
	
	КолКолонок	= 6;
	КолСтрок	= 8;
	ШагМин = 30;
	
	ШиринаКолонки = 9;
	ВысотаСтроки  = 20;
	Шрифт		  = Новый Шрифт(ШрифтыСтиля.ОченьКрупныйШрифтТекста,,ШрифтыСтиля.ОченьКрупныйШрифтТекста.Размер);
	ЛинияОбвода	  = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 1);
	
	ПолеВыбораВремени.Очистить();
	
	ОбластьГраница = ПолеВыбораВремени.Область(,1,,1);
	ОбластьГраница.ШиринаКолонки = 0.13;
	
	ОбластьГраница = ПолеВыбораВремени.Область(1,,1);
	ОбластьГраница.СоздатьФорматСтрок();
	ОбластьГраница.ВысотаСтроки = 0.25;
	
	Для вПоз = 2 По КолСтрок+1 Цикл
		
		ПолеВыбораВремени.Область(вПоз,,вПоз).ВысотаСтроки = ВысотаСтроки;
		
		Для гПоз = 2 По КолКолонок+1 Цикл
			Если вПоз = 2 Тогда
				ПолеВыбораВремени.Область(,гПоз,,гПоз).ШиринаКолонки = ШиринаКолонки;
			КонецЕсли;
			
			ВремяЯчейки = ВремяЯчейки(вПоз, гПоз);
			Область = ПолеВыбораВремени.Область(вПоз, гПоз);
			Область.Текст = Формат(ВремяЯчейки, "ДФ=HH:mm; ДП=");
			
			Область.ГоризонтальноеПоложение	= ГоризонтальноеПоложение.Центр;
			Область.ВертикальноеПоложение	= ВертикальноеПоложение.Центр;
			Область.ЦветРамки = ЦветаСтиля.ЦветРамкиКнопки;
			Область.Обвести(ЛинияОбвода,ЛинияОбвода,ЛинияОбвода,ЛинияОбвода);
		КонецЦикла;
	КонецЦикла;	
	
	ПолеВыбораВремени.Область().Шрифт = Шрифт;
	
	ОбновитьЦветаФона();
	
КонецПроцедуры

&НаКлиенте
Функция ВремяТекущейЯчейки()
	
	Текст = ПолеВыбораВремени.ТекущаяОбласть.Текст;
	
	Если ЗначениеЗаполнено(Текст) Тогда
		Возврат Дата("0001.01.01 " + Текст + ":00");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВыбратьЗначение()
	
	Если Параметры.ВыбиратьПериод Тогда
		ЗначениеВыбора = Новый Структура("ВремяНачала, ВремяОкончания", ВремяНачала, ВремяОкончания);
	Иначе
		ЗначениеВыбора = ВремяНачала;
	КонецЕсли;
	
	Закрыть(ЗначениеВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЦветаФона()
	
	ЦветНеактуальногоВремени = ДопСерверныеФункцииПовтИсп.ЦветСтиля("НедоступныеДанныеЦвет"); 
	ЦветВыбранногоВремени	 = ДопСерверныеФункцииПовтИсп.ЦветСтиля("ЦветАктивности"); 
	ЦветОбычногоФона		 = ДопСерверныеФункцииПовтИсп.ЦветСтиля("ЦветФонаФормы"); 
	
	Для вПоз = 2 По КолСтрок+1 Цикл
		Для гПоз = 2 По КолКолонок+1 Цикл
			
			Область = ПолеВыбораВремени.Область(вПоз,гПоз);
			ВремяЯчейки = ВремяЯчейки(вПоз, гПоз);
			
			Если Параметры.ВыбиратьПериод
				И ВремяЯчейки >= ВремяНачала
				И ВремяЯчейки <= ВремяОкончания
			Тогда
				Область.ЦветФона = ЦветВыбранногоВремени;
			Иначе
				Если ВремяЯчейки < Параметры.ВремяНачалаАктуальности
					Или (ЗначениеЗаполнено(Параметры.ВремяОкончанияАктуальности) И ВремяЯчейки >= Параметры.ВремяОкончанияАктуальности)
				Тогда
					Область.ЦветФона = ЦветНеактуальногоВремени;
				Иначе
					Область.ЦветФона = ЦветОбычногоФона;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВремяЯчейки(вПоз, гПоз)
	Возврат Дата(1,1,1) + ШагМин * 60 * (гПоз-2 + (вПоз-2)*КолКолонок);
КонецФункции

&НаКлиенте
Процедура ВремяНачалаПриИзменении(Элемент)
	
	ВремяОкончания = Макс(ВремяОкончания, ВремяНачала);
	ОбновитьЦветаФона();
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяОкончанияПриИзменении(Элемент)
	
	ВремяНачала = Мин(ВремяНачала, ВремяОкончания);
	ОбновитьЦветаФона();

КонецПроцедуры

#КонецОбласти

