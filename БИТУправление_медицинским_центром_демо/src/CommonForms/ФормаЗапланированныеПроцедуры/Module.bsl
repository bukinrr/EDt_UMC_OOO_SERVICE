#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Клиент = Параметры.Клиент;
	ЗаполнитьСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЗаявку(Команда)
	
	ВыделенныеСтроки = Элементы.ТаблицаНазначений.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() <> 0 Тогда
		
		ТекущиеДанные = ТаблицаНазначений.НайтиПоИдентификатору(ВыделенныеСтроки[0]);
		
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("Клиент", Клиент);
		ДанныеЗаполнения.Вставить("Сотрудник", ТекущиеДанные.Врач);
		
		ДанныеЗаполнения.Вставить("Работы", Новый Массив);
		
		Для Каждого ВыделеннаяСтрока Из Элементы.ТаблицаНазначений.ВыделенныеСтроки Цикл
			ДанныеСтроки = ТаблицаНазначений.НайтиПоИдентификатору(ВыделеннаяСтрока);
			Если ЗначениеЗаполнено(ДанныеСтроки.Услуга) Тогда
				СтруктураУслуги = Новый Структура("Номенклатура, Сотрудник", ДанныеСтроки.Услуга, ДанныеСтроки.Врач);
				ДанныеЗаполнения.Работы.Добавить(СтруктураУслуги);
			КонецЕсли;
		КонецЦикла;
		
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ДанныеЗаполнения);
		ОткрытьФорму("Документ.Заявка.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьСервер();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСервер()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
				"ВЫБРАТЬ
				|	ВЫБОР
				|		КОГДА ПланыЛечения.Номенклатура ССЫЛКА Справочник.Номенклатура
				|			ТОГДА ПланыЛечения.Номенклатура
				|		ИНАЧЕ NULL
				|	КОНЕЦ КАК Услуга,
				|	ВЫБОР
				|		КОГДА ПланыЛечения.Номенклатура ССЫЛКА Справочник.Номенклатура
				|			ТОГДА ПланыЛечения.Номенклатура.КатегорияВыработки
				|		ИНАЧЕ ПланыЛечения.Номенклатура
				|	КОНЕЦ КАК Специализация,
				|	ПланыЛечения.Дата КАК Дата,
				|	ПланыЛечения.Сотрудник КАК Врач,
				|	ПланыЛечения.Количество КАК Количество,
				|	ПланыЛечения.ИдентификаторЭлемента КАК ИдентификаторЭлемента,
				|	ПланыЛечения.ПервичныйПрием КАК ПервичныйПрием
				|ПОМЕСТИТЬ ПланыЛечения
				|ИЗ
				|	РегистрСведений.ПланыЛечения КАК ПланыЛечения
				|ГДЕ
				|	ИСТИНА
				|	И ПланыЛечения.ДокументРегистратор В(&МассивПриемов)
				|	И ПланыЛечения.Дата <= &Дата
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ПланыЛечения.Услуга КАК Услуга,
				|	ПланыЛечения.Специализация КАК Специализация,
				|	ПланыЛечения.Дата КАК Дата,
				|	ПланыЛечения.Врач КАК Врач,
				|	ПланыЛечения.Количество - СУММА(ЕСТЬNULL(ВыполнениеПланаЛечения.Количество, 0)) КАК Количество
				|ИЗ
				|	ПланыЛечения КАК ПланыЛечения
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыполнениеПланаЛечения КАК ВыполнениеПланаЛечения
				|		ПО ПланыЛечения.ИдентификаторЭлемента = ВыполнениеПланаЛечения.ИдентификаторЭлемента
				|			И ПланыЛечения.ПервичныйПрием = ВыполнениеПланаЛечения.ПервичныйПрием
				|
				|СГРУППИРОВАТЬ ПО
				|	ПланыЛечения.Услуга,
				|	ПланыЛечения.Специализация,
				|	ПланыЛечения.Дата,
				|	ПланыЛечения.Врач,
				|	ПланыЛечения.Количество
				|
				|ИМЕЮЩИЕ
				|	СУММА(ВыполнениеПланаЛечения.Количество) < ПланыЛечения.Количество";
	
	Если Дата = Дата("00010101") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И ПланыЛечения.Дата <= &Дата","");
	Иначе
		Запрос.УстановитьПараметр("Дата", Дата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Клиент) Тогда
		ЗапросПриемы = Новый Запрос;
		ЗапросПриемы.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	Прием.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.Прием КАК Прием
	               |ГДЕ
	               |	Прием.Клиент = &Клиент";
		ЗапросПриемы.УстановитьПараметр("Клиент", Клиент);
		МассивПриемов = ЗапросПриемы.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

		Запрос.УстановитьПараметр("МассивПриемов", МассивПриемов);
		ТаблицаНазначений.Загрузить(Запрос.Выполнить().Выгрузить());
	Иначе
		ТаблицаНазначений.Очистить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КлиентПриИзменении(Элемент)
	ЗаполнитьСервер();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ЗаполнитьСервер();
КонецПроцедуры

#КонецОбласти