
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Сертификат = Параметры.Сертификат;
	Свойства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сертификат,
		"ДанныеСертификата, УсиленнаяЗащитаЗакрытогоКлюча, Пользователь, Добавил, Программа");
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Свойства);
	ДанныеСертификата = ДанныеСертификата.Получить();
	
	Если УсиленнаяЗащитаЗакрытогоКлюча Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Программа) Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьПравоЗаписиПароля = ПроверитьПравоЗаписи();
	
	УстановитьПривилегированныйРежим(Истина);
	Данные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Сертификат,"ПаролиСертификатов");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТипЗнч(Данные) <> Тип("Соответствие") Тогда
		Данные = Новый Соответствие;
	КонецЕсли;
	
	Пользователь = Пользователи.ТекущийПользователь();
	Пароль = Данные.Получить(Пользователь);
	
	Если НЕ ЗначениеЗаполнено(Пароль) Тогда   
		Пользователь = Справочники.Пользователи.ПустаяСсылка();
		Пароль = Данные.Получить(Пользователь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Пароль) Тогда
		ПарольУстановлен = Истина;
		Пароль = "********";
	Иначе
		Пользователь = Пользователи.ТекущийПользователь();
		ПарольУстановлен = Ложь;
	КонецЕсли;
	
	Элементы.ФормаУдалитьПароль.Доступность = ПарольУстановлен;
	ИзменитьНадписьДоступности();
	Элементы.ПользователиПароляДоступенВсем.Пометка = НЕ ЗначениеЗаполнено(Пользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если УсиленнаяЗащитаЗакрытогоКлюча Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'У сертификата установлена усиленная защита закрытого ключа.
			           |В таком случае пароль запрашивает программа электронной подписи и шифрования,
			           |а программа 1С:Предприятия должна передать пустой пароль, чтобы не было ошибки.
			           |
			           |Запоминание и запись пароля невозможны.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Программа) Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'У сертификата не указана программа для закрытого ключа.
			           |Невозможно проверить пароль перед записью.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЕстьПравоЗаписиПароля Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Недостаточно прав для записи пароля.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольИзменен = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьПароль(Команда)
	
	УдалитьПарольНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПароль(Команда)
	
	ПроверитьПароль();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПарольПослеПроверки(Результат, ОписаниеОшибки)
	
	Если Результат Тогда
		ЗаписатьПарольНаСервере();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ПоказатьПредупреждение(, СокрЛП(ОписаниеОшибки));
	КонецЕсли;
	
	Если Результат Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДоступностьПароля(Команда)
	
	ИзменитьДоступностьПароляНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УдалитьПарольНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(Сертификат,"ПаролиСертификатов");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Элементы.ФормаУдалитьПароль.Доступность = Ложь;
	Пользователь = Пользователи.ТекущийПользователь();
	Пароль = "";
	ПарольИзменен = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПароль()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПослеПолученияМенеджера", ЭтотОбъект);
	ПроверитьНаКлиенте(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПослеПолученияМенеджера(Результат, ДополнительныеПараметры) Экспорт
	
	ОписаниеОшибки = "";
	Если ТипЗнч(Результат) = Тип("МенеджерКриптографии") Тогда
		Успех = СертификатПодписан(ЭтотОбъект,ДанныеСертификата,Программа, Пароль, ОписаниеОшибки, Результат);
	Иначе
		Успех = Ложь;
		ОписаниеОшибки = Результат;
	КонецЕсли;
	
	ЗаписатьПарольПослеПроверки(Успех, ОписаниеОшибки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СертификатПодписан(Форма,ДанныеСертификата,Программа, Пароль, ОписаниеОшибки, МенеджерКриптографии)
	
	Сертификат = Новый СертификатКриптографии(ДанныеСертификата);
	
	МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = Пароль;
	Попытка
		МенеджерКриптографии.Подписать(ДанныеСертификата, Сертификат);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + Символы.ПС
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось пройти проверку подписания с помощью программы %1 по причине:
				           |%2'"),
				Программа,
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗаписатьПарольНаСервере()
	
	Если Не ПроверитьПравоЗаписи() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для записи пароля.'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Данные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Сертификат,"ПаролиСертификатов");
		Если ТипЗнч(Данные) <> Тип("Соответствие") Тогда
			Данные = Новый Соответствие;
		КонецЕсли;
		Если ПарольИзменен ИЛИ НЕ ЗначениеЗаполнено(Пароль) Тогда
			Данные.Вставить(Пользователь,Пароль);
		Иначе
			СохраненныйПароль = Данные.Получить(Пользователь);
			Если НЕ ЗначениеЗаполнено(СохраненныйПароль) Тогда
				
				ВызватьИсключение НСтр("ru = 'Введите пароль повторно.'");
			КонецЕсли;
		КонецЕсли;
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Сертификат,Данные,"ПаролиСертификатов");
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Элементы.ФормаУдалитьПароль.Доступность = Истина;
	Пароль = "********";
	ПарольИзменен = Ложь;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПроверитьПравоЗаписи()
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Свойства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сертификат, "Пользователь, Добавил");
	Если Свойства.Добавил = Пользователи.ТекущийПользователь()
	 Или Свойства.Пользователь = Пользователи.ТекущийПользователь() Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ИзменитьДоступностьПароляНаСервере()
	
	Если Элементы.ПользователиПароляДоступенВсем.Пометка Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	Иначе
		Пользователь = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	ИзменитьНадписьДоступности();
	Элементы.ПользователиПароляДоступенВсем.Пометка = НЕ ЗначениеЗаполнено(Пользователь);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьНадписьДоступности()
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		Элементы.НадписьДоступность.Заголовок = НСтр("ru = 'Пароль доступен пользователю:'")
			+ " " + Пользователь;
	Иначе
		Элементы.НадписьДоступность.Заголовок = НСтр("ru = 'Пароль доступен всем пользователям.'")
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНаКлиенте(Оповещение)
	
	Контекст = Новый Структура("Оповещение", Оповещение);
	                                             
	НачатьПодключениеРасширенияРаботыСКриптографией(Новый ОписаниеОповещения(
		"ПроверитьПослеПодключенияРасширения", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПослеПодключенияРасширения(Подключено, Контекст) Экспорт
	
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Новый ОписаниеОповещения(
			"ПроверитьПослеПолученияМенеджера", ЭтотОбъект, Контекст),
		"ПроверкаСертификата", Истина, Программа);
	
КонецПроцедуры

#КонецОбласти
