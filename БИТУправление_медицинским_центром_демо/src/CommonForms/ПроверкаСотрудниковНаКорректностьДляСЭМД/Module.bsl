#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьТаблицыСотрудников();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаСотрудников

&НаКлиенте
Процедура ТаблицаСотрудниковПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элемент.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда
		ИзменитьДекорациюУсловия(0,Ложь);
		ИзменитьДекорациюУсловия(1,Ложь);
		ИзменитьДекорациюУсловия(2,Ложь);
		ИзменитьДекорациюУсловия(3,Ложь);
		Возврат;
	КонецЕсли;
	
	Элементы.ДекСотрудник.Заголовок = "Сотрудник: " + Строка(ТекДанные.Сотрудник);
	Если ТекДанные.ВсеКорректно Тогда
		Элементы.Статус.Заголовок = "Статус: Все хорошо";
		Элементы.Статус.ЦветТекста = WebЦвета.Зеленый;
	Иначе
		Элементы.Статус.Заголовок = "Статус: Есть проблемы";
		Элементы.Статус.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
	СтруктураКорректности = ТекДанные.СтруктураКорректности;
	ИзменитьДекорациюУсловия(0, СтруктураКорректности.ЕстьСертификат);
	ИзменитьДекорациюУсловия(1, СтруктураКорректности.СертификатДействителен);
	ИзменитьДекорациюУсловия(2, СтруктураКорректности.ЗаполненоФИО);
	ИзменитьДекорациюУсловия(3, СтруктураКорректности.ЗаполненСНИЛС);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСотрудниковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("Ключ", Элемент.ТекущиеДанные.Сотрудник);
	ОткрытьФорму("Справочник.Сотрудники.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьТаблицыСотрудников();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицыСотрудников()
	
	ТаблицаСотрудников.Очистить();
	РезультатПроверки = ИнтеграцияЕГИСЗ_СЭМД.СписокСотрудниковИКорректностиЗаполненияДляПодписанияСЭМД();
	
	Для каждого РезультатСотрудника Из РезультатПроверки Цикл
		НоваяСтрока = ТаблицаСотрудников.Добавить();
		НоваяСтрока.Сотрудник = РезультатСотрудника.Сотрудник;
		НоваяСтрока.СтруктураКорректности = РезультатСотрудника;
		НоваяСтрока.ВсеКорректно = РезультатСотрудника.ВсеКорректно;
	КонецЦикла;
	
	ТаблицаСотрудников.Сортировать("ВсеКорректно Возр, Сотрудник Возр");
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДекорациюУсловия(НомерУсловия, Результат)
	
	Если Результат Тогда
		Элементы["УсловиеКартинка" + Строка(НомерУсловия)].Картинка = БиблиотекаКартинок.Успешно;
	Иначе
		Элементы["УсловиеКартинка" + Строка(НомерУсловия)].Картинка = БиблиотекаКартинок.НеудачнаяПроверкаНаличияМДЛП;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти