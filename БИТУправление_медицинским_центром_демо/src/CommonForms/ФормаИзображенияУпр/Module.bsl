#Область РазделОписанияПеременных

Перем ПутьТекКартинки;

&НаКлиенте
Перем ИскомаяПапка;

&НаКлиенте
Перем КлиентВладелец;

#КонецОбласти

// ОБРАБОТКА КОМАНД ТЧ СОДЕРЖИМОЕ ДИРЕКТОРИИ
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	ОбщегоНазначенияКлиент.ДобавитьИзображение(ИскомаяПапка);
	ЗаполнитьТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	ТС = Элементы.СодержаниеДиректории.ТекущиеДанные;
	
	Если ТС <> Неопределено Тогда
		ЗапуститьПриложение(ТС.ПолныйПуть);
	Иначе	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Файл не выбран");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПапку(Команда)
	
	ИскомаяПапка = ПроцедурыСпециализацииПоставки.ПолучитьБазовыйКаталогФото(КлиентВладелец);
	
	Файл = Новый Файл(ИскомаяПапка);
	
	Если Файл.Существует() Тогда
		ЗапуститьПриложение(ИскомаяПапка);
	Иначе	
		СоздатьКаталог(ИскомаяПапка);
		ЗапуститьПриложение(ИскомаяПапка);
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяФотография(Команда)
	
	Если Элементы.СодержаниеДиректории.ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не выбрана фотография в списке");
		Возврат;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(Объект) Тогда
		Предупреждение("Клиент не выбран");
		Возврат;
	КонецЕсли;
	
	Оповестить("ОбновитьФотографию", Новый Структура("Фотография, ВладелецФормы, Модифицированность",Элементы.СодержаниеДиректории.ТекущиеДанные.ПолныйПуть, ВладелецФормы, Истина), Объект);
	ТекОснИзображение = Элементы.СодержаниеДиректории.ТекущиеДанные.ПолныйПуть;
	
	УстановитьУсловноеОформление();
	Элементы.СодержаниеДиректории.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайл(Команда)
	ТекДанные = Элементы.СодержаниеДиректории.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Ответ = Вопрос("Удалить файл " +  ТекДанные.Файл, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			форма = новый Файл(ТекДанные.ПолныйПуть);
			Если форма.Существует() Тогда
				Попытка
					УдалитьФайлы(форма.ПолноеИмя);
					ЗаполнитьТЗ();
				Исключение
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
// 
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СодержаниеДиректорииПриАктивизацииСтроки(Элемент)
	
	ОтображениеКартинки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектПриИзменении(Элемент)
	
	ИскомаяПапка = ПроцедурыСпециализацииПоставки.ПолучитьБазовыйКаталогФото(Объект);
	
	КаталогНаДиске = Новый Файл(ИскомаяПапка);
	Если НЕ КаталогНаДиске.Существует() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("У данного клиента нет фотографий");
		СодержаниеДиректории.Очистить();
	Иначе
		ЗаполнитьТЗ();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.Объект.Заголовок = "Клиент";
	
	Если ТипЗнч(ВладелецФормы.Объект.Ссылка) = Тип("ДокументСсылка.ОказаниеУслуг") Тогда
		КлиентВладелец = ВладелецФормы.Объект.Клиент;
	Иначе
		КлиентВладелец = ВладелецФормы.Объект.Ссылка;
	КонецЕсли;
	
	Если КлиентВладелец <> Неопределено Тогда
		
		ИскомаяПапка = ПроцедурыСпециализацииПоставки.ПолучитьБазовыйКаталогФото(КлиентВладелец);
		
		Объект = КлиентВладелец;
		
		ПутьТекКартинки = ТекОснИзображение; 
		ЗаполнитьТЗ();	
		Если ЗначениеЗаполнено(ПутьТекКартинки) И ТипЗнч(ПутьТекКартинки) <> Тип("СправочникСсылка.ХранилищеДополнительнойИнформации")   Тогда
			
			Файл = Новый Файл(ПутьТекКартинки);
			
			Если Файл.Существует() Тогда
				
				ПолеИзображения = ПоместитьВоВременноеХранилище(Новый Картинка(ПутьТекКартинки), УникальныйИдентификатор); 		
				
				Пар = Новый Структура("ПолныйПуть", Строка(ПутьТекКартинки));
				масСтрок = СодержаниеДиректории.НайтиСтроки(Пар);
				Если масСтрок.Количество() > 0 Тогда
					Элементы.СодержаниеДиректории.ТекущаяСтрока = масСтрок[0].ПолучитьИдентификатор();
				КонецЕсли;	
				
			КонецЕсли;
		Иначе
			Если СодержаниеДиректории.Количество() = 1 Тогда
				ТекОснИзображение = СодержаниеДиректории[0].ПолныйПуть;
				Оповестить("ОбновитьФотографию", Новый Структура("Фотография, ВладелецФормы, Модифицированность", СодержаниеДиректории[0].ПолныйПуть, ВладелецФормы, Истина), Объект);
				УстановитьУсловноеОформление();
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ТекФото") Тогда
		ТекОснИзображение = Параметры.ТекФото;
	КонецЕсли;	
	УстановитьУсловноеОформление();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Ожирнить срочку
	
	УО = УсловноеОформление.Элементы;
	
	Если УО.Количество() > 0 Тогда
		ЭлементУО = УО[0];
	Иначе
		ЭлементУО = УО.Добавить();
	КонецЕсли;
	
	ЭлементУО.Отбор.Элементы.Очистить();
	ЭлементУсловия  = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста",WebЦвета.Черный);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый  Шрифт(WindowsШрифты.ШрифтДиалоговИМеню, , , Истина, , , ) );
	
	// Условие форматирования
	
	ЭлементУсловия.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СодержаниеДиректории.ПолныйПуть");
	ЭлементУсловия.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементУсловия.ПравоеЗначение = ТекОснИзображение;
	
	// Оформляемое поле
	ЭлементУО.Поля.Элементы.Очистить();
	ОформлПоле1 = ЭлементУО.Поля.Элементы.Добавить();
	ОформлПоле1.Поле = Новый ПолеКомпоновкиДанных("СодержаниеДиректорииФайл");
	
	// Оформляемое поле
	ОформлПоле2 = ЭлементУО.Поля.Элементы.Добавить();
	ОформлПоле2.Поле = Новый ПолеКомпоновкиДанных("СодержаниеДиректорииПолныйПуть");
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеКартинки()
	
	ТекДанные = Элементы.СодержаниеДиректории.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Попытка
			ПолеИзображения = ПоместитьВоВременноеХранилище( Новый Картинка(ИскомаяПапка + "\" + ТекДанные.Файл), УникальныйИдентификатор); 
		Исключение
			ПолеИзображения = "";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось отобразить изображение " + ТекДанные.Файл);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура  ЗаполнитьТЗ()  
	
	СодержаниеДиректории.Очистить();
	
	НайденныеФайлы = НайтиФайлы(ИскомаяПапка,"*.*");
	
	Для Каждого ИскФайл из НайденныеФайлы Цикл 
		Если ИскФайл.ЭтоФайл() Тогда
			НС = СодержаниеДиректории.Добавить();
			НС.Файл = ИскФайл.Имя;
			НС.ПолныйПуть = ИскФайл.ПолноеИмя;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти




