#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипСовпадения = "EXACT";
	МенеджерСправочника = Справочники[Параметры.НаименованиеСправочника];
	СписокПолей = МенеджерСправочника.СписокПолейДляЗагрузкиПоОтбору();
	Первое = Истина;
	Для Каждого ПолеОтбора Из СписокПолей Цикл
		Элементы.Поле.СписокВыбора.Добавить(ПолеОтбора.Ключ,ПолеОтбора.Значение.Представление);
		Если Первое Тогда
			Поле = ПолеОтбора.Ключ;
			Первое = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ТестовыйКонтур.Видимость = Параметры.Свойство("ДоступенВыборТестовогоКонтура") И Параметры.ДоступенВыборТестовогоКонтура;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолеПриИзменении(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеПриИзменении(Элемент)
	
	ПараметрыПоля = СписокПолей[Поле];
	ТипСовпадения = "EXACT";
	Элементы.ТипСовпадения.Доступность = Не ПараметрыПоля.ТолькоТочноеСовпадение;
	ТипДанных = ПараметрыПоля.ТипДанных;
	МассивТипов = Новый Массив();
	МассивТипов.Добавить(ТипДанных);
	Элементы.Значение.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	МинДлинаОтбора = 3;
	
	Если ТипСовпадения = "LIKE"
		И ТипЗнч(Значение) = Тип("Строка")
		И СтрДлина(Значение) <= МинДлинаОтбора
	Тогда
		// Слишком короткое значение строки отбора
		ПоказатьПредупреждение(, "Значение отбор слишком короткое, укажите минимум " + МинДлинаОтбора + " символа.");
	
	ИначеЕсли ЗначениеЗаполнено(Значение) Тогда
		Если ТипЗнч(Значение) = Тип("СправочникСсылка.РеестрМедицинскихОрганизаций") Тогда
			ТестовыйКонтур = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Значение, "ТестовыйКонтур");
		КонецЕсли;
		СтруктураПараметры = Новый Структура("НаименованиеСправочника, Отбор, ПоследнююВерсию, ТестовыйКонтур, ВыбратьЕдинственный", Параметры.НаименованиеСправочника, ПараметрыОтбора(), Истина, ТестовыйКонтур, Параметры.ВыбратьЕдинственный);
		Оповещение = Новый ОписаниеОповещения("ЗагрузкаКлассификатораЗавершена",ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.ЗагрузкаНСИЕГИСЗ", СтруктураПараметры,, ЭтаФорма,,,Оповещение);
	Иначе
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Заполните поле значения!'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПараметрыОтбора()
	
	ПараметрыОтбора = Новый Структура();
	СтрокаФильтра = "%1|%2|%3";
	МассивФильтров = Новый Массив();
	ЗначениеОтбора = Значение;
	Если ТипЗнч(ЗначениеОтбора) = Тип("СправочникСсылка.РеестрМедицинскихОрганизаций") Тогда
		ЗначениеОтбора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеОтбора,"OIDОрганизации");
	КонецЕсли;
	МассивФильтров.Добавить(СтрШаблон(СтрокаФильтра,Поле,ЗначениеОтбора,ТипСовпадения));
	ПараметрыОтбора.Вставить("filters", МассивФильтров);
	
	Возврат ПараметрыОтбора;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузкаКлассификатораЗавершена(Результат, ДополнительныеПараметры) Экспорт
	
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти