#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	АвторизованныйПользователь = ПараметрыСеанса.ТекущийПользователь;
	ЗаправшиватьПодтверждениеПриЗакрытииПрограммы = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(,"ЗапрашиватьПодтверждениеПриЗакрытии");
	ПоказыватьПодменюПерейти = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(,"ПоказыватьПодменюПерейти");
	РасположениеПрограммыWkhtmltopdfНаКлиенте = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(,"РасположениеПрограммыWkhtmltopdfНаКлиенте");
	Элементы.ГруппаОтправкаМедДокументовВФорматеPDF.Видимость = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(,"ФормироватьPDFНаКлиенте");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СведенияОПользователе(Команда)
	
	ПоказатьЗначение(, АвторизованныйПользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПарольПользователя(Команда)
	ОткрытьФорму("ОбщаяФорма.ИзменениеПароляПользователяИБ");
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСканера(Команда)
	РаботаСФайламиКлиент.ОткрытьФормуНастройкиСканирования();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	УправлениеПользователями.УстановитьЗначение(,"ЗапрашиватьПодтверждениеПриЗакрытии", ЗаправшиватьПодтверждениеПриЗакрытииПрограммы);
	УправлениеПользователями.УстановитьЗначение(,"ПоказыватьПодменюПерейти", ПоказыватьПодменюПерейти);
	УправлениеПользователями.УстановитьЗначение(,"РасположениеПрограммыWkhtmltopdfНаКлиенте", РасположениеПрограммыWkhtmltopdfНаКлиенте);
	ПараметрыПриложения.Вставить("ЗапрашиватьПодтверждениеПриЗакрытии", ЗаправшиватьПодтверждениеПриЗакрытииПрограммы);
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПроксиСервера(Команда)
	
#Если ВебКлиент Тогда
	ПоказатьПредупреждение(, НСтр("ru = 'В веб-клиенте параметры прокси-сервера необходимо задавать в настройках браузера.'"));
	Возврат;
#КонецЕсли
	
	ОткрытьФорму("ОбщаяФорма.ПараметрыПроксиСервера", Новый Структура("НастройкаПроксиНаКлиенте", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура Скачатьwkhtmltopdf(Команда)
	ЗапуститьПриложение("https://wkhtmltopdf.org/downloads.html");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РасположениеПрограммыWkhtmltopdfНаКлиентеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Каталог	 = "C:\Program Files\wkhtmltopdf\bin\";
	ИмяФайла = "wkhtmltopdf.exe";
	
	Если ЗначениеЗаполнено(РасположениеПрограммыWkhtmltopdfНаКлиенте) Тогда
		Файл = Новый Файл(РасположениеПрограммыWkhtmltopdfНаКлиенте);
		Если Файл.Существует() Тогда
			Каталог	 = Файл.Путь;
			ИмяФайла = Файл.Имя;
		КонецЕсли;
	КонецЕсли;
	
	ДиалогВыборФайла.Каталог = Каталог;
	ДиалогВыборФайла.Фильтр = "wkhtmltopdf.exe|wkhtmltopdf.exe";
	ДиалогВыборФайла.МножественныйВыбор = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("РасположениеПрограммыWkhtmltopdfНаКлиентеВыбор", ЭтотОбъект);
	ДиалогВыборФайла.Показать(Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РасположениеПрограммыWkhtmltopdfНаКлиентеВыбор(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив")
		И ВыбранныеФайлы.Количество() <> 0
	Тогда
		РасположениеПрограммыWkhtmltopdfНаКлиенте = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
